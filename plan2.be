 МАСRО
∧М БЛПР
∧М УИА ’2003’
 МЕND
**********          **********
 МАСRО
∧М ОТПР
∧М УИА 3
 МЕND
**********          **********
 МАСRО
∧М ВХ
∧М НОП
 ВХОДН ∧М
 МЕND
**********          **********
ПЛАН2 СТАРТ ’11000’
 GВLВ ∧НБОП
∧НБОП УСТВ 0
 Б
 Е
 М
Б ВНЕШН ПЛАН.ПЛАН
М ЭКВ М7
 УПОТР ПЛАН2-512(М1)
 СЧ МГРП
 И ММГРП-Б(М)    БЕЗ ВНЕШНИХ, СТОП’А  И  ...
 ЗП КМГРП(М1)
 СЧ ШИФРАЦ
 И Е30П25-Б(М)
 ЗП РАЯ-Б(М)
 ПЕ ЕСБОИ         СБОИ БЫЛИ
 СЧ ВСБОЯ(М1)
 ПО РАНО
 ПБ НЕТСБО
ЕСБОИ СЧ ВСБОЯ(М1)
 ПЕ ШЕФ           БЫЛ СБОЙ РАНЬШЕ
 СЧ ВРЕМЯ
 ПБ ВРЕСБО
ШЕФ ВЧОБ ВРЕМЯ
 ВЧАБ Е16           11 МИНУТ
 ПЕ РАНО          ПОВТ ВЫДАЧИ РАНО
 СЧ ШИФРАЦ
 НТЖ РАЯ-Б(М)
 ЗП ШИФРАЦ        УБР.ПРИЗНАК СБОЯ
 СЧ РАЯ-Б(М)
 СДА 64-13
 ИЛИ ШКПЛАН
 ЗП ШКПЛАН        ПОВТОРИТЬ ВЫДАЧУ
НЕТСБО НОП ,
 СЧ 0
ВРЕСБО ЗП ВСБОЯ(М1)
 УИА 1(М6)
РАНО НОП
*
********** ПРОВЕРКА ПЕРЕПОЛНЕНИЯ БУФЕРА ВЫВОДА **********
 ОТПР
 СЧ РЯПСЗ
 И Е12
 ПО ХОРБУФ        НЕТ ПЕРЕП.БУФЕРА
 МОДА ВЫВОД-Б(М)
 УИА (М16)
 МОДА ПЛОХБФ
 УИА (М15)
 УИИ М32(М15)
 СЧ Е23
 ПБ ТВ40
ПЛОХБФ СЧ ВРЕ1(М1)
 ПЕ ПЛОХБ1
 СЧ ВРЕМЯ
 ПБ ВРЕПЕР
ПЛОХБ1 ВЧОБ ВРЕМЯ
 ВЧАБ Е17           21 МИНУТА
 ПЕ ХОРБУФ        ПОВТОРЯТЬ ВЫДАЧИ РАНО
 БЛПР
 СЧ РЯПСЗ
 НТЖ П77
 И П77
 ИЛИ ШКПЛАН
 ЗП ШКПЛАН
 СЧ П77
 ИЛИ РЯПСЗ
 ЗП РЯПСЗ
 ОТПР
 СЧ 0
ВРЕПЕР ЗП ВРЕ1(М1)
 УИА 1(М6)
ХОРБУФ НОП
*
********** ПРОВЕРКА "СТОП" **********
 СЧ МГРП
 И ЕСТОП
 ПЕ ВЫХОД-Б(М)
*
********** ПРОВЕРКА "К РАБ.НЕ ГТ" **********
 СЧ ЗАНЯТА
 И Е7
 ПО ВЫХОД-Б(М)
*
********** НАЧАЛЬНЫЕ УСТАНОВКИ ДЛЯ КАНАЛОВ **********
*
* НАЧАЛЬНОЕ ЗНАЧЕНИЕ ЯЧЕЕК: МОДКАН,ПОРКАН,НЕТКАН = 0
*
	уиа	'2003'
	уиа	-7(М16)
G11042	сч	МПРЗ+7(М16)
	или	Е46П48
	нтж	Е46П48
	зп	МПРЗ+7(М16)
	сч	МПЗС+7(М16)
	зп	МПЗ+7(М16)
	мода	G11042
	цикл	0(М16)
 СЧ МГРП
 И Е16
 пе СЧТРЕЖ        РЕЖИМ СЧЕТА
        сч        МПРЗ
        сда       64+33
        и         П7
        сда       64-1
        или       МПЗ
        зп        МПЗ
СЧТРЕЖ НОП ,
       отпр
*СЧ ’5555’         НЕ УБИРАТЬ ! ШЕБАНОВ
*
********** ПРИЗНАКИ БЮДЖЕТ И СПЕЦ **********
 СЧ ПРЕДЕЛ
 И Е32
 СДА 64+5
 ЗП ПРБЮД-Б(М)
 СЧ ПРЕДЕЛ
 И Е17
 СДА 64+2
 ЗП ПРСПЕЦ-Б(М)
 СЧ ГОД
 СДА 64-45
 ЗП НЭВМ-Б(М)
 СТРОК 3
*******************************************
********** В Ы Б О Р   З А Д А Ч **********
*******************************************
 УИИ М3(М2)        М3 - БАЗА ТЕКУЩЕЙ ЗАДАЧИ В Б.К.
 СЧ 0
 ЗП АДРБУФ-Б(М)   ДЛЯ П/П ДАЙЗОН
 ЗП ТЕКЗОН-Б(М)   ДЛЯ П/П ДАЙЗОН
 ЗП РУСМК1-Б(М)   КУС ЧТЕНИЯ ДОП.М.К.
 СЧ Е39П37              свс: П7
 ЗП ТЕКПРТ-Б(М)
 СЧ ШКРЗ
 ИЛИ ШКОР
 НТЖ СКБЭ+1             свс: ШКАП
 ЗП КАНАЛЫ-Б(М)
ЗАДАЧА ОТПР
 УИИ М4(М3)
 СЧ БК7(М4)
 И П1777
 ПО КОНЗАД
 УИ М3
 СДА 64+3
 И П37
 УИ М16
 СЧИ М3
 СДА 64+8
 УИ М15           М15 И М16 - ДЛЯ ПРОВЕРКИ ШКАГЛ
 СЛИ М3(М2)
 СЧ БК7(М3)
 СДА 64+41
 И П7
 УИ М10           М10:= КЛАСС
        сч      МПРЗ(М10)
 НТЖ ТЕКПРТ-Б(М)
        и       Е39П37
 ПЕ ЗАДАЧА
        сч      МПРЗ(М10)
        и       Е46
 ПЕ ЗАДАЧА        НЕТ КАНАЛОВ ДЛЯ ЭТОГО КЛАССА
 СЧ БК6(М3)
 И Е26
 ПО ОБЫЧНО        НЕ АРХИВ
 СЧ КАНАЛЫ-Б(М)
 И ЕЗАР
 ПО ЗАДАЧА        КАН.40 ЗАНЯТ
 ЗП РАЯ-Б(М)
 ПБ ГОТАРХ
ОБЫЧНО НОП
 СЧ БК1(М3)
 НТЖ ПРСПЕЦ-Б(М)
 И ПРСПЕЦ-Б(М)
 ПЕ ЗАДАЧА
        сч      МПРЗ(М10)
        и       Е46
        пе      ЗАДАЧА
 СЧ КАНАЛЫ-Б(М)
 ИЛИ ЕЗАР
 НТЖ ЕЗАР
 И МПЗ(М10)
 ЗП РАЯ-Б(М)      РАЯ - КАНАЛЫ КЛАССА
ГОТАРХ НОП
 СЧ БК6(М3)
 И Е25
 ПЕ ЗАДАЧА        ЗАПР.АРХ.
 МОДА (М15)
 СЧ ШКАГЛ-Б(М)
 И Е48(М16)
 ПО ЗАДАЧА        ЖДЕТ ИЛИ НЕТ ГЛАВНОЙ
        ago       .s1
 РЕГ ’254’
 МОДА ВСЕСВС
 ИЛИ 0
 СДА 64+38
 И П17
 ЧЕД
 НТЖ Е1
 ПО НЕТЕЛЕ        ОДИН СВС(’МАШ N’ ИГНОРИРУЕМ)
.s1     anop 
 СЧ ЯЧЗПЕР(М3)
 СДА 64-ЕЗПЕР+Е44
 ИЛИ ТРЕГ6
 И Е44
 ПЕ ПРЭВМ         ЕСТЬ "МАШ" ИЛИ 44Р ТР6
 СЧ БК6(М3)
 И Е33
 ПО НЕТЕЛЕ
 СЧ БК0(М3)
 И Е38
 ПЕ НЕТЕЛЕ        НЕТ ТЕРМ. ИЛИ ТЕРМ.АС
ПРЭВМ СЧ БК7(М3)
 НТЖ НЭВМ-Б(М)
 И Е46П48
 ПЕ ЗАДАЧА
НЕТЕЛЕ НОП
*
********** ПРОВЕРКА НАЛИЧИЯ ТОМОВ **********
 СЧ ЯЧКУПР(М3)
 СДА 64+СДВКУП-СДВТУП
 И ЕТЗНУП
 НТЖ ЕТЗНУП        0 - УПР.ЗАДАЧА
 ИЛИ ПРСВЛ
 ЗП МАСКЗН-Б(М)   МАСКА ДЛЯ ПРОВЕРКИ ЗАНЯТОСТИ
 МОДА ТОМА-Б(М)
 УИА (М12)         СЧЕТЧИК ТОМОВ
 МОДА НАЧТОМ
 ПВ (М16)
 ПО УСТТОМ        ВСЕ ТОМА СТОЯТ
 ИЛИ Е48
 ЗП (М12)
 СЛИА 1(М12)
 И Е13
 ПО ДАЙТОМ        КУСКИ ПОКА НЕ ВАЖНЫ
 СЧ ТЕКНОМ-Б(М)
 И Е12
 ПО ИЩИЛЕН
 СЧ ТЕКНОМ-Б(М)
 ВЧОБ NБЛ
 ПО ИЩИМД
ИЩИЛЕН НОП ,
	сч	ГОД
	и	Е46П48
	по	G11167
	сч	ЗОНАОЛ-Б(М7)
	мода	ДАЙЗОН
	пв	0(М13)
G11146	уии	М15(М16)
	сч	ГОД
	и	Е46П48
	сда	64+40
	вчоб	Е16Е1-Б(М7)
	уи	М13
G11151	сч	ТЕКНОМ-Б(М7)
	нтж	0(М16)
	и	П7777
	пе	G11155
	сч	0(М16)
	и	МАСКЗН-Б(М7)
	по	G11157
G11155	слиа	1(М16)
	мода	G11151
	цикл	0(М13)
	пб	НТОМ
G11157	счи	М16
	сда	64+5
	и	П7
	уи	М14
	счи	М16
	и	П37
	слц	П30
	сда	64+3
	мода	0(М15)
	нтж	'674'(М14)      уточнить
	по	G11155
	счи	М16
	и	П377
	или	E28
	пб	ВОТТОМ
G11167	ноп
 УИА -32+1(М13)
ЦИЩИЛ СЧ ТЗНМЛ+32-1(М13)
 НТЖ ТЕКНОМ-Б(М)
 И МАСКА-Б(М)
 ПЕ КЦИЩИЛ
 СЧ ТЗНМЛ+32-1(М13)
 И МАСКЗН-Б(М)
        по      ЕМЛ     свс: ПЕ КЦИЩИЛ
* ЛЕНТА СВОБОДНА НА ЭТОМ СВС ,
* ПРОВЕРИМ ЗАНЯТОСТЬ ЛЕНТЫ НА ДРУГИХ СВС
           ago       .s2
 УИИ М15(М13)
 СЛИА 32-1(М15)
 СЧИ М15
 СДА 64+3
 УИ М16
 СЛИА ТАБЗМЛ(М16)
 СЧМР 64
 СДА 64+43
 УИ М15
 СЧ =М44Х’F’
 СДА 64(М15)
 И (М16)
 ПО ЕМЛ
* ЛЕНТА ЗАНЯТА НА ДРУГОМ СВС
.s2            anop
КЦИЩИЛ МОДА ЦИЩИЛ
 ЦИКЛ (М13)
 ПБ НТОМ
ЕМЛ СЛИА 32-1+’30’(М13)
 СЧИ М13
 ИЛИ Е46+ДТОМА
 ПБ ВОТТОМ
ИЩИМД УИА -64+1(М13)
ЦИЩИМД СЧ ТЗНМД+64-1(М13)
 НТЖ ТЕКНОМ-Б(М)
 И МАСКА-Б(М)
 ПЕ КИЩИМД
 СЧ ТЗНМД+64-1(М13)
 РЖА ’23’
 ПЕ ЕМД          ОБЩИЙ ТОМ
 И МАСКЗН-Б(М)
 ПО ЕМД
 И Е47
 ПО КИЩИМД
 СЧ ТЕКТОМ-Б(М)
 И Е14
 ПЕ ЕМД           ОБЛАСТЬ НА 2053
КИЩИМД МОДА ЦИЩИМД
 ЦИКЛ (М13)
НТОМ СЧ БК6(М3)
 И Е46
 ПЕ ЗАДАЧА
 СЧ Е46
 ИЛИ БК6(М3)
 ЗП БК6(М3)
 УИА 1(М5)
 ПБ ЗАДАЧА
ЕМД СЛИА 64-1(М13)
 СЧИ М13
ВОТТОМ СДА 64-ДТОМА
 ИЛИ ТЕКНОМ-Б(М)
 ЗП -1(М12)
 СЧ ТЕКТОМ-Б(М)
 И Е14
 СДА 64-24
 ИЛИ -1(М12)
 ЗП -1(М12)
 ПБ ДАЙТОМ
УСТТОМ НОП
 СЧ ВСЕЕД
 ЗП (М12)
 СЧ БК6(М3)
 И Е46
 ПО УСТТ
 НТЖ БК6(М3)
 ЗП БК6(М3)
 СЧ БК7(М3)
 СДА 64-3
 СДА 64+3
 ИЛИ НЭВМ-Б(М)
 ЗП БК7(М3)
 УИА 1(М5)
УСТТ НОП
*
********** ПРОВЕРКА БЮДЖЕТА **********
 СЧ БК6(М3)
 И ПРБЮД-Б(М)
 ПЕ ЗАДАЧА        БЛОКИРОВКА ОТ Б.С.
*
********** ПРОВЕРКА ТЕРМИНАЛА **********
 СЧ БК0(М3)
 СДА 64+29
 И П377
 ПО БЕЗТТ         НЕТ ТЕРМИНАЛА
 УИ М16
 СЧ ЗАНЯТА
 И Е43
 ПО ДИОТКР        ДИАЛОГ ОТКРЫТ
 СЧИ М10
 НТЖ П5
 ПО ЗАДАЧА        ПЯТЫЙ КЛАСС НЕ ПУСКАТЬ
ДИОТКР СЧ БК0(М3)
 И Е38
 ПО НЕАС          НАПРЯМУЮ
 МОД ТАС(М16)
 УИА (М16)
 МОДА ЗАДАЧА
 ПИО (М16)         НЕТ ТЕРМИНАЛА НА ЭВМ
НЕАС СЧ ШКУСТР
 ИЛИ ШКАС
 И Е48-1(М16)
 ПО ЗАДАЧА     НЕТ ГТ ТЕРМ
 СЧ ШЗНКТ
 ИЛИ ШЗПРИМ
 И Е48-1(М16)
 ПЕ ЗАДАЧА
БЕЗТТ НОП
*
********** ПРОВЕРКА КАНАЛОВ **********
 БЛПР
        сч      Е48
        или     МПРЗ(М10)
        зп      МПРЗ(М10)
 СЧ РАЯ-Б(М)
 ПЕ ЕКАНАЛ
        сч      Е46
        или     МПРЗ(М10)
        зп      МПРЗ(М10)
 ПБ ЗАДАЧА
ЕКАНАЛ НОП
*
********** ПРОВЕРКА РЕСУРСОВ **********
 СЧ БК6(М3)
 И П7777
 СЛЦ Е1
 СЛЦ ЕДОБ
 ЗП РАЯ-Б(М)      РАЯ:= ЛИСТЫ
 СЧ БК0(М3)
 СДА 64+20
 И П777
 СЛЦ РАЯ-Б(М)
 ЗП РАЯ1-Б(М)     РАЯ1:= ТРАКТЫ
 СЧ Е9
 ПВ ЗАХЗАН(М15)   З А Х В А Т   Е 9
 ОТПР
 СЧ ОЗУ
 ВЧ РАЯ-Б(М)
 УИА 1(М11)
 ПЕ ЗППРИЧ         РЕС.ОЗУ
 СЧ ТОБ
 ВЧ РАЯ1-Б(М)
 УИА 2(М11)
 ПЕ ЗППРИЧ         РЕС.МБ
 СЧ ШКРЗ
 И ЕЗАР
 ПЕ ЕЗАРХ      ЕСТЬ АРХ
 СЧ БК1(М3)
 ИЛИ БК2(М3)
 ИЛИ БК3(М3)
 ИЛИ БК4(М3)
 И ЕЕЕАРХ-Б(М)
 УИА 4(М11)        М11:= 4 - РЕС АРХ
 ПО ЕЗАРХ
ЗППРИЧ НОП
*
********** ЗАДАЧА НЕ ПРОХОДИТ. ЗАНОСИМ ПРИЧИНУ **********
 СЧ Е9
 ПВ ГАШЗАН(М15)   О Т К А З   О Т   Е 9
 ОТПР
 СЧИ М11
 СДА 64-45
 РЗБ КПРИЧ-Б(М)
 НТЖ БК2(М3)
 И КПРИЧ-Б(М)
 ПО ЗАДАЧА
 НТЖ БК2(М3)
 ЗП БК2(М3)
 УИА 1(М5)
 ПБ ЗАДАЧА
ЕЗАРХ НОП
*
********** ПРОВЕРКА КУСКОВ **********
 МОДА ТОМА-Б(М)
 УИА (М12)
 СЧ 0                   свс: КУСКИ
 ЗП СВКУС-Б(М)
ЦТОМКУ СЧ (М12)
 НТЖ ВСЕЕД
 ПО ВСЕКУС
 СЧ (М12)
 РЖА ’13’
 ПЕ СЛТОМК        НЕ КУСОК
  УИ М14           ЧИСЛО КУСКОВ
 СЧ ВСЕЕД
 СДА 64-48(М14)
        зп      Х-Б(М)
        уиа     -32(М16)
        сли     М16(М14)
 УИА 0(М15)        М15  -  НОМЕР НАЧАЛЬНОГО КУСКА
ПОИСКК НОП ,
 сч СВКУС-Б(М)
        нтж     КУСКИ
 И Х-Б(М)
        нтж     Х-Б(М)
 ПО НАШЛИК        НАШЛИ КУСОК
 СЧ Х-Б(М)
 СДА 64+1
        зп      Х-Б(М)
 СЛИА 1(М15)
 МОДА ПОИСКК
 ЦИКЛ (М16)
        пам     0
        уиа     3(М11)
 ПБ ЗППРИЧ
*
НАШЛИК НОП ,
 СЧ (М12)
 И П17
 СДА 64-12
 ИЛИ Е48
 ЗП (М12)
 СЧИ М15
 ИЛИ (М12)
 ЗП (М12)
 СЧ Х-Б(М)
 ИЛИ СВКУС-Б(М)         свс: НТЖ
 ЗП СВКУС-Б(М)
СЛТОМК СЛИА 1(М12)
 ПБ ЦТОМКУ
ВСЕКУС НОП
*
********** ПРОВЕРКА ИНД.ОБЛАСТЕЙ **********
 СЧ ЯЧБКИО(М3)
 И ЕБКИО
 ПО НЕТИО
 СЧ ЯБКЗПА(М2)
 И П7
 УИА 4(М11)
 ПЕ ЗППРИЧ        И.О.ВКЛЮЧАТЬ НЕЛЬЗЯ
 СЧ ГОД
 И П7
 ИЛИ ЯБКЗПА(М2)
 ЗП ЯБКЗПА(М2)
НЕТИО НОП
*
********** ЗАНЯТИЕ РЕСУРСОВ **********
        блпр
 СЧ ОЗУ
 ВЧ РАЯ-Б(М)
 ЗП ОЗУ
 СЧ ТОБ
 ВЧ РАЯ1-Б(М)
 ЗП ТОБ
 СЧ СВКУС-Б(М)
        нтж     КУСКИ
 ЗП КУСКИ
 СЧ Е9
 ПВ ГАШЗАН(М15)   О Т К А З   О Т   Е 9
 УИА 3
*
********** ЗАНЯТИЕ ТОМОВ **********
 МОДА ТОМА-Б(М)
 УИА (М12)
ЗАНТОМ СЧ (М12)
 НТЖ ВСЕЕД
 ПО ГТТОМ
 СЧ (М12)
 РЖА ’13’
 ПО СЛТОМ         КУСОК
 СДА 64+ДТОМА
 УИ М16
        и       Е28
        пе      G11377
 СЛИА ТЗНМЛ-’30’(М16)
        сч      0(М12)
        и       Е46
        пе      ДИСК
        слиа    ТЗНМД-ТЗНМЛ+'30'(М16)
*
ДИСК НОП ,
 БЛПР
 СЧ (М16)
 НТЖ (М12)
 И МАСКА-Б(М)
 ПЕ УШЕЛ
 СЧ (М16)
 РЖА ’23’
 ПЕ ЕСТЬ
 И МАСКЗН-Б(М)
 ПО НЕЗАН
 И Е47
 ПО УШЕЛ
 СЧ (М12)
 И Е38           ОБЛАСТЬ
 ПО УШЕЛ
НЕЗАН НОП ,
      ago       .s3
 МОДА ДИСДИС
 ПИО (М15)
*ЗАНЯТИЕ МЛ В АДАП-Е
 СЧИ М15
 ПВ ЗАНМЛ(М15)
 ПЕ УШЕЛ          МЛ ЗАНЯТА НА ДРУГОМ СВС
.s3     anop 
ДИСДИС НОП ,
 СЧ (М16)
 И ЕТЗНУП
 ЗП РАЯ-Б(М)
 НТЖ (М16)
 ЗП (М16)
 СЧ (М12)
 ИЛИ РАЯ-Б(М)
 ЗП (М12)
ЕСТЬ НОП
	сч	0
	пб	G11405
G11377	мод	'32746' это еще кто - уточнить 
	слиа	(М16)
	сч	0(М16)
	нтж	0(М12)
	и	П7777
	пе	УШЕЛ
	сч	0(М16)
	и	МАСКЗН-Б(М)
	пе	УШЕЛ
	сч	НЭВМ-Б(М7)
	сда	64+18
	или	E14
G11405	ноп
 или Е42
 ИЛИ (М16)
 ЗП (М16)
 ОТПР
СЛТОМ СЛИА 1(М12)
 ПБ ЗАНТОМ
УШЕЛ СЧ 0
 ЗП (М12)
 ПБ СЛТОМ
ГТТОМ НОП
*
********** ВЫБОР КАНАЛА **********
 СЧ БК6(М3)
 И Е26
 ПЕ КАНАРХ
 СЧ КАНАЛЫ-Б(М)
 И МПЗ(М10)
 ИЛИ ЕЗАР
 НТЖ ЕЗАР
 ПБ ЕКАН
КАНАРХ СЧ КАНАЛЫ-Б(М)
 И ЕЗАР
ЕКАН НОП
 ЗП РАЯ-Б(М)      РАЯ:= ШКАЛА КАНАЛОВ
 И Е17П1          свс: ВСЕЕД
 ПЕ СТАНД
 СЧ РАЯ-Б(М)
СТАНД НЕД
 УИ М11           М11:= КАНАЛ
 СДА 64-17
 ИЛИ НЭВМ-Б(М)
 НТЖ БК7(М3)
 И МАСКА7-Б(М)
 НТЖ БК7(М3)
 ЗП БК7(М3)
 СЧ КАНАЛЫ-Б(М)
 НТЖ Е48-1(М11)
 ЗП КАНАЛЫ-Б(М)
 СЧ ШКОР
 ИЛИ Е48-1(М11)
 ЗП ШКОР
 СЧ ТУСП-1(М11)
 И П77777
 ПЕ ИПЗЕСТ
*
********** НОВЫЙ ЛИСТ ИПЗ **********
 СЧ 0
 ПВ ТБУФ(М15)     ЛИСТ ДЛЯ ИПЗ
 СДА 64-8
 УИ М16
 И П77777
 СДА 64+10
 УИ М15
 СЧ ШКЗЛ
 ИЛИ Е48(М15)
 ЗП ШКЗЛ
 УИА -12(М12)
 УИА -1024+1(М15)
 СЧ 0
ЦНУЛЬ МОДА 1024-1(М15)
 ЗП (М16)
 МОДА ЦНУЛЬ
 ЦИКЛ (М15)
 СЧИ М11
 ВЧ Е1
ЦИПЗ ВЧ П14
 СЛИА 12(М12)
 ПО ЦИПЗ
 УИА -12+1(М14)
ИПЗИПЗ НОП
 СЧ ТУСП(М12)
 И П77777
 ПЕ ИПЗЕСТ        ДОШЛИ ДО КАНАЛА 40
 СЧИ М16
 ИЛИ ТУСП(М12)
 ЗП ТУСП(М12)
 СЛИА 1(М12)
 СЛИА ДЛИН(М16)
 МОДА ИПЗИПЗ
 ЦИКЛ (М14)
ИПЗЕСТ СЧ ТУСП-1(М11)
 УИ М12           М12 - АДРЕС ИПЗ
 СЧ ЯЧБКИО(М3)
 И ЕКТ
 ПЕ ВОССТ       КОНТР.ТОЧКА
 СЧ ЯЧКУПР(М3)
 И ЕКУПР
 ПО НЕВОСТ
ВОССТ СЧ ШКВМК
 ИЛИ Е48-1(М11)
 ЗП ШКВМК
НЕВОСТ НОП
 БЛПР
        сч      Е47
        или     МПРЗ(М10)
        зп      МПРЗ(М10)
********** ЗАПОЛНЕНИЕ ИПЗ **********
 СЧ БК0(М3)
 И Е48П41
 СДА 64+1
 ИЛИ ЯЧНОМ(М12)
 ЗП ЯЧНОМ(М12)
 ОТПР
 СЧ Е48П17
 ЗП 22(М12)
 ЗП 32(М12)
 ЗП ЛИСТМБ(М12)
*
********** ЗАНЕСЕНИЕ КВНЗАД **********
 СЧ БК5(М3)
 СДА 64-13
 РЗБ ДЛЯКВА-Б(М)
 ПО СТАКВА
 ЗП КВНЗАД(М12)
**************  ДЛЯ   И Т М  И  В Т   ***********
* И =М8В’7’
* НТЖ =М8В’6’
* ПЕ НЕ6КЛ
* СЧ =В’32524’
* ЗП КВНЗАД(М12)
*НЕ6КЛ НОП ,
**************************************************
 СЧ БК5(М3)
 И Е25
 СДА 64+16
 ПБ ФОРИПЗ
СТАКВА УИИ М16(М10)
 СЧ ТРЕГ3
 И Е8
 ПЕ СТАКВ         БЕЗ КЛАССОВ ПО Б.С.
 СЧИ М10
 ВЧ П5
 ПО СТАКВ
 СЧ БК6(М3)
* какой-то явный хак стало -- было 
        уиа     1(М16)            И Е44П42
        пб      СТАКВ             ПО СТАКВ
 СДА 64-4
 НЕД
 УИ М16
СТАКВ СЧИ М16
 СДА 64-8
 ЗП КВНЗАД(М12)
 СЧИ М10
 СДА 64-3
 ИЛИ Е6
ФОРИПЗ ИЛИ Е48
 СЛЦ КВНЗАД(М12)
 ЗП КВНЗАД(М12)
        ago     .s4
 И =М8В’7’
 НТЖ =М8В’7’
 ПЕ НЕ7КЛ
 СЧ =М12В’3’       ИЗМЕН. КЛАССА И КВАНТА
 ИЛИ КВНЗАД(М12)
 ЗП КВНЗАД(М12)
НЕ7КЛ НОП ,
.s4       anop
*
********** ВНЕСЕНИЕ КУСКОВ **********
 МОДА ТОМА-Б(М)
 УИА (М13)
 АIF (∧НБОП).НБОП
 МОДА ОБЩТОМ(М12)
 УИА (М14)
 УИА -48(М15)
ФОРКУС СЧ (М13)
 НТЖ ВСЕЕД
 ПО ГОТКУС
 СЧ (М13)
 И Е48
 ПО НКУСОК
 НТЖ (М13)
 СДА 64+16(М15)
 ИЛИ (М14)
 ЗП (М14)
НКУСОК СЛИА 16(М15)
 СЛИА 1(М13)
 МОДА ФОРКУС
 ПИНО (М15)
 СЛИА 1(М14)
 УИА -48(М15)
 ПБ ФОРКУС
ГОТКУС НОП
 АGО .СТБОП
.НБОП АNОР
 МОДА 6(М12)
 УИА (М17)
ФОРТОМ СЧ (М13)
 ЗП (М17)
 СЛИА 1(М13)
 ПЕ ФОРТОМ
.СТБОП АNОР
*
********** ЧТЕНИЕ ДОПОЛНИТЕЛЬНОГО М.К. **********
 СЧИ М11
 И П17
 УИ М13
 СЛИ М13(М1)
 СЧИ М11
 СДА 64+4
 ВЧОБ Е2            СЕКТОР М.К.
 РЖА 7
 ПЕ НЕМК0         ДОП.М.К.
 УИА 1(М6)
 ПБ ЕМК1
НЕМК0 НОП
 СЛИА 256(М13)      ДОП.М.К. - В 1-Й АБЗАЦ
 ИЛИ РУСМК-Б(М)
 ИЛИ Е11
 ЗП РАЯ-Б(М)
 НТЖ РУСМК1-Б(М)
 ПО ЕМК1
 СЧ РУСМК1-Б(М)
 ПО ЧИТМК1                                                     И.О.ВК
 ПВ ФИЗЗП(М16)
ЧИТМК1 СЧ РАЯ-Б(М)
 ЗП РУСМК1-Б(М)
 ПВ ФИЗОБМ(М16)
ЕМК1 НОП
*
********** ЗАПОЛНЕНИЕ МАЛОГО КАТАЛОГА **********
 УИА 3
 СЧ БК6(М3)
 ЗП ТЕР(М13)
 СЧ БК0(М3)
 СДА 64+29
 И П777
 ПО ГТТЕРМ        НЕТ ТЕРМИНАЛА
 УИ М16
 И Е9
 ПО ПРОСТТ        ТЕРМИНАЛ НЕ АС
 СЧ ТАС-256(М16)
 УИ М16
ПРОСТТ СЧ ТЕР(М13)
 И Е34П1
 ЗП ТЕР(М13)
 СЧИ М16
 СДА 64-34
 ИЛИ ТЕР(М13)
 ЗП ТЕР(М13)
 БЛПР
 СЧ Е48-1(М16)
 ИЛИ ШЗНКТ
 ЗП ШЗНКТ
ГТТЕРМ НОП
 ОТПР
 СЧ БК5(М3)
 И П1777
 ЗП ИН(М13)
 СЧ БК0(М3)
 И Е48П41
 ИЛИ ИН(М13)
 ЗП ИН(М13)
 СЧ БК0(М3)
 СДА 64+5
 И Е24П16
 СДА 64-1
 ИЛИ ИН(М13)
 ЗП ИН(М13)
 СЧ БК7(М3)
 СДА 64-3
 СДА 64+26
 НТЖ П77777
 ЗП ВВОД(М13)
*
********** КОРРЕКЦИЯ ССЫЛОК **********
 СЧ БК6(М3)
 ИЛИ Е31
 ЗП БК6(М3)
 СЧ БК7(М3)
 НТЖ БК7(М4)
 И П1777
 НТЖ БК7(М4)
 ЗП БК7(М4)
 УИИ М3(М4)        ВОЗВРАТ ДЛЯ ПЕРЕХОДА НА СЛЕД.ЗАДАЧУ
*
********** СЧЕТЧИК ЗАДАЧ **********
 СЧ ПОРНЗ(М2)
 СЛЦ Е25
 НТЖ ПОРНЗ(М2)
 И Е36П25
 НТЖ ПОРНЗ(М2)
 ЗП ПОРНЗ(М2)
*
********** СНЯТИЕ ПРИЗНАКА ТОМОВ УПР.ЗАДАЧ **********
 СЧ ШКВМК
 И Е48-1(М11)
 ПО НУПР
 СЧ 0
ЦУПР ЗП РАЯ-Б(М)
 МОДА ТОМА-Б(М)
 УИА (М14)
ЦУПРТ СЧ (М14)
 НТЖ ВСЕЕД
 ПО НУПРТ         КОНЕЦ ТОМОВ
 СЧ (М14)
 И ЕТЗНУП
 ПО КУПРТ
 СЧ (М14)
 И Е12
 НТЖ РАЯ-Б(М)
 ПЕ КУПРТ
 СЧ РАЯ-Б(М)
 СДА 64-13
 СЛЦ ЗОНПАР-Б(М)
 МОДА ДАЙЗОН
 ПВ (М13)
 СЧ (М14)
 СДА 64+1
 И П1777
 УИ М15
 СЛИ М16(М15)
 СЧ (М14)
 И Е1
 НТЖ Е1
 УИ М15
 СЧ Е48
 СД КСО(М15)
 НТЖ ВСЕЕД
 И (М16)
 ЗП (М16)
КУПРТ СЛИА 1(М14)
 ПБ ЦУПРТ
НУПРТ СЧ РАЯ-Б(М)
 ПЕ НУПР
 СЧ Е12
 ПБ ЦУПР
НУПР НОП
*
********** ВКЛЮЧЕНИЕ ЗАДАЧИ **********
 УИА 1(М5)
 ПБ ЗАДАЧА
 СТРН
*
********** КОНЕЦ ЗАДАЧ **********
КОНЗАД НОП
 СЧ ТЕКПРТ-Б(М)
 СЛЦ Е39П37     свс: П7
 И Е39П37       --"--
 ЗП ТЕКПРТ-Б(М)
 УИИ М3(М2)
 ПЕ ЗАДАЧА
*
********** ЗАПИСЬ Б.К. **********
 МОДА НЗПБК
 ПИО (М5)
 СЧ РУСБК-Б(М)
 ПВ ФИЗЗП(М16)
НЗПБК СЧ КУСЗОН-Б(М)
 ПО НЗПЗОН
 ПВ ФИЗЗП(М16)
 СЧ КУСЗОН-Б(М)
 ПВ ТБУФ(М15)
НЗПЗОН СЧ РУСБК-Б(М)
 ПВ DЕQ(М15)
 СЧ РУСБК-Б(М)
 ПВ ТБУФ(М15)
 ОТПР
 ПБ ВЫХОД-Б(М)
 СТРОК 5
********** П Е Р Е В Р **********
* ВХОД: СМ = КУС ЗОНЫ
*       М16= 1 - ЗОНА СЧИТАНА
* ВЫХОД ПО М12: М6 = 1 - ТРЕБУЕТСЯ ЗАПИСЬ
*
ПЕРЕВР ВХ
 ЗП РАЯ-Б(М)
 И П3
 УИ М15
 УИА -15(М13)
 УИИ М14(М1)
 ПО ГТСЕКТ
 СЛИА 256(М14)
 НТЖ Е1
 ПО ГТСЕКТ
* СЛИА 2(М14)
 УИА -14(М13)
ГТСЕКТ МОДА (М15)
 СЧ ШКАНМК-Б(М)
 ЗП РАЯ1-Б(М)
 И ШКРЗ
 ПО (М12)
 МОДА ЕСЕКТ
 ПИНО (М16)
 СЧ РАЯ-Б(М)
 ПВ ФИЗОБМ(М16)
ЕСЕКТ ОТПР
 СЧ РАЯ1-Б(М)
 НЕД
 УИ М16
ЦЗАДМК СЧ ШКРЗ
 И Е48-1(М16)
 ПО КЗАДМК
 МОД ТУСП-1(М16)
 УИА (М15)
 СЧ 35(М15)
 СДА 64+32
 ЗП ВРОС(М14)
 СЧ 33(М15)
 И Е48П26-Б(М)
 ИЛИ ВРОС(М14)
 ЗП ВРОС(М14)
 СЧ ЯЧВР(М15)
 СДА 64+39
 СДА 64-16
 ИЛИ ВРОС(М14)
 ЗП ВРОС(М14)
 СЧ 39(М15)
 ЗП ВРЕР(М14)
КЗАДМК СЛИА 2(М14)
 СЛИА 1(М16)
 МОДА ЦЗАДМК
 ЦИКЛ (М13)
 УИА 1(М6)
 ПБ (М12)
 СТРОК 3
********** Д А Й Т О М **********
* НАЧАЛЬНЫЙ ВХОД -  ПВ НАЧТОМ(М16)
* ПОСЛЕДУЮЩИЕ ВХОДЫ - ПБ ДАЙТОМ
*
* ПРИ ВЫХОДЕ: СМ = ТЕКТОМ = КВАНТ ТОМА
*                  ТЕКНОМ = N ТОМА
*
НАЧТОМ СЧИ М16
 ЗП КУДА-Б(М)
 СЧ БК1(М3)
 ЗП ТЕКЯЧТ-Б(М)
ДАЙТОМ СЧ ТЕКЯЧТ-Б(М)
 ПО НОВСЛ
 СДА 64-16
 ЗП ТЕКЯЧТ-Б(М)
 СЧМР
 ИЛИ Е15
 НТЖ Е15
 ЗП ТЕКТОМ-Б(М)
 И П7777
 ЗП ТЕКНОМ-Б(М)
 СЧ ТЕКТОМ-Б(М)
КОНТОМ МОД КУДА-Б(М)
 ПБ
НОВСЛ СЧ КУДА-Б(М)
 СЛЦ Е25
 ЗП КУДА-Б(М)
 И Е26Е25
 ПО КОНТОМ
 СЧ КУДА-Б(М)
 СДА 64+24
 УИ М16
 МОДА (М16)
 СЧ БК1(М3)
 ЗП ТЕКЯЧТ-Б(М)
 ПБ ДАЙТОМ
 СТРОК 3
********** Д А Й З О Н **********
* ЧТЕНИЕ ЗОНЫ
* ВХОД:   СМ = НОМЕР ЗОНЫ НОММЛ1 В 34-25Р
* ВЫХОД ПО М13:   М16 = АДРЕС БУФЕРА
*
ДАЙЗОН НТЖ ТЕКЗОН-Б(М)
 ПО ЭТАЗОН
 НТЖ ТЕКЗОН-Б(М)
 ЗП ТЕКЗОН-Б(М)
 СЧ АДРБУФ-Б(М)
 ПЕ ЕСТЬБФ
 ПВ ТБУФ(М15)
 ОТПР
 СДА 64-8
ДАЙЗО1 ЗП АДРБУФ-Б(М)
 СДА 64-2
 ИЛИ НОММЛ1
 СЛЦ ТЕКЗОН-Б(М)
 ЗП КУСЗОН-Б(М)
 ПВ ФИЗОБМ(М16)
ЭТАЗОН МОД АДРБУФ-Б(М)
 УИА (М16)
 ОТПР
 ПБ (М13)
ЕСТЬБФ СЧ КУСЗОН-Б(М)
 ПВ ФИЗЗП(М16)
 СЧ АДРБУФ-Б(М)
 ПБ ДАЙЗО1
 СТРОК 3
*
* РАБОЧИЕ
*
МОДКАН ПАМ 1
МОДПОР ПАМ 1
НЕТКАН ПАМ 1             Е48- НЕТ КАНАЛОВ 0-ОГО КЛАССА,...
РАБ КОНД В’0’
 ЭКВИВ (БК0,0),(БК1,1),(БК2,2),(БК3,3)
 ЭКВИВ (БК4,4),(БК5,5),(БК6,6),(БК7,7)
* МАЛЫЙ КАТАЛОГ
 ЭКВИВ (ФИО,’140’),(ФИО1,’160’),(СТПМК,0)
 ЭКВИВ (ИН,’40’),(ВВОД,’60’),(ВРОС,’100’),(ВРЕР,’101’)
 ЭКВИВ (ВСБОЯ,’207’),(ВРЕ1,’210’),(ТЕР,’240’),(КМГРП,’347’)
 ВХОД ФИО,ФИО1,СТПМК,ИН,ВВОД,ВРОС,ВРЕР,ВСБОЯ,ВРЕ1,ТЕР,КМГРП
 СТРОК 3
*  П Л А Н
ПЛАН ВНЕШ НЭВМ,КАНАЛЫ,КУСЗОН,МАСКЗН,ПРБЮД,ПРСПЕЦ,СВКУС
ПЛАН ВНЕШ ТЕКПРТ,РУСМК1,РУСБК,РУСМК,ТОМА,РАЯ,РАЯ1,КУДА
ПЛАН ВНЕШ Х,У,Z,ТЕКТОМ,ТЕКНОМ,ТЕКЯЧТ,АДРБУФ,ТЕКЗОН,ШКАГЛ
ПЛАН ВНЕШ МАСКА,ДТОМА,ЕЕЕАРХ,КПРИЧ,Е16Е1,МАСКА7,Е30П25
ПЛАН ВНЕШ Е41Е33,Е48П26,ЗОНАОЛ,ЗОНПАР,ШКАНМК,ВЫВОД,ММГРП
ПЛАН ВНЕШ ДЛЯКВА,ВЫХОД
 СТРОК 3
* ВНЕШНИЕ КОНСТАНТЫ
ДИСП70 ВНЕШ Е17П1,Е46П48,П17,П37,П77,П7777,П77777,П3
ДИСП70 ВНЕШ П7,П777,П177,П30,П14,ВСЕЕД,П1777,Е48П25
ДИСП70 ВНЕШ Е26Е25,Е24П16,Е36П25,П5,Е44П42,П377
КИТ ВНЕШ Е48П41,Е48П42,Е39П37,Е34П1
КАЧКА ВНЕШ Е48П17
* ПОДПРОГРАММЫ
ДИСП70 ВНЕШ ТБУФ,ТВ40,ЗАХЗАН,ГАШЗАН
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
ДИСКИ ВНЕШ ЕNQ,DЕQ
*ПВВ ВНЕШ ЗАНМЛ
* ПРОЧЕЕ
ДИСП70 ВНЕШ СДВКУП,СДВТУП,ЕЗАР,ТАС,ТОБ,ШКАП,ШИФРМ,ШКИВ
ДИСП70 ВНЕШ НОММБ1,ЕБКИО,ЕДОБ,ЕКТ,ЕКУПР,ЯБКЗПА
ДИСП70 ВНЕШ ПРЕДЕЛ,ГОД,ТУСП,НОММЛ1,МАЛКАТ,КУСБК
ДИСП70 ВНЕШ ТЗНМЛ,ТЗНМД,ЕТЗНУП,МГРП,НОММБ,ЯЧБКИО,ЯЧКУПР
ДИСП70 ВНЕШ ШЗНМБ,ЯЧВР,УКАЗД,ОБЩТОМ,КУС545,ЯЧНОМ,NБЛ
ДИСП70 ВНЕШ ПРСВЛ,РЯПСЗ,ЕСТОП,ШКПЛАН,ШИФРАЦ,ДЛИН,ШКЗЛ
ДИСП70 ВНЕШ ВРЕМЯ,БУДЖ,КВНЗАД,КСО,МДР,ОЗУ
ДИСП70 ВНЕШ МЧПЗ,ЗАНЯТА,МПЗ,СКБЭ
БОЭК1 ВНЕШ ЛИСТМБ
ПЧКЗ ВНЕШ ПОРНЗ
КИТ ВНЕШ ШКОР,ШКВМК,МПРЗ,МПЗС
НОМБОБ ВНЕШ КЕСМЛ
СОСТАВ ВНЕШ ЕОТКД,ЕОТМД,ЯЧГЛАВ,ЕГЛАВ,ЕЗПЕР,ЯЧЗПЕР
СОСТАВ ВНЕШ ТРЕГ3,ТРЕГ6
КАЧКА ВНЕШ ШКРЗ
ДИСКИ ВНЕШ КТОМ,КУСКИ
МОТТ ВНЕШ ПРОБ6,ШЗНКТ,ШЗПРИМ,ШКУСТР,ШКАС
*ПВВ ВНЕШ ТАБЗМЛ,ВСЕСВС
 ФИНИШ
*КОНЕЦ ЧАСТИ

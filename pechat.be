ПЕЧАТЬ СТАРТ ’10000’
 Б
 Е
 М
	ГБЛВ ∧DW21
∧DW21 УСТВ 1
*
*ШКАРУ 48Р. - АЦПУ0, 47Р. - АЦПУ1
* М1 - АДРЕС ПЕРВОЙ ЯЧЕЙКИ БУФЕРА N1 вывода на АЦПУ
* М2 - АДРЕС НАКОПИТЕЛЬНОГО БУФЕРА ДРАЙВЕРА ?
* ПЕЧАТИ
* М5 - АДРЕС ПЕРВОЙ ЯЧЕЙКИ 
* накопительного БУФЕРА N2 РАЗМЕРОМ АБЗАЦ
* В ЛИСТЕ ДРАЙВЕРА ПЕЧАТИ.
* М6 - НОМЕР УСТРОЙСТВА
* М7 - РЕГИСТР БАЗЫ (ДЛЯ НЕРЕЗИДЕНТОВ).
* ЗАГРУЗ. ЧЕРЕЗ ВИСП
* ВЫДЕЛЯЕТСЯ ЛИСТ ПАМЯТИ.
* 0-50 - ИПП ПРОЦЕССА.
* М10,М11 - ИНДЕКС РАБОТЫ ОТНОСИТЕЛЬНО МОДУЛЯ ПЕЧАТЬ.
* ЯЧЕЙКА В МОДУЛЕ ПЕЧАТЬ.
* М12 - ОБРАТНЫЙ ИНДЕКС УСТРОЙСТВ.
*
*Печать печатает 1 зону по парам.
* получ. из модуля РОБОТ ОС ДИСПАК
*
 УПОТР ПЕЧАТЬ(М7)
 КОНД В’0’      НАЧАЛО ИПП ДРАЙВЕРА ПЕЧАТИ. Поле сумматора
 КОНД М41В’3’   Поле регистров нормализации и округления.
	пам	20 ПОЛЕ упрятывания 
*РМР,Р33,Р32,Р27,Р20,М17-М1
D10026	пам	2
NНПРDW	пам	2
ТСБОЙ	конд	п'4СБОЙ '
режdw1	пам	1
РЗБ111	конд	в'0000000100010001'  разборка 48-46 рр в 24,13,1 рр
D10035	пам	4
	конд	в'4000000000000000'  Е48
рчтбуф	пам	3
D10045	конд	в'5000200000000004'   Е48,46,35
cимвdw	пам	2 ВХОД. Использует (сч,зп,слц,чед) обработчик прер-й АЦПУ
* конец ИПП печати
* вход в 50-ю ячейку процесса печати по заказу процесса ВИСП
*
*** НА ЗАПУСК АЦПУ DW-21
*
НАЧАЛО	пв	начdw(М15) !В КОНФУС. На запуск АЦПУ DW-21.
*
*** СТАНДАРТНАЯ ПРОЦЕДУРА ДЛЯ ВСЕХ АЦПУ
*
НАЧАЦП	уиа	'2003' Блокировка внешних прерываний
	сч	ШКАРУ  Шкала Работ?? Устойств?
	и	Е48П37
	по	КОНЕЦ  -> ЕСЛИ В ШКАРУ.49-37 НЕТ РАБОТ
	нед	П77777 НОМЕР РАБОТЫ
	уи	М6  уст на М6
	сч	ШКАРУ := погасил ЕДИНИЦУ АЦПУ
	нтж	E48(М6)
	зп	ШКАРУ
	счи	М6	НОМЕР АЦПУ ПЕЧАТИ
	сда	64-1 УМН. НА 2
	уи	М11  на М11 уст. удвоенный номер работы (0/2/4/…/22)
	сли	М11(М7)
	нтж	E2   
	уи	М12  М12:=2/0/6/4…22/20, 
*инверс ном работы – 1,0,3,2  
	уии	М10(М6) М10=М6 (0/1/2/…/11)
	сли	М10(М7) М10=М10+М7 
*М10 + база драйвера для работы
	сч	0 ноль во всех 48 разрядах. 
*Ниже верно при М6=0 или 1.
* ДАЛЕЕ РАБОТАЕТ КОРРЕКТНО ТОЛЬКО ЕСЛИ В ШКАРУ 
*М6 ЕД. В 48 ИЛИ 47Р.	
	зп	упсктр-П(М10) :=0 пишем в 5 чет или 
*нечет ячеек упрбф1:упсктр
	зп	упрбф1-П(М10) упрятанное знач. М2:=0
	зп	упрм4-П(М10) упрятанное знач. М4:=0 
	зп	упрбф2-П(М10) упрят. знач. М2:=0
*Только эта команда безопасна 
*для любой работы в ШКАРУ
	зп	упрм4а-П(М10) упрятанное знач. М4:=0
*
	сч	ШИФРАЦ Установили бит работы АЦПУ 
*в шкале ШИФРАЦ.
*Е3 соотв. АЦПУ-1 И Е1 – АЦПУ-0 в ШИФРАЦ
	или	E3(М12) Е3/Е1 при АЦПУ-1/АЦПУ-0 или Е1 при 
*при М12=0/2
	зп	ШИФРАЦ При М12 > 2 ошибка !!!
	рег	'101' команда 002, признаки ПКП и БРО
	сч	ПРЕДЕЛ Е25, Е24 – тип АЦПУ 0:1:  БЭСМ-6 или ЕС 
	и	E25(М6)   Е25=0 в ПРЕДЕЛ - АЦПУ-128,
	зп	МЕТОД-П(М10) :=0, ЕСЛИ БЭСМ-АЦПУ, 
*ИНАЧЕ ЕС-АЦПУ, 
	сч	ВРЕМЯ  смысл?? Цена разряда 
* ВРЕМЯ = 1/50 сек
*М.быть число врем.прер-й
	вч	рзвре1-П(М10) =Е24П1
	вчаб	E8 =256
	пе	ВКЛАЦП меньше времени прошло
	сч	шпр4к7(М6) Возможно шкала пропуска выдачи АЦПУ 0:1
	зп	шкприк-П(М10) :=ШПР4К7(М6) !!ШКАЛА ПРИКАЗОВ?
	пб	ВКЛАЦП       ->ВКЛАЦП    может хак
*НЕ ИСП.
	зп	МЕТОД-П(М10)
	пб	ЗАНЯТО
*
ВКЛАЦП	сч	E3   <-НАЧАЦП.	признак
	увв	'14'(М6) ВКЛ. АЦПУ0/АЦПУ1
	сч	E39(М6) Е39 или Е38
	пв	ТВ67(М15) ->ТВ67 выз проц-ры ТВ67 из ДИСП70.
*ГАШ. 39Р.(38Р.) ГРП
	сч	шdw21а(М6)  <-ТВ67  это ячейки КОНФУС
	зп	NНПРDW-П(М10) ЗП. В ЯЧ. ИПП NНПРDW-П
	сч	E1 признак выделить память размером абзац
	пв	ТБУФ(М15) ->ТБУФ ФУНКЦ. ВЫД. ПАМЯТИ БУФЕРА
* НОМЕР БУФЕР НА СУММАТОРЕ
	сда	64-8 ->ТБУФ 
	уи	М1  :=ФИЗ. АДРЕС ПЕРВОЙ ЯЧЕЙКИ БУФЕРА
* РАЗМЕРОМ В АБЗАЦ - 400 СЛОВ.
	зп	ФАДБУФ-П(М10) 
*ФАДБУФ[nАЦПУ]:=Физ.адр.абзаца АЦПУ-0 или 1. 
*
ЗАНЯТО	пам	0  <-НАЧАЦП 
	сч	E37
	зп	сектрк-П(М10) сектрк[nАЦПУ]:=Е37
	сч	занац0 ПРИЗНАК ЗАНЯТОСТИ АЦПУ0
	или	занац1 ПРИЗНАК ЗАНЯТОСТИ АЦПУ1
	пе	ДОПБУФ если яч занац0 или  занац1 
*заняты, то возьмем буфер
	мода	'1400'(М7) М5:=М7+’1400’ – 
*адр первой яч буфера в листе драйвера
*если оба АЦПУ не заняты
	уиа	0(М5)  М5:=М7+’1400’
*ФИЗИЧЕСКИЙ АДРЕС ПЕРВОЙ ЯЧЕЙКИ накопительного БУФЕРА
*РАЗМЕРОМ АБЗАЦ
	счи	М5 
	зп	занац0-П(М10) :=адр 0-й яч. буфера 
*в листе драйвера.
	пб	ФОРКУС ->ФОРКУС,обход ТБУФ, т.к. есть
*буфер в драйвере
*
ДОПБУФ	сч	E1 <-ЗАНЯТО. Запрос буфер размером абзац
	пв	ТБУФ(М15) Дай доп. накопит. буфер.
	сда	64-8  умножили на 256 и получли 
*физ.адр. первой яч буфера
ФОРКУС	уи	М5  <-ЗАНЯТО,ДОПБУФ. 
*М5:= физ.адр. 0 яч. накопит. буфера 
*размером абзац (15:9)рр
	сда	64-2 физ номер абзаца в 17:11 рр КУС обмена с МБ
	или	НОММБ номер направления и МБ
	или	КУСАЦП-П(М10) номер тракта и 
*сектора на МБ для АЦПУ-0 или АЦПУ-1
	зп	шкусац-П(М10) :=КУС АЦПУ0 ИЛИ АЦПУ1
неизв1	пам	0   <-ФОРКУС НЕ ИСП.?
	мода	чисвзв
	уиа	0(М15) М15:= адр.выхода из цикла на чисвзв
чистка	уиа	'3' <-НЕИЗВ1,G10330,зпупря. разрешить прерывание
*ДРАЙВЕРА ПЕЧАТЬ
	мода	(М15)
	пио	(М1) ->(М15), если нет выводного буфера
	уиа	-'377'(М16) циклить 400 раз. ОБНУЛЕНИЕ БУФЕРА N1 
*НА М1 РАЗМЕРОМ В АБЗАЦ
счтчис	сч	0 <-ЧИСТКА,СЧТЧИС. цикл росписи нулем 
	мода	(М1)
	зп	'377'(М16) 
	мода	счтчис
	цикл	(М16) ->СЧТЧИС СКОЛЬКО ЯЧЕЕК БУФЕРА
* НУЖНО ОБНУЛИТЬ.
	зп	шкусац+2-П(М11) нулим ШКУСАЦ+2/ШКУСАЦ+4 
*для АЦПУ0/АЦПУ1
	зп	шкусац+3-П(М11) нулим ШКУСАЦ+3/ШКУСАЦ+5 
*для АЦПУ0/АЦПУ1
	пб	(М15) ->G10127,G10132,ОЧСИМВ(2)
*.почист выв буф драйвера
*
G10124	зп	рчтбуф-П(М10) <-G10334, рчтбуф-П(М10):= 0
	сч	сектрк-П(М10) 
	зп	упсктр-П(М10) сектрк-П(М10)
	слц	E1
	зп	сектрк-П(М10) сектрк-П(М10) + 1
чисвзв	уиа	-5(М4) возврат после чистки 
*выводного буфера
	уии	М2(М5) М2= адрес накопительного буфера драйвера
	сч	сектрк-П(М10) изначально сектрк-П(М10)=Е37
	и	E3
	по	чт1п3 -> ЕСЛИ СЕКТОРА ТРАКТА не= 4
	пб	отраб -> 4 СЕКТОРА ТРАКТА ОБРАБОТАНЫ.
*
*Чтение с 1 по 3 сектор МБ.
чт1п3	уиа	0(М13)  М13:=0
	сч	сектрк-П(М10)
	и	П3 номер сектора тракта добавили 
*в КУС ФИЗОБМ с МБ
	или	шкусац-П(М10)
	пв	ФИЗОБМ(М16) прочли очеред.сектор МБ 
*в накопитель.буферa
	сч	сектрк-П(М10)
	и	П3
	пе	ОЧСИМВ -> ЕСЛИ не 0-Й сек ТРАКТА МБ АЦПУ0/АЦПУ1
	сч	(М5)
	и	E31 Выяснить. РИМ - РОБОТ.
	по	бвывdw  0-ЯЧ 0-ГО сектор МБ (в М5 адрес абзаца)
	сч	,
	зп	счзнбф-П(М10) :=0
*Считывание очередного символа с прочтенного
*сектора(зоны) МБ(МД)
ОЧСИМВ	ноп	,
	мода	ВЫБСИМ 	
	пв	(М16)  ->ВЫБСИМ
	уиа	'3'    <-ВЫБСИМ
	по	КОНЗОН -> ЕСЛИ СМ = 0
	нтж	П177
	по	ПОВТОР ->ПОВТОР, ЕСЛИ СМ = 177(8)
*переход на новую строку
	и	E8
	зп	cимвdw-П(М10)  8Р. символа
	сч	,
	зп	СИ-П(М10) :=0
	сч	cимвdw-П(М10)
	по	очсим1 ЕСЛИ СИМВDW-П(М10) = 0
	нтж	символ-П(М10) УДАЛИЛИ 8-ОЙ РАЗРЯД В СИМВОЛЕ
	уи	М13 	М13:=СИМВОЛ 1-7РР.
	пб	ОЧСИМВ
***
неисп1	сч	сектрк-П(М10)
	или	E48
	зп	сектрк-П(М10)
	сч	ШКАРУ
	и	E24(М6)
	нтж	ШКАРУ
	зп	ШКАРУ
	сч	
	зп	D11324-П(М10)
	пб	G10310
***
* Блок обработки символа АЦПУ DW-21
***
пркдdw	сч	шdw21б ПРОВ. ГОТОВНОСТИ DW-21
	и	е17dw(М6) NKAН ИНДЕКСИРУЕТСЯ ПО М6
	по	(М17)
	мода	пркод7
	пио	(М6)  если ацпу-1, то -> конфус
	сч	cимвdw-П(М10)
	и	П17
	уи	М16
	нтж	cимвdw-П(М10)
	сда	64+1
	уи	М3
	мода	(М16)
	сч	ТАБЛИЦ+5 (+?)
	сда	64(М3)
*М3 := 0..7?
	и	П377
	зп	cимвdw-П(М10)
	пб	(М17)
***
очсим1	сч	символ-П(М10) <-ОЧСИМВ 
	вч	E1
	зп	cимвdw-П(М10) :=символ-П(М10) - 1
	вч	П140
	по	G10334 ЕСЛИ символ-П(М10) >= 141(8)
	мода	пркдdw
	пв	(М17) ->пркдdw
***
пркdw1	сч	cимвdw-П(М10) <-пркдdw
	мода	шкусац+2-П(М11) *ДЛЯ АЦПУ0/1
	уиа	0(М16) М16:=А(ШКУСАЦ+2/ШКУСАЦ+4) 
*ДЛЯ АЦПУ0/1	
	уи	М17  М17:=СИМВDW-П(М10)
	вч	П60  м17-48(10)
	пе	пркdw2 символ < 60(8)
	слиа	1(М16) если >= 60(8)
*то м16+1
	уи	М17  ИНДЕКСИРУЮТСЯ СТРОКИ,
*ВЫД. НА АЦПУ.
пркdw2	сч	E48(М17) НОМЕР СТРОКИ.
	или	(М16) если символ > 48,
*для ацпу-0 шкусац+2/+3
*для ацпу-1 шкуcац+4/+5
	зп	(М16)
	сч	cимвdw-П(М10)
	и	П37 выделили младшие 5рр. символа
	сда	64-3 симвdw*8
	уи	М3  устан. симвdw на м3
	сли	М3(М1) + абзац буфера
	сч	cимвdw-П(М10)
	сда	64+5
	вчоб	E2
	сда	64-4 символ*16
	зп	cимвdw-П(М10)
	пб	чтспец
ПОВТОР	мода	ВЫБСИМ
	пв	(М16)
дальше	вч	E1 СИМВОЛ-1
	зп	СИ-П(М10)
чтспец	мода	чтобщ
	пио	(М6) 
*
	сч	шdw21б РАБОТА с АЦПУ DW-21
	и	е17dw(М6) ПРОВЕРКА NКАН DW-21
	по	чтобщ если ацпу-128
	счи	М13
	сда	64+4
	уи	М16
	сли	М16(М3)
	счи	М13
	и	П17
	пб	чтобщ1
*
*** ОБЩИЙ БЛОК ЧТЕНИЯ СИМВОЛА
*
чтобщ	счи	М13 1-7рр. яч. символ
	и	П7 3 младших разряда
	уи	М16
*Если АЦПУ-128, то М3 не опред.
	сли	М16(М3)
	счи	М13
	сда	64+3
чтобщ1	нтж	П17 4 младших разряда
	слц	cимвdw-П(М10)
	уи	М17
	сч	E48(М17)
	или	(М16)
	зп	(М16)
	слиа	1(М13)
	сч	СИ-П(М10)
	по	ОЧСИМВ ->ОЧСИМВ, ЕСЛИ СИ-П(М10) = 0
	пб	дальше
*
*** КОНЕЦ ЧТЕНИЯ ЗОНЫ/ТРАКТА
*
КОНЗОН	сч	E35 <-ОЧСИМВ
	уиа	'2003'
	или	сектрк-П(М10)
	зп	сектрк-П(М10) :=Е35 ИЛИ сектрк-П(М10)
	пб	отраб
*
G10231	сч	П177 <-G10334 
	зп	рчтбуф-П(М10) :=П177
*
*** УПРЯТЫВАНИЕ РЕГИСТРОВ АЦПУ-0/1
*** ПОСЛЕ ЗАВЕРШЕНИЯ ОБМЕНА С МБ
*
отраб	сч	шкусац+2-П(М11) <-G10127,G10231,КОНЗОН,G10334.
* ТРАКТ ОБРАБОТАН
	чед	
	зп	cимвdw-П(М10) :=ЧЕД(ШКУСАЦ+2/4) ДЛЯ АЦПУ0/1
	сч	шкусац+3-П(М11)
	чед	cимвdw-П(М10) =ЧЕД(ШКУСАЦ+3/5)+ЧЕД(ШКУСАЦ+2/4) 
*ДЛЯ АЦПУ0/1
	сда	64-15 СМ.22-16РР:= ЧЕД(ШКУСАЦ+3/5)+
*ЧЕД(ШКУСАЦ+2/4)
	или	рчтбуф-П(М10) =0/П17/П177/Е1/ И ДР.
	зп	рчтбуф-П(М10) :=СМ.22-16РР + 
*рчтбуф-П(М10).15-1РР
	сч	E34 
	уиа	'2003' ЗАПРЕТ ВНЕШН ПРЕРЫВАНИЙ
	или	сектрк-П(М10) :=сектрк-П(М10)+Е34+Е48-У37
	нтж	E37
	или	E48
	зп	сектрк-П(М10)
	сч	ВРЕМЯ
	зп	рзвре1-П(М10) :=ВРЕМЯ
	сч	ШКАРУ :=Е23/Е23 + ШКАРУ ДЛЯ АЦП-0/1
	или	E24(М6) 
	зп	ШКАРУ
	уиа	'3'
	мода	упрят0
	уиа	0(М17) М17:=А(упрят0) ДЛЯ АЦП-0
	счи	М6
	по	отраб1 -> ЕСЛИ М6=0 (ПЕЧАТЬ НА АЦП-0)
	слиа	13(М17) М17:=А(упрят1) ДЛЯ АЦП-1. 
отраб1	уиа	11(М15) <-G10232.	М15:=11
	счи	М14 СМ:== М14 ДЛЯ УПРЯТ В СТЕК АЦП-0/АЦП-1
отраб2	слиа	-1(М15) М15:=М15 - 1
	счим	М1(М15) УПРЯТ ОЧЕРЕД М14-М2 В СТЕК АЦП-0/АЦП-1
	мода	отраб2
	пино	(М15) ->G10250, ЕСЛИ М15 НЕ= 0
	зп	(М17) УПРЯТ М1 В СТЕК, М17:=А(СТЕК)*13
	сч	ВРЕМЯ
	зп	(М17) :=ВРЕМЯ В А(СТЕК)*13, М17:=А(СТЕК)*14
	пб	НАЧАЦП
*
G10254	сч	шкприк
	сл	D10674
	зп	D10675
	сч	E7
	пв	ТАЙМЕР(М15)
	пв	ТБУФ13(М16)
	уиа	'2003'
	сч	ШКАРУ
	и	Е24П13
	по	G10273
	сч	упрят0+12
	вчаб	упрят1+12
	вчаб	E10
	пе	G10273
	сч	ШКАРУ
	сда	64-24
	нед	П77777
	уи	М6
	сч	шкприк
	сл	D10674
	по	G10271
	нтж	D10675
	пе	G10254
G10271	сч	ШКАРУ
	нтж	E24(М6)
	или	Е36(М6)
	зп	ШКАРУ
G10273	сч	ШКАРУ
	сда	64-12
	и	Е48П37
	пе	G10345 ->G10345, ЕСЛИ ШКАРУ.36-25РР >0
*уходим сюда
	сч	ШКАРУ
	сда	64-24
	и	Е48П37
G10277	по	G10277 <-G10273,<->G10277, ЕСЛИ ШКАРУ.24-13РР=0 ??
	нед	П77777
	уи	М6
	сч	ШКАРУ
	нтж	E24(М6)
	зп	ШКАРУ
G10302	уиа	-11(М16) <-G10277,<-G10345
	мода	упрят0+12
	уиа	0(М17)
	счи	М6
	по	G10305
	слиа	13(М17)
G10305	уиа	'3'  <-G10302(2), РАЗРЕШИТЬ ВНЕШН ПРЕРЫВАНИЯ
	сч	(М17)
G10306	уим	М14(М16) <-G10305,G10306. ВОССТ М14-М1 ИЗ СТЕКА
	мода	G10306
	цикл	(М16) ->G10306, ЕСЛИ М16=<0
G10310	пам	0  <-G10306,G10151
	сч	сектрк-П(М10)
	рзб	РЗБ111
	слц	СТУСТР+7
	зп	СТУСТР+7
	сч	сектрк-П(М10)
	и	E47
	по	G10350 ->G10350, ЕСЛИ сектрк-П(М10).47-47РР=0
	нтж	сектрк-П(М10)
	зп	сектрк-П(М10)
	сч	упрбф2-П(М10) СОДЕРЖИТ УПРЯТАННОЕ М2 СОГЛАСНО зпупря
	пе	встбуф ->встбуф, ЕСЛИ сектрк-П(М10) > 0.
	уии	М2(М5)
	уиа	-5(М4)
	пб	G10321 ->G10321
*
встбуф	уи	М2 <-G10310, ВОССТАНОВЛЕНИЕ м2
	мод	упрм4а-П(М10)
	уиа	0(М4)
G10321	уиа	0(М13) <-G10310,встбуф. М13:=0
	сч	E1
	зп	рчтбуф-П(М10) :=Е1
	сч	усктр1-П(М10)
	нтж	сектрк-П(М10)
	и	П3
	по	G10327 ->G10327, если 
*сектрк-П(М10).2-1=усктр1-П(М10).2-1
	сч	сектрк-П(М10)
	и	E3
	пе	отраб -> ЕСЛИ сектрк-П(М10).3-3 = 1
	уиа	чт1п3-П(М15)
	пб	G10330 ->G10330
G10327	уиа	ОЧСИМВ-П(М15)
G10330	сч	усктр1-П(М10)
	и	П3
	или	E37
	зп	сектрк-П(М10) := Е37+усктр1-П(М10).2-1
	сли	М15(М7) M15:=А(ПЕЧАТЬ)+А(G10132-П)/A(ОЧСИМВ-П)
	пб	чистка ->ЧИСТКА, ОТКУДА ПЕРЕЙДЁТ ПО М15
	сч	E1              зачем это
G10334	зп	рчтбуф-П(М10) <-G10167. 
*рчтбуф-П(М10):=D10703-П(М10) - 141(8)
	нтж	П37
	нтж	E3
	по	G10124 -> ЕСЛИ рчтбуф-П(М10)=33(8)
	нтж	П6
	по	G10231 -> ЕСЛИ …
	нтж	E1
	пе	отраб  -> ЕСЛИ …
	уиа	'3'
	уиа	0(М2)
	уиа	-2(М4)
	сч	ВРЕМЯ
G10342	зп	ряучет
	мода	час
	вч	2(М4)
	пе	пчтном печать номера эвм,
*ацпу, даты и времени.
	слиа	1(М2)
	пб	G10342
*
G10345	нед	П77777 <-G10273 установка Nкан ацпу
	уи	М6 
	сч	ШКАРУ
	нтж	Е36(М6)
	зп	ШКАРУ
	пб	G10302 
*
G10350	сч	сектрк-П(М10)
	и	D10045
	по	счзон
	сч	E48(М6)
	или	E39(М6)
	или	МЕТОД-П(М10)
	пв	ТВ67А(М15)
	уиа	'3'
	счи	М1
	по	G10357
	сда	64-2
	пв	ТБУФ(М15)
G10357	сч	кусвыв-П(М10)
	по	G10362
	пв	ТБУФ(М15)
	сч	
	зп	кусвыв-П(М10)
G10362	сч	занац0-П(М10)
	пе	G10364
	сч	шкусац-П(М10)
	пв	ТБУФ(М15)
G10364	сч	
	зп	занац0-П(М10)
	сч	E3
	или	E35
	и	сектрк-П(М10)
	по	выдсб
	сч	ШКПЛАН
	или	E3(М12)
	зп	ШКПЛАН
	сч	сектрк-П(М10)
	и	E3
	пе	КОНЕЦ
	сч	ШИФРАЦ
	нтж	E3(М12)
	зп	ШИФРАЦ
стацпу	пам	0
	сч	П12   2р. стоп копирование
*4р. - стоп двиг. ацпу.
	увв	'14'(М6) управление ацпу-0 
*если м6=1, то увв '15' - для ацпу-1
КОНЕЦ	уиа	'2003' <-НАЧАЦП,cтацпу.
	сч	ШКАРУ
	сда	64-12
	и	Е48П37
	пе	G10345 -> ЕСЛИ ШКАРУ.36-25 > 0
	сч	ШКАРУ
	и	Е48П37
	пе	НАЧАЦП -> ЕСЛИ ШКАРУ.48-37 > 0
	сч	ШКАРУ ШКАРУ.48-37 = 0
	и	Е24П13
	пе	G10254 -> ЕСЛИ ШКАРУ.24-13 > 0
	пв	ЗАНОСМ(М16) ШКАРУ 24-13рр. = 0. УЧТЁМ ВРЕМЯ РАБОТЫ ОС
	сч	E47  конец нерезидентного процесса
	пб	ОТКАЗН ??Е47 РАЗРЯД ПРОЦ ПЕЧАТЬ В ШКАЛЕ ГОТОВЫХ
*
*** ЗАПИСЬ БУФЕРА ВЫВОДА ПРЕДНАЗНАЧ. ДЛЯ DW-21
*** НА СПЕЦ. НОСИТЕЛЬ
*
бвывdw	сч	1(М2) <- бвывdw. 1-ю яч печатаемой зоны вывода
	зп	первая-П(М10)
	сч	счзнбф-П(М10) ??видимо счетчик зон буфера вывода
	или	кусвыв-П(М10) КУС каталога вывода
	по	заказд -> если КУС и счетчик зон пусты
счзон	сч	кусвыв-П(М10)
	пе	непуст -> ЕСЛИ не пуст
	сч	счзнбф-П(М10)
	по	зпупря если счзнбф-П(М10) = 0
	нтж	E7
	нтж	П5
	пе	прибав -> ЕСЛИ  cчзнбф-П(М10) > 105(8)
	зп	счзнбф-П(М10) :=0, нулим счетчик зон буфера вывода
заказд	пв	ТБУФ(М15) <-G10404, Дай лист
	сда	64-8
	уи	М14  М14= адрес листа
	сда	64-2  адрес листа в КУС МД
	или	КУСУЧТ читать каталог вывода. Зона 737
	слц	НОММЛ1  + направление и устройство каталога вывода
	зп	кусвыв-П(М10)
	пв	ЕNQ(М15) захват этой БЭСМ-6 диска 
*с каталогом вывода
	сч	кусвыв-П(М10)
	пв	ФИЗОБМ(М16) чтение каталога в запрошенный лист ОП
	уиа	'3'
	пб	учетdw
*
прибав	сч	счзнбф-П(М10)
	слц	E1
	зп	счзнбф-П(М10)
	пб	зпупря
*
*** ОТРАБОТКА АВАРИИ АЦПУ
*
выдсб	мода	стацпу
	уиа	0(М15)
	уиа	ТСБОЙ-П(М16)
	уиа	'2003'
	сч	сектрк-П(М10)
	и	E48
	пе	ВЫДСБ1
	слиа	ТНЕТИН-ТСБОЙ(М16)
ВЫДСБ1	сч	ШИФРАЦ
	или	E27(М12)
	зп	ШИФРАЦ
	сч	
	зп	D11265-П(М10)
	счи	М6
	сда	64-8
	или	АЦПУ0
	зп	ТСБОЙ+1
	пб	авdw21 !В КОНФУС ПРИ АВОСТЕ DW-21
ВЫДОПТ	уии	М32(М15)
	сли	М16(М7)
	сч	E23
	пб	ТВ40
***
ВЫБСИМ	сч	2(М2)
	мода	5(М4)
	сд	ДВБАЙТ
	и	П377
	зп	символ-П(М10)
	мода	(М16)
	цикл	(М4)
	слиа	1(М2)
	уиа	-5(М4)
	пб	(М16)
        отмен   (М7)
*
********************
* ОБРАБОТЧИКИ ПРЕРЫВАНИЙ, база на М17
*
        употр   ПЕЧАТЬ(М17)
НОЛЬ	слиа	-10(М16)
	мод	ТУС+1
	уиа	0(М17)
	счи	М3
	уии	М3(М16)
	сли	М16(М17)
	зп	ИР3
	сч	шкприк-П(М16)
	по	G10452
	слц	П77777
	зп	шкприк-П(М16)
	пб	G10463
*
G10452	сч	E48(М3)
	пв	ТВ67(М15)
	сч	NНПРDW-П(М16)
	нтж	шdw21а(М3)
	по	G10456
	сч	сектрк-П(М16)
	или	E47
	зп	сектрк-П(М16)
G10456	сч	
	зп	NНПРDW-П(М16)
	сч	E34
	или	Е36
	и	сектрк-П(М16)
	пе	G10465
G10461	сч	сектрк-П(М16)
	и	E37
	по	G10650
G10463	пам	0
	мод	ИР3
	уиа	0(М3)
	пб	БМВ
G10465	сч	сектрк-П(М16)
	и	E46
	пе	G10461
	увв	'4031'
	и	E24(М3)
	пе	G10463
	сч	сектрк-П(М16)
	слц	E42
	зп	сектрк-П(М16)
	пб	G10463
СИАЦПУ	слиа	-1(М16)
	мод	ТУС+1
	уиа	0(М17)
	счи	М3
	уии	М3(М16)
	сли	М16(М17)
	зп	ИР3
	сч	NНПРDW-П(М16)
	зп	РЭ3
	слц	E1
	зп	NНПРDW-П(М16)
	ржа	'3'
	сч	сектрк-П(М16)
	и	Е36
	по	G10611
	увв	'4031'
	и	E24(М3)
	по	G10544          идет протяжка бумаги
	сч	сектрк-П(М16)
	или	КСД3
	нтж	КСД3
	зп	сектрк-П(М16)
	увв	'4031'
	и	E20(М3)
	по	G10530
	сч	сектрк-П(М16)
	или	E30
	зп	сектрк-П(М16)
	пб	G10530
G10511	сч	сектрк-П(М16)
	нтж	Е36
	зп	сектрк-П(М16)
G10513	пам	0
	сч	сектрк-П(М16)
	и	E30
	по	G10517
	нтж	сектрк-П(М16)
	зп	сектрк-П(М16)
	сч	Е7Е4
	вч	D11322-П(М16)
	зп	D11265-П(М16)
G10517	сч	D11265-П(М16)
	нтж	Е7Е4
	пе	G10521
	зп	D11265-П(М16)
G10521	сч	D10701-П(М16)
	нтж	П177
	пе	G10546
	сч	D11265-П(М16)
	пе	G10550
	зп	D10701-П(М16)
	сч	МЕТОД-П(М16)
	по	G10530
G10525	сч	
	зп	D11245-П(М16)
	сч	сектрк-П(М16)
	нтж	Е36
	зп	сектрк-П(М16)
	пб	G10650
G10530	сч	ПРЕДЕЛ
	и	E47
	по	G10535
	сч	D10026-П(М16)
	слц	E1
	зп	D10026-П(М16)
	нтж	шdw21(М3)
	пе	G10544
	зп	D10026-П(М16)
G10535	пам	0
	сч	D10701-П(М16)
	пе	П3(М17)
	сч	ПРЕДЕЛ
	и	E47
	пе	G10511
	сч	D10026-П(М16)
	слц	E1
	зп	D10026-П(М16)
	нтж	П12
	пе	G10544
	зп	D10026-П(М16)
	пб	G10511
G10543	мод	ИР2
	уиа	0(М2)
G10544	мод	ИР3
	уиа	0(М3)
	пб	БМВ
G10546	сч	D10701-П(М16)
	по	G10544
	вч	E1
	зп	D10701-П(М16)
G10550	сч	МЕТОД-П(М16)
	пе	G10552
	сч	E1
	увв	'14'(М3)
G10552	сч	сектрк-П(М16)
	или	Е36
	зп	сектрк-П(М16)
 СЧ АЗУ
 СЛЦ Е25
 ЗП АЗУ          КОЛ-ВО СТРОК
	сч	D11265-П(М16)
	слц	E1
	зп	D11265-П(М16)
	сч	МЕТОД-П(М16)
	по	G10544
	мода	G10571
	уиа	0(М15)
G10561	пам	0
	сч	E11
	увв	'70'(М3)
	сда	64+56
взв1	пам	0
	увв	'4070'
	уи	
	зп	D11254-П(М16)
	сч	E10
	увв	'70'(М3)
	сда	64+56
	сч	D11254-П(М16)
	и	E10(М3)
	по	(М15)
	сч	
	зп	D11324-П(М16)
	пб	G10654
G10571	сч	К2013
	увв	'70'(М3)
	сда	64+56
	мода	G10544
	уиа	0(М15)
	пб	взв1
G10574	слиа	-31(М16)
	мод	ТУС+1
	уиа	0(М17)
	счи	М3
	уии	М3(М16)
	сли	М16(М17)
	зп	ИР3
	мода	G10561
 	пв	(М15)
	сч	E18(М3)
	пв	ТВ67(М15)
	сч	сектрк-П(М16)
	и	E34
	по	G10607
	нтж	сектрк-П(М16)
	или	Е36
	зп	сектрк-П(М16)
	сч	рчтбуф-П(М16)
	и	П7777
	зп	D10701-П(М16)
G10607	пам	0
	сч	D10701-П(М16)
	зп	D11324-П(М16)
	по	G10525
	пб	G10517
G10611	счи	М2
	зп	ИР2
	сч	сектрк-П(М16)
	и	E34
	по	G10521
	уиа	0(М2)
	сч	РЭ3
	уи	М15
	вч	П60
	пе	G10620
	слиа	1(М2)
	уи	М15
	вч	П60
	по	G10543
G10620	сч	E48(М15)
	мода	(М3)
	мода	шкусац+2-П(М16)
	и	(М2)
	по	G10662
	сч	РЭ3
	и	П37
	сда	64-3
	слц	ФАДБУФ-П(М16)
	уи	М2
	сч	РЭ3
	сда	64+5
	сда	64-4
	уи	М15
	счи	М1
	зп	РЭ3
	уиа	-7(М1)
	счи	М16
	и	E1
	пе	МОЛОТ1
МОЛОТ0	мода	7(М1)
	сч	(М2)
	сда	64(М15)
	увв	'47'(М1)
	уи	
	сч	
	увв	
	мода	МОЛОТ0
	цикл	(М1)
G10637	пам	0
	мод	РЭ3
	уиа	0(М1)
	мод	ИР2
	уиа	0(М2)
	сч	рчтбуф-П(М16)
	вч	E16
	зп	рчтбуф-П(М16)
G10643	пам	0
	зп	D10701-П(М16)
	и	Е24П16
	пе	G10544
	сч	сектрк-П(М16)
	нтж	E34
	или	Е36
	зп	сектрк-П(М16)
	и	E3
	пе	G10521
G10650	пам	0
	сч	сектрк-П(М16)
	нтж	E48
	или	E37
	зп	сектрк-П(М16)
	сч	ШКАРУ
	нтж	E24(М3)
	или	Е36(М3)
	зп	ШКАРУ
G10654	мод	ИР3
	уиа	0(М3)
	сч	ШЗБУДС
	и	E47
	по	БМВ
	нтж	ШЗБУДС
	зп	ШЗБУДС
	сч	ШГ
	или	E47
	зп	ШГ
	пб	БПУ
G10662	сч	рчтбуф-П(М16)
	мод	ИР2
	уиа	0(М2)
	пб	G10643
МОЛОТ1	мода	7(М1)
	сч	(М2)
	сда	64(М15)
	увв	'57'(М1)
	уи	
	сч	
	увв	
	мода	МОЛОТ1
	цикл	(М1)
	пб	G10637
        отмен   (М17)
*
* КОНЕЦ ОБРАБОТЧИКОВ ПРЕРЫВАНИЙ
********************
рзвре1	конд	в'0000000077777777' исп. для
	конд	в'0000000077777777' рзб ВРЕМЯ
шкприк	пам	1
D10674	пам	1
D10675	пам	1
ФАДБУФ	пам	2 адр буферов АЦПУ0 и АЦПу1
ИР3	пам	1
D10701	пам	2
символ	пам	2
e31учт	пам	1
тконуч	пам	1
	конд	п'0ЛПОЖЧ'м40в'240'              КОНЕЦ
	конд	п'00ФШУЖ'м40в'246'м32в'247'      УЧТЕ
	конд	п'ОЬЦ0МЙ'м16в'257'             НЫХ ЛИ
	конд	п'ТУПГ00'м8в'266'в'142'        СТОВ
*
*     ПОСЛЕДНИЕ СТРОКИ ВЫДАЧИ
*     -----------------------
ТЕККНЦ НОП
 КОНД П’00БЧРФ’М40В’261’М32В’32’                АЦПУ
 КОНД П’/000ЮГ’М24В’271’М16В’272’             -0  ЭВ
 КОНД П’Н/0000’М16В’300’М8В’301’В’301’        М-0
 КОНД П’11 11 ’                               00.00.
 КОНД П’110000’М24В’32’М16В’142’М8В’32’В’177’ 00*
	конд	м40в'177'м32в'142'м24в'006'м16в'177'м8в'176'в'176'
	пам	1
********************
*
*** ПЕЧАТЬ НОМЕРА ЭВМ И НОМЕРА
*** АЦПУ, ДАТЫ И ВРЕМЕНИ (В ОБЩЕМ РЕЖИМЕ)
*
        употр   ПЕЧАТЬ(М7)
пчтном	сч	типтик <-G10342
	сда	64-16
	зп	типтик
	счи	М2
	пв	ПЕРЕВ(М15)
	или	типтик
	зп	типтик
	сч	ряучет
	уиа	0(М2)
	мода	G10342
	цикл	(М4)
	сч	типтик
	рзб	РЗБВРЕ
	слц	ТВРЕ1
	зп	ТЕККНЦ+3
	сч	типтик
	сда	64-32
	слц	ТВРЕ2
	зп	ТЕККНЦ+4
	уии	М2(М6)
	сч	
	зп	счзнбф-П(М10)
	зп	тконуч
	сч	E31
	зп	e31учт
	сч	ГОД
	и	П7
	слц	E1
	сда	64-24
	или	ТНЭВМ
	зп	ТЕККНЦ+2
	счи	М2
	слц	E1
	сда	64-32
	или	ТНАЦП
	зп	ТЕККНЦ+1
	уиа	-12(М16)
цикпчт	мода	12(М16)
	сч	e31учт
	мода	12(М16)
	зп	(М5)
	мода	цикпчт
	цикл	(М16) цикл 13 раз?
	сч	первая-П(М10)
	пе	спецоб сюда не переходим
	уии	М2(М5)
	слиа	4(М2)
	зп	1(М2)
	пб	заверш
*
*** УЧАСТОК ОБМЕН СО СПЕЦНОСИТЕЛЕМ
*** ЕСЛИ В БУФЕР НЕ ПУСТ
спецоб	сч	
	зп	первая-П(М10)
	пв	ТБУФ(М15)
	сда	64-8
	уи	М2
	сда	64-2
	или	КУСУЧТ
	слц	НОММЛ1
	зп	рфизоб
	пв	ЕNQ(М15)
	сч	рфизоб
	пв	ФИЗОБМ(М16)
	уиа	'3'
	сч	ГОД
	и	П7
	уи	М16
	сли	М16(М2)
	сч	
	зп	СТУСТР+9(М16)
	сч	рфизоб
	пв	ФИЗЗП(М16)
	сч	рфизоб
	пв	DЕQ(М15)
	сч	рфизоб
	пв	ТБУФ(М15)
	уии	М2(М5)
*
заверш	уиа	-5(М4)
	пб	зпупря
ряучет	пам	1
типтик	пам	1
час	конд	в'0000000000537440'
* =60*60*50=180000 ВРЕМ.ПРЕР В 1 ЧАСЕ
	конд	в'0000000000005670'
*=60*50=3000 ВРЕМ.ПРЕР В 1 МИНУТЕ
	конд	В’0000000000000062’
*=50 ВРЕМ ПРЕР В СЕК
РЗБВРЕ	конд	в'7777740077777400'
КУСУЧТ	конд	в'0000063700400000'
рфизоб	пам	1
учетdw	счи	М2 <-G10413. М2=АДРЕС М2=НАКОПИТ. БУФЕРА
	зп	абуфdw-П(М10) :=М2 
	счи	М4 =-5
	пб	G11021
	конд	в'0300601403006142'
*	пам	1
	конд	м40в'130'м32в'177'м24в'037'м16в'241'м8в'242'в'243'
шифр1	пам	1
шифр2	пам	1
	конд	м40в'261'м32в'262'м24в'263'м16в'062'м8в'063'в'061'
D11012	конд	в'5560040100200674'
D11013	конд	м40в'275'м32в'042'м24в'014'м16в'001'м8в'301'в'302'
часмин	конд	п'11 11 '
D11015	конд	в'0020071363000401'
D11016	конд	п'.00.00'
	конд	м40в'326'м32в'325'м24в'130'м16в'177'м8в'045'в'143'
G11020	сч	E1 <-G11035
	пб	G11035
*
G11021	сда	64-15 <-учетdw. 30-16РР=-5 . М4
	или	абуфdw-П(М10) 15-1рр= АДРЕС НАКОПИТ. БУФЕРА. М2
	зп	абуфdw-П(М10) :=М4 В 30-16РР, :=М2 В 15-1РР
	сч	первая-П(М10) = 1-Я ЯЧ ПЕЧАТАЕМОЙ ЗОНЫ ВЫВОДА
	зп	ряучет := 1-Я ЯЧ ПЕЧАТАЕМОЙ ЗОНЫ ВЫВОДА
	рзб	ПРОБ6 48-25РР ПО 4РР РАСТЯНУЛИ В 6 СИМВ
	слц	ЕДИНИЦ 6 СИМВОЛОВ УВЕЛИЧИЛИ НА 1
	зп	шифр1 :=ПЕРВЫЕ 6 ЦИФР ШИФРА З-ЧИ??
	сч	ряучет
	сда	64-24 24-01РР ПО 4РР
	рзб	ПРОБ6 РАСТЯНУЛИ В 6 СИМВ
	слц	ЕДИНИЦ СЛЕД 6 СИМВОЛОВ УВЕЛИЧИЛИ НА 1
	зп	шифр2 :=ВТОРЫЕ 6 ЦИФР ШИФРА З-ЧИ?? 
	уиа	0(М2) М2=0
	уиа	-2(М4) М4==-2
	сч	ГОД
	и	П7 3-1РР ЯЧ ГОД ИЗ ДИСП70
	уи	М16 М16= NБЭСМ ??
	сда	64-16 СДВИНУЛИ В 24-17РР
	слц	D11235 +1 В 17РР К NБЭСМ
	зп	D11013 := КАКОЙ-ТО ТЕКСТ С NБЭСМ
	сли	М16(М14) М16:= NБЭСМ + АДР ЛИСТА КАТ БУФ ВЫВОДА
	сч	'1761'(М16) 
	слц	E1 +1
G11035	зп	'1761'(М16) <-G11020 НА СМ Е1. ’1761’(М16):=СМ
	зп	D10035-П(М10) D10035-П(М10):= ’1761’(М16)
	и	E13 ?? СМЫСЛ  ’1761’(М16) В КАТ ВЫВ??
	пе	G11020 -> ЕСЛИ ’1761’(М16) >= 4096
	сч	'1761'(М16) 
	пв	ПЕРЕВ(М15) ?? В ДИСП70: ПЕРЕВОД В 10-УЮ СИСТЕМУ
	сда	64-8 4 ДЕСЯТИЧ ЦИФРЫ СДВИНУЛИ ВЛЕВО НА 8РР
	слц	D11236 + ИХ С 1 И ДОБАВИЛИ 2 СИМВ СЛЕВА-СПРАВА
	зп	D11012 :=КАКОЙ-ТО ТЕКСТ 4-МЯ ЦИФРАМИ
	сч	ВРЕМЯ ВРЕМЯ ИЗ ДИСП70. 1 ТИК = 1/50 СЕК
G11042	зп	ряучет <-G11042,G11045. ОСТАТОК ВРЕМЕНИ (Т)
	мода	час ЧАС=537440ТИК, МИН=5670ТИК, СЕК=62ТИК
	вч	2(М4) М4=-2: ЧАС, =-1: МИНУТА, =0: СЕКУНДА
	пе	G11045 -> ЕСЛИ < 0	
	слиа	1(М2) М2:=М2+1 (ЧИСЛО ЧАСОВ, МИНУТ, СЕКУНД)
	пб	G11042 -> ПРОДОЛЖ.ПОДСЧЕТ ТЕКУЩ ТИПА Т
G11045	сч	типтик <-G11042. М2=ЗНАЧ-Е ТЕКУЩ ТИПА Т
	сда	64-16
	зп	типтик := ОСВ. МЕСТО ДЛЯ 2-Х СИМВ ТИПА Т 
	счи	М2 М2 = ЧИСЛО ЕДИНИЦ ВРЕМЕНИ ТЕКУЩ ТИПА Т
	пв	ПЕРЕВ(М15) ПЕРЕВ. 2 СИМВОЛА ТИПА Т В ДЕСЧТИЧ. ВИД
	или	типтик 
	зп	типтик := ЗНАЧЕНИЕ ТЕКУЩ ТИПА Т
	сч	ряучет ОСТАТОК Т ПОСЛЕ ПОДСЧЕТА ТЕКУЩ ТИПА Т
	уиа	0(М2) М2:= 0 ЕДИНИЦ ДЛЯ СЛЕДУЮЩ ТИПА Т
	мода	G11042 
	цикл	(М4) ->G11042, М4:=М4+1 (М4=-1 И =0)
	сч	типтик G11042=6 ЦИФР: 2-ЧАС,2-МИНУТА,2-СЕКУНДА
	рзб	РЗБВРЕ 48-33Р=ЧАС, 24-09РР=МИНУТА
	слц	ТВРЕ1 4 ЦИФРЫ=+1(ЧАС,МИНУТА) И +2 ПРОБЕЛА
	зп	часмин := 2 СИМВ ЧАС, ПРОБЕЛ, 2 СИМВ МИНУТА, ПРОБЕЛ
	сч	типтик
	сда	64-32
	слц	D11237 2 ЦИФРЫ=+1(СЕКУНДА), +2 СПЕЦ СИМВ
	зп	D11015 := 2 СИМВ СЕКУНДА, 2 СПЕЦ СИМВ
	сч	ГОД !! УЗНАТЬ РАЗРЯДЫ ЯЧЕЙКИ ГОД
	сда	64-13 35-1РР ГОД СДИНУЛИ В 48-14РР
	рзб	D11203 
	слц	D11015
	зп	D11015 := ДОБАВИЛИ ЧТО-ТО?? ИЗ ГОД В D11015
	сч	ГОД 
	сда	64-19 29-29РР,28-25РР,24-21РР ГОД СДИНУЛИ В
	рзб	D11204 33-33РР, 28-25РР, 4-1РР
	слц	D11240 + .. В 48-41 И 24-17РР, + 0 В 16-9РР
	зп	D11016 := ДОБАВИЛИ 3 ЦИФРЫ ИЗ 29-21РР 
*ГОД В D11016
	сч	ГОД 
	сда	64+16
	и	П17 20-17РР ЕЩЁ ОДНА ЦИФРУ ГОД
	сда	64-8 СДВИНУЛИ В 12-9РР
	слц	D11016 + К ТРЁМ ЦИФРАМ ГОД В ЯЧ D11016
	слц	ЕДИНИЦ 6 СИМВОЛОВ УВЕЛИЧИЛИ НА 1
	зп	D11016 := 4 ЦИФРЫ ИЗ 29-17РР ГОД В D11016
	уии	М2(М14) М2:= АДРЕС ЛИСТА С КАТАЛОГОМ ВЫВОДА	
	уиа	-14(М16) М16:= ЦИКЛ ПОВТОРЯТЬ 15 РАЗ
G11071	мода	14(М16) <-G11045,G11071. ПРЕРЕПИСЬ ЯЧ. ДРАЙВЕРА АЦПУ
	сч	учетdw С учетdw ПО G11021
	мода	14(М16) В ПЕРВЫЕ 15 ЯЧЕЕК
	зп	(М2)  ЛИСТА ??С КАТ. ВЫВОДА
	мода	G11071 ->G11071. КОНЕЦ ЦИКЛА ПЕРЕПИСИ
	цикл	(М16) М4:-5
	уиа	-5(М4) 
	сч	кусвыв-П(М10) КУС КАТАЛОГА БУФЕРА ВЫВОДА
	пв	DЕQ(М15) ОСВОБОДИЛИ ДИСК КАТАЛОГА БУФЕРА ВЫВОДА
	пб	прибав
*
непуст	сч	(М14)
	по	бфдисw
	сч	
	зп	(М14)
	уии	М2(М14)
	слиа	1(М2)
	уиа	-5(М4)
	пб	зпупря
*
бфдисw	сч	абуфdw-П(М10)
	уи	М2
	сда	64+15
	уи	М4
	сч	кусвыв-П(М10)
	пв	ЕNQ(М15)
	сч	кусвыв-П(М10)
	пв	ФИЗОБМ(М16)
	сч	ГОД
	и	П7
	уи	М16
	сли	М16(М14)
	сч	D10035-П(М10)
	зп	'1761'(М16)
	сч	кусвыв-П(М10)
	пв	ФИЗЗП(М16)
	сч	кусвыв-П(М10)
	пв	DЕQ(М15)
	сч	кусвыв-П(М10)
	пв	ТБУФ(М15)
	сч	
	зп	кусвыв-П(М10)
зпупря	мода	ОЧСИМВ <-счзон,прибав,заверш,G11077,бфдисw
	уиа	0(М15) М15:=ОЧСИМВ. ВЫХОД ИЗ БЛОКА ЧИСТКА
	сч	упрбф1-П(М10) УПРЯТАННОЕ ЗНАЧЕНИЕ М2
	зп	упрбф2-П(М10) :=упрбф1-П(М10)
	сч	упрм4-П(М10) УПРЯТАННОЕ ЗНАЧНИЕ М4
	зп	упрм4а-П(М10) :=упрм4-П(М10)
	сч	упсктр-П(М10) УПРЯТАННОЕ ЗНАЧЕНИЕ сектрк-П(М10)
	зп	усктр1-П(М10) :=упсктр-П(М10)
	сч	сектрк-П(М10) 
	зп	упсктр-П(М10) :=сектрк-П(М10)
	счи	М2 
	зп	упрбф1-П(М10) :=М2
	счи	М4
	зп	упрм4-П(М10) :=М4
	уиа	0(М13) М13:=0
	пб	чистка
ТНЕТИН	текст	п'4НЕТ ИНТЕРВ.'
режdw2	пам	1
ТДКОИ НОП
 КОНД Х'8F08F18F28F3'      0 1 2 3
 КОНД Х'8F48F58F68F7'      4 5 6 7
 КОНД Х'8F88F984E860'      8 9 + -
 КОНД Х'86186B84B840'      / , . ПР
 КОНД Х'8C587C84D85D'      E ↑ ( )
 КОНД Х'85C87E85E84A'      Х = ; [
 КОНД Х'85A85C87F87D'      ] * ' '
 КОНД Х'87B84C86E87A'      ≠ < > :
 КОНД Х'8C18BA8C28BF'      А Б В Г
 КОНД Х'8BC8C58EC8FA'      Д Е Ж З
 КОНД Х'8CB8CC8D28CE'      И Й К Л
 КОНД Х'8D48C88D68DC'      М Н О П
 КОНД Х'8D78C38E38EB'      Р С Т У
 КОНД Х'8BE8E78BB8FE'      Ф Х Ц Ч
 КОНД Х'8FB8FD8EF8EE'      Ш Щ Ы Ь
 КОНД Х'8FC8B88DD8C4'      Э Ю Я D
 КОНД Х'8C68C78C98D1'      F G I J
 КОНД Х'8D38D58D88D9'      L N Q R
 КОНД Х'8E28E48E58E6'      S U V W
 КОНД Х'8E986D84C86E'      Z НАДЧ < >
 КОНД Х'86A85085D85F'      V ∧ ) ОТРИЦ
 КОНД Х'86087E86C85B'      : = % ◇
 КОНД Х'86A86086D84F'      I -
 КОНД Х'87F87D86F87D'
упрбф1	пам	2
упрбф2	пам	2
упрм4	пам	2
упрм4а	пам	2
упсктр	пам	2
усктр1	пам	2
*
ДВБАЙТ НОП .             СДВИГИ ДЛЯ БАЙТОВ
 КОНД М41В’150’
 КОНД М41В’140’
 КОНД М41В’130’
 КОНД М41В’120’
 КОНД М41В’110’
 КОНД М41В’100’
*
D11203	конд	п'00003 '
D11204	конд	п'01 00 '
	пам	1
ЕДИНИЦ	конд	п'111111'
ПРОБ6	конд	п'      '
*
*       КУСЫ ЧТЕНИЯ ВЫХОДНОГО ПОТОКА
КУСАЦП НОП
 КОНД М17В’1’М2В’20’    20 ТРАКТ
 КОНД М17В’1’М2В’22’    22 ТРАКТ
*
шкусац	пам	6
абуфdw	пам	2
СИ	пам	2
ИР2	пам	1
	увв	'4100'
	пб	vt19
	мод	ТУС+1
	пб	G10574-П
ТНАЦП КОНД П’/000ЮГ’М24В’271’М16В’272’
ТНЭВМ КОНД П’Н/0000’М16В’300’М8В’301’В’301’
ТВРЕ1 КОНД П’11 11 ’
ТВРЕ2 КОНД П’110000’М24В’32’М16В’142’М8В’32’В’177’
кусвыв	пам	2
D11235	конд	м40в'275'м32в'042'м24в'014'м16в'001'м8в'301'в'302'
D11236	конд	п'011110'м40в'267'в'274'
D11237	конд	п'110011'м24в'313'м16в'314'
D11240	конд	п'.00.00'
занац0	пам	1
занац1	пам	1
МЕТОД	пам	2       0 - АЦПУ-128, не 0 - ЕС
D11245	пам	4
	конд	п'000041'
	конд	п'00008F'
	пам	1
D11254	пам	2
	пам	1
первая	пам	2
счзнбф	пам	2
сектрк	пам	2
D11265	пам	2
упрят0	пам	13 поле упрят. регистров
*ацпу-0
упрят1	пам	13 поле упрят. регистров
*ацпу-1
АЦПУ0 КОНД П’АЦПУ0↑’
D11322	пам	2
D11324	пам	2
П ЭКВ ПЕЧАТЬ
*
 АДРЕС П+’1400’
 ПАМ 256
*
 ВХОД ВЫДОПТ,СИАЦПУ,НОЛЬ,режdw1,режdw2,cимвdw
*
*//////////////////////////////////
*//                              //
*//        В Н Е Ш Н И Е         //
*//                              //
*//////////////////////////////////
*
*
ВЗУ	ВНЕШ	РАЗРЕ,ФИЗОБМ,ФИЗЗП
ДИСКИ	ВНЕШ	DЕQ,ЕNQ
ДИСП70	ВНЕШ	ПЕРЕВ,ЗАНОСМ,ТБУФ13,ТБУФ,ТВ67А,ТВ67,БПУ,ТАЙМЕР
ДИСП70	ВНЕШ	БМВ,ОТКАЗН,ШЗБУДС,РЭ3,ПРЕДЕЛ,ВРЕМЯ,АЗУ
ДИСП70	ВНЕШ	П7,ШКПЛАН,НОММЛ1,НОММБ,ШИФРАЦ,П12,Е48П37,К2013
ДИСП70	ВНЕШ	П377,П177,П140,П60,П37,П3,Е24П13,Е24П16
ДИСП70	ВНЕШ	ШГ,П17,П5,П6,П7777,ГОД,ТВ40,ШКАРУ,СБОИМБ
ДИСП70	ВНЕШ	ТУС,П77777,КСД3,СТУСТР,ОЧКУС
КИТ	ВНЕШ	Е7Е4
МОТТ	ВНЕШ	vt19
КОНФУС	ВНЕШ	начdw,авdw21,пркод7,шdw21,шdw21а,шdw21б,е17dw
КОНФУС	ВНЕШ	шпр4к7,ТАБЛИЦ
	ФИНИШ

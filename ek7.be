ЭК7	СТАРТ	'11400'
	Б
	Е
	М
*
 ГБЛВ ∧К71
∧К71 УСТВ 1
*
        АПУ     (&К71).ДА
	АПБ	.НЕТ
*
.ДА АНОП
	употр	ЭК7-768(М7)
*
*** ПРИ "ХОЛОДНОМ СТАРТЕ" СИСТЕМЫ
*** НЕ ЗАГРУЖ.
*
Э133	сч	базсв7
	пе	э133а
	мода	0
	пв	ТБУФ(М15)
	сда	64-8
	зп	базсв7
	сда	64-2
	или	КУССВ7
	слц	НОММЛ1
	пв	ФИЗОБМ(М16)
э133а	мод	базсв7
	пб	Э50133-СВЯЗЬ7 !УХОД В СВЯЗЬ7
*
*** ПРОГРАММА ДЛЯ ТРАССИРОВКИ ОШИБОК
*** УДАЛ. НМД. ДКС К-71
* (РАНЕЕ АВТОНОМНЫЙ МОДУЛЬ "ЭКО")
*
изсв7	уиа	'3' !ВХОД ИЗ СВЯЗЬ7
	сч	0(М3)
	сда	64-12
	счмр	
	и	П7
	уи	М15
	мода	дайлт 0 - ДАЙ БУФЕР И ВКЛЮЧИ ТРАССИРОВКУ
	пио	0(М15)
	слиа	-1(М15)
	мода	стопт 1- ПРИОСТАНОВИ ТРАССИРОВКУ
	пио	0(М15)
	слиа	-1(М15)
	мода	возлт 2 - ВЕРНИ БУФЕР И ВЫКЛЮЧИ ТРАССИРОВКУ
	пио	0(М15)
	слиа	-1(М15)
	мода	ямге  3 - ПЕРЕПИСЬ ИЗ ФИЗ. ЛИСТА В МАТ. ЛИСТ.
	пио	0(М15)
	слиа	-1(М15)
	мода	мавмл7
	пио	0(М15)
	слиа	-1(М15)
	мода	э7прцс
	пио	0(М15)
	слиа	-1(М15)
	мода	пр7кз1
	пио	0(М15)
	уиа	36(М6) !ЗАПРЕЩ. ЭКСТРАКОД
	пб	запэк7
*
дайлт	мод	ук
	сч	0
	пе	выхэк7
	мод	ук+1
	сч	0
	пе	вклт
	мода	0
	пв	ТБУФ(М15)
	сда	64-8
	уи	М15
* СМ 3700 000N 0000 0000, N - НОМЕР ВАРИАНТА
	сч	0(М3)
	сда	64+23
	и	П17
	уи	М17
	мода	бат(М17)
	уиа	(М16)
	счи	М16
	зп	ук
	слц	E1
	зп	ук+1
	счи	М15
	мод	ук+1
	зп	0
	счи	М17
	сда	64-6
	уи	М5
	сли	М5(М15)
	счи	М15
	сда	64-2
	или	кусвар !НЕТ РЕАЛЬН. РАБ. С АВТ. "КОНВОЕМ"
	слц	НОММЛ1
	пв	ФИЗОБМ(М16)
	уиа	-127(М15)
*
*** !ЗАГРУЗКА НУЖНО ВАРИАНТА
*** ПРОГРАММЫ "КОНВОЙ"
* В ВЕРСИИ ДЛЯ К-71 НЕИСП?
*
вызвар	мода	0(М5)
	сч	127(М15)
	уиа	'2003'
	рег	'101'
	мода	тпрер
	зп	127(М15)
	рег	'107'
	уиа	'3'
	мода	вызвар
	цикл	0(М15)
	мод	ук+1
	сч	0
	уи	М16
	слц	сдвиг
	зп	начад1
* РОСПИСЬ НУЛЯМИ
	уиа	-1023(М15)
	счи	
росп	мода	0(М16)
	зп	1023(М15)
	мода	росп
	цикл	0(М15)
* РОСПИСЬ ЗАГОЛОВКА
	сч	ГОД
	зп	0(М16)
	сч	ВРЕМЯ
	зп	1(М16)
	уиа	-2(М15)
	сч	начад1
авсад	зп	уобщ(М15)
	мода	2(М16)
	зп	2(М15)
	слц	д3(М15)
	зп	конад1+2(М15)
	сч	ход3(М15)
	мода	5(М16)
	зп	2(М15)
	сч	конад1+2(М15)
	мода	авсад
	цикл	0(М15)
вклт	мод	ук+1
	сч	0
	мод	ук
	зп	0
выхэк7	пб	КОНЕЦ1-Э(М7)
возлт	уиа	'2003'
	мод	ук+1
	сч	0
	сда	64-2
	или	E19
	пв	ТБУФ(М15)
	счи	
	мод	ук+1
	зп	0
стопт	счи	
	мод	ук
	зп	0
	пб	выхэк7
*
* СМ: 3703 FINI 0000 0000
* FI(35:31) - ФИЗ. ЛИСТ
* NI(29:25) - МАТ. ЛИСТ.
*
ямге	сч	0(М3)
	сда	64+14
	уи	М5 АНАЧ. МАТЕМАТИЧ.
	сда	64+16
	сда	64-10
	уи	М16 АНАЧ. ФИЗИЧ.
	уиа	'72'(М6) ЧЛИСТ
	мода	запэк7
	уиа	(М15)
	счи	М15
	мода	0
	пв	ЗАПВЫХ(М15)
	уиа	-1023(М15)
пере	мода	0(М16)
	сч	1023(М15)
	уиа	
	мода	0(М5)
	зп	1023(М15)
	уиа	'3'
	мода	пере
	цикл	0(М15)
	пб	выхэк7
*
запэк7	пб	АВОС1-Э(М7) !ОШИБКА
*
*** НОВЫЕ ПРОЦЕДУРЫ, ОТСУТСТВ.
*** В ТЕКСТЕ "ЭКО" ОТ 1984 Г.
*
*** БЛОК АВМЛ
*
мавмл7	сч	0(М3)
	и	E4
	пе	авмл7б
	сч	0(М3)
	и	П7
	зп	гтмдл7
	уиа	-15(М15)
	счи	
авмл7а	зп	шмлк71+15(М15)
	мода	авмл7а
	цикл	0(М15)
авмл7б	уиа	гтмдл7(М15)
	счи	М15
	зп	0(М3)
	пб	выхэк7
*
*** БЛОК "КАЧКА"
*
э7прцс	сч	0(М3)
	и	П1777
	уи	М15
	нтж	0(М3)
	сда	64+10
	и	КОНФИЛ+1
	уи	М16
	и	П37
	сда	64-5
	зп	ряэ7
	счи	М16
	сда	64-38
	рзб	Е34Е30
	или	ряэ7
	зп	ряэ7
	пв	УПРИ1(М10)
	мода	выэк7б
	уиа	(М16)
	сч	К2003
	уи	М21
	счи	М16
	сда	64-24
	зп	4(М7)
	уиа	'2002'
	сч	ТОП(М15)
	уиа	'2003'
выэк7а	зп	0(М3)
	пб	выхэк7
*
выэк7б	сч	СМ
	пб	выэк7а
*
*** БЛОК "ПРИКАЗ"
* ДЛЯ РАБОТЫ С МОДИФИЦИР.
* ПРОЦЕДУРАМИ РАБОТЫ С ПРИКАЗАМИ ПОЛЬЗ.
пр7кз1	сч	0(М3)
	сда	64+24
	и	П77
	уи	М15
	сч	Е48-1(М2)
	зп	шсост7
	сч	0(М3)
	и	Е33
	или	'50'(М3)
	зп	'50'(М3)
	мода	выэк7в
	пио	0(М15)
	сч	ШКРЗ
	и	Е48-1(М15)
	по	выэк7в
	мод	НЗАД(М15)
	уиа	(М17)
	сч	27(М17)
	сда	64+21
	и	П77
	пе	выэк7в
	сч	0(М3)
	сда	64+6
	и	П77
	нтж	П77
	по	G11601
	уиа	'2003'
	сч	E40(М3)
	или	Е48-1(М15)
	зп	E40(М3)
	сч	ШКОК
	нтж	Е48-1(М15)
	зп	ШКОК
	сч	ОСПОД
	или	Е48-1(М15)
	зп	ОСПОД
	счи	М2
	сда	64-21
	или	27(М17) !ДИСП70.ДС, СДТ, И?
	зп	27(М17)
*ЗАВЕРШЕНИЕ РАБ.
G11573	сч	'44'(М17)
	и	Е41П1
	или	Е48П42
	зп	'44'(М17)
	сч	Е48-1(М15)
	нтж	ВСЕЕД
	и	МШГП
	зп	МШГП
	уиа	рабдкс+22(М17)
	счи	М17
	пб	выэк7а
G11601	сч	ПАК
	или	Е48-1(М15)
	зп	ПАК
	пб	G11573
*
выэк7в	счи	
	пб	выэк7а
ряэ7	пам	1
кусвар	конд	м24в'644'м17в'1'      !АВТОНОМНЫЙ КОНВОЙ
БОЭК1	ВНЕШ	ЗАПВЫХ
ВЗУ	ВНЕШ	ФИЗОБМ
ДИСП70	ВНЕШ	ТБУФ,Е41П1,НЗАД,СМ,МШГП,ОСПОД,ВРЕМЯ,П7,НОММЛ1
ДИСП70	ВНЕШ	П37,П17,ПАК,ШКОК,П1777,ГОД,К2003,СТЗОН,П77,ВСЕЕД
КОНФУС	ВНЕШ	базсв7,КУССВ7,рабдкс,шсост7,тпрер
СВЯЗЬ7  ВНЕШ	СВЯЗЬ7,Э50133
КАЧКА	ВНЕШ	ШКРЗ,КОНФИЛ
КИТ	ВНЕШ	Е48П42,Е34Е30
МОТТ	ВНЕШ	ук,бат,гтмдл7,шмлк71,уобщ,начад1,сдвиг,д3,ход3
МОТТ	ВНЕШ	конад1
СОСТАВ	ВНЕШ	ТОП
ХЛАМ	ВНЕШ	УПРИ1
ЭКВВОД	ВНЕШ	(Э,ЭКВВОД),КОНЕЦ1,АВОС1
	ВХОД	Э133,изсв7
.НЕТ АНОП
	ФИНИШ

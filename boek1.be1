БОЭК1 старт '15000'
 Е
 М
*******************************************
ЧСВОИХ ЭКВ 33
ЧФОНЛ ЭКВ 25
*******************************************
*              СТРУКТУРА ТОП-ОВ:
*
*************************************************************
*       *    *     *       *       *    *    *       *      *
* 48-42 * 41 * 40  * 39-35 * 32-21 * 20 * 19 * 18-17 * 16-1 *
*       *    *     *       *       *    *    *       *      *
*************************************************************
*       *    *     *       *       *    *    *       *      *
*   I   * II * III *  IV   *   V   * VI * VII* VIII  *  IХ  *
*       *    *     *       *       *    *    *       *      *
*************************************************************
*
*48-42 - НОМЕР ЗАДАЧИ, 41 - ЕОТКЛМ ( 0-ЛИСТ У ЗАДАЧИ, 1-ОТКЛ
*40-35 - N МАТ. ЛИСТА (Е40 - ЕБУФЛ), 32-21  - N ВИРТ. ЛИСТА
*20 - ПРИЗНАК ТРАКТА МБ, 19 - ЕОБМ ( ПРИЗНАК ОБМЕНА)
*18-17 - СЧЕТЧИК ПРИ ЗАПИСИ НА МБ, 16-1  - ССЫЛКА.
**************************************************
КАЧК НОП
 УИА -1(М10)
КУК СЧ ПОДНОС+1(М10)
 ИЛИ КЛИЕНТ+1(М10)
 ЗП ПОДНОС+1(М10)
 ЦИКЛ КУК(М10)
КАЧА СЧ ПОДНОС(М10)
 ПО НЕТ
К1 НОП ,
 УИА 3
 ЗП ЗАК
 НЕД
 УИ М12           М12 := НОМЕР СТАРШЕГО ЗАКАЗЧИКА
ОБХОД НОП ,
 УИИ М6(М12)
 ПИО ОС(М10)
 СЛИА ПЗ(М6)
ОС НОП ,
 УИА 1(М14)        М14 := 1 - ЗАКАЗАЗ НЕ ОТ ТБУФ(ДБУФ)
 УИА 2(М3)         М3  := 2 - НУЖЕНН ЛИСТ ДЛЯ МАТЕМ.
 СЧ ТУС-1(М6)
 И ЕПОДКЛ
 УИА 1027
 ПЕ ПОИСКЛ
 УИА 0(М14)        М14 := 0 - ЗАКАЗАЗ ОТ ТБУФ(ДБУФ)
 УИА 1(М3)         М3  := 1 - НУЖЕН ЛИСТ ДЛЯ ДБУФ
 МОД ТУС-1(М6)
 СЧ 0
 ЗП ЖДУ
 И Е25
 ПЕ ПОИСКЛ        ЗАКАЗ ОТ ДБУФ
 УИА 0(М3)         М3 := 0 - НУЖЕН ЛИСТ ДЛЯ ТБУФ
*
ПОИСКЛ НОП ,
 СЧ НАЧФИЛ(М3)
 СДА 64+5
 УИ М1            М1 := НОМЕР НАЧАЛЬНОЙ СТРОКИ ТЗФСТР
 ЗП R
 СЧ КОНФИЛ(М3)
 СДА 64+5
 ВЧОБ R
 УИ М17
ПОТЗС НОП ,
 ПИО ЗТБУФ(М3)     ЗАКАЗ ОТ ТБУФ
 СЧ ТЗФСТР(М1)
 И Е1
 ПЕ СЛЕДС         ИЗ 0 СЕГМЕНТА БАЗ.СЕК.ОС НЕЛЬЗЯ
ЗТБУФ НОП ,
 СЧ ТЗФСТР(М1)
 И Е48П17
 НТЖ Е48П17
 ПЕ ДЛ2           ЕСТЬ СВОБОДНЫЕ ЛИСТЫ
СЛЕДС НОП ,
 СЛИА 1(М1)
 ЦИКЛ ПОТЗС(М17)
*
*
* НАДО  ОТКАЧАТЬ
*
 СЧ ФИЛ(М3)
 ЗП ПАМФИЛ        ПАМФИЛ := ФИЛ(М3)
*
 СЧ ШГП
 И МШГП
 ИЛИ ШКОТКЗ
 НТЖ ШКОТКЗ
 ИЛИ ПОДП
 ЗП ШКФОНЗ        ШКАЛА ФОНОВЫХ ЗАДАЧ
*
 СЧ ШКОТКЗ
 ИЛИ ПОДП
 НТЖ ПОДП
 ЗП ШКОТКЗ        ШКАЛА ОТКАЧИВАЕМЫХ ЗАДАЧ
 УИА -ЧФОНЛ(М2)
	пб      НАЙДИ
A15037 	сч	E5
	пв	ГАШЗАН(М15)
*
НАЙДИ НОП ,
 УИА 3
 СЧ ФИЛ(М3)
 СЛЦ Е1
 ЗП ФИЛ(М3)
 ВЧОБ КОНФИЛ(М3)
 ПО НОРМА
 СЧ НАЧФИЛ(М3)
 ЗП ФИЛ(М3)
НОРМА НОП ,
 СЧ ФИЛ(М3)
 УИА 1027
 УИ М11           М11 := НОМЕР ФИЗИЧЕСКОГО ЛИСТА
 СДА 64+5
 УИ М16
 СЧ ФИЛ(М3)
 И П37
 УИ М15
 ПИО З1ТБУФ(М3)    ЗАКАЗ ОТ ТБУФ
 СЧ ТЗФСТР(М16)
 И Е1
 ПЕ СЛЕДС1         ИЗ 0 СЕГМЕНТА БАЗ.СЕК.ОС НЕЛЬЗЯ
З1ТБУФ НОП ,
 СЧ ТЗФСТР(М16)
 И Е48(М15)
 ПО КАЧК          КТО-ТО УЖЕ ОСВОБОДИЛ ЛИСТ
 СЧ КАЧКА+33
 И Е31
 ПО ИЩУ           РАБОТАЕМ НЕ ТАК ДОЛГО
 ПИНО ХВАТИТ(М10)   ПРИ ЗАКАЗЕ ОТ МАТЕМАТИКА
 СЧ ОТВКАЧ
 И Е48-1(М12)
 ПЕ A15155 было ЗОТВ          ЗАКАЗ С ОТВЕТОМ
 ПИНО ХВАТИТ(М14)   ЗАКАЗ НЕ ОТ ТБУФ(ДБУФ)
 СЧ ЖДУ
 И Е25
 ПЕ ХВАТИТ        ЗАКАЗ ОТ ДБУФ
*
* ЗАКАЗЫ ОТ ТБУФ ОТЛОЖИМ
* ПОПРОБУЕМ ВЫПОЛНИТЬ ДРУГИЕ ЗАКАЗЫ
*
 УИА КАЧКА(М7)
 ПВ ЗАНОСС(М16)
*
ОТТБУФ НОП ,
 СЧ Е48-1(М2)
 ИЛИ КЛИЕНТ
 ЗП КЛИЕНТ
*
 СЧ Е48-1(М12)
 НТЖ ПОДНОС
 ЗП ПОДНОС
 ПО ПОВ1          ВСЕ ЗАКАЗЫ ПРОСМОТРЕЛИ
 НЕД
 УИ М12
 СЧ ТУС-1(М12)
 И ЕПОДКЛ
 ПЕ ОБХОД         ПОПРОБУЕМ ПОДКАЧАТЬ ЛИСТ МАТЕМАТИКА
 МОД ТУС-1(М12)
 СЧ 0
 И Е25
 ПЕ ОБХОД         ПОПРОБУЕМ ПОДКАЧАТЬ ЛИСТ ДЛЯ ДБУФ-А
 ПБ ОТТБУФ
*
* ПЕРЕЙДЕМ К СЛЕДУЮЩИМУ СЕГМЕНТУ
СЛЕДС1 НОП ,
 СЧ ФИЛ(М3)
 СЛЦ П37
 ЗП ФИЛ(М3)
 ПБ НАЙДИ
*
ИЩУ НОП ,
 СЧ Е5
 ПВ ЗАХЗАН(М15)   ЗАНЯТИЕ Е5 (ЗАНЯТО)
 УИА 1026
 СЧ ТОП(М11)
 УИА 1027
 ЗП R2            R2 := СТРОКА ТОП
 И ЕОБМ
 ПЕ НЕЛЬЗЯ        ЛИСТ ИЛИ ТРАКТ В ОБМЕНЕ
 СЧ R2
 И ЕТРА
 ЗП РТР           РТР :=  ПРИЗНАК ТРАКТА
 СЧ R2
 СДА 64+ТОПНЗ
 И П77
 УИ М16           М16 := НОМЕР ЗАДАЧИ
 ПО НЕЛЬЗЯ        N ЗАДЧИ =0
 СЧ ТУСП-1(М16)
 УИ М13           М13 := АДРЕС ИПЗ ОТКАЧИВАЕМОЙ ЗАДАЧИ
 СЧ РТР
 ПЕ НУЖНО         ТРАКТ МОЖНО ОТКАЧАТЬ
*
* ПРОВЕРЯЕМ ЛИСТ НА ПРИНАДЛЕЖНОСТЬ ЕГО К ЗАДАЧАМ,
* КОТОРЫЕ НЕЖЕЛАТЕЛЬНО УКАЧИВАТЬ
*
 ПИО A15120(М10) СВС: МОЖНО    ДЛЯ СИС.ЗАКАЗА МОЖНО ВСЕ ОТКАЧИВАТЬ
 СЧ ШКФОНЗ
 И Е48-1(М16)
 ПО МОЖНО         ЛИСТ НЕ ПРИНАДЛЕЖИТ ФОНОВЫМ ЗАДАЧАМ
 ЦИКЛ A15037(М2) было ОСВЕ5
* ТЕПЕРЬ ПО СВОИМ ЛИСТАМ
*
 СЧ ПАМФИЛ
 ПО МОЖНО         ПО СВОИМ ЛИСТАМ УЖЕ ХОДИЛИ
 ЗП ФИЛ(М3)       ВОССТАНОВИЛИ ФИЛ
 СЧ Е48-1(М12)
 ЗП ШКФОНЗ        ШКФОНЗ := ТОЛЬКО СЕБЯ
 СЧ 0
 ЗП ПАМФИЛ        ПАМФИЛ := 0 - ПРИЗНАК СВОИХ ЛИСТОВ
 УИА -ЧСВОИХ(М2)   М2 := -ЧСВОИХ
	пб	A15037
*ОСВЕ5 НОП ,             ОСВОБОЖДЕНИЕ Е5 (ЗАНЯТО)
* СЧ Е5
* УИА НАЙДИ(М15)
* ПБ ГАШЗАН
*
МОЖНО НОП ,
 СЧ Е48-1(М12)
 ИЛИ ШКОТКЗ
 ЗП ШКОТКЗ
*
A15120 СЧ R2
 СДА 64+СДВМЛ
  И П77
 УИ М16           М16 := МАТ. НОМЕР ОТКАЧИВАЕМОГО ЛИСТА
 НТЖ П77
 УИ М1
 СЧ R2
 И ЕОТКЛ
 ПЕ НУЖНО
 СЧ 32(М13)
 ИЛИ ЛИСТМБ(М13)
 И Е48-32(М1)
 ПЕ НЕЛЬЗЯ        ЛИСТ В ОБМЕНЕ(32) ИЛИ ФИКСИР(ЛИСТМБ)
 пв A77613(М15) было СЧИ М16
 СДА 64-10
 НТЖ 3(М13)
 И Е15П11
 ПЕ НУЖНО         М33 -> НЕ НА ЭТОТ ЛИСТ
 СЧ 5(М13)
 И П460
 ПЕ НЕЛЬЗЯ
*
* НАШЛИ ЛИСТ ИЛИ ТРАКТ,
* КОТОРЫЙ МОЖНО ОТКАЧАТЬ
*
*    М11   = НОМЕР ФИЗИЧЕСКОГО ЛИСТА
*    R2    = СОДРЕЖИМОЕ СТРОКИ ТОП(М11)
*
НУЖНО НОП ,
 СЧИ М11
 СДА 64-СДФЛИ
 ЗП R5
* СЧ Е1
* ВЧ ГРАНЬБ
* УИ М1
* УИА (М17)
 wtc '3232'
 vtm 0(M17)
ПОИСКТ utm -1(M17)
 xta ШЗНМБО(М17)
 aax Е48П17
 u1a А3 
 v1m ПОИСКТ(M17)
*
*
*
*
*ПОИСКТ НОП ,
* СЧ ШЗНМБО(М17)
* И Е48П17
* ПЕ А3
* СЛИА 1(М17)
* ЦИКЛ ПОИСКТ(М1)
 СЧ Е5
 уиа	БУД(М15) СВС: НЕЛЬЗЯ+5 
 пб ГАШЗАН
* УИА 1(М1)         ВЫЗВАТЬ УКАЧКУ
* ПБ БУД
ВЫЗИС ИЛИ ШКИВ
 ЗП ШКИВ
 СЧ ВИГ11
 ПЕ (М17)
 УИА БПУ(М16)
 ПБ ВИСП15
НЕЛЬЗЯ НОП ,             НЕЛЬЗЯ ОТКАЧАТЬ ЛИСТ ИЛИ ТРАКТ
 СЧ Е5
 ПВ ГАШЗАН(М15)   ОСВОБОЖДЕНИЕ Е5 (ЗАНЯТО)
 ПИНО НАЙДИ(М14)    ЗАКАЗ СИСТЕМНЫЙ
 СЧ СЧЕ
 СЛЦ Е42
 ЗП СЧЕ
 ПЕ НАЙДИ         ПРОДОЛЖИМ ПОИСК
* СЧ ОТВКАЧ
* И Е48-1(М12)
* ПЕ ЗОТВ
 СЧ 0
 ЗП СЧЕ
 СЧ ЗАК
 НТЖ Е48-1(М12)
* УИА 0(М1)         УКАЧКУ НЕ ВЫЗЫВАТЬ
 ПЕ К1
БУД ПИНО БУД1(М10)
 СЧ ОТВКАЧ
 И Е48-1(М12)
 ПО БУД1
A15155 МОД ТУС-1(М6)
 УИА (М15)
 СЧ 0
 ЗП (М15)   ОТВЕТ
 СЧ 9(М15)
 ЗП 3(М15)
* ПБ ДАЛИ
ЗОТВ ноп
 пб ДАЛИ
БУД1 УИА (М7)
 ПВ ВБФ(М12)
 ПИНО КАЧК(М7)
 СЧ Е5
 ПВ ТАЙМЕР(М15)
 уиа	'4541'(М17)
 УИА КАЧК(М16)
* ПИО ТБ13(М1)      УКАЧКУ НЕ ВЫЗЫВАТЬ
*  УИА ТБ13(М17)
 СЧ Е1
* ПБ ВЫЗУКА
 УИА 1027
 ИЛИ УКАЧАЙ
 ЗП УКАЧАЙ
 СЧ УКА
 ИЛИ ШКИВ
 ЗП ШКИВ
 СЧ МШГ
 ИЛИ Е36
 НТЖ Е36
 ЗП МШГ
 СЧ ВИГ11
 ПЕ (М17)     ВИСП ЗАНЯТ
 ПВ ВИСП15(М16)
        уиа     БОЭК1(М16)      
        пб      '4541'
А3 НОП
 ПВ ОФТО3(М7)
 ИЛИ КДД
 НТЖ КДД
* ПРЕ 1026
 ЗП СМ СВС: ТБД(М4)
* ПРЕ 1027
 СЧ R2
 И Е48П17
 ИЛИ СМ СВС: ТБД(М4)
 ПРЕ 1026
 ЗП ТБД(М4)
 ПРЕ 1027
 СЧ ШЗНМБО-8(М1)
 НТЖ Е48(М3)
 ЗП ШЗНМБО-8(М1)  ЗАНЯЛИ ТРАКТ
 СЧ ТМБ
 ВЧ Е1
 ЗП ТМБ
 СЧ Е41П1
 УИА 1026
 И ТОП(М11)
 ЗП ТОП(М11)      УБРАЛИ НОМЕР ЗАДАЧИ
 УИА 1027
 ЗП ОЧЕНЬ         ОЧЕНЬ := СОДЕР.СТРОКИ ТОП (КОТРОЛЬ)
 ПВ ФОРНУ(М17)     R := КУС ОБМЕНА С МБ
 СЧ РТР
 ПО ОТКАЧЛ
*
* ОТКАЧИВАЕМ ТРАКТ
*
* СЧ ОЗУ
* СЛЦ Е1
* ЗП ОЗУ
 УИИ М17(М11)
 СЛИА ТОП-ТБД+1(М17)
 СЧИ М17
 ЗП R2            R2 := N СТРОКИ ТОП
 СЧ ШЗНМБ(М13)
 СДА 64+ССТБД
 УИА (М5)
ПРЕД И П77777
 ПО СТ414         ИСПОРЧЕНА ИНФОР. О ТРАКТАХ ЗАДАЧИ
 УИ М1
 НТЖ R2
 ПО НАШИЛ
 УИА 2
 СЧ ТБД-1(М1)
 УИА 3
 УИИ М5(М1)
 ПБ ПРЕД
НАШИЛ НОП ,
 УИА 1026 СВС: 1024
 СЧ ТБД-1(М1)
 УИА 3
 ЗП СТАРОЕ        СТАРОЕ := СОДЕРЖ. СТРОКИ ТОП (КОНТРОЛЬ)
 ПВ СИ2(М17)
 УИИ М17(М4)
 СЛИА 1(М17)
 СЧ ШЗНМБ(М13)
 И Е31П17
 ПЕ СИТ
 СЧИ М17
 СДА 64-ССТБД
 ИЛИ ШЗНМБ(М13)
 ЗП ШЗНМБ(М13)
СИТ10 СЧ ШЗНМБ+1(М13)
 И ОССТБД
 ЗП ШЗНМБ+1(М13)
 СЧИ М17
 СДА 64-ССТБД
 ИЛИ ШЗНМБ+1(М13)
 ЗП ШЗНМБ+1(М13)
 ПБ ГАШЕ5
*
*    ОТКАЧИВАЕМ ЛИСТ
*
ОТКАЧЛ НОП ,
 СЧ R2
 И ЕОТКЛ
 ПЕ ОТКЧБЛ
 СЧИ М16
 НТЖ П37
 УИ М7
*
 СЧ 32(М13)
 ИЛИ Е48(М7)
 ЗП 32(М13)
*
 СЧ 22(М13)
 ИЛИ Е48(М7)
 ЗП 22(М13)
*
 СЧИ М16
 И Е2Е1
 УИ М7
 СЧИ М16
 СДА 64+2
 НТЖ Е3П1
 УИ М17
 СЛИ М17(М13)
*
 СЧ 24(М17)
 ИЛИ ПРИП(М7)
 НТЖ ПРИП(М7)
 ЗП 24(М17)
*
ОТКЧБЛ НОП ,
 СЧ R2
 ПВ ИСКЛИС(М17)
 УИИ М2(М11)       В М2 ЗАПОМНИЛИ М11
 СЛИА ТБД-ТОП+1(М4)
 УИИ М11(М4)
 ПВ ССЫ(М17)
 УИИ М11(М2)       ВОССТАНОВИЛИ М11 - N ФИЗ.ЛИСТА
ГАШЕ5 НОП ,
 СЧ Е5
 ПВ ГАШЗАН(М15)
*
**********************************************
*                                            *
*               К О Н Т Р О Л Ь              *
*                                            *
**********************************************
*
 СЧ ОЧЕНЬ
 НТЖ СТАРОЕ
 ПЕ СТ415        НЕ СРАБОТАЛ СЕМОФОР  Е5(ЗАНЯТО)
*
**********************************************
*
 УИА 3
 УИА НАШФЛ(М17)    М17 *= НАШФЛ
ФОРКУС СЧ Е1
 СЛЦ ЧИП
 ЗП ЧИП
 СЧ ЧПОД
 СЛЦ Е1
 ЗП ЧПОД
 СЧ 0
 ЗП РЗ
ПЧ СЧ R5
 УИА 1027
 ЗП РМР
 СЧ R
 ИЛИ Е19
 ПВ ФИЗУР(М16)
 УИА 3
 НТЖ
 ПЕ (М17)     НЕТ ОШ
 СЧ R
 И Е18
 ПО ОШЗП      ОТКАЧКА
 СЧ 0
 МОДА (М17)
 ПИНО (М14)  ПОДКАЧКА ЛИСТА ПОЛЬ-ЛЯ
 ПБ A77634   ПЧ       ПОВТОР
ОШЗП НОП 0
* УИА 2
* СЧ ТОП(М11)
	сч      ОЧЕНЬ
 ПРЕ 3
 И ЕБУФЛ
 ПО     U77627 СВС: (М17)      ЛИСТ ПОЛЬ-ЛЯ
 ПБ A77634  СВС: ПЧ        ПОВТОР
ДЛ2 НЕД П77777        ФОРМИРОВАНИЕ ФИЗ. N ЛИСТА
 УИ М11
 СЧ Е48(М11)      ЗАНЯЛИ ЛИСТ
 ИЛИ ТЗФСТР(М1)
 ЗП ТЗФСТР(М1)
 УИА 3             СНТ. БЛПР
 СЧИ М1
 СДА 64-5
 УИ М1
 СЛИ М11(М1)
 СЧИ М11
 СДА 64-СДФЛИ
 ЗП R5
*
*   Н А Ш Л И    Ф И З.  Л И С Т
*
НАШФЛ НОП ,
 СЛИА 1(М11)        М11:= ССЫЛКА
 ПИНО ОТМАТ(М14)    ЗАКАЗ НА ПОДКАЧКУ МАТ. ЛИСТА
 СЧ ТУС-1(М6)
 УИ М13
 СЧ ЖДУ
 И Е25
 ПЕ ОТДБУФ        ЗАКАЗ ОТ ДБУФ
 СЧИ М11
 СЛЦ П37           -1
 И П37
 СДА 64-2
 ЗП (М13)          ТОЛЬКО N ЛИСТА 0 СЕГМ.БАЗ.СЕК.ОС
 СЧ ЖДУ
 ПЕ ЗКБУФЛ
* ЗАКАЗ ОТ ТБУФ ( СМ =0)
 СЧИ М12
 УИА 1026
 ЗП ТОП-1(М11)
 УИА 1027
 ПБ ДАЛИ
*ЗАКАЗ ОТ ТБУФ ( СМ = NЗАД,NЛИСТА)
ЗКБУФЛ НОП ,
 И П77
 СДА 64-СДВМЛ
 ИЛИ ЕОТКЛ
 ЗП РАБТОП        РАБТОП := ЕОТКЛ + N БУФ.ЛИСТА
 СЧ ЖДУ
 СДА 64+6
 УИ М1
 СЧ ТУСП-1(М1)
 УИ М13
 СЧ Е48
 ЗП NЛИСТА        NЛИСТА := Е48
 ПБ ПОИСК
* ЗАКАЗ ОТ ДБУФ
ОТДБУФ НОП ,
 СЧ ЖДУ
 И П37
 ЗП NЛИСТА
 СЛИА -1(М11)
 СЧИ М11
 СДА 64-36
 ИЛИ NЛИСТА
 ЗП (М13)         СМ :=  N ФИЗ.Л.(С 37Р.) + N МАТ.ЛИСТА
 ПБ КОНЗАК
* ЗАКАЗ НА ПОДКАЧКУ МАТ.ЛИСТА
ОТМАТ НОП ,
 СЧ ТУС-1(М6)
 СДА 64+15
 И П377
 УИИ М13(М6)
 ПО ОТМАТ1
 УИ М13
ОТМАТ1 НОП ,
 СЧ ТУС-1(М6)
 СДА 64+23
 И П37
 ЗП NЛИСТА
 СДА 64-СДВМЛ
 ЗП РАБТОП
 СЧИ М13
 СДА 64-ТОПНЗ
 ИЛИ РАБТОП
 ЗП РАБТОП        РАБТОП := NЗАД + N МАТ.ЛИСТА
 СЧ ТУС-1(М13)
 УИ М13
 СЧ NЛИСТА
 НТЖ П37
 УИ М7
  УИА 1027
 СЧ 32(М13)
 И Е48(М7)
 ПО МА            ЛИСТ УЖЕ ДАЛИ
 СЧ 22(М13)
 И Е48(М7)
 ПО МА             ЛИСТ УЖЕ ДАЛИ
 ПБ ПОИСК
ИСКБЛ  уии     М13(М2)
        пв      ИСКМЛ(М17)     
        уиа     '2003'
        по      СТ414
        счи     М11
        и       П37
        уи      М1
        счи     М11
        сда     64+5
        уи      М5
        сч      ТЗФСТР(М5)      
        нтж     E48(М1)
        зп      ТЗФСТР(М5)      
        пб      (М16)
*
*
* ПОД-МА ВКЛЮЧЕНИЯ ЛИСТА В ЦЕПОЧКУ ЛИСТОВ ЗАДАЧИ
*
*
* ВХОД : М11 -   НОМЕР СТРОКИ ТОП(ТБД) + 1
*        М13 -   АДРЕС  И П З  ЗАДАЧИ
* РАБОЧИЕ МОДИФИКАТОРЫ : М4
*
ССЫ СЧ ШЗНМБ+ЯЧТОП(М13)
 СДА 64+16
 И П17777
 ПЕ ССЫ2
 СЧИ М11
 СДА 64-ССТБД
 ИЛИ ШЗНМБ+ЯЧТОП(М13)
 ЗП ШЗНМБ+ЯЧТОП(М13)
ССЫ1 СЧ ШЗНМБ+Л(М13)
 И О46П31
 ЗП ШЗНМБ+Л(М13)
 СЧИ М11
 СДА 64-30
 ИЛИ ШЗНМБ+Л(М13)
 ЗП ШЗНМБ+Л(М13)
 ПБ (М17)
ССЫ2 СЧ ШЗНМБ+Л(М13)
 СДА 64+30
 И П77777
 УИ М4
 СЧИ М11
 ПРЕ 1026
 ИЛИ ТОП-1(М4)
 ЗП ТОП-1(М4)
 ПРЕ 1027
 ПБ ССЫ1
*
*
*
* ПОД-МА ПОИСКА И ИСКЛЮЧЕНИЯ ЛИСТА ИЗ ЦЕПОЧКИ
*
* ВХОД  : ПЕРЕД УХОДОМ НА ОТЛИС Е5(ЗАНЯТО)
*         ДОЛЖНА БЫТЬ  З А Х В А Ч Е Н А ! ! !
*        R2 - НОМЕР ЛИСТА ( С 21 Р.)
*        М13 - АДРЕС  И П З   ЗАДАЧИ
* ВЫХОД :
*         СМ = 0  - ЛИСТ НЕ НАШЛИ
*         СМ = 1  - ЛИСТ НАШЛИ И ИСКЛЮЧИЛИ ИЗ ЦЕПОЧКИ
* РАБОЧИЕ МОДИФИКАТОРЫ :  М1 , М5
*
ИСКЛИС НОП ,
 УИА 2
 И МИН СВС: ВИОЛПМ
 ЗП РАБ1М
 СЧ МИН СВС: ВИОЛПМ        41-35,32-21 Р.Р.
 ЗП РАБ2М
 ПБ ИСКЛИ1
ИСКМЛ НОП ,
 УИА 2
 ЗП РАБ1М
 СЧ МИН+1 СВС: ВИОЛМ         41-35РР
 ЗП РАБ2М
 ПБ ИСКЛИ1
ИСКВЛ НОП ,
 УИА 2
 ЗП РАБ1М         ИСКОМЫЙ ВИР.ЛИСТ
 СЧ КВВЛМ         МАСКА ПОИСКА (Е40,32-21)
 ЗП РАБ2М
ИСКЛИ1 НОП ,
 УИА 3
 СЧ ШЗНМБ+ЯЧТОП(М13)
 СДА 64+16
 УИА 0(М5)
 УИА 2
ИСКЛИ2 И ШАРСИС (был ГЕНС.М17777)
 ПО НЕТЕГО
 УИ М1
 СЧ ТОП-1(М1)
 НТЖ РАБ1М
 И РАБ2М
 ПО НАЙДЕН
 СЧ ТОП-1(М1)
 УИИ М5(М1)
 ПБ ИСКЛИ2
НАЙДЕН НОП 0
 ПРЕ 1026
 СЧ ТОП-1(М1)
 ПРЕ 1027
 И П77777
 ПИО ОП4(М5)
 ЗП СМ
 ПРЕ 1026
 СЧ ТОП-1(М5)
 ПРЕ 1027
 И КОК
 ИЛИ СМ
 ПРЕ 1026
 ЗП ТОП-1(М5)
 ПРЕ 1027
 ПБ ОП7
НЕТЕГО УИА 3
 ПБ (М17)
ОП4 СДА 64-16
 ЗП СМ
 СЧ ШЗНМБ+ЯЧТОП(М13)
 И О29П17
 ИЛИ СМ
 ЗП ШЗНМБ+ЯЧТОП(М13)
ОП7 НОП ,
 УИА 1026
 СЧ ТОП-1(М1)
 ЗП РАБ2М
 И КВСЧКМ
 ЗП ТОП-1(М1)     ОСТАВИЛИ ТОЛЬКО СЧЕТЧИК ЗАПИСЕЙ
 СЧ РАБ2М
 И КВВЛМ
 ЗП РАБ1М         РАБ1М := N ВИРТУАЛЬНОГО ЛИСТА
 СЧ РАБ2М
 НТЖ РАБ2М
 ЗП РАБ2М         РАБ2М := 0
 УИА 1027
 СЧМР ,
 ЗП СТАРОЕ        СТАРОЕ := <ТОП>
 СЧ СМ
 ПЕ ГГ
 СЧ ШЗНМБ+Л(М13)
 И О46П31
 ЗП ШЗНМБ+Л(М13)  КОН СС НА ТОП
 СЧИ М5
 СДА 64-30
 ИЛИ ШЗНМБ+Л(М13)
 ЗП ШЗНМБ+Л(М13)
 ПБ ГГ
*
* ПОД-МА ИСКЛЮЧЕНИЯ ТРАКТА ИЗ ЦЕПОЧКИ
*
* ВХОД : М5 - НОМЕР ПРЕДЫДУЩЕГО КВАНТА
*        М1 - НОМЕР ИСКЛЮЧАЕМОГО КВАНТА
*        М13- АДРЕС  И П З
СИ2 НОП 0
 УИА 1026
 СЧ ТБД-1(М1)
 ПРЕ 1027
 И П77777
 ПИО СИ4(М5)
 ЗП СМ
 УИА 1026
 СЧ ТБД-1(М5)
 ПРЕ 1027
 И КОК
 ИЛИ СМ
 УИА 1026
 ЗП ТБД-1(М5)
 ПРЕ 1027
 ПБ СИ7
СИ4 СДА 64-ССТБД
 ЗП СМ
 СЧ ШЗНМБ(М13)
 И ОССТБД
 ИЛИ СМ
 ЗП ШЗНМБ(М13)
СИ7 СЧ ВСЕЕД
 НТЖ КДД
 УИА 1026
 И ТБД-1(М1)
 ЗП ТБД-1(М1)
 ПРЕ 1027
 СЧ СМ
 ПЕ ГГ
 СЧ ШЗНМБ+1(М13)
 И ОССТБД
 ЗП ШЗНМБ+1(М13)
 ПИО ГГ(М5)
 СЧИ М5
 СДА 64-ССТБД
 ИЛИ ШЗНМБ+1(М13)
 ЗП ШЗНМБ+1(М13)
ГГ СЧ Е1
 ПБ (М17)
*
*
ПОИСК НОП ,             ПОИСК ТРЕБУЕМОГО ЛИСТА НА МБ
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 СЧ РАБТОП
 ПВ ИСКМЛ(М17)
 ЗП R
 ПВ ССЫ(М17)
 СЧ R
 ПО НЕНАМБ        ЛИСТ НЕ НАШЛИ НА МБ
 СЧ РАБТОП
 УИА 1026
 ИЛИ РАБ1М         ВИРТ.ЛИСТ (ЗАПИСАЛ ’ИСКЛИС’)
 ЗП ТОП-1(М11)
 УИА 3
 СЧИ М1
 ВЧОБ АКТОП
 ПО СТ416         ИСПОРЧИНА ИНФОР.О ЛИСТАХ ЗАДАЧИ
 СЛИА ТОП-ТБД+255(М1)
 СЧИ М1
 И П37
 УИ М3
 СЧИ М1
 СДА 64+5
 УИ М1
 СЧ ШОПТИМ
 И Е48-1(М12)
 ПО ЖАЛЬ
 ПАМ 0
 НТЖ ШОПТИМ        БЛПР  НЕ ТРЕБУЕТСЯ
 ЗП ШОПТИМ
 СЧ ИНФСЛП(М13)
 СДА 64+39
 И П7
 НТЖ Е1
 ПЕ ЖАЛЬ
 СЧ ИНФСЛП(М13)
 И Е37Е36
 ПЕ ЖАЛЬ
 СЧ ИНФСЛП(М13)
 И Е48
 ПО НЕТЧТ
ЖАЛЬ НОП ,
* СЧ Е1
* МОДА КОЛПОД-1
* СЛЦ (М12)
* МОДА КОЛПОД-1
* ЗП (М12)
 ПВ ФОРНУ(М17)
 ИЛИ Е18
 ЗП R
 ПВ ФОРКУС(М17)
 НТЖ
 ПО ТРАКТ         ОТБРАКОВАЛСЯ ТРАКТ
НЕТЧТ1 НОП
 УИА ’2003’
 СЧ ШЗНМБО-8(М1)
 ИЛИ Е48(М3)
 ЗП ШЗНМБО-8(М1)  СВОБ. ТРАКТ В СИСТЕМУ
НАШ СЧ Е1
 УИА ’2003’
 СЛЦ ТМБ
 ЗП ТМБ
ПОЧВСЕ НОП ,             ПОЧТИ ВСЕ СДЕЛАЛИ ПО ЭТОМУ ЗАКАЗУ
 СЧ Е5
 ПВ ГАШЗАН(М15)
КОНЗАК НОП ,
 СЧ NЛИСТА
 РЖА ’13’
 ПО ДАЛИ         ЗАКАЗ НА БУФ.ЛИСТ ОТ ТБУФ
 УИ М16
 НТЖ П37
 УИ М7
 СЧ 32(М13)
 ИЛИ Е48(М7)
 НТЖ Е48(М7)
 ЗП 32(М13)
 СЧ 22(М13)
 ИЛИ Е48(М7)
 НТЖ Е48(М7)
 ЗП 22(М13)
*
 СЧИ М16
 И Е2Е1
 УИ М7
 СЧИ М16
 СДА 64+2
 НТЖ Е3П1
 УИ М4
 СЛИ М4(М13)
*
 СЧ 24(М4)
 ИЛИ ПРИП(М7)
 НТЖ ПРИП(М7)
 ЗП 24(М4)
 СЧ R5
 СДА 64-2
 РЗБ ПРИП(М7)
 ИЛИ 24(М4)
 ЗП 24(М4)
ДАЛИ НОП ,
 СЧ ТУС-1(М6)
 И КОНСТ
 ЗП ТУС-1(М6)
 СЧ Е48-1(М12)
 УИА ОТКР(М15)
 ПИО ВШГ(М10)
 ПВ ВШГП(М15)
ОТКР НОП 0
*
*
*      З А К А З   О Б С Л У Ж И Л И
*
*
 СЧ КЛИЕНТ(М10)
 ИЛИ ПОДНОС(М10)
 НТЖ Е48-1(М12)
 ЗП ПОДНОС(М10)
 ЗП КЛИЕНТ(М10)
 СЧ 0
 ЗП СЧЕ
 СЧ ЗАК
 НТЖ Е48-1(М12)
 ПЕ К1
 СЧ КЛИЕНТ(М10)
 ПЕ КАЧА
НЕТ ПИО ВСИ(М10)
 СЧ ПОДНОС
 ИЛИ ПОДП
 ПЕ КАЧК
 СЧ ШКЗЗК
 ПВ ВШГП(М15)
ПОВ1 УИА КАЧКА(М7)
 ПВ ЗАНОСС(М16)   ВРЕМЯ
 СЧ ИВ25
 ЗП КАЧКА+5
 УИА КАЧКА1(М16)
 СЧИ М16
 ЗП КАЧКА+3
 УИА ВИСП16(М12)
*
* СЧ НАЛЕВО
* ИЛИ Е2
* НТЖ Е2
* ЗП НАЛЕВО
* СЧ КЛИЕНТ
* ПО ВБФ           СИСТЕМНЫЕ ЗАКАЗЫ ВСЕ ВЫПОЛНИЛИ
* СЧ НАЛЕВО
* ИЛИ Е2
* ЗП НАЛЕВО        НАЛЕВО := * + Е2(НЕТ МЕСТА ДЛЯ ОС)
*
ВБФ СЧ ШКСВН
 И Е31
 ПО (М12)
	сч	ВРЕМЯ
	вчаб	БУДГДР
	вчаб	E10
	пе	(М12) 
 УИА -1(М14)
ОТКГДР СЧ ОПТИМ+1(М14)
 И П77777
 ПО ОТКГ
 НТЖ ОПТИМ+1(М14)
 ЗП ОПТИМ+1(М14)
 СЧМР
 СДА 64-2
 ИЛИ Е19
 УИА 1(М7)
 ПВ ТБУФ(М15)
ОТКГ ЦИКЛ ОТКГДР(М14)
 ПБ (М12)
*
ХВАТИТ НОП ,
 СЧ ПОДНОС(М10)
 ЗП КЛИЕНТ(М10)
 СЧ 0
 ЗП ПОДНОС(М10)
 ПИНО ПОВ1(М10)
ВСИ УИА 1(М10)
 ПБ КАЧА
НЕТЧТ СЧ ИНФСЛП(М13)
 СДА 64-30
 СЛЦ Е46
 ПО НЕТЧТ1
 СЧ ИНФСЛП(М13)
 СБР НБТ
 СДА 64+38-ТБДТР
 ЗП R
 СЧ ШЗНМБ(М13)
 СДА 64+ССТБД
ИИИ И П77777
 ПО ЖАЛЬ
 УИ М16
 УИА 2
 СЧ ТБД-1(М16)
 ПРЕ 3
 НТЖ R
 И КДВ
 ПО НЕТЧТ1
 УИА 2
 СЧ ТБД-1(М16)
 ПРЕ 3
 ПБ ИИИ
АВДРУГ СЧ ЛИСТМБ(М15)
 И Е48(М17)
 ПО АВДР1
 СЧ ЛИСОСТ(М15)
 И Е13
 ПО АВДР1
 СЧ ГУС
 И Е48
 ПЕ АВ1
 МОД И17
 УИА (М17)
 МОД И15
 УИА (М15)
 МОД ГУС
 СЧ 23
 СДА 64+32
* ПО СТ422         НЕТ АДРЕСА ДЛЯ ВОЗВР.ПРИ Ч.Л.
 УИА 6(М16)
 ПБ  ПБСМ
АВ1 СЧИ М27
 И Е3
 ПО ТВ205
 СЧИ ’32’
 УИ ’33’
 ПБ АНВО8
АВДР1 СЧ ПОДНОС
 ИЛИ ПОДП
 ПЕ АВДРНЛ
 СЧ Е38
 ПВ ВШГ(М15)
АВДР СЧ ГУС
 УИА 1(М15)
 И Е48
 ПЕ АВДРП
 УИА (М15)
АВДРП МОД НЗАД
 СЧ Е48-1
 ИЛИ ПОДНОС(М15)
 ЗП ПОДНОС(М15)
АВДРОТ СЧ Е14
 ПБ ЗАКЛИ
АВДРНЛ НОП 0
 СЧ ГУС
 И Е48
 ПО АВДР СЛУЖ ЗАД
 МОД НЗАД
 СЧ Е48-1
 ИЛИ ШКЗЗК ЗАДАЧУ ОТЛОЖИЛИ
 ЗП ШКЗЗК
 ПБ АВДРОТ
* ПОД-МА ПОИСКА ЛИСТА ЗАДАЧИ В ОЗУ
*
ПЛВОЗУ СЧ СМ
	зп      СМ2
 И П77   ВРЕМ ЛИСТ 6 РАЗР
 СДА 64-СДВМЛ
 ИЛИ ЕОТКЛ
 ЗП РМР2
 УИИ М17(М10)
 ПВ УПРИ(М10)
	уиа     3 
 УИИ М10(М17)
 СЧ СМ2
 СДА 64+6
 УИ М17
 МОД ТУСП-1(М17)
 СЧ ШЗНМБ+ЯЧТОП
 СДА 64+16
СЛЛИС И П17777
* ПО ТБУФ1
 УИ М16
	пио     ТБУФ1(M16)
        уиа     2
* ПРЕ 1026
 СЧ ТОП-1(М16)
* ПРЕ 1027
* УИ М17
* ЗП И15
	уиа	'3'
	зп	И152
 И Е48П42
 ПО ЗАНЛИ
 СЧ И152
 НТЖ РМР2
* УИА 1026
 И ВИОЛМ
* УИА 1027
 ПО ВОТЛИС
ЗАНЛИ СЧ И152 М17
 ПБ СЛЛИС
ВОТЛИС СЛИА -1(М16)
 СЧИ М16
 ВЧ АКТОП
 ПО ТБУФ1  НА МБ
 СЧ Е41П1
 ПРЕ 1026
 И ТОП(М16)
 ЗП ТОП(М16)
 ПРЕ 1027
        уиа     -6(М17)
A15703  зп      3
        цикл    A15703(М17)     
        сч      
        зп      СМ2
 СЧИ М16
 И П37           ВЫДЕЛ.N ЛИСТА В 0-ОМ СЕГМ. БАЗОВОЙ СЕК.ОС
 СДА 64-2
 ПБ (М15)
ТБУФ1   сч      СМ2
        уиа     '2003'
        зп      СМ
        сч      
        зп      СМ2
        пб      '4532'
*
* ПОД-МА ФОРМИРОВАНИЯ КУС-А   М Б
*
* ВХОД :  М1 - НУ  МБ
*         М3 - НОМЕР ТРАКТА
* ВЫХОД : R  - КУС МБ
*
ФОРНУ НОП ,
 СЧИ М3
 СДА 64-2
 ЗП R
 СЧИ М1
 СДА 64-42
 РЗБ НАПРМГ
 ИЛИ ЕМБФ
 ИЛИ R
 ЗП R
 ПБ (М17)
*
ЗАПВЫХ СДА 64-32
* УИА 1027
* ЗП СМ
 МОД ГУС
 нтж 23 СВС: СЧ 23
 И Е47П33 СВС: О47П33
* ИЛИ СМ
  мод ГУС
  нтж 23
 МОД ГУС
 ЗП 23
 УИА 3
 ПБ (М15)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    ОБРАЗОВАНИЕ НОВОГО ЛИСТА
*
НЕНАМБ НОП ,
 ПИНО ОБРНЛМ(М14)
* ОБРАЗОВАНИЕ НОВОГО БУФ.ЛИСТА
 СЛИА -1(М11)
 СЧИ М11
 СДА 64-10
 УИ М1
 СЧ 0
 ЗП (М1)
 СЧ РАБТОП
 УИА 2
 ЗП ТОП(М11)
 УИА 3
 ПБ ПОЧВСЕ
*
* ОБРАЗОВАНИЕ НОВОГО ЛИСТА МАТЕМАТИКА
*
ОБРНЛМ НОП ,
 СЧ ТУС-1(М6)
 СДА 64+15
 И П377
 УИИ М2(М6)
 ПО ОТМАТ2
 УИ М2
ОТМАТ2 НОП ,
 СЧ ТУС-1(М2)
 УИ М13
 СЧ NЛИСТА
 ЗП R             R:=ВИРТУАЛЬНЫЙ НОМЕР ЛИСТА
 НТЖ П37
 УИ М7
 СЧ ШКЭВЛ
 И Е48-65(М2)    Е48-1-64(М2)
* ПО НЕЭ601
  пе	A15772
  сч	ЕЗАНМД(М13)
  и	Е48(М7)
  по    ЗАПТОП
  нтж	ЕЗАНМД(М13)
  зп	ЕЗАНМД(М13)
* ОБРАЗОВАНИЕ ЛИСТА ДЛЯ ЭК-КОДА  057 0601
* НТЖ ШКЭВЛ
* ЗП ШКЭВЛ
* СЧ УПОТ(М13)
* ПБ ЗАПТ
НЕЭ601 НОП ,
* СЧ МОДЛИ(М13)
* И Е48(М7)
* ПО НЕМОД
* СЧ МОДЛИ(М13)
 СДА 64-5
 ИЛИ R <-- НЕМОД
* ПО ЗАПТОП        НУЛЕВОЙ ЛИСТ НЕЛЬЗЯ ПРОВЕРЯТЬ
ЗАПТ И П7777
 ЗП R
* СДА 64-ТБДТР
* ПВ ИСКВЛ(М17)
* ПЕ СТ440
ЗАПТОП НОП ,
 СЧИ М2
 СДА 64-ТОПНЗ+СДВМЛ
 ИЛИ NЛИСТА
 СДА 64-СДВМЛ+ТБДТР
 ИЛИ R
 СДА 64-ТБДТР
 УИА 2
 ЗП ТОП-1(М11)
 УИА 1027
 СЧ ЛИСТМБ(М13)
 НТЖ Е48(М7)
 ЗП ЛИСТМБ(М13)
 СЧ ЯЧНИА(М13)
 И ЕНИИАС
	уиа	-7(М16)
 ПЕ НИ
 СЧ ЛИСОСТ(М13)
 СЛЦ Е1
 ЗП ЛИСОСТ(М13)
НИ НОП 0
	зп 1
        цикл *(M16)
* ДК ’36’,ВЫТБРЗ
ВЫТБРЗ НОП ,
 СЧ R5
 СДА 64-2
 РЗБ ПРИП
 ЗП КАЧКА+24
 РЕГ ’27’          ПРИПИСКА ДЛЯ 34-ОГО ЛИСТА МАТЕМАТИКА
 СЧ ШЗНМБ+Л(М13)
 СДА 64+28
 И П3
 УИ М16
 СЧ РОСППМ(М16)
 УИА ’70000’(М17)
 УИА 2             БЛЗ
 УИА -1023(М16)
Н2 ЗП (М17)
 ЦИКЛ Н2(М16)
 УИА 3
 ПБ ПОЧВСЕ
A15772  нтж     ШКЭВЛ
        зп      ШКЭВЛ        
        сч      '45'(М13)    
        пб      ЗАПТ
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ТРАКТ УИА 1027
 УИА КАЧКА(М7)
 УИА НАШ(М15)
 СЧ ЯЧЛИС(М13)
 СЛЦ О42 СВС: Е48П2
 ЗП ЯЧЛИС(М13)
ВЫЗКЗ СЧ ЕОТКЗ
 ИЛИ ЯЧОТБР(М13)
 ЗП ЯЧОТБР(М13)
 СЧ ЕОТБ
 ИЛИ ЯЧОТБ(М13)
 ЗП ЯЧОТБ(М13)
 УИА АВРАСП(М1)
 СЧИ М1
 ЗП 3(М13)
 СЧ ИВ25
 НТЖ Е19
 ЗП 5(М13)
.ВЫЗКЗ АNОР
 СЧ 34(М7)
 ЗП 34(М13)
 ПБ (М15)
СИТ СЧ ШЗНМБ+1(М13)
 СДА 64+ССТБД
 И П77777
 УИ М4
 СЧИ М17
 ПРЕ 1026 СВС: 1024
 ИЛИ ТБД-1(М4)
 ЗП ТБД-1(М4)
 ПРЕ 1027
 ПБ СИТ10
ОФТО3 НЕД Е18П1
 УИ М3
 УИ М4
 УИИ М1(М17)
 СЧИ М1
 СДА 64-5
 УИ М5
 СЛИ М4(М5)
 СЛИА 8(М1)
 ПРЕ 1026
 СЧ ТБД(М4)
 ПРЕ 1027
 ПБ (М7)
МА НОП ,
 СЛИА -1(М11)
 СЧИ М11
 И П37
 УИ М2
 СЧИ М11
 СДА 64+5
 УИ М1
 СЧ Е48(М2)
 НТЖ ТЗФСТР(М1)
 ЗП ТЗФСТР(М1)
 СЧ ВСЕЕД
 НТЖ Е48-1(М12)
 И ШОПТИМ
 ЗП ШОПТИМ
 ПБ ДАЛИ
ДИСП70 ВНЕШ Е40П1,РЭ1,ЗАПВЕТ,РЭ2,ТИВ
ЭКВЛ НОП ,
        сч      E1
        зп      И16
КИПР УИА (М16)          Э57’600’, Э57’601’
 ЗП И17           ИНФСЛП = М15,М11,М12
 СЧИ М15           УПОТ := М17,М13,М14
ЗПИР УИА -1(М15)
 РЖА 7
ЦИР СДА 64-15
 ЗП И15
 МОДА (М15)
 СЧИ ’12’(М16)
 ИЛИ И15
 ЦИКЛ ЦИР(М15)
A16042  ПИНО ПС(М16)
 СДА 64-3
 ИЛИ И17
 МОД ГУС
  УИА 0(М16)
  ЗП ИНФСЛП(М16)
 СДА 64+33
 УИ М15
        сч      '26'(М16)
        нтж     ВСЕЕД
        и       ЛИСТМБ(М16)
        и       КОНСТ+1
        пе      ОШИ
        сч      ЛИСТМБ(М16)
        или     П17777
        и       E42(М16)
        зп      E42(М16)
        и       КОНСТ+1
        пе      A16054
        зп      E42(М16)
A16054  СЧ СМ
 ЗП ИНФ(М16)
 СЧ И16
 УИА 3             ВПЕРВЫЕ РАЗРЕШЕНЫ ПРЕРЫВ.
 И П7
 ПО РП600
 НТЖ Е1
 ПЕ ОШИ
 СЧ ИНФ(М16)
 УИА 1(М16)
 ПБ ЕП6
РП600 СЧ ИНФ(М16)
 И П177
 ПО МОД0          ПРИ ПОДКЛ. 0-Й МОДИФ
 НТЖ МОДЛИ(М16)
 И П177
 ПО МОД177
 СЧ МОДЛИ(М16)
 ПО ФМО
 НТЖ ИНФ(М16)
 И ЛИСТМБ(М16)
 И Е48П17
 ПО ФМО             МОДЛИ ОБНОВИТСЯ
 И МОДЛИ(М16)
 ЗП УПОТ(М16)
ЗАПРЛ МОД ГУС           ЗАПРОС ЛИСТОВ НЕ ПЕРЕКЛ. И НЕОБРАЗОВ.
 СЧ УПОТ
 ПО ДСС1 СВС: ФМО
 НЕД П77777
 УИ М16
 СЧ Е48(М16)
 МОД ГУС
 НТЖ УПОТ
 МОД ГУС
 ЗП УПОТ
 СЧИ М16
 НТЖ П37
 СДА 64-10
 УИ М16
 УИА 0
 СЧ ’20’(М16)
 УИА 3
 ПБ ЗАПРЛ        
ФМО СЧ ИНФ(М16)
 ПБ ДСС1
МОД177 СЧ ИНФ(М16)
 И Е48П17
 ИЛИ МОДЛИ(М16)
 ПБ ДСС1
МОД0 СЧ МОДЛИ(М16)
 И Е48П17
 ПО ДСС1
 И ИНФ(М16)
 НТЖ МОДЛИ(М16)
ДСС1 НОП 0
        мод     ГУС
        уиа     0(М16)
        зп      E42(М16)
.КОБГР АNОР
* ЕСТЬЛ
 СЧ ИНФ(М16)
 СДА 64-5
 УИА 0(М16)
ЕП6 И П7777
 УИА 1027
 УИИ М12(М16)
 УИ М11
* УИА 2(М16)
* СЧИ М17
* ПБ ЗПИР
*
*ПС МОД ГУС
* УИА (М13)
* ЗП УПОТ(М13)
        сч      ШЗАПОС
        мод     НЗАД
        или     МДР+1
        зп      ШЗАПОС
        счи     М17
        мод     ГУС
        зп      АТ-3
 СЧ Е5
 ПВ ЗАХЗАН(М15)
        уиа     2(М16)
* СЧ ШЗАПОС
* МОД НЗАД
* ИЛИ Е48-1
* ЗП ШЗАПОС
* УИА 3
* СЧИ М11
* ЗП РАБ600         ТО, ЧТО ИЩЕМ
* УИИ М11(М13)
        мод     ГУС
        сч      АТ-3
        пб      ЗПИР
ПС      мод     ГУС
        зп      АТ-3
        счи     М11
        зп      КВЭ600+2
        уиа     -2(М14) LB: почему так мало для выт
        мод     ГУС
        уиа     0(М11)
 СЧ ИНФ(М11)
 ЗП R2
 СЧ ЛИСТМБ(М11)
 НТЖ ВСЕЕД
 И R2
 ЗП R3    СОХРАНЕНИЕ ПРИПИСКИ ПОЛЬЗОВАТЕЛЯ
 СЧ КАЧКА+31
* ДК ’36’,ВЫТ
ВЫТ НОП
        зп      2
        цикл    ВЫТ(М14)
 УИА 1027
 РЕГ 16
 УИ 0
 СЧ РАБ600
 СДА 64-ТБДТР
 УИА 1026
 ЗП РАБ2М
 УИА 1027
 СЧ Е1
 ЗП РАБ600           ФЛАГ ОШИБКИ
 СЧ КВЭ600(М12)
 УИА 1026
 ЗП РАБ1М
 УИА 3
ВТОЗАХ НОП 0
 СЧ ШЗНМБ+ЯЧТОП(М11)
 СДА 64+16
 И П17777
 УИ М16
ПЛВ0 УИА 3
ПЛВ ПИО КОНЭВЛ(М16)
 УИА 1027
 СЧ КАЧКА+31
 РЕГ 16
 УИ 0
 УИА 1026
 УИИ М17(М16)
 УИА 1(М14)
 СЧ ТОП-1(М16)
 И ЕОТКЛМ
 ПО БЗЗ
ПЛВ1 СЧ ТОП-1(М16)
 УИ М16
 НТЖ РАБ2М
 И РАБ1М
 УИА 3
 УИ 0
 ПЕ ПЛВ
 УИА (М14)
*
БЗЗ УИА ’2003’
 СЧ КАЧКА+31
 РЕГ 16
 УИ 0
 СЧИ М16
 ЗП И16
 УИА ’2002’
 СЧ ТОП-1(М17)
 УИА ’2003’
 ЗП И20         НАЙДЕНО ЧТО-ТО
 ПИНО МС602(М12)    601,175
*
 ПИНО ОБС(М14)
 СДА 64+ТБДТР
 И П37
 ЗП РМР          НОМЕР МАТ.ЛИСТА В ВИРТ. ЛИСТЕ
 НТЖ П37
 УИ М13
 СЧ Е48(М13)
 И R2            ИМЕЕТСЯ ЛИ В ШКАЛЕ
 ПО ПЛВ0
КЛИПС УИА ЛУЦК(М16)
 ПБ КУЗЯ
ОБС НОП 0
 СДА 64+СДВМЛ
 И П37
 ЗП РМР
 НТЖ П37
 УИ М13
 СЧ R2
 И Е48(М13)
 ПО ПСС600
 УИА 1(М15)
 И ИНФ(М11)
 ПО УЖЕПОД         - НА ЭТО МЕСТО УЖЕ ПОДКЛ.ЛИСТ
 ПВ КУЗЯ(М16)
 ПО ОТКАЧН
 УИИ М16(М17)
УЖЕПОД УИА ’2002’
 СЧ ТОП-1(М17)
 ПИНО НЕОБММ(М15)
 ИЛИ ЕОБММ
НЕОБММ ИЛИ ЕОТКЛМ
 ЗП ТОП-1(М17)
 УИА 1027
 СЧ R3
 И Е48(М13)
 НТЖ R3
 ЗП R3
 ИЛИ ИНФ(М11)
 И Е48П17
 ПО КОНЭВЛ        РАБОТА ЗАКОНЧЕНА
ПСС600 УИА 1026
 ПБ ПЛВ1
*
МС602 СЧ ИНФ(М11)
 СДА 64+12
 И П37
 ЗП РМР
 НТЖ П37
 УИ М13
 ПИО КЛИПС(М14)
 СЧ И20
 СДА 64+СДВМЛ
 И П77
 НТЖ РМР
 ПЕ ПСС600        НЕ ОТКЛЮЧАЕМЫЙ ЛИСТ
* НАШЛИ ЗАДАННЫЙ РЕГИСТР ПРИПИСКИ
 УИА ’2002’
 СЧ ТОП-1(М17)
 НТЖ РАБ2М
 И РАБ1М
 ПО КОНЕЦЭ        ВЛ УЖЕ ТАМ, ГДЕ НАДО
 УИА ’2003’
 СЧ ИНФСЛП(М11)
 УИА 1(М15)
 И Е2
 ЗП И17
 ПЕ СОNТ      - ПОДКЛЮЧЕНИЕ УЖЕ ПРОИДЕНО
 ПВ КУЗЯ(М16)        - ОСВОБОДИМ МЕСТО
СОNТ ПО ОТКАЧН
 СЧ ИНФСЛП(М11)
 ИЛИ Е2
 ЗП ИНФСЛП(М11)
 И Е1
 УИ М14
 СЧ И17        М14=1 - Э50 ’175’
 УИ М16
 СЛИА -2(М16)          - М16=0 - ОБХОД ЗАКОНЧИТЬ
 УИА ’2002’
 СЧ ТОП-1(М17)
 ПИО ИР160(М16)
 УИ М16
ИР160 ИЛИ ЕОТКЛМ
 ПИНО НЕВОБМ(М15)
 ИЛИ ЕОБММ        - М15=0 - В ОБМЕНЕ
НЕВОБМ ЗП ТОП-1(М17)        ОТКЛЮЧИЛИ ЛИСТ ОТ ЗАДАЧИ
 ПИО ПЛВ0(М14)     Э50’601’
 И РАБ1М
 НТЖ ТОП-1(М17)
 ИЛИ РАБ2М
 ЗП ТОП-1(М17)    ИЗМЕНИЛИ N ВИРТ.ЛИСТА
 ПБ ПЛВ0
*ОТКЛЮЧАЕМ ЛИСТ ПО ТЕКУЩЕМУ МАТ.НОМЕРУ
КУЗЯ СЧ РМР
 И П3
 УИ М14
 СЧ РМР
 СДА 64+2
 НТЖ П7
 УИ М15
 СЛИ М15(М11)
 СЧ Е48(М13)
 ЗП Е48(М13)
 И ЛИСТМБ(М11)
 ПЕ (М16)         НЕТ ЛИСТА У ЗАДАЧИ
 СЧ Е48(М13)
 И ’26’(М11)
 ПЕ ОТКАЧН        ЛИСТ ОТКАЧАН НА МБ
 СЧ Е48(М13)
 И ’40’(М11)
 ПЕ ЛИОБМ         ЛИСТ В ОБМЕНЕ
 СЧ ’30’(М15)
 ИЛИ ПРИП(М14)
 НТЖ ПРИП(М14)
 ЗП ’30’(М15)
ОТКАЧН СЧ Е48(М13)
 ИЛИ ’40’(М11)
 ЗП ’40’(М11)
 СЧ Е48(М13)
 ИЛИ ’26’(М11)
 ЗП ’26’(М11)
 СЧ Е48(М13)
 ИЛИ ЛИСТМБ(М11)
 ЗП ЛИСТМБ(М11)
 ПБ (М16)
ЛИОБМ УИА (М15)
 СЧ 0
 ПБ (М16)
* ПОДКЛЮЧЕНИЕ ЛИСТА
ЛУЦК НОП
 МОД И16
 УИА (М16)
 СЧ И20
 ПИНО СОХВХ(М15)    МОЖНО ПОДКЛЮЧАТЬ
 СЧ ИНФСЛП(М11)   ЗАКАЗ НА ПОВТОРНЫЙ ПРОХОД
 ИЛИ Е3
 ЗП ИНФСЛП(М11)
 ПБ ПЛВ0          ПОДКЛЮЧИТЬ НЕЛЬЗЯ, МЕСТО В ОБМЕНЕ
СОХВХ НОП
 И ЕОТКЛ
 ПЕ НЕУЗАД
 ЗП РАБ600
 ПБ НЕ175         АВОСТ: ПОДКЛЮЧ. ЛИСТ У ЗАДАЧИ
НЕУЗАД НОП
 СЧ Е48(М13)
 НТЖ ЛИСТМБ(М11)
 ЗП ЛИСТМБ(М11)
 СЧИ М17
 ВЧ Е1
 ВЧ АКТОП
 ПО ДС8          ЛИСТ НА МБ
 СЧ Е48(М13)
 НТЖ ’26’(М11)
 ЗП ’26’(М11)
 СЧ И20
 И ЕОБМ           ПОДКЛЮЧАЕМЫЙ ЛИСТ В ОБМЕНЕ
 ПЕ ДС8
 СЧИ М17
 СЛЦ П77777
 СДА 64+10
 СЧМР
 РЗБ ПРИП(М14)
 ИЛИ ’30’(М15)
 ЗП ’30’(М15)
 СЧ Е48(М13)
 НТЖ ’40’(М11)
 ЗП ’40’(М11)
ДС8 НОП
 СЧ РМР
 СДА 64-СДВМЛ
 УИА ’2002’
 НТЖ ТОП-1(М17)
 И МИН+1 СВС: ВИОЛМ
 НТЖ ТОП-1(М17)
 ЗП ТОП-1(М17)
 УИА ’2003’
 СЧ ИНФСЛП(М11)
 И Е1
 ПО НЕ175
 СЧ РМР
 СДА 64-ТБДТР
 УИА ’2002’
 НТЖ ТОП-1(М17)
 И РАБ1М
 НТЖ ТОП-1(М17)
 ЗП ТОП-1(М17)
НЕ175 ПИНО КОНЭВЛ(М12)   ПОПЫТКА ОКОНЧИТЬ ЭК
 УИА 1027 СВС: 3
 СЧ Е48(М13)
 НТЖ ИНФ(М11)
 ЗП ИНФ(М11)      ШКАЛА ПРОЙДЕННЫХ ЛИСТОВ
 ИЛИ R3
 И Е48П17
 ПЕ ПЛВ0          ЕЩЕ НАДО ИСКАТЬ
*
КОНЭВЛ УИА 3
 СЧ ИНФСЛП(М11)
 И Е3
 ПО НЕТОБМ
 НТЖ ИНФСЛП(М11)
 ЗП ИНФСЛП(М11)
 ПБ ВТОЗАХ
НЕТОБМ НОП
 ПИНО КОН602(М12)
        сч      ЛИСТМБ(М11)
        и       КОНСТ+1
        или     П177
        и       E42(М11)
        зп      E42(М11)
 СЧ 0
 ПБ КОНЕЦЭ
КОН602 СЧ ИНФСЛП(М11)
 И Е2
 ПЕ ВЗЯ
 СЧ Е2
 ИЛИ ИНФСЛП(М11)
 ЗП ИНФСЛП(М11)
 ПБ ПЛВ0
ВЗЯ СЧ ИНФ(М11)
 СДА 64+12
 НТЖ П37
 И П37
 УИ М15
 СЧ Е48(М15)
        или     E42(М11)
        нтж     E48(М15)
        зп      E42(М11)
        сч      E48(М15)
 И ЛИСТМБ(М11)
 ПО КОНЕЦЭ
 СЧ ИНФ(М11)
 СДА 64+12
 СДА 64-10
 ИЛИ П17
КОНЕЦЭ УИА ’2003’
 УИ М12
 СЧ Е5
 ПВ ГАШЗАН(М15)
 СЧ ШЗАПОС
 МОД НЗАД
 НТЖ Е48-1
 ЗП ШЗАПОС
 УИИ М17(М11)
 ПВ БИЗОН1(М15)
 СЧ 0
 ЗП СМ            КО
 УИИ М16(М12) СВС: УИИ перед сч-зп
 СЧ УПОТ(М11)
 УИ М14
 СДА 64+15
 УИ М13
 СДА 64+15
 УИ М17
 СЧ ИНФ(М11)
 ЗП УПОТ(М11)
 СЧ ИНФСЛП(М11)
 ЗП РМР
 СДА 64+3
 УИ М12
 СДА 64+15
 УИ М11
 СДА 64+15
 УИ М15
 СЧ РАБ600
 ПО ФАТОШИ        - ЗАКАЗАН ФАТАЛЬНЫЙ АВОСТ
 ПИО ЭКВЫХ(М16)
*
 СЧ РМР
 И Е1
 ПЕ ЭКВЫХ         50’175’ - НЕ ЗАПРАШ.ЛИСТ
 СЧ ШКЭВЛ
 МОД НЗАД
 ИЛИ Е48-1
 ЗП ШКЭВЛ
 УИА ’2000’
 СЧ (М16)
 УИА ’2003’
 ПБ ЭКВЫХ
*
ОШИ УИА 36(М16)       ЗАПРЕЩЕННЫЙ ЭКСТРАКОД
 ПБ ЗАПЭКС
ФАТОШИ НОП
 МОД ГУС
 УИА (М16)
 ЗП ’34’(М16)
 ЗП МАСГЛ(М16)
 ЗП ШПЭГ(М16)
 ЗП СОБЫТ(М16)
 УИА 35(М16)       СВС: 64 ЧУЖАЯ ИНФОРМАЦИЯ
 ПБ ЗАПЭКС
CТ414	ноп
СТ415   ноп
СТ416   ноп
        стоп
       	пб	СТ414
ДИСП70 ВНЕШ БУДГРА,ГРАФАЛ,БМВ,ГАШПРП,ГРАСИ
 ВХОД КОБГР
ДИСП70 ВНЕШ РЭ4,И20,КЛИН,Е24П1
 ВХОД ВЫЗОВ
ВЫЗОВ НЕД 0
 УИА 1027
 СДА 64-15
 ЗП РЭ2
 СЧИ М16
 ИЛИ РЭ2
 ЗП РЭ2
 МОД ГУС
 СЧ 36
 СДА 64+21
 СДА 64-21
 ИЛИ РЭ2
 МОД ГУС
 ЗП 36
ТВ1 СЧИ М17
 ЗП И17
 СЧИ М15
 ЗП И15
 МОД ГУС
 УИА (М16)
* СЧ ЯЧУПО(М16)
* И ЕУПО
* ПЕ ТИВ
 СЧ 22(М16)
 НТЖ 32(М16)
 И Е48П17
 ПО ТИВ
ЗАКУПО НОП
 НЕД П77777
 НТЖ П37
 НТЖ Е16
 ЗП РЭ1
Ж5 СЧИ М33
 СДА 64-15
 ЗП РЭ4
 СЧИ ’27’
 ИЛИ РЭ4
 СДА 64-15
 ЗП УПОТ(М16)
 СЧ И20
 И П77777
 ИЛИ УПОТ(М16)
 ЗП УПОТ(М16)
 СЧ ИВ25
 УИ ’27’
 УИА ТВ1А(М16)
 УИИ ’33’(М16)
 ПБ КЛИН
ТВ1А МОД ГУС
 СЧ 37
 УИ ’20’
 СДА 64+15
 УИ ’27’
 СДА 64+15
 УИ ’33’
 ПБ ТВ1
КОБГР СЧ БУДГРА
 И Е40П1
 ЗП БУДГРА
 И Е7
 ПЕ ОКВЫВ
КРИКИ МОД БУДГРА
 СЧ ГРАФАЛ
ВГЕ СДА 64-6
 МОД БУДГРА
 ЗП ГРАФАЛ
 СЧМР
ПАП ЗП РЭ1
 И Е6
ПУП ПЕ ОКВЫВ
 СЧ РЭ1
 РЕГ 29 ВЫВ НА ГРАФ
 СЧ ГРАФАЛ+64
 СДА 64+1
 ЗП ГРАФАЛ+64
ВГИ ПЕ БМВ
 СЧ Е8
 ЗП ГРАФАЛ+64
 СЧ БУДГРА
 СЛЦ Е1
 ЗП БУДГРА
 ПБ БМВ
ОКВЫВ СЧ Е8
 ПВ ГАШПРП(М15)
 СЧ 0
 ЗП БУДГРА
 СЧ ГРАСИ+1
 ПВ ВШГП(М15)
 СЧ 0
 ЗП ГРАСИ+1
 СЧ ГРАСИ
 НТЖ Е1
 ЗП ГРАСИ
 ПО БМВ
 НЕД 0
 ИЛИ Е46 ОСВОБ УСТР
 УИА БПУ(М16)
 ПБ ЗАПВЕТ 
ЭКПАУЗ УИИ М27
 СЧИ М15
 ЗП И15
 СЧИ М17
 ЗП И17
 СЧРЖ ’77’
 ЗП РК
 СЧИ ’32’
 УИ ’33’
 УИА ВОЗМАТ(М15)
  СЧ СМ
  И П3777
 ПО ВРЕСОБ
 СЧ СМ
 НТЖ ВСЕЕД
  СДА 64+24
 ПЕ НЕБД
 МОД ГУС
 СЧ СОБЫТ
 И Е25
 ПЕ ВРЕСОБ
НЕБД УИА ВИСП3(М15)
  СЧ ЭКБУД
  МОД НЗАД
  ИЛИ Е48-1
  ЗП ЭКБУД
 ПБ ВРЕСОБ
ВОЗМАТ МОД И15
 УИА (М15)
 МОД И17
 УИА (М17)
 ПБ ЭКВЫХ
 ВХОД ПРОВЛ
*  ПОДПРОГРАММА ПРОВЕРКИ ВЫБРАННОЙ БВЗ ЗАДАЧИ
*  - НЕ БЫЛА ЛИ ОТЛОЖЕНА ПО ПОДКАЧКЕ.
*  ВОЗВРАТ - М14, НЗАД - М15
ПРОВЛ ПАМ 0
 СЧ ШКЗЗК
 И Е48-1(М15)
 ПО (М14) НА СЧЕТ
 СЧ ШКЗЗК
 ИЛИ ШГП
 НТЖ ШКЗЗК
 ЗП ШГП  ЗАКР  ОТЛОЖ В ШГП
 СЧ ПОДНОС
 ИЛИ ПОДП
 ИЛИ КЛИЕНП
 ИЛИ КЛИЕНТ
 ПЕ БВЗ КАЧКА ЗАНЯТА
 СЧ Е48-1(М15)
 ЗП ПОДП
 НТЖ ШКЗЗК И ИСКЛЮЧИЛИ
 ЗП ШКЗЗК ИЗ ШКЗЗК
 СЧ Е38
 УИА БВЗ(М15)
 ПБ ВШГ
 пам 3
****************** ДИСКИ ****************** 
ДАЙНУД УИ М16
 МОДА (М15)
 СЧ ШЗНМБ-12(М2)
 СДА 64+8(М16)
 И П17
 ПО НЕТМЛ  ОБРАЩ К НЕЗАК МЛ
 ЗП РЯД2
 И П3
 УИ М16
 СЧ РЯД2
 СДА 64+2
 УИ М4 НОМ СТР ОБЩТОМ:3
 МОДА -1(М16)
 СЧ 28(М15)
 МОДА (М4)
 СДА 64+20(М4)
 И П3
 ЗП РЯД
 МОДА (М4)
 СЧ ОБЩТОМ(М15)
 СД КТОМ-1(М16)
 ЗП РЯД2
 И П77
 УИ М16
 УИА 1(М7)
 СЧ ИНФСД
 СЛЦ Е16
 И Е18
 ПО БМ
 СЧ МДР(М15)
 И Е32-24(М6)
 ЗП ПРД
 ПЕ МЛМД
 ПБ БМ
МЯ	сч      УСМД
        сда     64-1
        и       Е48П43
        или     ФКУСД
        зп      ФКУСД
        счи     М16
        сда     64-42
        рзб     НАПРМГ
        или     ФКУСД
        зп      ФКУСД
        и       E7
        пе      A16560
        пино    A16556(М11)
        сч      ФКУСД
        и       Е34П25
        нтж     Е34П25
        пе      A16556
        сч      ВСЕЕД
        нтж     Е34П25
        и       ФКУСД
        слц     Е26Е25
        пб      A16557
A16556  сч      ФКУСД
        слц     E27
A16557  или     E19
        зп      ФКУСД
A16560  счи     М14
        уиа     ФЗ7В(М6)
        сда     64-36
        пб      ФЗ7В
A16652  и       Е36П25
        пб      ОБХПР
ФЗ7В ЗП ФКУСАД
 СЧИ М13
 ИЛИ ФКУСАД
 ЗП ФКУСАД
 СЧ ИНФСД
 СДА 64+22
 И Е18
 ИЛИ ФКУСД
 ЗП ФКУСД
 УИА (М10)
ФЗ9А УИА 3
 СЧ ФКУСД
 СДА 64+41
 И Е1
 СЛЦ П3
 УИ М16
 СЧ ФКУСД
 СДА 64+42
 УИ М12
        пино    ФЗ9Б(М10)
        сч      ФКУСД
        нтж     D73517(М16)
        и       D73524
        по      A16631
ФЗ9Б СЧ СЧЗАКД
 СЛЦ Е1
 УИ М14
 ЗП СЧЗАКД
 СЧ фкусад
 ЗП ОЧЛИСД-1(М14)
* МОДА Е18П13
* И 0             ОЧЛИСД 18-13 Р.Р. = ВН
* ИЛИ ФКУСАД
* ЗП ОЧЛИСД-1(М14)
 СЧ ФКУСД
 И ОСЧК
 ЗП ОЧКУСД-1(М14)
 И ЕФ
 УИИ М2(М14)
 ПЕ Г
 СЧ ШЗЗ
 ИЛИ Е48-1(М12)
 ЗП ШЗЗ
 ПБ ПОЛЬЗ
Г СЧ ОЧКУСД-1(М14)
 СЛИА -1(М2)
 ПИО Г1(М2)
 НТЖ ОЧКУСД-1(М2)
 И Е36П25
 ПЕ Г
 СЧ ОЧКУСД-1(М14)
 НТЖ Е43
 И Е48П43
 по ПОЛЬЗ
 ПБ Г
Г1 СЧ Е48-1(М14)
 ИЛИ СИСТ
 ЗП СИСТ
ПОЛЬЗ СЧ ОЧКУСД-1(М14)
 ПВ МДНАП(М2)
 СЧ ЗУС-3(М16)
 ИЛИ НГД-3(М16)
 И Е32(М4)
 ПО ФЗ1Е
 И НГД-3(М16)
 ПО НУР
 УИА НУР(М2)
ОПР СЧИ М4
 НТЖ П7
 УИ М10
 СЧ Е32(М10)
 ИЛИ ВЫК-3(М16)
 ЗП ВЫК-3(М16)
 ЗП ДАЙГОТ-3(М16)
 ПБ (М2)
НУР     уиа     ФЗ15(М10)
        пб      U75301
A16631  сч      '3117'(М16)
        пв      A74611(М10)    
        уиа     1(М2)
        сч      ФКУСАД
        мода    
        пв      ПРУ12(М10)      
        уиа     Е18П13(М14)     
        пв      ПЕРЕН(М10)      
        сч      ФКУСАД
        зп      РЯД1
        сч      ФКУСД
        зп      РЯД2
        мода    
        пв      СНЗЩД(М11)      
        уиа     Д3(М10)
        пб      А75272
****************** КОНЕЦ ДИСКОВ ******************
ПЕЭКС НОП ,
 ЗП РМР3
 СЧИ М16
 ЗП РЭ1
 СЧ ГУС
 И Е48
 ПО ННС
 СЧИ М15
 ЗП И15
 МОД ГУС
 УИА (М15)
 СЧИ М16
 ЗП ИНФСЛП(М15)
 СЧ СОБЫТ(М15)
 И Е43
 ПО МЕГЛ
 СЧ ЯЧК(М15)
 И ЕКА
 ПО МЕГЛ
 СЧ 33(М15)
 И РМР3
 ПО МЕГЛ
 ЗП РЭ3
 НТЖ 33(М15)
 ЗП 33(М15)
 ПБ МЕПЭ
 ЭКВИВ (АРХМБ,52),(АРХМЛ,53),(ШЗНМБ,41)
 ЭКВИВ (КАЧКА1,КАЧК),(Л,10),(ЛИСТМБ,64)
 ЭКВИВ (ЕПЕР,Е40)
ДИСП70 ВНЕШ П60,П6,П17,П77,ЗАХЗАН,ГАШЗАН,(ЗАНОСС,ЗАНОСМ)
ДИСП70 ВНЕШ (П77777,Е15П1),Е15П11,ВСЕЕД,ТУС,П377,ГУС,П7
ДИСП70 ВНЕШ И15,И17,РК,ЭКВЫХ,ВРЕСОБ,ВИСП3,ЭКБУД
ДИСП70 ВНЕШ НЗАД,П3,(Е2Е1,П3),П37,(Е3П1,П7),ВИСП16
ДИСП70 ВНЕШ ОБЩТОМ,Е37Е36,О29П17,РТР,О46П31
ДИСП70 ВНЕШ ТАЙМЕР,ТБУФ
ДИСКИ ВНЕШ БМ,НЕТМЛ,ПРД,РЯД2,РЯД,КТОМ
ХЛАМ ВНЕШ МЛМД,СС,УПРИ
МЕГЛ ЗП РЭ3
 СЧ РМР3
 СДА 64-5
 И ШПЭГ(М15)
 ПО НЕПЕР
 СЧ МАСГЛ(М15)
 И Е43
 ПО НЕПЕР
МЕПЭ УИИ М27
 СЧИ М32
 УИ М33
 СЧИ М17
 ЗП И17
 СЧ НЗАД
 И П377
 ИЛИ Е43
 ПВ ЗАПВЕТ(М16)
 ПЕ СС
 СЧ СОБЫТ(М15)
 И Н19
 ЗП СОБЫТ(М15)
 СЧ МАСГЛ(М15)
 И Н19
 ЗП МАСГЛ(М15)
 СЧ 33(М15)
 ИЛИ РЭ3
 ЗП 33(М15)
 МОД И17
 УИА (М17)
НЕПЕР МОД И15
 УИА (М15)
ННС МОД И16
 УИА (М16)
 МОД РЭ1
 ПБ 0
НТ8 НОП ,
* МОД НЗАД
* СЧ Е48-1
* ИЛИ ШКЭ65
* ЗП ШКЭ65
 СЧИ М16
 ЗП И16
 СДА 64+3
 ПО ПОПИ
	сда	64+3
	по	ЭКВЫХ1 
* СДА 64+7
        сч      НЗАД
        нтж     СОБА40
        и       П377
        по      ПОПИ
        счи     М16
        сда     64+10
 ЗП РМР
 СЧ ШКЗЛ
 МОД РМР
 И Е48
 ПЕ ПОПИ СВС: ИЗАДАП
 МОД ГУС
 СЧ ТЕРЕЗА
ЗП ЗП СМ
 ПО ЭКВЫХ1 было ВЫХЭ65
 НЕД 0
 ЗП РМР
 МОД РМР
 СЧ ТСЛ-1
 НТЖ И16
 СДА 64+5
 И П1777
 ПО ПОПИ
 СЧ СМ
 МОД РМР
 НТЖ Е48-1
 ПБ ЗП
ПОПИ НОП ,
*  СЧ ПРИРЕК
*  РЕГ ’63’
   сч (М16)
   пб ЭКВЫХ1
СЧЕТРВ СЧИ М16
 ЗП ВОЗТРВ(М15)   ВОЗВР.
 УИА 1027
 СЧ УЧЕТРВ
 ЗП ДЛИИНТ(М15)    СТАР.ТЖОС
 СЧ Т
 СЛЦ ЖИЗОС
 ЗП УЧЕТРВ   НОВ.ТЖОС
 УИА 3
 ВЧ ДЛИИНТ(М15)
 ИЛИ С640
 ЗП ДЛИИНТ(М15)     ДЛ.ПРОШ.ИНТ.
 УИА -МЧПЗ(М16)
 СЧ 0
ВЫЧОЗ ЗП ШКОСТЗ(М15)
 МОД ТУСП+МЧПЗ+НПЗ(М16)
 СЧ КВНЗАД
 И П177
 ПЕ КВЫЧОЗ
 СЧ ШКОСТЗ(М15)
 ИЛИ Е48+МЧПЗ+НПЗ(М16)
КВЫЧОЗ ЦИКЛ ВЫЧОЗ(М16)
 ЗП ШКОСТЗ(М15)    ШК.ВСЕХ ОСТАН.ЗАД.
 И ГРУП
 НТЖ ГРУП
 ЗП ШКНОСЗ(М15)   ШК.НЕОСТАН.Р.ЗАД.
 ПО ОБРКОН
 МОДА С6401
 ЧЕД 0
 РЖА 0
 ЗНАК 0
 ЗП РАБТРВ(М15)
 СЧ ДЛИИНТ(М15)
 ДЕЛ РАБТРВ(М15)
 РЖА 3
 СЛ С640
 СДА 64-16
 ЗП РАБТРВ(М15)     ДОБ.Р.ЗАД.
 СЧ ШКНОСЗ(М15)
ДОБКРЗ ЗП ШКРАБЗ(М15)
 НЕД П77777
 УИ М16
 УИА 1027
 МОД ТУСП(М16)
 СЧ ШЗНМБ+ТРВР
 СЛЦ РАБТРВ(М15)
 МОД ТУСП(М16)
 ЗП ШЗНМБ+ТРВР     ДОБ.Р.ЗАД.
 УИА 3
 СЧ ШКРАБЗ(М15)
 НТЖ Е48(М16)
 ПЕ ДОБКРЗ
 ПБ ОБРОСЗ    НА ПРОП.ИНТ
ОБРКОН СЧ ГРУП+1
 ИЛИ ГРУП+2
 ИЛИ ГРУП+3
 ЗП ШКНОСЗ(М15)
 И ШКОСТЗ(М15)
 НТЖ ШКНОСЗ(М15)
 ЗП ШКНОСЗ(М15)    ШК.НЕОСТ.КОНКУР.
 ПО ОБРОСЗ
 ЧЕД 0
 УМН ДЛИИНТ(М15)
 СЧМР 64
 СДА 64-16
 ЗП РАБТРВ(М15)    ИНТ-Л * NКОНК.
 СЧ ШКНОСЗ(М15)
ДОБКОН ЗП ШКРАБЗ(М15)
 НЕД П77777
 УИ М16
 УИА 1027
 МОД ТУСП(М16)
 СЧ ШЗНМБ+ТРВ
 СЛЦ РАБТРВ(М15)
 МОД ТУСП(М16)
 ЗП ШЗНМБ+ТРВ
 УИА 3
 СЧ ШКРАБЗ(М15)
 НТЖ Е48(М16)
ОБРОСЗ ПЕ ДОБКОН
 СЧ ДЛИИНТ(М15)
 СДА 64-16
 ЗП РАБТРВ(М15)
 СЧ ШКНОСЗ(М15)
 НТЖ ШКРЗ
ДОБОСЗ ЗП ШКОСТЗ(М15)
 МОД ВОЗТРВ(М15)
 ПО 0
 НЕД П77777
 УИ М16
 УИА 1027
 МОД ТУСП(М16)
 СЧ ШЗНМБ+ТРВ+1
 СЛЦ РАБТРВ(М15)
 МОД ТУСП(М16)
 ЗП ШЗНМБ+ТРВ+1    КОРР.НАЧ.РЕШ.
 УИА 3
 СЧ Е48(М16)
 НТЖ ШКОСТЗ(М15)
 ПБ ДОБОСЗ
ПКУСМД СЧ СЧЗАКД
 ПО (М17)
 ВЧОБ Е1
 УИ М15
 УИА (М16)
ПОБД1 СЧИ М2
 СДА 64-42
 НТЖ ОЧКУСД(М16)
 И Е48П43
 ПЕ НЕОБД
 СЧ ОЧКУСД(М16)
 НТЖ '44'(7) РЯ2-УКАЧ(М7) fixme
 МОДА КНУЗ
 И
 ПО '376'(7) НТ1-УКАЧ(М7) С ЭТОЙ ЗОНОЙ fixme
НЕОБД СЛИА 1(М16)
 ЦИКЛ ПОБД1(М15)
 ПБ (М17)
A17017  сч      E5
        пв      ГАШЗАН(М15)
        уии     М33(М16)
        мод     НЗАД
        уиа     0(М16)
        пб      U5222
КНУЗ КОНД М39В’7’М24В’7777’М7В’7’
ОТБТД СЧИ М17
 СДА 64-37
 МОДА Е47Е46
 СЛЦ
 ЗП '44'(7) РЯ2-УКАЧ(М7) fixme
 МОДА Е47Е46
 И
 СДА 64+17
 ИЛИ '44'(7) РЯ2-УКАЧ(М7) fixme
 МОДА Е47Е46
 ИЛИ
 МОДА Е47Е46
 НТЖ
 ИЛИ Е35
 ЗП 34(М7) ИНФ.О ПЛОХОМ ТР-ТЕ ДЛЯ КЗ
ОТБТБ УИИ М17(М13)
 УИИ М13(М3)
 ПВ ВЫЗКЗ(М15)
 УИИ М13(М17)
 ПБ (М16)
Е47Е46 КОНД М45В’3’
* кусок из В1К
В1К ВНЕШ (В1,В1К),РЯ,СТВУ,КСДВИГ,ВЫХПР,ЧИСЛСИ
В1К ВНЕШ БУФПР,ЗАПБУФ
УПАК1 СЧ РЯ-В1(М17)
 И П37
 ЗП РЯ-В1(М17)
УПАК СЧ СТВУ-В1(М17)
 И П377
 УИ М16
 СЛИ М16(М17)      А БУФЕРА ПРИЕМА
 СЧ РЯ-В1(М17)
 МОД КСДВИГ-В1(М17)
 СДА 64-40
 ИЛИ БУФПР-В1(М16)
 ЗП БУФПР-В1(М16) СИМВОЛ НА БУФЕР
 СЧ КСДВИГ-В1(М17)
 СЛЦ Е4
 ЗП КСДВИГ-В1(М17)
 НТЖ П60
 ПЕ ВЫХПР-В1(М17) СЛОВО НЕ ПОЛНОЕ
 ЗП КСДВИГ-В1(М17)
 СЧ СТВУ-В1(М17)
 СЛЦ Е1
 ЗП СТВУ-В1(М17)
 СЧ ЧИСЛСИ-В1(М17)
 СЛЦ Е1
 ЗП ЧИСЛСИ-В1(М17)
 СДА 64+1
 НТЖ П12
 ПЕ ВЫХПР-В1(М17) НЕ ВСЯ ПК
 ЗП ЧИСЛСИ-В1(М17)
 СЧ АЗУ+1
 СЛЦ Е37
 ЗП АЗУ+1         ПК С ФСМ
 ПБ ЗАПБУФ-В1(М17)
* здесь начинается кусок, относящийся к терминалам
G17060	сч	МОТТR(М13)
	и	П177
	зп	МОТТR(М13)
	уи	М15
	сч	ШVD130
	и	E48(М17)
	по	A17071
	сч	D77212
	и	E48(М17)
	по	A17071
	нтж	D77212
	зп	D77212
	слиа	-'104'(М15)
	пио	SW2(М15)
	слиа	-14(М15)
	пио	КПФШ1(М15)
	пб	ВЫХОД
A17071	сч	МОТТR(М13)
	сда	64+5
	по	A17074
A17073	сч	МОТТR(М13)
	пб	НОВКОД
A17074	слиа	-2(М15)
	пио	A17114(М15)
	слиа	-1(М15)
	пио	G17130(М15)
	слиа	-2(М15)
	пио	A17113(М15)
	слиа	-3(М15)
	пио	SW2(М15)
	слиа	-2(М15)
	пио	A17073(М15)
	слиа	-2(М15)
	пио	А76364(М15)
	слиа	-1(М15)
	пио	A17120(М15)
	слиа	-1(М15)
	пио	A17143(М15)
	слиа	-1(М15)
	пио	A17133(М15)
	слиа	-4(М15)
	пио	SW15(М15)
	слиа	-6(М15)
	пио	SWCV(М15)
	слиа	-1(М15)
	пио	SW2(М15)
	слиа	-1(М15)
	пио	A17140(М15)
	слиа	-3(М15)
	пио	A17116(М15)
	пб	ВЫХОД
A17113	уиа	5(М15)
	пб	ВУПП
A17114	сч	ШVD130
	и	E48(М17)
	по	ВЫХОД
A17116	сч	E48(М17)
	или	D77213
	зп	D77213
	пб	ВЫХОД
A17120	сч	D77213
	и	E48(М17)
	по	A17124
	сч	ШVD130
	и	E48(М17)
	по	ВЫХОД
	сч	П12
	пб	НОВКОД
A17124	сч	ШVD130
	и	E48(М17)
	по	SW5
	или	D77212
	нтж	E48(М17)
	зп	D77212
	пб	SW5
G17130	сч	D77213
	или	E48(М17)
	нтж	E48(М17)
	зп	D77213
	пб	SW5
A17133	сч	ШVD130
	и	E48(М17)
	пе	СТRLОП
	сч	ШЛАТР
	или	E48(М17)
	зп	ШЛАТР
	сч	ТСЛ(М17)
	или	E38
A17137	зп	ТСЛ(М17)
	пб	ВЫХОД
A17140	сч	ШVD130
	и	E48(М17)
	по	СТRLОП
	или	D77212
	зп	D77212
	пб	ВЫХОД
A17143	сч	ШЛАТР
	или	E48(М17)
	нтж	E48(М17)
	зп	ШЛАТР
	сч	ВСЕЕД
	нтж	E38
	и	ТСЛ(М17)
	пб	A17137
G17147	нтж	П16
	пе	ЗПСИМВ
	сч	ШНР
	и	E48(М17)
	пе	ETX1
	пб	ЗПСИМВ
A17152	сч	ШНР
	или	D77334
	и	E48(М2)
	по	A17155
	сч	E1
A17155	зп	46(М7)
	сч	ШРИН
	и	E48(М2)
	по	ОЧСИМВ
	сч	ШКАС
	и	E48(М2)
	пе	ОЧСИМВ
	сч	E2
	зп	46(М7)
	сч	E12
	зп	(М1)
	пб	ОЧСИМВ
* из ТЕРМ - УППСЛУ
G17163	зп	42(М7)
	зп	45(М7)
 УИ М14
 СЧ ШНР
 И Е48(М2)
 ПО СЧ42
	сч	42(М7)
	и	П177
	уи	М14
	слиа	-8(М14)
	уиа	40(М6)
	пио	A17223(М14)
	слиа	-4(М14)
	уиа	32(М6)
	пио	A17223(М14)
	слиа	-12(М14)
	уиа	24(М6)
	пио	A17223(М14)
	слиа	-1(М14)
	уиа	16(М6)
	пио	A17223(М14)
	слиа	-1(М14)
	уиа	8(М6)
	пио	A17223(М14)
	слиа	-5(М14)
	уиа	0(М6)
	пио	A17223(М14)
*
 СЧ 42(М7)
 ПО СЧ42        ПРОП."NUL"
*
 НТЖ П12          ПРОПУСТ."LF"
 ПО СЧ42
*
 НТЖ ПЕОТ
 ПО СЧ42        ПРОПУСТ.СИМВ."СR"
 	сч	ШVD130
	и	E48(М2)
	пе	СЧ42
*
*
*------ БЛОК ПЕРЕКЛЮЧЕНИЯ РЕГИСТРОВ (ЭЛ-КА И НР2640С)
*
 СЧ 42(М7)
*  ЗП 45(М7) только на СВС
 И П140
 НТЖ П140
 ПО РУС
*       ПЕРЕКЛЮЧЕНИЕ НА ЛАТ.РЕГ.
 СЧ ТСЛ(М2)
 И ЕЛАТР
 ПЕ СЛВНР1      РЕГ.УЖЕ БЫЛ ЛАТИНСКИМ
 УИА П17(М14)
 СЧ ТСЛ(М2)
 ИЛИ ЕЛАТР
ЛАТ УИА 1027
 ЗП ТСЛ(М2)
 УИА 3
 УИА СЛВНР1(М17)
 МОДА НАДОLF
 ПИО (М5)
ЛАТ1 СЧ (М14)
 ПБ ПРНОЛЬ
*      ПЕРЕКЛЮЧЕНИЕ НА РУС.РЕГ.
РУС УИА SО(М14)
 СЧ ТСЛ(М2)
 И ЕЛАТР
 ПО СЛВНР1       РЕГ.УЖЕ БЫЛ РУССКИМ
 НТЖ ТСЛ(М2)
 ПБ ЛАТ
НАДОLF УИА СЛВНР2(М17)
 СЛИА 1(М5)
 ПБ ЛАТ1
*
*------  Е N D  ------*
*
*
A17223	сч	ШVD130
	и	E48(М2)
	по	A17231
	счи	М6
	зп	D77217
	уиа	27(М17)
	счи	М17
	уиа	A17230(М17)
	пб	ПРНОЛЬ
A17230	сч	D77217
	уи	М6
A17231	сч	ШVD130
	и	E48(М2)
	уиа	0(М14)
	по	ЭЛЕКТР
	уиа	1(М14)
ЭЛЕКТР	сч	СЛКОД(М14)
	сда	64(М6)
	и	П377
	уиа	0(М6)
ВЫДСИМ УИА ОЧСИМВ(М17)
 ПБ ПРНОЛЬ
*
*      ПОСЛЕ ПЕРЕКЛЮЧ.РЕГИСТРА НАДО
*      НАДО ВЫДАТЬ И САМ СИМВОЛ
СЛВНР1 СЧ 45(М7)
 ПБ ВЫДСИМ
СЛВНР2 СЛИА -1(М5)
 ПБ СЛВНР1
*    НА ПРИЕМЕ РЕГИСТР ДОЛЖЕН
*    БЫТЬ РУССКИМ.
*        
G17241	ноп
 СЧ ШНР
 И Е48(М2)
 ПО КМ2А
 УИА КМ2А(М17)
 СЧ ШЛАТР
 И Е48(М2)
 УИА SО(М14)
 ПО РУСРЕГ
 УИА П17(М14)
 СЧ ТСЛ(М2)
 ИЛИ ЕЛАТР
 ЗП ТСЛ(М2)
РЕГГ СЧ (М14)
 ПБ ПРНОЛЬ
РУСРЕГ СЧ ВСЕЕД
 НТЖ ЕЛАТР
 И ТСЛ(М2)
 ЗП ТСЛ(М2)
 ПБ РЕГГ
*
ОБЛ МОД ТБЛ(М17)      ЕПРИМ -> ТСЛ; М16:= НАЧ.А РЕКОРДА;
 УИА (М16)         ГАШЕНИЕ БУДИЛЬНИКА:Е45 -> ВППС.
 СЧ ШСТRLО
 И Е48(М17)
 ПЕ ОБЛ1
	сч	ШКУСТР
	или	ШКАС
	и	E48(М17)
	по	ОБЛ1
 СЧ Е45
 ЗП ВППС(М17)
ОБЛ1 СЧ ТСЛ(М17)
 ИЛИ ЕПРИМ
 ЗП ТСЛ(М17)
 ПБ (М15)
*
G17263	сч	ТСЛ(М2)
	и	Е18Е17
	нтж	Е18Е17
	пе	(М15)
	сч	П377
	увв	'144'(М2)
G17266	пб	1(М15)
G17267	сч	ТСЛ(М17)
	нтж	Е18Е17
	и	Е18Е17
	пе	(М15)
	сч	П377
	уиа	'2003'
	увв	'144'(М17)
	пб	1(М15)
G17273	сч	4(М16)
	слц	E46
	по	A76734
	зп	4(М16)
	сч	'2415'(М15)
	и	E28
	по	A17376
	сч	ГТСЛ+32(М15)
	увв	'174'(М15)
	пб	БМВ
*
***************************
******   О Т М Е Н А   С И М В О Л А   ******
ОТСИМВ СЧ ШКВЫД
 И Е48(М17)
 ПЕ ВЫХОД
 УИА (М15)      ОТМ СИМВ
 СЧ (М16)
 НТЖ Е46Е45
 ПО О3      РЕКОРД ПУСТ
 И Е48П45
 ПО О1     НЕТ СИМВ В СЛ
 УИА КОТ2-КОТ1(М15)
О1 СЧ (М16)
 СЛЦ КОТ1(М15)     ИЗМ СЧ
 ЗП (М16)
 ПБ О2
О3 СЧ ВВ(М16)
 ПО ВЫХОД    НЕТ ОТМЕНИЛИ ВЕСЬ БУФ
 УИ М16
 ЗП ТБЛ(М17)
О2 МОД (М16)
 СЛИА (М16)
 СЧ Н(М16)
 СДА 64+8
 ЗП Н(М16)
	пб	ВЫХОД
G17314	сч	ШКУСТР
	и	E24(М16)
	по	БМВ
	и	ШЗПРИМ
	пе	БМВ
	увв	'4174'(М16)
	и	П377
	зп	ГТСЛ+32(М16)
	сч	'2415'(М16)
	и	Е18Е17
	нтж	Е18Е17
	пе	A17325
	сч	ГТСЛ+32(М16)
	чед	
	и	E1
	по	G17345
	сч	
	пб	A17330
A17325	сч	ГТСЛ+32(М16)
	чед	
	и	E1
	пе	G17345
	сч	E8
A17330	зп	ГТСЛ+32(М16)
	счи	М16
	слц	СВИСТ1
	пб	ПРЯТ
ИМИТАЦ	и	E30
	по	G67256
	зп	SSЕСТЬ
	нтж	ШКПРЕ
	зп	ШКПРЕ
	сч	'77203'
	или	ЭКО
	по	A17337
	сч	E6
	увв	'31'
A17337	сч	E35
	пб	ТВ41
A17340	зп	'77203'
	сч	D01462
	и	ШКОПР
	зп	D77202
	сч	ШКОПР
	или	D01462
	зп	ШКОПР
	сч	
	зп	D01462
	пб	A17354
G17345	сч	ГТСЛ+32(М16)
	по	A17354
	сч	ШКОПР
	или	E24(М16)
	зп	ШКОПР
	сч	ТАЙНА
	и	E24(М16)
	по	A17354
	и	ШКВЫД
	пе	A17354
	сч	E12(М16)
	пв	ГАШПРП(М15)
	сч	ГТСЛ+32(М16)
	увв	'174'(М16)
A17354	сч	ШГ
	и	E45
	пе	БМВ
	сч	E45
	уиа	БПУ(М15)
	пб	ВШГ
G17357	сч	'72416'(М15)
	уи	М16
	сч	(М16)
	уи	М17
	сли	М17(М16)
	увв	'4102'
	мода	(М15)
	и	E7(М15)
	по	A17365
	счи	М15
	слц	СВИСТ2
	пб	ПРЯТ
A17365	зп	4(М16)
	сч	ШКВЫД
	и	E24(М15)
	по	A17377
	сч	E45
	зп	'72751'(М15)
	сч	9(М17)
	сда	64-8
	по	ВАСЗВ
	зп	9(М17)
G17372	сда	64+40
	по	A17365
	увв	'174'(М15)
A17374	сч	E12(М15)
	уиа	БМВ(М15)
	пб	УСТПРП
A17376	сч	9(М17)
	пб	G17372
A17377	сч	'2415'(М15)
	и	E28
	по	A17374
	увв	'4102'
	мода	(М15)
	и	E8(М15)
	пе	A17374
	сч	E10(М15)
	уиа	БМВ(М15)
	пб	ГАШПРП
* cнова кусок из БОЭК1
ПР СЧ ОЧКУС-1(М14)
 И Е22
 ПО ПР1
 СЧ НЕН
 И ОЧКУС-1(М14)
 ЗП ОЧКУС-1(М14)
 СЧ Е18
 ИЛИ Е19
 ИЛИ НОММБ2
 ЗП РЯО5
 УИА 1027
 СЧ ОЧЛИС-1(М14)
 ЗП РМР
 СЧИ М2
 И П3
 СЛЦ Е1
 СДА 64-2
 СЛЦ РЯО5
 УИИ М6(М16)
 УИА ПР2(М16)
 ПБ ФИЗУР
ВЫМ1 СЧ РЯО
 НТЖ Е48-1(М14)
ВХ ЗП РЯО
 ПО ВХСМ
 УИА 3
 НЕД 0
 УИ М14
 СЧ ОЧКУС-1(М14)
 И Е20
 ПО ВЫМ1 МЛ НЕ НА ДРУГОЙ Б
 СЧ ОЧКУС-1(М14)
 СДА 64+42
 ПО ВЫМЛ
 УИ М15
 СЧ ОЧКУС-1(М14)
 И П14
 ПО ВЫП
 СЧ 0
 МОД ТУСП-1(М15)
 ЗП 0
ВЫП УИА 1(М1)
 ПВ СНЗКМЛ(М11)
 ПБ ВХСМЕ
ВЫМЛ СЧ ОЧКУС-1(М14)
 СДА 64+6
 И Е1
 УИ М15
 СЧ 0
 ЗП ШИМЛ(М15)
 СЧ ОЧЛИС-1(М14)
 ПО ВЫМ
 СДА 64+24
 ИЛИ Е19
 ПВ ТБУФ(М15)
ВЫМ СЧ Е18
 ПВ ЗАХЗАН(М15)
 УИА (М17)
 ПВ ДВОЧ1(М5)
 СЧ Е18
 ПВ ГАШЗАН(М15)
ВХСМЕ СЧ ВЫМГ
 ПБ ВХ
ВХСМ СЧ СМЕМЛ
 ПО СМЕ1
 УИА 3
 НЕД П7
 УИ М1
 СДА 64+3
 УИ М16
 СЧИ М1
 И П7
 УИ М17
 НТЖ П7
 УИ М15
 СЧ Е32-24(М1)
 НТЖ СМЕМЛ
 ЗП СМЕМЛ
 СЧ ШОЧМГ-24(М1)
 ПО ВХСМ
 ПБ ДИХ
A17456	сч	ЯКУС(М17)
	и	E4
	уи	М4
	сч	ЯЛИС
	и	Е48П37
	сда	64+24
	зп	ЯЛИС
	сч	ЯКУС
	уии	М12(М16)
	и	П3
	пе	A17470
	сч	ЯКУС
	и	Е18Е11
	нтж	Е18Е11
	по	A17470
	и	E18
	пе	A17470
	уиа	A17470(М16)
	пио	А67353(М4)
A17470	сч	ЯКУС
	или	E18
	нтж	E18
	зп	ЯКУС
	сч	ЯЛИС
	сда	64+2
	уи	М10
	пе	A17477
	мода	
	пв	ТБУФ(М15)
	сда	64-8
	уи	М10
	сда	64-2
	зп	ЯЛИС
A17477	сч	НОММЛ1
	пв	ENQ(М15)
	сч	ЗКАТ
	пв	G67355(М16)
	уиа	'3'
	сч	ГОД
	и	П7
	уи	М16
	сли	М16(М10)
	сч	ШИФРПЛ(М16)
	пб	G67314
	пам	1
A17506	сч	7
	и	Е48Е47
	по	БМВ
	уиа	БМВ(М15)
	и	E48
	пе	A17520
	увв	'4100'
	сда	64-24
	зп	Р
	нтж	Е48П25
	и	D77000
	или	D77001
	зп	D77001
	и	Р
	по	A17520
G17516  ноп
	сч	МГРП
	и	E19
	нтж	E19
	пб	ТВ67
A17520	сч	РАБШК
	или	ШКВЫД
	и	D77001
	пе	БМВ
	сч	E19
	пб	ТВ67А
Ф4 ПАМ 0
 МОД ГУС
 УИА (М15)
 СЧ 36(М15)
 СДА 64+41
 УИ М17
 СДА 64+6
 УИ М16
 СЧИ М17
 СДА 64+1
 И П37
 УИ М17
 СЧ 0
 ЗП РЭ1
 ЗП РЭ4
 СЧ СОБЫТ(М15)
 И Е41
 ПО МЕГ
 СЧ ЯЧК(М15)
 И ЕКА
 ПО МЕГ
 МОДА (М16)
 СЧ '55'(М15) свс: ШЗНМБ+Т
 И Е48(М17)
 ЗП РЭ4
МЕГ ПАМ 0
 МОДА (М16)
 СЧ ШЗНМБ+У(М15)
 И Е48(М17)
 ПО НЕПР
 СЧ МАСГЛ(М15)
 И Е41
 ПО НЕПР
 ЗП РЭ1
ЕПР СЧ НЗАД
 И П377
 ИЛИ Е41
 ПВ ЗАПВЕТ(М16)
 ПЕ БУП
 ЗП РЭ4
 СЧ ВСЕЕД
 НТЖ Е17
 ЗП РЭ2
 И СОБЫТ(М15)
 ЗП СОБЫТ(М15)
 СЧ МАСГЛ(М15)
 И РЭ2
 ЗП МАСГЛ(М15)
НЕПР ПАМ 0
 СЧ РЭ4
 ПЕ ЕПР
 МОД И15
 УИА (М15)
 ПБ ЭК74
БУП ПАМ 0
 СЧ РЭ1
 ПЕ БПУ
 СЧ ВСЕЕД
 НТЖ Е17
 И МАСГЛ(М15)
 ЗП МАСГЛ(М15)
 ПБ БПУ
****************** ДИСКИ ******************        
МДНАПС И П7
 ИЛИ ЗАНКМД-3(М16)
 ПБ МДНАП1
УСТШЗ УИА 1027
 СЧ ВСЕЕД
 НТЖ Е47(М16)
 И ШЗПОМД
 ИЛИ Е40
 ЗП ШЗПОМД
 ПБ (М2)
****************** КОНЕЦ ДИСКОВ ******************        
АВАРИЯ МОД ГУС
 СЧ 4
 СДА 64+24
	уиа	5(М16)
ПБСМ НОП ,
	и	Е15П1
	по	СТ175
 МОД И16
 УИА (М16)
 ЗП И16
 МОД И16
 ПБ 0
ВЫЗРЕЗ НОП ,
 СЧ ШКОРАБ
 ПО НЕТР          НЕТ РАБОТЫ РЕЗОТ-У
	сч	ЗАНЯТА
	и	E16
	по	БМВ
	сч	ШКСВН
	и	E30
	по	БМВ
	и	ШКИВ
	пе	БМВ
	сч	E30
	пб	ТВ41
НЕТР МОД ГУС
 СЧ 33
 СЛЦ КВАНТ
 ПО БМВ
 ПБ БПУ        
* внешние для кода выше
ДИСП70 ВНЕШ ТВ41,ТВ67А,ГОД,СТ175,Е18Е17,Е48Е47,Е48П25,МГРП
ДИСП70 ВНЕШ НОММБ2,НОММЛ1,П140,П14,Р,РАБШК,РЯО5,РЯО,D01462
ДИСП70 ВНЕШ ШЗПОМД,ШИФРПЛ,ШКПРЕ,ЯКУС,ЯЛИС,ВЫМГ,SSЕСТЬ
ДИСП70 ВНЕШ УСТПРП,КВАНТ,ЭК74,Е18Е11
ДИСП70 ВНЕШ Е15П1,Е44П42,ЗАНОСМ,ЗАНЯТА,НОММБ1,НРЕ,РШЗН,РШКИВ
КИТ ВНЕШ ВППС,ТБЛ,ЗКАТ
ХЛАМ ВНЕШ ДИ,A67353
КАЧКА ВНЕШ П16
АВМД ВНЕШ АВМД
ДИСКИ ВНЕШ ЕNQ
МОТТ ВНЕШ ШЗПРИМ,ШКАС,ШКУСТР,ШНР,ШРИН,ШСТRLО,ЭКО,D77000
МОТТ ВНЕШ D77001,ШКОПР,ПРЯТ,D77202,СВИСТ1,СВИСТ2,CTRLN,ШКВЫД
МОТТ ВНЕШ D77334,ТАЙНА,ПЕОТ,ЗПСИМВ,ВАСЗВ,ШКОРАБ,ETX1,КОТ1,КОТ2
ТЕРМ ВНЕШ СЧ42,ПРНОЛЬ,ОЧСИМВ,КМ2А
 ЭКВИВ (ВОЗТРВ,0),(ДЛИИНТ,1),(ШКОСТЗ,2)
 ЭКВИВ (ШКНОСЗ,3),(ШКРАБЗ,4),(РАБТРВ,5)
ВЗУ ВНЕШ ЭКВЫХ1,U5222
ВЗУ ВНЕШ ФИЗУР,ФИЗОБМ,АНВО8,ЕФ,ЗАПЭКС
ВИСП ВНЕШ ВШГ,ВШГП
 ВХОД АРХМБ,АРХМЛ,ЭКПАУЗ,СИ2,Л,ИЩУ
 ВХОД ДАЙНУД,ПЕЭКС,НТ8,ВЫЗИС
 ВХОД ЛИСТМБ,АВДРУГ,ЗАПВЫХ,ПЛВОЗУ
 ВХОД НЕЛЬЗЯ,Ж5,ИСКЛИС,ЭКВЛ
 ВХОД ОПР,ФЗ9А,ССЫ,A17365,G17314,G17357
 ВХОД ОТБТД,ОТБТБ,Ф4,АВАРИЯ,МДНАПС,УСТШЗ,ВХСМ,ВХСМЕ
ДИСКИ ВНЕШ Д3,РАБМД,ОБХПР,A75272,A74611
ДИСКИ ВНЕШ ЗАНКМД,D73524
ДИСКИ ВНЕШ ИНФСД,U75301
ДИСКИ ВНЕШ СЧЗАКД,ОСЧК,МДНАП,МДНАП1,УСМД,D73517
ДИСКИ ВНЕШ ФЗ1Е,ЗУС,НГД,ВЫК,ДАЙГОТ,ФЗ15,РЯД1
ДИСП70 ВНЕШ БПУ,РМР,НБТ,ПЗ,ИВ25,ИВ34,ЛИСТЫ,ШКНПП,ИВ23,П50
ДИСП70 ВНЕШ Е18П1,СМ,КДВ,П3777,КДД,ТВ67
ДИСП70 ВНЕШ Е31П17,ОССТБД,ЕЗП,ШКОГ,П60П44
ДИСП70 ВНЕШ Е36П25,Е34П25,Е26Е25,ФКУСАД,ОЧЛИСД,ШЗЗ
ДИСП70 ВНЕШ Е46Е45,БИЗОН1
ДИСП70 ВНЕШ ВРЕМЯ,БУДГДР,ОПТИМ,ШКСВН
ДИСП70 ВНЕШ ШАРСИС,Е47П33,ТМД,П12,АЗУ
ДИСП70 ВНЕШ КВЭ600
ДИСП70 ВНЕШ КНАПР
ДИСП70 ВНЕШ Н19,ШКЗЗК,И16,ШЗАПОС,ФКУСД
ДИСП70 ВНЕШ НПЗ,МЧПЗ,ШГП,МШГП,ЗАКЛИ
ДИСП70 ВНЕШ ОЧКУСД,ШКЗЛ,П1777,ТУСП
ДИСП70 ВНЕШ П17777,Е41П1,СКБЭ,РАБ600
ДИСП70 ВНЕШ РОСППМ,ЧПОД,РЗ,Е48П43
ДИСП70 ВНЕШ (С640,ЭК9),ТРВ,ТРВР,П177
ДИСП70 ВНЕШ СОБЫТ,ЯЧК,ЕКА,РЭ3,ШПЭГ,МАСГЛ,СОБА40
ДИСП70 ВНЕШ ТЕРЕЗА,ГРУП,НАПРМГ,КРОМЕ
ДИСП70 ВНЕШ Т,ЖИЗОС,КВНЗАД,УЧЕТРВ
ДИСП70 ВНЕШ ТСЛ,ОЗУ,МИН,ОЧКУС
ДИСП70 ВНЕШ УКАЧАЙ,ТВ205,МШГ,БВЗ,УКА
ДИСП70 ВНЕШ ШГ,ПРИП,КОК,ШЗНМБО,ТЗФСТР,АВРАСП,ТМБ
ДИСП70 ВНЕШ ШКИВ,ВИГ11,ВИСП15,ОТВКАЧ,ОЧЛИС
ДИСП70 ВНЕШ ШОПТИМ,ШЗАВЕТ,ИНФ,Е48П37,П7777,ШОЧМГ
КАЧКА ВНЕШ Е48П17,ЖДУ,КЛИЕНТ,ПОДНОС,ПАМФИЛ
КАЧКА ВНЕШ ОЧЕНЬ,СТАРОЕ
КАЧКА ВНЕШ ПОДП,КЛИЕНП
КАЧКА ВНЕШ ФИЛ,НАЧФИЛ,КОНФИЛ,ШКОТКЗ,ШКФОНЗ
КАЧКА ВНЕШ ШКРЗ,О42,КОНСТ,ЧИП,R1,R2,R3,R5,R,П460,КАЧКА
КАЧКА ВНЕШ ШКЭВЛ,РАБТОП,NЛИСТА
КИТ ВНЕШ НЕН,ШИМЛ,СМЕМЛ,СМ2,РМР2,РМР3,И152,ВИОЛМ
КИТ ВНЕШ СИСТ,СЧЕ,ЗАК,Е48П2,C6401
КИТ ВНЕШ ТОПЛИС,АКТОП,Е48П42
МОТТ ВНЕШ НОВКОД,ВЫХОД,ВУПП,D77212,A76364,D77213,SW5
МОТТ ВНЕШ ШVD130,SWCV,SW15,КПФШ1,(МОТТR,R),D77217,ЕПРИМ
РЕЗАРХ ВНЕШ Е18П13
СОСТАВ ВНЕШ ЕМБФ,ТБДТР,ТБД,ОБЩМЛ,ЛИСОСТ,АТ,МДР
СОСТАВ ВНЕШ ЕОТКЗ,ЯЧОТБР,ЕОТБ,ЯЧОТБ,ЯЧЛИС,ССТБД
СОСТАВ ВНЕШ МОДЛИ,ЕБУФЛ,ЕОТКЛ,СДВМЛ
СОСТАВ ВНЕШ ТОП,ТОПНЗ,ЕПОДКЛ,СДФЛИ,ЕЗАНМД,НАРМД
СОСТАВ ВНЕШ ЯЧТОП,ВР,ИНФАВ,УПОТ,ЕТРА,ЕОБМ
СОСТАВ ВНЕШ ЯЧУПО,ЕУПО,ИНФСЛП
ХЛАМ ВНЕШ ПРУ12,ПЕРЕН,СНЗЩД,ДИХ,СНЗКМЛ,G67314,G67256,G67355
ДМЛМБ ВНЕШ СМЕ1,ПР1,ПР2,ДВОЧ1
 ЭКВИВ (У,6)
 ВХОД КИПР,СЧЕТРВ,КАЧК,ФЗ7В,ОБЛ,G17241,G17266,G17163,G17267
 ВХОД ИСКВЛ,ЗАКУПО,МЯ,ОТСИМВ,G17273,G17345,G17263,G17516
 ВХОД G17147,G17372,G17060,ПКУСМД,УПАК,УПАК1,ИСКБЛ,G17130
 ВХОД ИМИТАЦ
ЕОБММ equ '3002'
РАБ1М экв '3036' ОЧКУСД+5
РАБ2М экв '3037' ОЧКУСД+6
КВВЛМ экв '3006'
КВСЧКМ экв '3007'
ЕОТКЛМ экв '3003'
ЯЧНИА экв '66'
ЕНИИАС экв Е26
МОТТ ВНЕШ A77634,U77627,СТRLОП,ШЛАТР,A77613,A76734
МОТТ ВНЕШ СЛКОД,SW2,ГТСЛ,ЕЛАТР,SO,Е48П45,ВВ,Н
 финиш

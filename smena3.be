СМЕНА3	СТАРТ	'11400'
	Б
	Е
	М
	употр	СМЕНА3-768(М7)
заказы	сч	общ-Н(М7) ИЗ НОМБОБ.ВЫЗСМ3
	по	НВМЛ4-Н(М7)
	мод	НОВД-Н(М7)
	уиа	(М10)
	сли	М10(М3)
пм	сч	ВЫМГ 
воч	зп	Р7-Н(М7) 
	по	зк
	нед	0
	уи	М1
	сч	АЗУ+2(М1)
	и	E20
	пе	воч2 МЛ НА ДРУГОЙ БОБИНЕ
	сч	АЗУ+2(М1)
	сда	64+4
	и	П7
	зп	НОВД+2-Н(М7) НОМ. БОБИНЫ, ГДЕ МЛ.
	мода	зас-Н(М7)
	мода	0
	пв	0(М11)
	сч	АЗУ+2(М1)
	и	П14
	пе	cзазу
	сч	ВЫМГ
	нтж	Е48-1(М1)
	зп	ВЫМГ
cзазу	сч	АЗУ+2(М1) !В НБОБЕ НЕ ИСП.
	или	E20
	зп	АЗУ+2(М1)
	уиа	'3'
воч2	сч	Р7-Н(М7)
	нтж	Е48-1(М1)
	пб	воч
зк	сч	выбз-1(М10) 
	по	зк2
	нед	П77777
	уи	М4
	сч	выбз-1(М10)
	нтж	E48(М4)
	зп	выбз-1(М10)
	сч	ТЗНМД(М10)
	и	E48(М4)
	пе	зкс
	уиа	'2003'
	сч	ШОЧМГ(М4)
	или	ВЫМГ
	зп	ВЫМГ
	уиа	'3'
зкс	сч	закдрб-1(М10)
	или	перст-1(М10)
	нтж	перст-1(М10)
	уиа	зк-У3(М17)
	мода	возв-Н(М7)
	пв	0(М1)
	мода	зкс
	уиа	(М17)
зкы	сч	крмл-1(М3)
	вч	E1
	зп	крмл-1(М3)
	сч	оскус1(М16)
	сбр	ЗОБ
	сда	64+42
	уи	М1
	сч	зон(М3)
	нтж	E48(М1)
	зп	зон(М3)
	уиа	-А(М1)
	сч	ВСЕЕД
	нтж	E48(М11)
	зп	Р7-Н(М7)
зка	мода	ктзнмл(М3)
	сч	закдрб+а(М1)
	сбр	Р7-Н(М7)
	мода	ктзнмл(М3)
	зп	закдрб+а(М1) 
	мода	зка
	цикл	0(М1)
	счи	М11
	слц	E1
	нтж	П60
	по	зк1
	слиа	-46(М11)
	уии	М1(М11)
	сли	М1(М3)
зка1	сч	очкус1+47(М1)
	зп	очкус1+46(М1)
	сч	очкс1+47(М1)
	зп	очкс1+46(М1)
	сч	ши+47(М1) 
	зп	ши+46(М1)
	слиа	1(М1)
	мода	зка1
	цикл	0(М11)
	сч	0
зк1	зп	очкус1+47(М3)
	пб	0(М17)
зк2	сч	заме(М3)
	по	зк20 МЛ ЭТОГО МГ НЕ ПЕРЕСТ
*НА ДРУГИХ БОБ.
	мод	НОВД-Н(М7)
	и	Е48-1
	по	зк20
	нтж	заме(М3)
	зп	заме(М3)
	сч	общ-Н(М7)
	уи	М12
	слиа	-1(М12)
зкзан	уии	М4(М12)
	уиа	'3'
	сли	М4(М3)
	сч	ктзнмл(М4)
	и	E39
	по	за НЕ ЗАМЕНЯЛИ
	сч	ктзнмл(М4)
	нтж	НОВД+3-Н(М7)
	и	е30п28-Н(М7)
	пе	за МЛ НЕ ЭТОГО МГ
	сч	ктзнмл(М4)
	нтж	E39
	зп	ктзнмл(М4)
	сда	64+30
	и	П377
	зп	Р-Н(М7)
	и	П37
	уи	М4 МГ, ГДЕ СТОЯЛА МЛ
	уии	М14(М12)
	счи	М12
	и	П37
	уи	М12
	мода	ЕСТЬБ-Н(М7)
	пв	0(М17)
	счи	М14
	сда	64+5
	слц	E1
	зп	НОВД+2-Н(М7) НОВАЯ БЭСМ
	зп	Р1-Н(М7)
	зп	ПРИ-Н(М7)
	сч	Р-Н(М7)
	сда	64+5
	слц	E1
	зп	НОВД+1-Н(М7) НОМ. МГ СТАРЫЙ
	мод	Р-Н(М7)
	сч	ктзнмл(М3)
	или	E39
	нтж	E39
	мод	Р-Н(М7)
	зп	ктзнмл(М3)
	пб	СМЛ1-Н(М7)
зкэт	сч	НЕТНАП
	и	ОЧКУС+15(М2)
	и	номм-Н(М7)
	зп	Р-Н(М7)
	сч	ЯМ16(М15)
	сда	64-4
	или	Р-Н(М7)
	мода	1(М4)
	или	Р4-Н(М7)
	зп	ОЧКУС+15(М2)
за1а	мода	за2г
	цикл	0(М2)
за1	уии	М12(М14)
	пб	за
зк2в	уиа	-15(М2)
	уиа	'2003'
за2г	уиа	-1(М4)
	уии	М15(М7)
зказ	сч	ОЧКУС+15(М2)
	по	за1
	и	E20
	по	за1а
	сч	ОЧКУС+15(М2)
	сда	64+4
	мода	1(М4)
	нтж	НОВД+1-Н(М7)
	и	П7
	пе	за1б
	сч	ОЧКУС+15(М2)
	нтж	ОСПОД+5(М15)
	и	НАПРМГ
	по	зкэт
за1б	слиа	-1(М15)
	мода	зказ
	цикл	(М4) 
	пб	за1а
зк30	уиа	'3'
	сч	закдрб-1(М10)
зк2а	зп	Р7-Н(М7)
	по	зк4а
	нед	П77777
	уи	М4
	уи	М11
	сли	М4(М3)
	сч	выза-1(М10)
	и	E48(М11)
	пе	зк4 ЗАКАЗ УЖЕ ВЫДАН
	уиа	-6(М14)
зк2б	мода (М3)
	сч	шквып+6(М14)
	и	E48(М11)
	пе	зк4 ЗАКАЗ ВЫПОЛНЕН
	мода	зк2б
	цикл	(М14)
	уиа	'2003'
	сч	СЧЗАК
	слц	МИН17
	по	зк4в
	сч	ШЗЗОБМ
	пе	зк4в
	сч	выза-1(М10)
	чед	E2
	сда	64+2
	пе	зк4в
	сч	выза-1(М10)
	или	E48(М11)
	зп	выза-1(М10)
	сч	СЧЗАК
	слц	E1
	зп	СЧЗАК
	уи	М14
	сч	очкс1(М4)
	зп	ОЧКУС+15(М14)
	сч	оскус1(М4) 
	и	ПРИ+1-Н(М7)
	или	E20
	зп	АЗУ+2(М14)
	сда	64+39
	и	П7
	уи	М16
	сч	0
	зп	ФКУСАД(М14)
	уиа	(М2)
	сч	ШИМЛ
	по	зк2с
	слиа	1(М2)
зк2с	сч	ши(М4)
	зп	ШИМЛ(М2)
	счи	М2
	сда	64-6
	или	АЗУ+2(М14)
	зп	АЗУ+2(М14)
	мода	0
	пв	ПРЛ17А(М2)
	уии	М15(М3)
	пв	зквоч(М2)
	уии	М3(М15)
	сч	ОЧКСУМ+13(М1)
	чед	0
	сда	64+1
	пе	зк4
	сч	СМЕМЛ
	или	Е32-24(М1)
	зп	СМЕМЛ
зк4	сч	Р7-Н(М7)
	уиа	'3'
	нтж	E48(М11)
	пб	зк2а
за	слиа	-1(М12)
	мода	зкзан
	пино	0(М12)
	мод	НОВД-Н(М7)
	уиа	(М10)
	сли	М10(М3)
зк20	уиа	'3'
	сч	шквып-1(М10)
вп	зп	Р-Н(М7)
	уиа	'3'
	по	зк30
	нед	П77777
	уи	М4
	уи	М11
	сли	М4(М3)
	уиа	-6(М15)
вып	мода	(М3)
	сч	закдрб+6(М15)
	и	E48(М11)
	пе	вып1
	мода	вып
	цикл	(М15)
вып1	слиа	7(М15) НОМ. МГ, ГДЕ МЛ
	счи	М15
	зп	НОВД+2-Н(М7)
	уиа	-15(М15)
	уиа	'2003'
выпа	сч	ОЧКУС+15(М15)
	по	вып2
	и	E20
	по	вып2
	сч	ОЧКУС+15(М15)
	нтж	очкус1(М4)
	и	конс-Н(М7)
	по	вып3
вып2	мода	выпа
	цикл	0(М15)
выпл	уиа	-15(М15)
	сч	Р-Н(М7)
	нтж	E48(М11)
	пб	вп
вып3	сч	ОЧКУС+15(М15)
	сда	64+4
	нтж	НОВД+2-Н(М7)
	и	П7
	пе	вып2
	сч	ОЧКУС+15(М15)
	сда	64+42
	уи	М2
	сч	ВЫМГ
	и	Е48+15(М15)
	пе	вып2 УЖЕ БЫЛ
	сч	ВЫМГ
	или	Е48+15(М15)
	зп	ВЫМГ
	сч	отв(М3)
	сч	ТБЗН+6(М10) 
	слц	E1
	зп	ТБЗН+6(М10)
	и	E48(М11)
	по	вып5
	мод	НЗАД(М2)
	уиа	(М2)
	уиа	ВЫБМГ(М17)
	счи	М17
	зп	3(М2)
	сч	5(М2)
	и	E20
	или	ИВ25
	нтж	E19
	зп	5(М2)
	уиа	'3'
	сч	ОЧКУС+15(М15)
	сда	64-9
	рзб	ЕNR
	зп	0(М2)
	сч	ОЧКУС+15(М15)
	и	E18
	сда	64-22
	или	0(М2)
	слц	Е10П3
	нтж	E11
	зп	0(М2)
	сч	НОВД+2-Н(М7)
	вч	E1
	сда	64-5
	уи	М16
	сч	ОЧКУС+15(М15)
	сбр	НАПРМГ
	сда	64+42
	уи	М15
	сли	М16(М15)
	мода	ктзнмл(М3)
	сч	ктзнмл-24(М16)
	и	П7777
	мода	0
	пв	ПЕРЕВ(М15)
	сда	64-8
	или	в1-Н(М7)
	зп	2(М2)
вып5	сч	ОЧКУС+15(М15)
	и	П3
	пе	вып6
	сч	ОЧКУС+15(М15)
	и	Е21Е18
	нтж	Е21Е18
	по	вып6 ЗП
	и	E18
	пе	вып6 ЗП
	уиа	'3'
	мода	ктзнмл(М3)
	сч	оскус1(М11)
	сбр	ЗОБ
	сда	64+18
	слц	ДЕЛ
	или	E18
	слц	НОММЛ1
	зп	Р7-Н(М7)
	сч	ОЧЛИС+15(М15)
	уиа	'2003'
	зп	РМР
	сч	Р7-Н(М7)
	мода	0
	пв	ФИЗУР(М16)
вып6	уиа	'3'
	уии	М16(М11)
	сли	М16(М3)
	мода	зк20
	уиа	(М17)
	пб	зкы
зк4в	сч	РАОБЩ
	или	E25
	зп	РАОБЩ
зк4а	сч	0
	зп	шквып-1(М10)
	зп	выбз1-1(М10)
	зп	перст-1(М10)
	зп	просм-1(М10)
	сч	ВСЕЕД
	зп	вр-1(М10)
	сч	E18
	мода	0
	пв	ГАШЗАН(М15)
	сч	кус-Н(М7)
	пв	ФИЗЗП(М16)
	сч	кус-Н(М7)
	пв	DЕQ(М15) 
	сч	кус-Н(М7)
	пв	ТБУФ(М15)
	уиа	'3'
	пб	рабди-Н(М7) в НОМБОБ
*
	ЭКВИВ (У3,СМЕНА3-768)
*
ВЗУ	ВНЕШ	ФИЗУР,ФИЗЗП,зквоч
ДИСКИ	ВНЕШ	DЕQ,РАОБЩ
ДИСП70	ВНЕШ	НЕТНАП,ПЕРЕВ,ГАШЗАН,ТБУФ,ШЗЗОБМ,ИВ25,ФКУСАД,НЗАД
ДИСП70	ВНЕШ	РМР,МИН17,Е35П31,ВЫМГ,ШОЧМГ,АЗУ,П7,ТЗНМД,СЧЗАК
ДИСП70	ВНЕШ	НОММЛ1,НОММБ,П377,ШИФРПЛ,ШКМГОС,П60,П777,П37,П3
ДИСП70	ВНЕШ	П17,П14,Е21Е18,НАПРМГ,П7777,П77777,ВСЕЕД,ОЧЛИС
ДИСП70	ВНЕШ	ТЗНМЛ,ЕNR,Е10П3,ОЧКУС,ОЧКСУМ,ТБЗН,ОСПОД,ЯМ16
ДМЛМБ	ВНЕШ	ПРЛ17А,ВЫБМГ
КИТ	ВНЕШ	СМЕМЛ,ДЕЛ,ЗОБ,ШИМЛ,ММГРП
НОМБОБ	ВНЕШ	(Н,НОМБОБ),НВМЛ4,СМЛ1,возв,ЗАММЛ,ЕСТЬБ,перст
НОМБОБ	ВНЕШ	Р7,Р4,Р1,Р,общ,НОВД,выбз,очкс1,заме
НОМБОБ	ВНЕШ	конс,в1,ПРИ,номм,е30п28,кус,рабди,оскус1,очку1а
НОМБОБ	ВНЕШ	крмл,зон,ячпр,зас,зсса,закдрб,А,очкус1,ши
НОМБОБ	ВНЕШ	ктзнмл,выза,шквып,отв,выбз1,просм
НОМБОБ ВНЕШ вр
**
	ВХОД 	У3,ЗК2В,заказы
*
	ФИНИШ

Э71ТК СТАРТ ’10000’
 УПОТР Э71ТК(М7)
ИПЗ КОНД В’0’
 КОНД М41В’3’
 КОНД (20)В’0’
L КОНД В’500’       МАХ ДЛИНА СООБЩЕНИЯ
*
 КОНД В’0’    НЕ ЗАНИМАТЬ
*
ОШШ МОДА NЕW
 ПИНО (М16)
ОШ УИИ М15(М3)
 ПВ АВОСТА(М16)
ВХД НОП ,
 СЧ 2(М3)         ЕД.ЗАДАЧИ ПОЛЬЗ.
 ПВ ВШГП(М15)
ВХД2 СЧ 33(М7)
 НТЖ Е48
 СЛЦ 33(М3)
 ЗП 33(М3)
 СЧ Е48
 ЗП 33(М7)
        сч      Е10
 ПБ '4511'
*
*
 ПАМ 1   ПРИП
 КОНД В’26’         40 ЯЧ.ИПЗ(48-17 Р.Р. РАВНЫ 26 ЯЧ.ИПЗ)
 КОНД М47В’1’
ИС КОНД В’0’
МИНУС КОНД М40П’-’М32В’146’М24Х’FF’  СТАНД.ПРИГЛ.
 КОНД В’0’          44 ЯЧ.ИПЗ - ИСПОЛЬЗУЕТ ТВ40
Р КОНД В’0’
Р1 КОНД В’0’
О48П34 КОНД М33В’-1’В’-1’
*
 И П77
 ПБ ВЕGIN
БОПТТ ПАМ 5      ДЛЯ ПМВЫД
 ПАМ 9
ВЕGIN ПАМ 0
 УИ М2
 СЧ Е48-1(М2)
 МОД ТУСП-1(М2)
 УИА (М3)
 ЗП 2(М3)
 МОД 8(М3)
 УИА (М4)          М4:= <М16>
 МОДА АИС
 ПИНО (М4)          <М16> ≠ 0
ОПРПОД УИА ’2003’        О П Р О С   П О Д К Л Ю Ч Е Н И Я
 СЧ ЯЧОПР(М3)
 И ЕОПР
 ПЕ ОПРП2         ЕСТЬ ПРИЗНАК ’NЕW’
ОПРП1 СЧ ТЕРЕЗА(М3)    СТАРЫЙ ОПРОС
 И Е24П1
 УИА ’107’(М17)
 ПЕ ОШ            N ТЕРМИНАЛА > 24
 СЧ ТЕРЕЗА(М3)
 И ШКВИД
 СДА 64+24
 ИЛИ ТЕРЕЗА(М3)
ОПРП3 ЗП (М3)
 ПБ ВХД
*
ОЧИС1 СЛИА 1(М4)         ОБРАБОТАЛИ ИНФ.СЛОВО
 СЧ ИС
 И Е19
 ПЕ ОПРПОД        ОНО ПОСЛЕДНЕЕ
АИС УИА 57(М17)       М17:= N ПРИЧИНЫ
 УИИ М14(М4)
 МОДА ПМ2
 ПВ (М5)          ПРОВЕРКА НА ЧУЖОЙ ЛИСТ
 ЗП ИС
 НТЖ ВСЕЕД
 ПЕ АИС1          НЕ УСТАНОВКА ПРИЗНАКА ’NЕW’
* УИА ’2003’
 сч ЕОПР
 или ЯЧОПР(М3)
 ЗП ЯЧОПР(М3)
ОПРП2 СЧ ТЕРЕЗА(М3)    НОВЫЙ ОПРОС
 ПБ ОПРП3
АИС1 И Е43
 ПЕ НЕОПР         ВЫДАЧА ИЛИ ПРИЕМ
*
* ОПРОС ИЛИ ОТКАЗ. СТАРЫЙ ИЛИ НОВЫЙ *
*
 СЧ ЯЧОПР(М3)
 И ЕОПР
 ПО ОLD           СТАРЫЙ: <М5> = 0
 УИА (М5)          НОВЫЙ:   М5 := 0
 УИА -12(М16)
 ПБ ПКОРР
NЕW СЧ ВСЕЕД         ОПРАШИВАЮТСЯ ВСЕ ПОДКЛЮЧЕННЫЕ
 ПБ ФОМА
ОLD СЧ ИС            ВЫДЕЛЕНИЕ ОПРАШИВАЕМЫХ ИЗ ИС
 СДА 64-24
ФОМА ЗП Р1            МАСКА ОПРОСА ГОТОВА
 УИА ’2003’        У С Т.  Б Л П Р
 СЧ ТЕРЕЗА(М3)
 И ШКЭ71
 И Р1
 ЗП (М3)          РАБОТАЮЩИЕ -> СМ
 НТЖ ТЕРЕЗА(М3)
 И Р1
 ЗП Р             ШКАЛА ОПРОСА ГОТОВА
 СЧ ИС
 И Е26
 ПЕ ОТКАЗ         ОТКАЗ ОТ ТЕРМИНАЛОВ
 МОДА ОПОР3
 УИА (М13)         М13:= ОПОР3
 МОДА ОПОР4
 УИА (М14)         М14:= ОПОР4
 СЧ ИС
 И Е25
 ПО ОПОР2         ОПРОС ’БЕЗ ПРОВАЛА’
 СЛИА ОПОР3Б-ОПОР4(М14) М14 := ОПОР3Б
ОПОР2 СЧ Р
 ПО (М14)         ОПРОСИЛИ ВСЕХ
 НЕД
 УИ М2            М2:= N ТЕРМИНАЛА
 СЧ Р
 НТЖ Е48-1(М2)     ГАШЕНИЕ В ШКАЛЕ ОПРОСА
 ЗП Р
 ПБ (М13)
ОПОР3 СЧ ТСЛ-1(М2)     ПРОСМОТР ТСЛ
 И КОТ
 ПО ОПОР2         НЕТ КОДА ОТВЕТА
 НТЖ ТСЛ-1(М2)
 ЗП ТСЛ-1(М2)
 СЧМР 64
 МОДА ОПОР3А
 ПИО (М5)          NЕW
 СДА 64-28
 РЗБ Е48Е24        ОТВЕТ ПО СТАРОМУ
 СДА 64-1(М2)
 ПБ ОПОР3Б
ОПОР3А СДА 64-6          ОТВЕТ ПО НОВОМУ
 ЗП (М3)          48-46 - ТИП.25-26 - КОТ.1-15 - НОМЕР
 СЧИ М2
 ИЛИ (М3)
 ЗП (М3)
 СЧ ТСЛ-1(М2)
 СДА 64+АТ
 СДА 64-15
 ИЛИ (М3)          21-16 - ФИЗ.N ТЕРМИНАЛА
 ЗП (М3)
 СЧ ТСЛ-1(М2)
 И Q
 СДА 64-30
 ИЛИ (М3)
ОПОР3Б ЗП (М3)          КОД ОТВЕТА -> СМ
 ПБ ВХД
ОПОР4 СЧ (М3)
 ПО ВХД           НЕТ РАБОТАЮЩИХ И НЕ НОВЫЙ ОТЛАДЧИК
 СЧ ЯЧОК(М3)
 ИЛИ ЕОК
 ЗП ЯЧОК(М3)
 ПБ ВХД2          ЗАДАЧУ  Н Е   О Т К Р Ы В А Е М
НЕОПР СЧ ИС            АНАЛИЗ ВОЗМОЖНОСТИ I/О
 И Е44
 УИА (М16)
 ПЕ ПМ            ВЫДАЧА НА ОПЕРАТОРСКИЙ ТЕРМИНАЛ
ПКОРР УИА ’107’(М17)    ПРОВЕРКА ПРИНАДЛЕЖНОСТИ
 СЧ ИС            М2:= N ТЕРМИНАЛА
 СДА 64+12(М16)
 И П77
 УИ М2
 ПО ОШШ           N ТЕРМИНАЛА = 0
 ВЧОБ П60
 ПЕ ОШ
        сч      ШКАС
        или     ШКУСТР
        и       E48-1(М2)
        по      ОШ
 И ТЕРЕЗА(М3)
 ПО ОШ            НЕТ ТЕРМИНАЛА У ЗАДАЧИ
 МОДА ФОМА
 ПИНО (М16)         НОВЫЙ ОПРОС
 И ШКЭ71
 ПЕ ОШ            ТЕРМИНАЛ В РАБОТЕ
 СЧ ШКВЫД
 ИЛИ ШКОКВД
 ИЛИ ШКЗАК
 И Е48-1(М2)
 ПО ПМ            МОЖНО РАБОТАТЬ
 УИА ’2003’
 СЧИ М4
 ЗП 8(М3)
 СЧ 2(М3)         ЕД.ЗАДАЧИ ПОЛЬЗ.
 ИЛИ МЫЖДЕМ
 ЗП МЫЖДЕМ
 ПБ ВХД2
ПРОСВЯ И ШСВЯТ         СВЯЗНЫЕ ЛИНИИ
 ПО РЕКОРД        НА ВЫДАЧУ
 СЧ ТСЛ-1(М2)
 СДА 64+АТ
 УИ М6
 СЧ 9(М3)         <М15> ИЗ ИПЗ
 СДА 64-45
 НТЖ ТАС(М6)
 И Е48П46
 НТЖ ТАС(М6)
 ЗП ТАС(М6)       ТАС(N)[48:46 Р.]:=N ЭВМ-АДРЕСАТА
 ПБ РЕКОРД
ПМ УИА 58(М17)       ПРОВЕРКА МАССИВА
 УИА 24(М16)
 МОДА ПМ1
 УИА (М5)
ПМ1 УИИ М13(М14)      А НАЧ -> М13.А КОН -> М14
 СЧ ИС
 СДА 64(М16)
 И П7777
 УИ М14
 СЧ ИС
 СДА 64+20(М16)
 И П17
  ПО ПМ4           ИР НЕ УКАЗАН
 НТЖ П17           А:=А+<ИР>
 УИ М15
 МОДА (М3)
 МОД 7(М15)
 СЛИА (М14)
ПМ4 НОП ---
 СЛИА -24(М16)
 МОДА ПМ2
 ПИО (М16)         ’ВЫТАЩИЛИ’ А НАЧ
 СЛИА ПМ3-ПМ1(М5)
 СЧИ М13
 ЗП Р
 СЧИ М14           А КОН
 УИА 34(М17)
 ВЧ Р
 ПЕ ОШ            А НАЧ. > А КОН
 СЛЦ Е1
 СЛИА 1(М14)
 УИ М11           М11:= ДЛИНА МАССИВА
 ВЧ L
 ПЕ ПМ2           ДЛИНА МАССИВА (LМ) ≤ МАХ
 УИА ’500’(М11)    LМ := МАХ
 МОДА (М13)         А КОН:= А НАЧ + LМ
 УИА ’500’(М14)
ПМ2 СЧИ М14           ПРОВЕРКА НА ЧУЖОЙ ЛИСТ
 ЗП 41(М7)
 МОДА ОШ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА 0
 СЧ (М14)
 ПРЕ ’2003’
 ПБ (М5)
ПМ3 СЧ ИС            Е44 *
 И Е44
 ЗП 42(М7)
 ПО Э71           НЕ ВЫДАЧА НА ОПЕРАТОРСКИЙ
 УИА -13(М17)
 МОДА ОЧИС1
 УИА (М15)
 УИИ ’32’(М15)
ПЕРТ УИА 0             ПЕРЕНОС С ПОЛЯ МАТЕМАТИКА В Э71ТК
 МОДА (М17)
 СЧ 13(М13)
 УИА 3
 МОДА БОПТТ
 ЗП 13(М17)
 МОДА ПЕРТ
 ЦИКЛ (М17)
 СЧ Е23
 МОДА БОПТТ
 УИА (М16)
 ПБ ТВ40          ВЫДАЧА
*
*      Э    7 1
*
Э71 НОП ,
 МОД ТСЛ-1(М2)
 УИА (М15)          М15 := АДРЕС НАЧАЛЬНОГО РЕКОРДА
        уиа     -13(М12)
 СЧ ШЗПРИМ        ’1’->ШЗПРИМ:
 ИЛИ Е48-1(М2)     I - ДО ВЫДАЧИ ’РАЗРЕШЕНИЯ’
 ЗП ШЗПРИМ        О - ДО КОНЦА ПОДГОТОВКИ МАССИВА
 СЧ ШКЭ71
 ИЛИ Е48-1(М2)     ’1’ -> ШКЭ71 = З А Х В А Т
 ЗП ШКЭ71
 СЧ Е37
 ИЛИ Е40
 И ИС
 ИЛИ ТСЛ-1(М2)
 ИЛИ КОТ
 НТЖ КОТ           ГАШЕНИЕ КОТ-А ( НА ВСЯКИЙ СЛУЧАЙ )
 ЗП ТСЛ-1(М2)
 И Е40
 НТЖ Е40
        уии     М1(М15) 
 СДА 64-9(М2)
 ИЛИ ШКВЭ71        УСТАНОВКА Е В ШКВЭ71
 ЗП ШКВЭ71
 И Е48-1(М2)
 ПЕ ПРОСВЯ        *** В Ы Д А Ч А ***
 СЧ ТСЛ-1(М2)                    *** П Р И Е М ***
 ИЛИ ЕСЛВ          ЕСЛВ -> ТСЛ ДЛЯ ВЫДАЧИ ПРИГЛАШЕНИЯ
 ИЛИ ЕПРИГ
 ЗП ТСЛ-1(М2)
 СЧ Е37
 ИЛИ Е39
 И ИС
 ПЕ INS           ПРИГЛАШЕНИЕ НЕСТАНДАРТНОЕ
 СЧ МИНУС
 УИА 0             ЗАПИСЬ ’-’ МАТЕМАТИКУ
 ЗП (М13)
 УИА ’2003’
INS СЧ ИС            ПРИЕМ СЕКРЕТНЫЙ
 И Е38
 ПО РЕКОРД        НЕТ
 СЧ ТАЙНА         ’1’ -> ТАЙНА
 НТЖ Е48-1(М2)
 ЗП ТАЙНА
РЕКОРД НОП ,
 УИА 3
 СЛИА -1(М2)
        счи     М13
        сда     64-15
        или     E48
        зп      Р1
        сч      
        зп      E48(М3)
A11701  счи     М15
        зп      E38(М3)
        зп      ТБЛ(М2)
        слиа    -23(М11)        
        сч      Р1
        слц     32(М7)
        зп      3(М15)
        сч      Е46Е45
        зп      (М15)
        сч      П60
        зп      4(М15)
        сч      
        зп      1(М15)
        уиа     -26(М16)       
РОСРЕК НОП ,            РОСПИСЬ РЕКОРДА
 МОДА (М15)
 ЗП 31(М16)
 МОДА РОСРЕК
 ЦИКЛ (М16)
        слиа    '40'(М15)       
        счи     М15
        зп      Р
        сч      ИС
 И Е40
 ПЕ ПРИЕМ
        счи     М12
        зп      '66'(М7)
 СЛИА Н(М1)
* ЗАПРЕК НОП ,
 ПВ ДЛЯЭ71+1(М16)
 УИА 3
 МОДА ОЧИС1
 ПИО (М13)         М13 = 0 - ВСЕ ПЕРЕПИСАЛИ В РЕКОРДЫ
        мод     БОПТТ+13
        уиа     0(М12)
        пб      A11724
*
*     П Р И Е М
*
ПРИЕМ НОП ,
 СЧИ М11
 ПО РАВНО
 И Е15
 ПЕ ХВАТИТ
A11724  сч      ТСЛ-1(М12)
        пе      A11727
        сч      E48(М3)
        пе      A11730
        счи     М12
        зп      E48(М3)
A11727  и       '72050'
        пе      A11735
A11730  мода    A11724
        цикл    (М12)
        мод     E48(М3)
        уиа     0(М12)
        сч      
        пв      ТБУФ(М15)
        сда     64-8
        или     '72050'
        зп      ТСЛ-1(М12)
        уиа     '3'
A11735  нед     П77777
        уи      М16
        сда     64-5
        уи      М15
        уиа     '2003'
        сч      E48(М16)
        нтж     ТСЛ-1(М12)
        зп      ТСЛ-1(М12)
        уиа     '3'
        мод     ТСЛ-1(М12)
        слиа    0(М15)
        счи     М15
        зп      ТБЛ(М2)
        уии     М1(М15)
        нтж     Р
        по      A11750
        счи     М15
        мод     E38(М3)
        зп      1
        сч      E38(М3)
        зп      2(М15)
        пб      A11701
A11750  слиа    -32(М11)
        мод     E38(М3)
        сч      3
        слц     E6
        мод     E38(М3)
        зп      3
        уиа     -31(М16)
        слиа    -9(М1)
        сч      
        пб      РОСРЕК
РАВНО   ноп
ХВАТИТ  мод     E38(М3)
        уиа     0(М16)
        мод     3(М16)
        слиа    0(М11)
        счи     М11
        или     Р1
        зп      3(М16)
 МОДА ОЧИС1
 УИА (М16)
        сч      ТСЛ(М2)
        зп      ТБЛ(М2)
 ПБ ДЛЯЭ71        ВЫДАЧА ПРИГЛАШЕНИЯ
*
*
*
*
ОТКАЗ МОДА ОТКЗ          М14:= ОТКЗ
 УИА (М14)
 МОДА ОПОР2         ЗА НОМЕРОМ ТЕРМИНАЛА
 ПВ (М13)
 СЧ ТСЛ-1(М2)
 И ГТСЛ          ГАШЕНИЕ N ЗАДАЧИ В ТСЛ
 ЗП ТСЛ-1(М2)
 СЧ ШЗНКТ
 НТЖ Е48-1(М2)
 ЗП ШЗНКТ         ТЕРМИНАЛЫ ОСВОБОЖДЕНЫ
 И ШКОТЛ
 ЗП ШКОТЛ
 СЧ ШЗНКТ
 И ТЕРЕЗА(М3)
 ЗП ТЕРЕЗА(М3)
 ЗП (М3)          ОСТАВШИЕСЯ -> СМ
 И ШКОТЛ
 ПЕ ОПОР2
 СЧ АВОС(М3)
 И О48П34
 ЗП АВОС(М3)
 ПБ ОПОР2         ЗА N ОЧЕРЕДНОГО ТЕРМИНАЛА
ОТКЗ СЧ ШКИВ
 ИЛИ Е36
 ЗП ШКИВ          ВЫЗВАТЬ ПЛАНИРОВЩИК
 ПБ ВХД
*Р1 ПАМ 1
*
*
МОТТ ВНЕШ ЕСЛВ,ЕПРИГ,ШКОТЛ
*МОТТ ВНЕШ ПОИСК,НУЖРЕК,ДЛИНА,ШАКТИ,ЕАКТИВ,ШКРАБН
МОТТ ВНЕШ КОТ,ШЗПРИМ,ТАЙНА,ШЗНКТ
МОТТ ВНЕШ ШКАС,ШКУСТР,ШКЭ71,ШКВЭ71,(ШКВИД,ШКАЛ),МЫЖДЕМ
МОТТ ВНЕШ ШКВЫД,ШКОКВД,ШКЗАК,Е48Е24,ШСВЯТ,ГТСЛ
*МОТТ ВНЕШ ТСВ,ДЕЛЮ1
ДИСП70 ВНЕШ ТСЛ,ТБУФ,ТВ40,ШКИВ,ШКСВН,ШКНПП
ДИСП70 ВНЕШ АВОСТА,ШГ,ТУС,ТЕРЕЗА,П17,П77,П7777,(П77777,Е15П1)
ДИСП70 ВНЕШ П60,ВСЕЕД,(Q,Е18П16),Е24П1,Е46Е45
ДИСП70 ВНЕШ Е48П25,Е48П43
ДИСП70 ВНЕШ ТУСП,(Е48П46,Е46П48),ТАС
ВИСП ВНЕШ ВШГП
СОСТАВ ВНЕШ АТ,АВОС
ТЕРМ ВНЕШ ДЛЯЭ71
БОЭК1 ВНЕШ ЗАПВЫХ
КАЧКА ВНЕШ Е48П17
КИТ ВНЕШ ТБЛ
 Е
 М
 ЭКВИВ (Н,9)
 ЭКВИВ (ЯЧОПР,'66'),(ЕОПР,Е27)
 ЭКВИВ (ЯЧОК,'66'),(ЕОК,Е28)
 ФИНИШ

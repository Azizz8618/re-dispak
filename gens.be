ГЕНС	СТАРТ	'70000'
	Б
	М
	Е
*МЫ SЕТА ∧ВS
*∧С УСТВ 1   ИМЕЕТ ДР.СМЫСЛ,ЧЕМ ДЛЯ ОБЩИХ ДИСКОВ СВС
*∧Ф УСТВ 0
*∧НИ УСТВ 0
* GВLВ ∧Р,∧МВВ
*∧МВВ УСТВ 0
*∧Р УСТВ 0
*
***** ИНФОРМАЦИЯ К ГЕНС: *****
*
NМЛ ЭКВ ’71740’ НОМЕРА СИСТЕМНЫХ ЛЕНТ
МДС ЭКВ ’71741’ НОМЕР МД СТАТИСТИКИ
МВВ ЭКВ ’71742’ НОМЕР МД ВВОДА
МДВ ЭКВ ’71743’ НОМЕР МД ВЫВОДА
ПРОГОН ЭКВ ’71744’
СЛОЙК ЭКВ ’71745’ ВИД НАБИВКИ П/К,N МЛ ДЛЯ СВЯЗИ
МБКЧТ ЭКВ ’71746’ НАЧ.N ТРАКТА ДЛЯ КОНТРОЛЯ ЗП НА МЛ
ДЛТ ЭКВ ’71747’ ДЕЛЬТА ДЛЯ РАБМБ МДРЗО
NМБ ЭКВ ’71747’
ПМБ ЭКВ ’71750’ НОМЕР ПРОГРАМНОГО МБ
РАСПП ЭКВ ’71751’ ТАБЛИЦА СТРАНИЦ ОП
РАСП1 ЭКВ ’71752’
ШАС128 ЭКВ '71753'	ШКАЛА ТЕРМИНАЛОВ АС 64Р : 27
РАСП3 ЭКВ ’71754’
ТРАКТЫ ЭКВ ’71755’ МАХ ЧИСЛО ТРАКТОВ И ЛИСТОВ ДЛЯ1ЗАДАЧ
NБЭСМ ЭКВ ’71756’ НОМЕР МАШИНЫ,ПРИЗНАК ЧАСОВ ИПМ
КГОД ЭКВ ’71757’ МЛ.ЦИФРА ГОДА,ЧИСЛО И НАЧ.N ОБЩ.ДИСК
ВЛА ЭКВ ’71760’ ПЕРВ. СИМВОЛЫ ВЛАДЕЛЬЦА ЭВМ
ВИДМ ЭКВ ’71761’ ВИД ПОДКЛ.УВВК (ЗАВОДСКОЙ ИЛИ ИПМ)
УСТРМД ЭКВ ’71762’  NN УСТР.МД (48-17РР) 3-ГО НАПР.
МБС5 ЭКВ ’71763’ NN УСТР.МД (48-17РР) 4-ГО НАПР.
НОМРД ЭКВ ’71764’ МД РАБ ЗОН,РАБ МД
ВЛА1 ЭКВ ’71765’ ПОСЛ.6 СИМВ.ВЛАДЕЛЬЦА ЭВМ
НОМУСТ ЭКВ ’71766’ ТАБЛИЦА УСТРОЙСТВ ТТ И СОNSUL
ШКУСТР ЭКВ ’71767’
ШКОПТ ЭКВ ’71770’
ШКДТ ЭКВ ’71771’
ШКСВ ЭКВ ’71772’ ШКСВМ ДЛЯ ДИСП70 (+1 ЯЧЕЙКА ДЛЯ ТБ)
NТАС ЭКВ ’71773’ ФИЗ. НОМЕРА ТЕРМ. АС
ШКТТ ЭКВ ’71774’ В 1-Й ЯЧ ЗАНЯТ 48
ШКТТ2 ЭКВ ’71775’
ШКVТ ЭКВ ’71776’ ЗАНЯТЫ 48
ШКVТ2 ЭКВ ’71777’
*
*

	рег	'101' !МАЛ. БРО = 1, БЛОКИРОВКА ОСТАНОВА
* ПРИ ПРЕР. ПО АУ
* (ДЕЛЕНИЕ НА НУЛЬ, ПЕРЕПОЛНЕНИЕ, КОНТРОЛЬ ЧИСЛА)
	ржа	'3'
	уиа	1(М16)
	счи	М16
	сда	64-47
	уиа	-47(М16)
ИИ	зп	E1(М16)
	сда	64+1
	цикл	ИИ(М16)
	пам	0
	уиа	ФК1МЫ(М3)	ДЛЯ ВЫХОДА ПРИ ВРЕМ. ПРЕРЫВ.
	сч	
	рег	'37'	ГАШ. ГРП.
	сч	E40
	рег	'36'	ОТКРЫТИЕ МАСКИ ВРЕМЕННЫХ ПРЕРЫВАНИЙ
	уи	
ад	уиа	'3'
	слц	НЕТОБМ	СЛ КОД
	зп	КРОСП
	пб	ад
ФК1МЫ	сч	
	рег	'36'
	сч	3
	и	П16
	зп	КОДТР
	уиа	-2(М16)
фкус	сч	ТЗФСТР+1(М16)
	и	НАПД
	зп	ГЕНС+2(М16)
	цикл	фкус(М16)
	пио	РОСМБ(М13) НЕ С ПК
	сч	МБС5
	сда	64+3
	и	П7
	уи	М16	N МБ С ИНФ. И ВЫЗОВОМ
	сч	МБС5
	и	П7
	сда	64-7
	зп	КУСБ-1(М16)
	сч	МБС5
	сда	64+6
	и	П37
	сда	64-2
	или	E13
	или	E19
	или	КУСБ-1(М16)
	зп	КУСБ-1(М16)
	пв	ЗКМБ(М11)	ЗАПИСЬ ГЕНС НА МБ
РОСМБ	сч	1
	и	E48
	по	РОСМБ1
	зп	вж
	уиа	шкмб1(М1) !ГЕНС-117 ЗНАЧ "-5119(М1)"
	уиа	'74001'(М1)
	сч	дб
росли	зп	'7777'(М1)
	цикл	росли(М1)
	уиа	'76001'(М1)
	сч	все1
рлс	мода	КЗ1
	зп	'1777'(М1)
	цикл	рлс(М1)
	уиа	8(М3)
	уиа	ТБД(М2)
	уиа	зз8(М4)
зз	уиа	'76001'(М1) !ОТРИЦ.
	сч	
зз1	мода	(М2)
	слц	'1777'(М1)
	цикл	зз1(М1)
	уиа	-7(М1)
зпз	мода	7(М1)
	зп	(М3)
	слц	E1
	цикл	зпз(М1)
	пб	(М4)
зз8	уиа	16(М3)
	уиа	КРАХМЛ+4(М2)
	пв	зз(М4)
	уиа	'70'(М3)
	уиа	КЗ1(М2)
	пв	зз(М4)
	уиа	-7(М1)
выбтз	зп	2
	цикл	выбтз(М1)
	сч	к1
	увв	'1'
	уи	
	сч	к2
	увв	'2'
	уи	
	сч	6
	увв	'22'
	уи	
	сч	к4
	увв	'7'
	уи	
пупи	рег	'237'
	и	E46
	по	пупи
	рег	'237'
	и	E45
	по	пупи
	рег	'237'
	и	E25
	по	пупи
кукк	уиа	-55(М1)
	сч	E5
росин	зп	'77'(М1)
	цикл	росин(М1)
	уиа	шкмб1(М1)
	сч	E12
росэ	мода	5119(М1) !ШТАТНО
	зп	ТБД
	цикл	росэ(М1)
	уиа	-7(М1)
росэ1	зп	2
	цикл	росэ1(М1)
	сч	
	рег	'37'
	уи	
	сч	вку
	пв	ЧТМД1(М11)
	сч	E28
	нтж	ВСЕЕД
	рег	'37'
	уи	
	сч	вку1
	или	4
	пв	ЧТМД1(М11)
пов8	сч	E29
	нтж	ВСЕЕД
	уи	
	рег	'37'
	сч	к1
	увв	'1'
	уи	
	сч	к2
	увв	'2'
	уи	
	сч	6
	увв	'22'
	уи	
	сч	к17
	увв	'7'
	уи	
ЖДУПРЕ	рег	'237'
	и	E46
	по	ЖДУПРЕ
	рег	'237'
	и	E45
	по	ЖДУПРЕ
	рег	'237'
	и	E25
	по	ЖДУПРЕ
	рег	'237'
	и	E29
	по	ЖДУПРЕ
	рег	'237'
	и	E28
	по	ЖДУПРЕ
	уиа	4(М16)
	сч	вку
	зп	КУСД1
	пв	ПРЕР(М1)
	уиа	3(М16)
	сч	вку1
	зп	КУСД1
	пв	ПРЕР(М1)
ждупр1	уиа	'70'(М3)
	уиа	КЗ1(М2)
	пв	кс9(М4)
ждупр2	пб	кукк
вж	пам	1
вку	конд	в'0040050001450000'
вку1	конд	в'0000002001460000'
к1	конд	п'0004Б0'
к2	конд	в'0000000001030600'
к4	конд	в'0000000001041600'
	конд	п'0006Б0'
	конд	в'0000000001430600'
к17	конд	в'0000000001441600'
все1	конд	в'1111111111111111'
дб	конд	в'4102041020410204'
кс9	уиа	'76001'(М1)
	сч	
кс1	мода	(М2)
	слц	'1777'(М1)
	цикл	кс1(М1)
	нтж	(М3)
	по	(М4)
	стоп	1
	пб	(М4)
РОСМБ1 УИА -15(М3)
 ПВ РБ(М12) ОБОБЩ.ПРИЗН.РОСПИСИ МБ
 УИА (М14)
 УИА СТАТИ(М12)
	сч	
	пб	зпд	!МАЛ. 000100000003071511
*ДЛЯ ГАШ. РЕГ. ДЕФ. ПИТАНИЯ 06.12.2000
СТАТИ	уиа	0(М4)
	уиа	11(М1)          зона 611 пгс
	пв	ЧТГП(М11)
	уиа	6(М1)
	пв	ОБМПМБ(М15)
	уиа	14(М1)          зона 725 авмл
	пв	ЧТГП(М11)
	уиа	10(М1)
	пв	ОБМПМБ(М15)
* ПЕРЕПИСЬ КОНФУС С 712 ЗОНЫ НА 32 ТРАКТ
	уиа	-23(М4)
 УИА 2(М1)
 ПВ ЧТГП(М11) ЧТ.561-610 ЗОН НА 01-30 ЛЛ.
	сч	E44
	или	E45
	и	МДС
	по	зу
	сч	E1
	зп	прпе
	уиа	3(М16)
	пб	зу11
АЦП	уиа	0(М1)
	пв	ОБМИМБ(М15) ЧТ. НА 23 Л 6ТР. ИНФ. МБ.
 УИА -6(М4)
 УИА 1(М1) НА 1-7 СТР
 ПВ ЧТГП(М11)    ЧТ-Е 41-47 ЗОН(ДИСП,КАЧКА,ДИСКИ,КИТ)
*БЛОК ВЫЗОВА НЕРЕЗИДЕНТНЫХ ЧАСТЕЙ
*********************************
 УИА 22(М1)      ЧТЕНИЕ 1-ГО НЕРЕЗИДЕНТА
М244 УИА (М4)
 СЧ ЛСТБАЗ
 СДА 64-2
 ИЛИ КУСЧТЗ(М1)
 ЗП КУСЧТЗ(М1)
 СЧИ М7
 ЗП ЗАПМ7
 ПВ ЧТГП(М11)  ВЫЗОВ НЕР.ЧАСТИ
 СЧ ЗАПМ7
 УИ М7
 СЧ ЛСТБАЗ
 УИ М6
 ПБ (М6)       НАЧИНАЕТ РАБОТАТЬ НЕРЕЗИДЕНТ
КОН1 СЧ ЛСТБАЗ
 СДА 64-2
 ИЛИ КУСГ2
 СЛЦ НОММЛ
 УИИ М16(М6)
 ПБ ФИЗОБМ	ГЕНС2
кон2	уиа	24(М1)
	пб	М244
*****
вых777	сч	ЛСТБАЗ
	сда	64+10
	уи	М6
	уиа	'2003'
	сч	ТЗФСТР
	нтж	E48(М6)
	нтж	ЛИСТА3
	или	ЗАП20Р
	зп	ТЗФСТР
	пб	'60'(М7)
*** *** *** *** *** *** *** *** *** *** *** ***
*  ПО ОКОНЧАНИИ ГЕНЕРАЦИИ ВЫХОД ЧЕРЕЗ ГЕНС2   *
*** *** *** *** *** *** *** *** *** *** *** ***
А СЧ ЛСТБАЗ
 УИ М6
 ПБ АЛИСТЫ-ГЕНС1(М6)
ПОИСК СЧ ЛСТБАЗ
 УИ М6
 ПБ ПОИСКС-ГЕНС1(М6)
ОБЩГ СЧ ЛСТБАЗ
 УИ М6
 ПБ АОБЩГ-ГЕНС1(М6)
общ1	сч	ЛСТБАЗ
	уи	М6
	пб	аобщ1-ГЕНС1(М6)
вав	счи	М6
	зп	запм6
	сч	ЛСТБАЗ
	уи	М6
	пб	вав1-ГЕНС1(М6)
запм6	пам	1
*ПЕРЕПИСЬ СТРАНИЦ  *
* N СТРАНИЦЫ ЧТЕНИЯ В М1
* N СТРАНИЦЫ ЗАПИСИ В М2
* ЧИСЛО СТРАНИЦ ПЕРЕПИСИ В М4
* ВЫХОД ПО М15
*
ПЕРЕПС УИА -1023(М17)
Б2 МОДА 1023(М17)
 СЧ (М1)
 МОДА 1023(М17)
 ЗП (М2)
 ЦИКЛ Б2(М17)
 СЛИА 1024(М1)
 СЛИА 1024(М2)
 ЦИКЛ ПЕРЕПС(М4)
 ПБ (М15)
нпк	пам	1
зу11	уиа	-3(М1)
	сч	МДС
	и	E48(М16)
	по	зун
	сч	
	зп	РЯ3
зур	мода	-3(М16)
	сч	УСТРМД
	сда	64+16
	и	Е32П25+3(М1)
	по	зуст
	уии	М6(М1)
	слиа	3(М6)
	счи	М6
	сда	64-37
	зп	КУСД1
	пб	ЗАПР
зу1	уиа	-1(М6)
	уиа	3(М11)
зус	уии	М4(М6)
	слиа	17(М4)
	счи	М4
	увв	'20'(М16)
	уи	
	увв	'4000'(М16)
	уи	
	зп	РЯ2
	сч	
	увв	'20'(М16)
	уи	
	уиа	-12(М4)
зу2	слиа	3(М4)
	сч	П7
	сда	64(М4)
	зп	РЯ1
	и	РЯ2
	по	зув
	сч	NБЭСМ
	и	П7
	сда	64(М4)
	нтж	РЯ2
	и	РЯ1
	пе	зу3
	сч	РЯ1
	нтж	ВСЕЕД
	и	РЯ2
	зп	РЯ2
зув	сч	РЯ3
	или	E12(М11)
	зп	РЯ3
зу3	слиа	-1(М11)
	пино	зу2(М4)
	уии	М4(М6)
	слиа	15(М4)
	счи	М4
	увв	'20'(М16)
	уи	
	сч	РЯ2
	увв	'20'(М16)
	уи	
	сч	
	увв	'20'(М16)
	уи	
	уиа	7(М11)
	цикл	зус(М6)
	уиа	'61'(М4)
	счи	М4
	увв	'20'(М16)
	уи	
	увв	'4000'(М16)
	уи	
	или	РЯ3
	нтж	РЯ3
	зп	РЯ3
	уиа	'57'(М4)
	счи	М4
	увв	'20'(М16)
	уи	
	сч	РЯ3
	увв	'20'(М16)
	уи	
	сч	
	увв	'20'(М16)
	уи	
	пв	ОСВКМД(М6)
зуст	цикл	зур(М1)
зун	счи	М16
	и	E3
	пе	зу4
	уиа	4(М16)
	пб	зу11
зу4	сч	
	зп	прпе
зу	ноп
 УИА ’50000’(М17)
 СЧ ВЛА
 ЗП ВЛАД-ПЧКЗ(М17)
 ЗП ВЛИ-ЭКСТ-1024(М17)
 СЧ ВЛА1
 ЗП ВЛАД+1-ПЧКЗ(М17)
 ЗП ВЛИ-ЭКСТ-1023(М17)
 СЧ ВЕ-ПЧКЗ(М17)
 ЗП ВЕ1-ЭКСТ-1024(М17)
 СЧ П17
 СДА 64-20
 И КГОД
 СДА 64-4
 ЗП РЯ3
 СЧ П17
 СДА 64-16
 И КГОД
 СДА 64-16
 ИЛИ РЯ3
 ИЛИ ПЧГОД-ПЧКЗ(М17)
 ЗП ПЧГОД-ПЧКЗ(М17)
	уиа	-23(М4)
	уиа	0(М1)	ЗАПИСЬ 1-30 СТР. НА 0-27 ТР
	пв	ОБМПМБ(М15) ПРОГРАММНОГО МБ
	уиа	15(М1)  циркул
	пв	ЧТГП(М11)
	уиа	11(М1)
	пв	ОБМПМБ(М15)
	уиа	17(М1)  прик4
	пв	ЧТГП(М11)
	уиа	13(М1)
	пв	ОБМПМБ(М15)
	сч	ТРАКТЫ
	и	Е36     графопостр.
	по	устрЕС	НЕТ ГП НА ДАННОЙ Б-6
	рег	'101'
	сч	шкмб2
	зп	'4045'+1024 (15р прп)
	уиа	12(М1)      гп1 гп4
	пв	ЧТГП(М11)
	уиа	7(М1)
	пв	ОБМПМБ(М15)
	уиа	8(М1)
	пв	ОБМПМБ(М15)
устрЕС	сч	СЛОЙК
	сда	64+39
	и	П17
	по	АЦП
	уиа	7(М1)   ЕСПИ, ЕСПЛ
	пв	ЧТГП(М11)
	уиа	-1(М14)
циклес	сч	СЛОЙК	!ГЕНС-117 МЕТКА "ЕСАЦП"
	и	E40(М14)
	по	нетуст
	мода	4(М14)
	уиа	0(М1)
	пв	ОБМПМБ(М15)	ЗП НА МБ
нетуст	цикл	циклес(М14) !ГЕНС-117 МЕТКА "ЕСА"
	пб	АЦП
*
***** БЛОК ОДНОВРЕМЕННОГО ПОДГОНА ЛЕНТ К РАЗМЕЧЕННОЙ ЧАСТИ *****
* ВЫХОД ПО М12, ИСПОЛЬЗУЕМЫЕ МОДИФИКАТОРЫ: 1,2,3,11,15
*!ГЕНС-117 СТР.608/530
ПОДГ4	рег	'237'
	и	E40
	по	РОСПМБ
	нтж	ВСЕЕД
	уи	
	рег	'37'	ГАШ 40Р. ГРП
	сч	СЧВЫД
	слц	E1
	зп	СЧВЫД
	сда	64+2 ПОВЫШЕНИЕ ВЕСЫ ЕДИНИЦЫ "СЧВРЕМ" В СВЯЗИ С
	зп	СЧВРЕМ УВЕЛИЧЕНИЕМ ВРЕМЕНИ НА ОТБРАКОВКУ ТРАКТОВ
	пб	РОСПМБ
*
*** БЛОК ЧТЕНИЯ С МД ЗАГРУЗКИ ***
* НОМЕР НА КУС НА М1
* ЧИСЛО ЗОН НА М4, ВЫХОД ПО М11
*
ЧТМДВ	уиа	ГЕНС+1(М6)
	пб	ЧТСМД	ЧТ С МД ВВОДА
ЧТГП	уиа	ГЕНС(М6)
	пб	ЧТСМД
ЧТСМД	сч	(М6)
	или	КУСЧТЗ(М1)
ЧТМД1	зп	КУСД1	ФОРМ. КУС
	пв	ЧТМД(М1)
	сч	КУСД1
	слц	E13
	слц	E25
	цикл	ЧТМД1(М4)
	сч	ЛСТБАЗ
	уи	М6
	пб	(М11)
КУСЧТЗ КОНД М24В’546’М17В’3’М12В’20’ Б.КАТ С ’ТЕЛЕ’(0)
 КОНД М24В’621’М17В’3’М12В’1’ ДИСП70,КИТ,ДИСКИ,КАЧКА (3)
 КОНД М24В’561’М17В’3’М12В’01’ ЗОНЫ 561-600 свс: л11
 КОНД М24В’545’М17В’3’М12В’22’ КАТАЛОГ ШИФРОВ И ЗОН ВВОДА
 КОНД М24В’540’М17В’3’М12В’24’   ФИО
 КОНД М24В’755’М17В’3’М12В’32’ ДМЛМБ,ХЛАМ свс:0465, л25
 КОНД М24В’706’М17В’3’М12В’20’ нижний уровень устройств ЕС
 КОНД М24В’717’М17В’3’М12В’30’ ЕСПИ, ЕСПЛ
 КОНД М24В’677’М17В’3’М12В’30’ ДОПОЛН.РЕЗИДЕНТ
 КОНД М24В’671’М17В’3’М12В’31’ ЕСМЛ свс: 0520 табкод
 КОНД М24В’756’М17В’3’М12В’33’ ДМЛМБ,ХЛАМ свс: 0526 авост
 КОНД М24В’611’М17В’3’М12В’1’     ПГС
 КОНД М24В’730’М17В’3’М12В’30’    ГП1 ГП4
 КОНД М24В’726’М17В’3’М12В’14’    какие-то данные
 КОНД М24В’725’М17В’3’М12В’1’     авмл
 КОНД М24В’531’М17В’3’М12В’30’    ЦИРКУЛ
 КОНД М24В’712’М17В’3’М12В’1’  712 ЗОНА :КОНФУС НА СВС
 КОНД М24В’534’М17В’3’М12В’30’    ПРИК4
 КОНД М24В’550’М17В’3’М12В’20’    каталог
 КОНД М24В’520’М17В’3’М12В’32’    19 ТУПР свс: л32
 КОНД М24В’475’М17В’3’М12В’6’    БОЭК1-ВИСП-АВМД свс: л7
 КОНД М24В’751’М17В’3’М12В’21’      751 ЗОНА
 КОНД М24В’552’М17В’3’  КУС ЧТЕН.1 НЕРЕЗ.ГЕНСА
КУСГ2 КОНД М24В’471’М17В’1’     ГЕНС2
  КОНД М24В’414’М17В’3’М12В’20’ читается по ТМГУ ТЕРМАС
ЧТМД СЧ КУСД1
 СДА 64+7
 И П7
 УИ М17 N МД
 НТЖ П7
 УИ М15
 СЧ КУСД1
	сда	64+39
	и	П7
 УИ М16     N НАПРАВЛЕНИЯ
ЗАПР НОП
 СЧ 0
	рег	'37'
	сч	КОСВ
	увв	'20'(М16) !МАЛ. ОСВОБ. КМД
	уи	
	сч	КУСД1
	сда	64+37
	и	Е2Е1
	или	E13
	или	E10
	или	E9
	увв	'20'(М16)	*С ЗАПРОС КМД
	уи	
запр1	рег	'237'
	и	E32(М16)
	по	запр1	ОЖИДАН. ПРЕРЫВ.
	сч	прпе
	пе	зу1
	уии	М6(М16)
*****
	сч	КАЧКА
	зп	нпк
****
	пе	д31
	сч	КУСД1
	сда	64+37
	и	Е2Е1
	сда	64-3
	уи	М6
	сли	М6(М17)
	сч	УКАЧП(М16)
	и	E32(М6)
	пб	д32
д31	слиа	-3(М6)
	сч	УСТРМД
	пино	м(М6)
	сда	64+8
м	и	E8(М17)
д32	уиа	0(М7)
	по	З
	уиа	1(М7)	29 МГБ МД
З	сч	E4
	увв	'20'(М16) *С ГАШ. РС.
	уи	
	рег	'37'	*С ГАШ. ГРП
	сч	КУСД1
	сда	64+24
	слц	E3	ЗОНА+4
	и	П17777
	сда	64-1(М7)
	зп	РЯ2
	пио	з1(М7)
	и	E1
	сда	64-9
	зп	РЯ1
	сч	РЯ2
	сда	64+1
	зп	РЯ2
з1	уиа	опргд(М6)
	уиа	9(М2)
	сч	E8(М15)
	пио	з2(М7)
	или	РЯ1
з2	или	е19е11
	увв	'20'(М16) *С ВЫБОР N УСТР. ЗАНЕС В МКМД
чтрс	уи	
	счи	М2
	уи	0	ЗАДЕРЖКА ДЛЯ Б-6
	увв	'20'(М16) *С ОПРОС РС
	уи		
	увв	'4000'(М16) *С ЧТЕН. РС НА СМ
	пб	(М6)
опргд	и	E9	ПРОВЕРКА ГД
	уиа	НЕТГД(М5)	НЕТ ГД
	по	АВОСТ
 УИА -7(М6)
 СЧ 0
ОЧСЛ МОД КНАПР-1(М16) РОСП.СЛ.СЛОВ
 ЗП 7(М6)
 ЦИКЛ ОЧСЛ(М6)
 УИА -7(М6)
ВБРЗ ЗП 1 ВЫТАЛК.БРЗ
 ЦИКЛ ВБРЗ(М6)
 СЧ КУСД1
	сда	64-24
	рзб	длякус
	увв	(М16) *С ЗАДАНИЕ МАССИВА ОБМЕНА
	сч	Е2Е1
	или	ид
	увв	'20'(М16) *С ВЫДАЧА ТИПА КОМ. ОБМ.
	уи	
	сч	РЯ2
	или	E12
	уи	
	увв	'20'(М16)	*С ВЫБОР ДОРОЖКИ
	уи	
	увв	
	сч	вж
	пе	(М11)
	уиа	-40(М6)
выд	зп	1	ВЫДЕРЖКА
	цикл	выд(М6)
	сч	
	уи	
	рег	'37' *С ГАШ. ГРП, ОТКР. МГРП
	уиа	-1000(М6)
ожпр1	уиа	-160(М10)	ОЖИДАНИЕ ОКОНЧ. ОБМЕНА
ожпр	сбр	
	рег	'237'
	и	E32(М16)
	пе	ПРЕР
	цикл	ожпр(М10)
	цикл	ожпр1(М6)
	стоп		
* ОБМЕН НЕ ЗАКОНЧ.
	уиа	ЗАПР(М7)
	уиа	НЕТОБМ(М5)
	пб	АВОСТ
ЧТСЛ СЧ К2003   ЧТ. СЛ. СЛОВ
 УИ ’21’
 МОД КНАПР-1(М16)
 СЧ (М10)
 УИ 0
 ПБ (М6)
ПРЕР НОП ,
	уиа	9(М2)	ЧТЕН. РС
	пв	чтрс(М6)
	зп	РЯ2	12
	и	E12
	пе	авос
	увв	'4035'
	и	E8(М16)
	уиа	ОШМ(М5)
	пе	АВОСТ
	сч	РЯ2
	и	E11
	уиа	ОШКСМ(М5)
	пе	АВОСТ	ОШ. КОНТР. СУММИР. (11Р. РС)
	уиа	-3(М2)
	сч	КУСД1
	сда	64-24
	рзб	ПРИП
	уиа	-7(М12)
в	зп	1
	цикл	в(М12)
	рег	'20'	ПРИП. 1-МУ ЛИСТУ
 УИА КССС(М3)
 УИА 1(М10)
 ПВ ЧТСЛ(М6)
КССС ЗП РЯ2
	нтж	КЛЮЧ
 И Е39П25
 ПЕ авост         НЕВЕРНЫЙ КЛЮЧ В  СЛ.СЛ.
 СЧ РЯ2
 И Е41
 ПЕ НОВС	!МАЛ. СС ПО-НОВОМУ
 УИА -1(М10)
КСС1 УИА ПОВТД(М3)
 СЧ Е11
 УИ ’21’
 УИА -255(М6)
 СЛИА 2(М10)
 СЧ 0
КСС МОДА (М12) КОНТР.СУММИР.
 СЛЦ 1279(М6)
 ЦИКЛ КСС(М6)
 УИА ’2003’
 ЗП РЯ1
 УИА КСС2(М3)
 ПВ ЧТСЛ(М6)
КСС2 ЗП РЯ2
 НТЖ РЯ1
 И П7777
	уиа	ОШКСМ(М5)
 ПЕ ОШК
	сч	РЯ2
	и	E40
	по	ОШК	СЛ. СЛОВА ЗП ПО-СТАРОМУ
 СЛИА 256(М12)
КС ЦИКЛ КСС1(М2)
 УИА ГАШ(М6)
ОСВКМД СЧ К2013 ОСВОБ.КМД ,ПР.ПОК
 УИ ’21’
	сч	КОСВ
	увв	'20'(М16)
	уи	
 ПБ (М6)
ГАШ СЧ 0 ГАШ.ГРП И МГРП
	уи	
	рег	'37'
	пб	(М1)
ОШК СЛИА -1(М10)
 УИА ОШК1(М3)
 ПВ ЧТСЛ(М6) В ЧЕТН СЛ КС,ЗАПИСАН ПО-СТАРОМУ
ОШК1 НТЖ РЯ1
 И П7777
	пе	АВОСТ         НЕСОВПАДЕНИЕ  К С
 СЛИА 1(М10)
 СЛИА 256(М12)
 ПБ КС
авос	уиа	25(М2)	ОБРАБ. АВОСТН. СИТУАЦ.
	пв	чтрс(М6)
	зп	РЯ1	24
	сч	РЯ2
	и	E9
	по	раснгд	НЕТ ГД
	сч	РЯ1
	и	E3
	уиа	СПА(М5)
	пе	ош1 	СПА
	сч	РЯ1
	и	E5
	уиа	КА(М5)
	пе	АВОСТ	17Р. КА
	сч	РЯ1
	и	E4
	пе	нетдис	НЕСОВПАД. ИДЕНТИФ.
	сч	РЯ1
	и	E11
	уиа	ОШИБ(М5)
	пе	АВОСТ ОШБ (ОШИБКА БУФЕРА)
	уиа	АВ(М5)
АВОСТ	уиа	ЗАПР(М7)	ОСВ. КМД, РАСП. ОШИБКИ
	пв	ОСВКМД(М6)
ош2	уии	М6(М5)
	пб	и
раснгд	сч	РЯ2	НЕТ ГД
	уиа	НЕТГД(М5)
	и	E10
	по	АВОСТ	ПН (10Р. РС) = 0 (ПОДВОД ЗАВЕРШ.)
ош1	уиа	1(М2)	СБРОС НА 0 ЦИЛИНДР
	пв	чтрс(М6)
	пв	ОСВКМД(М6)
	уиа	-1000(М10)
	пв	и3(М7)	ВЫДЕРЖ. 1 СЕК.
	уиа	ЗАПР(М7)
	пб	ош2
НОВС	уиа	'76001'(М6)
	уиа	ПОВТД(М3)
	сч	E11
	уи	М21
	сч	
КСН	слц	'3777'(М6)
	цикл	КСН(М6)
	уиа	'2003'
	зп	РЯ1
	уиа	КСН+4(М3)
	уиа	3(М10)
	пв	ЧТСЛ(М6)
	нтж	РЯ1
	пе	АВОСТ
	уиа	ГАШ(М6)
	пб	ОСВКМД
нетдис	уиа	АВ(М5) НЕ СОВПАД. ИДЕНТИФ.
	пб	АВОСТ ВРЕМЕННО. ПОТОМ, ЧТ. ЗАГОЛ., ОПРЕД. ДОР. З
ПОВТД	уиа	ЗАПР(М6) ПОВТ. ОБМЕН ПРИ КЧ
	пб	ОСВКМД
НЕТГД	конд	в'2236040041762000'	НЕТ ГД
	конд	в'2220040040440000'
	конд	в'2220040040440000'
	конд	в'3636040040440000'
	конд	в'2220040040440000'
	конд	в'2236370074340000'
ОШМ	конд	в'0003437620220000'
	конд	в'0004222222200000'
	конд	в'0004222222200000'
	конд	в'0004222222200000'
	конд	в'0004222225200000'
	конд	в'0003422230600000'
ОШКСМ	конд	в'3476204634404000'
	конд	в'4252004442444000'
	конд	в'4252004440444000'
	конд	в'4252007040444000'
	конд	в'4252004442524000'
	конд	в'3452004634614000'
АВ	конд	в'0000002107400000'
	конд	в'0000002104200000'
	конд	в'0000003704200000'
	конд	в'0000002107400000'
	конд	в'0000002104200000'
	конд	в'0000001707400000'
СПА	конд	в'0000016104420000'
	конд	в'0000021104420000'
	конд	в'0000020104760000'
	конд	в'0000020104420000'
	конд	в'0000021104420000'
	конд	в'0000016174360000'
ОШИБ	конд	в'0000343763600000'
	конд	в'0000422222100000'
	конд	в'0000422222100000'
	конд	в'0000422223600000'
	конд	в'0000422222000000'
	конд	в'0000342223700000'
КА	конд	в'0000001144200000'	17Р. КА
	конд	в'0000001104200000'
	конд	в'0000001107600000'
	конд	в'0000001604200000'
	конд	в'0000001104200000'
	конд	в'0000001143600000'
	конд	в'0110165000400006'
	конд	в'0010000047000000'
*
***** БЛОК РОСПИСИ МБ *****
*
*
РБ	уиа	0(М1)
	уиа	0(М4)
	сч	E1
	зп	прмк
	сч	3	ПРОВЕРКА ПРИЗНАКА ЗАТИРАНИЯ КАТАЛОГОВ
	и	E4
	по	рб2	ЧТЕНИЕ МК С МБ.
	уиа	'76001'(М15)
	сч	0	РОСПИСЬ НУЛЯМИ 23-ГО ЛИСТА
ОБМК	мода	'46000'	
	зп	'1777'(М15)	ДЛЯ ПВП
	цикл	ОБМК(М15)	23.09.86. МАЛИЧЕВ В.В.
	уиа	2(М1)	ЗАПИСЬ МК НА МБ
рб2	пв	ОБМИМБ(М15)	ЧТ. 6 ТР. НА 23 Л.
рб1а	сч	
	зп	прмк
	уиа	-15(М1)
	уиа	-63(М3)
росс	сч	Е48П17 НЕ РАСП. ТРАКТ ГЕНС
	зп	ШОЧ+2(М3)
	цикл	росс(М3)
	мода	'46000'
	сч	КЛЮМК
	нтж	МВВ
	по	рбар	КЛЮЧ ВЕРЕН В МАЛ. КАТ.
РОСМК	сч	E4
	зп	КОДТР
РБ1	сч	МБС5
	и	П37
	уи	М3
	сч	МБС5
	сда	64+6
	и	П37 ТРАКТ С ГЕНС
	уи	М2
	сч	E48(М2)
	нтж	СТМГ+31(М3)
	зп	СТМГ+31(М3)
	счи	М1
	пе	НМГРА	РАСПИСАТЬ ВЕСЬ ИНФ. МБ
	сч	NМБ
	и	П37
	уи	М2
	сч	СТМГ+31(М2)
	или	ТРСОХР
	нтж	ТРСОХР
	зп	СТМГ+31(М2)
НМГРА	сч	E1
	зп	РОСПБ	РЕЖ. РОСПИСИ МБ
 УИА 0(М5)
 СЧ 3
 И Е12
 ПО (М12)
 СЧ 4  12Р 3ТР - ПРИЗН ИСКЛЮЧ МБ
 И П37 НОМ МБ В 1-3 РР 4 ТР
 УИ М2  48-17 РР - ТРАКТЫ
 СЧ 4
 И Е48П17
 НТЖ Е48П17
	и	СТМГ+31(М2)
	зп	СТМГ+31(М2)
 СЧИ М2
 НТЖ NМБ
 И П7
 ПЕ (М12)
	сч	СТМГ+31(М2)
 И ТРСИСТ
 ЗП ОТБРТР
 ПБ (М12)
РБА МОДА МК ОТБРАК.ТРАКТЫ НЕ ПРОВ
	сч	R(М1)
	зп	БРАКТР+15(М1)
	цикл	РБА(М1)
 СЧ NМБ
 И П37
 УИ М2 ИНФМБ
	сч	СТМГ+31(М2)
 НТЖ ТРСИСТ
 ЗП ОТБРТР 1-ТРАКТ ОТБРАКОВАН
 ИЛИ ТРСОХР
 НТЖ Е48П17
	зп	СТМГ+31(М2)
	пб	НМГРА
рбар	сч	КОДТР
	и	E4
	по	РБА
	уиа	0(М1)
	пб	РБ1
РОСПМБ ПИНО РОСБ2(М5) ИДЕТ КОНТРОЛЬНОЕ ЧТЕНИЕ С МБ
 СЧИ М14
 И Е1
 СЛЦ Е1
 УИ М16 N НАПР
 СЧ КУСБ-1(М16)
 И Е48
 ПЕ РОСБ2 НЕ ОБРАБОТАН ПРЕДЫДУЩИЙ ЗАКАЗ К НАПР
 СЧИ М14
 СДА 64+1
 СДА 64-2 ФОРМИРОВАНИЕ КУС ЗП НА МБ
 ИЛИ Е19
 ИЛИ Е17(М16) (10-Я ИЛИ 4-Я СТР.СООТВ.НАПР)
 ЗП КУСБ-1(М16)
 ПВ НОМСЧ(М1)
 УИА -1023(М1)
 СЧИ М10
 НТЖ NМБ
 И П37
 ПЕ РОСБ НЕ ИНФ.МБ
 СЧ СТМГ+31(М10)
 И Е48(М13)
 ПО РОСБ12 ТРАКТ НЕ РАСПИС.
	сч	деф
	пе	РОСБ1	!МАЛ. *NO LAST STEP
	сч
РОСБА МОД Е15(М16)
 ЗП 1023(М1) ОБНУЛ СТР.ПРИ РОСП.МБ
 ЦИКЛ РОСБА(М1)
 ПБ РОСБ2
РОСБ	сч	СТМГ+31(М10)
 И Е48(М13)
 ПО РОСБ12 ТРАКТ НЕ РАСПИС.
РОСБ1 СЧ КРОСП
 СЛЦ КРОСП
 ЗП КРОСП
 МОД Е15(М16) РОСПИСЬ СТРАНИЦЫ ЗП НА МБ
 ЗП 1023(М1)
 ЦИКЛ РОСБ1(М1)
РОСБ2 УИА -1(М16)
РОСБ2А СЧ КУСБ+1(М16)
 И Е48
 ПО РОСБ10 ЗАКАЗ К НАПРАВЛ.ЕЩЕ НЕ ВЫДАН
 СЧ КУСБ+1(М16)
 И Е47
 ПЕ РОСБ5А ЗАКАЗ УЖЕ ОБРАБОТАН
 РЕГ ’237’
 И Е45(М16)
 ПЕ РОСБ6 ОБМЕН ОКОНЧЕН,НА ОБРАБ.
 СЛИА 2(М16)
 ПВ НОМСЧ(М1)
	сч	СТМГ+31(М10)
 И Е15П1
 ЗП РЯ1
 СЧ СЧВРЕМ
 ВЧ РЯ1
 ВЧ е2е1        свс: П13	-48 CЕК
 СЛИА -2(М16)
 ПЕ РОСБ4
 СЧ 0 МБ ОТСУТСТВУЕТ
	зп	СТМГ+31(М10)
 СЧ КУСБ+1(М16)
 ИЛИ Е45 УСТ.ПРИЗНАК БЛОКИРОВКИ КОНТР.ЧТ
 ИЛИ Е47
 ЗП КУСБ+1(М16)
РОСБ5А ЦИКЛ РОСБ2А(М16)
 СЛИА 2(М16)
 ПБ РОСБ7
РОСБ4 ЦИКЛ РОСБ2А(М16)
 ПБ ПОДГ4 НА БЛОК ПОДГОНА МЛ
РОСБ6	нтж	ВСЕЕД *БЫЛА ОШИБКА -=- -=-
	рег	'37'	!МАЛ. ГАШ. 45Р. ГРП. 23.12.88
	слиа	2(М16)
	пв	ПРБ(М11) НА БЛОК ОБРАБОТКИ ОБМЕНА С МБ
	пио	РОСБ7(М5)	БЫЛА ЗП
	сч	ОШБ
	по	РОСБ7	ЧТ. БЕЗ ОШИБОК
	пв	НОМСЧ(М1)
РОСБ6В	сч	СТМГ+31(М10)
	или	E48(М13)
	нтж	E48(М13)
	зп	СТМГ+31(М10)	ТРАКТА НЕТ
РОСБ7 СЧ КУСБ-1(М16)
 ИЛИ Е47 УСТ.ПРИЗНАК ВЫП.ЗАКАЗА
 ЗП КУСБ-1(М16)
 И КУСБ
 И КУСБ+1
 И Е47
 ПО ПОДГ4 ВЫПОЛНИТЬ ЗАКАЗ ПО ДРУГОМУ НАПР.МБ
 ПИНО РОСБ9(М5) КОНТР.ЧТ.ОКОНЧЕНО НА ОБОИХ НАПР.
 УИА 1(М5)
 УИА -1(М16)
РОСБ8 СЧ КУСБ+1(М16)
 И Е46П1 ФОРМИРОВАНИЕ КУС КОНТРОЛЬНОГО ЧТЕНИЯ
 ИЛИ Е22Е18
 ЗП КУСБ+1(М16)
 ЦИКЛ РОСБ8(М16)
 ПБ ПОДГ4
РОСБ9 СЧ 0
 ЗП КУСБ
 ЗП КУСБ+1
 УИА 0(М5) УСТ.РАЗРЕШЕНИЕ ЗП НА МБ
 СЧИ М14
 НТЖ Е10
 ПЕ ПОДГ4
	сч	ТРАКТЫ
	и	E40
	по	ррр
	зп	прэ256
	сч	E20
	или	E14
	или	E19
	зп	КУСБ
	зп	КУСБ+1
	уиа	-7(М12)	МБ
	уиа	16(М14)	ВТОРЫЕ 16 МБ
	уиа	1(М16)
обмб	уиа	-31(М13)
обтр	уиа	-3(М4)
	уиа	0(М5)
	уиа	-1023(М1)
рсп	сч	КРОСП
	слц	КРОСП
	зп	КРОСП
	зп	КРАХМЛ+3(М1)
	цикл	рсп(М1)
обмч	пв	ЗКМБ(М11)
	уиа	-7(М1)
	уиа	1(М6)
вбр	зп	1
	цикл	вбр(М1)
ок	рег	'237'
	уи	
	и	E47(М16)
	пе	обок
	цикл	ок(М6)
	пб	трнет
обок	сч	
	рег	'37'
	уи	
	сч	КУСБ-1(М16)
	и	E18
	пе	окчт
	сч	КУСБ-1(М16)
	или	E18
	или	E24	КОНТР. ЧТ.
	зп	КУСБ-1(М16)
	уиа	-1023(М1)
	сч	
рли	зп	КРАХМЛ+3(М1)
	цикл	рли(М1)
	пб	обмч
окчт	сч	К2003
	уи	М21
	уиа	трнет(М3)	ПРИ КЧ.
	сч	КУСБ-1(М16)
	пв	КСМБ(М10)
окчт1	уиа	0(М2)
	уии	М6(М4)
	слиа	3(М6)
	уиа	0(М1)
	пв	ПРБ3(М11)
окчт2	счи	М14
	слц	E1
	зп	ГРАНБ
	пб	обок1
трнет	сч	БРАКТР(М14)
	или	E17(М13)
	нтж	E17(М13)
	зп	БРАКТР(М14)
обок1	сч	КУСБ-1(М16)
	слц	E3
	или	е18е24
	нтж	е18е24
	зп	КУСБ-1(М16)
	цикл	обтр(М13)
	слиа	1(М14)
	цикл	обмб(М12)
	счи	М16
	нтж	E2
	по	ровсе
	уиа	2(М16)
	уиа	-7(М12)
	пб	обмб
ровсе	сч	БРАКТР(М14)
	или	E17(М13)
	нтж	E17(М13)
	зп	БРАКТР(М14)
	цикл	ровсе(М13)
	уиа	-31(М13)
	слиа	1(М14)
	цикл	ровсе(М12)	МБ
ррр	сч	
	зп	РОСПБ
	зп	прэ256 	РОСП. ОКОНЧ.
	цикл	отбр(М4)	ДЛЯ ЦИКЛОВ ОТБРАКОВКИ МБ
	пб	СТАТИ
РОСБ10 СЛИА 2(М16)
 ПВ НОМСЧ(М1)
 СЧ СТМГ+31(М10)
 И Е48П17 УСТ.ВРЕМЯ ВЫДАЧИ КОМАНДЫ ОБМЕНА С МБ
 ЗП СТМГ+31(М10)
 СЧ СЧВРЕМ
 И П77777
 ИЛИ СТМГ+31(М10) УСТАН.ВРЕМЯ ВЫДАЧИ КОМАНДЫ ОБМЕНА СМБ
 ЗП СТМГ+31(М10)
 СЧ КУСБ-1(М16)
 ИЛИ Е48
 ЗП КУСБ-1(М16) УСТ.ПРИЗНАК ВЫДАННОГО ЗАКАЗА
 ПИО РОСБ11(М5) ПРИ ЗП
 СЧ КУСБ-1(М16)
 И Е45
 ПЕ РОСБ6В
РОСБ11 ПВ ЗКМБ(М11) ВЫДАЧА ЗАКАЗА НА ОБМЕН
 ПИНО ПОДГ4(М5) ПРИ ЧТ.
 СЛИА 1(М14) +1 К СЧ-КУ ЗАКАЗОВ
 ПБ ПОДГ4
РОСБ12 СЧ КУСБ-1(М16)
 ИЛИ Е48 ИММИТАЦИЯ ВЫДАЧИ ЗАКАЗА
 ИЛИ Е45
 ЗП КУСБ-1(М16)
 СЛИА 1(М14)
 ПБ РОСБ7
НОМСЧ СЧИ М16
 СДА 64-3
 УИ М10
 СЧ КУСБ-1(М16)
 СДА 64+7
 И П7 МБ
 УИ М13
 СЛИ М10(М13)
 СЧ КУСБ-1(М16)
 СДА 64+2
 И П37 ТРАКТ
 УИ М13
 ПБ (М1)
*
* БЛОК ОБМЕНА С ПРОГРАММНЫМ МБ
*
* ВХОДНАЯ ИНФ:
* НОМЕР ИСХОДНОГО КУС В М1
* ЧИСЛО ЦИКЛОВ ОБМЕНА В М4
* ВЫХОД ПО М15
*
ОБМПМБ СЧ NМБ+1
 СДА 64+3
 И Е3П1
 УИ М16 N НАПР.ПРОГРАММНОГО МБ
 СЧ NМБ+1
 И Е3П1
 СДА 64-7 N ПРОГРАММНОГО МБ
 ИЛИ КУСПМБ(М1) ИСХОДНЫЙ КУС ОБМЕНА
Б1 ЗП КУСБ-1(М16)
 ПВ ЗКМБ(М11) ОБМЕН С ПРОГРАММНЫМ МБ
 СЧ КУСБ-1(М16)
 СЛЦ Е13 +1 К N СТР.
 СЛЦ Е3 +1 К N ТР
 ЦИКЛ Б1(М4)
 СЧ ЛСТБАЗ
 УИ М6
 ПБ (М15) ВЫХОД
КУСПМБ	конд	в'0000000001010000'
	пам	2
 КОНД М10В’2’М12В’30’В’37’ ЕСПИ80
 КОНД М10В’3’М12В’30’В’33’ ЕСПЛ
 КОНД М17В’1’М12В’6’В’24’ 5 ТР 0С- ИПЗ,ХЛАМ
 КОНД М18В’1’М12В’1’В’140’       ПГС
 КОНД М12В’30’М2В’13’В’1’ ГП1
 КОНД М12В’30’М10В’1’В’132’   ГП4
 КОНД М12В’7’М17В’3’М2В’16’     БОЭК
 КОНД М12В’1’М18В’1’М2В’31’
 КОНД М12В’30’М18В’1’М2В’37’
 КОНД М18В’1’М12В’1’М2В’32’   КОНФУС РАБОТАЕТ НА СВС
 КОНД М18В’1’М12В’30’М2В’36’
*
*
* БЛОК ОБМЕНА С ИНФОРМАЦИОННЫМ МБ
*
* ВХОДНАЯ ИНФ:
* НОМЕР ИСХОДНОГО КУС В М1
* ЧИСЛО ЦИКЛОВ ОБМЕНА В М4
* ВЫХОД ПО (М15)
*
ОБМИМБ СЧ NМБ
 СДА 64+3
 И Е3П1
 УИ М16 N НАПР
 СЧ NМБ
 И Е3П1
 СДА 64-7 N МБ
 ИЛИ КУСИМБ(М1)
 ПБ Б1
КУСИМБ	конд	в'0000000001630030'
	конд	п'000700'
	конд	в'0000000001230030'
	конд	в'0000000001010030'
	конд	в'0000000001230024'
	конд	в'0000000001320014'	ТУПР
*
*               БЛОК ОБМЕНА С МБ
*
* ВХОДНАЯ ИНФ: М16 -N НАПРАВЛЕНИЯ ОБМЕНА, В ЯЧЕЙКЕ КУСБ НАПР.
* СФОРМИРОВАННЫЙ КУС. ВЫХОД ПО (М11)
*
ЗКМБ НОП
 СЧ РОСПБ
 ПЕ РПБ
 СЧИ М16
 СДА 64-39
 ИЛИ КУСБ-1(М16)
 ЗП КУСБ-1(М16)
РПБ СЧ КУСБ-1(М16)
 И Е18
 ПО МБ5 ЗП НА МБ
 УИА -7(М1)
 УИА -7(М10)
 СЧ 0
МБ4 МОД КНАПР-1(М16)
 ЗП 7(М1) ОЧИСТИТЬ ЯЧЕЙКИ СЛУЖ.СЛОВ НАПР.
 ЦИКЛ МБ4(М1)
 ПБ МБ7
МБ5 СЧ КУСБ-1(М16)
 ПВ КСМБ(М10) НА КОНТР.СУММИРОВАНИЕ МАССИВА ОБМ.
 УИА -8(М1)
 СЧ КУСБ-1(М16)
 СБР БТС
МБ6 МОД КНАПР-1(М16)
 ЗП 8(М1) ЗАПИСЬ ФИЗ.АДРЕСА СЕКТОРА В ЧЕТН.СС
 СЛЦ Е36 +1 К NСЕКТОРА ДЛЯ СЛЕД.СС.ФИЗ.АДР.
 ЗП РЯ2
 СЧ РЯ1 КОНТР.СУММА
 СДА 64-12
 ЗП РЯ1
 СЧМР 0
 МОД КНАПР-1(М16)
 ЗП 9(М1) ЗАПИСЬ КОНТР.СУММЫ СЕКТОРА В НЕЧ.СС
 СЛИА 2(М1)
 СЧ РЯ2 ФИЗ.АДРЕС ДЛЯ СС СЛЕД.СЕКТОРА
 ПИНО МБ6(М1) ПРОДОЛЖИТЬ РОСПИСЬ СС
 УИА -7(М10)
МБ7 ЗП 1
 ЦИКЛ МБ7(М10) ВЫТ С БРЗ
	сч	E23(М16)
	увв	'31'	УСТ. РБУС
	уи	
	сч	КУСБ-1(М16)
	и	е23п1
	увв	(М16)	ВЫДАЧА КОМАНДЫ ОБМЕНА В УВУ
	сч	РОСПБ
	пе	(М11) ВЫХОД ПРИ РОСП. МБ
	уиа	боэте3(М1)
	уиа	НЕТОБМ(М6)
мб8	рег	'237'
	и	E47(М16) ОЖИДАНИЕ КОНЦА ОБМЕНА
	пе	ПРБ
	цикл	мб8(М1)
ПОВТОБ УИА ЗКМБ(М7) НЕТ ОБМ.
 СЧ ПРВОС
 ПЕ (М11) РОСП.ТРАКТА ПРИ ВОССТАНОВЛ
	пб	и
*
* ОБРАБОТКА ПРЕРЫВАНИЯ ПО ОКОНЧАНИЮ ОБМЕНА
*
ПРБ	увв	'4035'
	и	E8(М16)
	по	ПРБ1	ОШМ = 0
	сч	КУСБ-1(М16)
	и	E18
	пе	ПРБ7	ОШМ = 1 ПРИ ЧТ. МБ
	уиа	ОШМ(М6)
	пб	ПОВТОБ
ПРБ1 СЧ КУСБ-1(М16)
 И Е18
 ПО ПРБ6 ЗП НА МБ
 УИА ПРБ7(М3) ДЛЯ ВЫХОДА ПРИ КЧ
 СЧ К2003
 УИ ’21’ ГАШ.ПОК
 СЧ КУСБ-1(М16)
 ПВ КСМБ(М10) НА КОНТР.СУММИРОВАНИЕ
 УИА 0(М2) НОМЕР СС
 УИА 0(М6) N СЕКТОРА
 УИА -3(М1) ЧИСЛО АБЗАЦЕВ
 СЧ КУСБ-1(М16)
 И Е19
 ПЕ ПРБ3 ПРИ ОБМ.СТРАНИЦЕЙ
 УИА 0(М1) ОДИН АБЗАЦ
 СЧ КУСБ-1(М16)
 И Е2Е1
 УИ М6 N СЕКТОРА
 СДА 64-1
 УИ М2 N СС
ПРБ3 СЧ КУСБ-1(М16)
 СБР БТС
 ЗП РЯ2
	счи	М6
	сда	64-35
 ИЛИ РЯ2
 МОД КНАПР-1(М16)
 НТЖ (М2)
 СДА 64-3
 ПЕ ПРБ7 НЕСОВП.СС
 УИА -1(М17)
 СЧ РЯ1 КОНТР СУММА
ПРБ4 СД КСД2(М6)
 И Е12П1
 МОД КНАПР-1(М16)
 НТЖ 1(М2)
 ПО ПРБ4А КСМ ПРАВИЛЬНАЯ
 ПИНО ПРБ7(М17) ИДЕТ СРАВНЕНИЕ СО СЧИТАННОЙ КОНТР.СМ
 СЧ РОСПБ
 ПЕ ПРБ7 ЗАФИКСИРОВАТЬ ОШ.ПРИ РОСП.МБ
	уиа	ЗАКРЫТ(М6)
	пв	НАБРЗ(М7)
 СТОП 0
 ПБ ПРБ7 ПОВТОРИТЬ ЗАПИСЬ НА МБ ПО ПУСКУ
ПРБ4А СЧ КУСБ-1(М16)
 И Е24 КОНТРОЛЬНОЕ ЧТЕНИЕ*
 ПО ПРБ4Б НЕТ
 СЧ КСУМБ-1(М16) КОНТР.СУММА,СФОРМИРОВАННАЯ ПРИ ЗП
 ЦИКЛ ПРБ4(М17)
ПРБ4Б СЛИА 2(М2)
 СЛИА 1(М6)
 ЦИКЛ ПРБ3(М1)
 СЧ 0
ПРБ6 ЗП ОШБ
 СЧ К2013
 УИ ’21’ УСТ ПОК
 СЧ КУСБ-1(М16)
 И Е18
 ПЕ ПРБ7А
 СЧ РОСПБ
 ПЕ (М11) ВЫХОД ПРИ РОСПИСИ МБ
 СЧ ПРВОС
 ПЕ (М11) РОСП.ТРАКТА(НЕ ЗАНЯТОГО)ПРИ ВОССТАНОВ
 СЧ КУСБ-1(М16)
 И КСТР
 ЗП СТРЗП-1(М16) СОХР.N СТРАНИЦЫ ЗП
 СЧ ВСЕЕД
 НТЖ КСТР
 И КУСБ-1(М16)
 ИЛИ СТРЧТ-1(М16) УСТ.СТРАНИЦУ КОНТР.ЧТЕНИЯ (31,32)
 ИЛИ Е22Е18 УСТ.ПРИЗН.КОНТР.ЧТ (КОНСТ.С 24Р)
 ЗП КУСБ-1(М16)
 ПБ ЗКМБ
ПРБ7	сч	прэ256
	пе	трнет
 СЧ Е1
 СЛЦ ОШБ
 ЗП ОШБ
 СЧ К2013
 УИ ’21’ УСТ.ПОК
ПРБ7А СЧ РОСПБ
 ПЕ (М11) ВЫХОД ПРИ РОСПИСИ МБ
 СЧ КУСБ-1(М16)
 И Е24
 ПО ПРБ8
 СЧ ВСЕЕД
 НТЖ КСТР
 НТЖ Е22Е18 ГАШ.ПРИЗНАКОВ КОНТР.ЧТЕНИЯ
 И КУСБ-1(М16)
 ИЛИ СТРЗП-1(М16) УСТ.СТР.ЗП
 ЗП КУСБ-1(М16)
ПРБ8 СЧ ОШБ
 ПО (М11)
	уиа	ОШКСМ(М6)
	нтж	E5
	пе	ПОВТОБ
	сч	прмк
	по	ПОВТОБ
	нтж	прмк
	зп	прмк
	уиа	-15(М1)
	уиа	-15(М3)
	пб	РОСМК
*
***** БЛОК КОНТРОЛЬНОГО СУММИРОВАНИЯ ПРИ ОБМЕНЕ С МБ *****
*
* ВХОДНАЯ ИНФ: НА СМ  СФОРМИРОВАННЫЙ КУС. ВЫХОД ПО М10, КС В РЯ1
* ИСПОЛЬЗУЕМЫЕ МОДИФИКАТОРЫ: 1,2,3,6,7,10
КСМБ	зп	РЯ1	КУС ОБМЕНА С МБ
	и	E18
	зп	КУСБ-1
	сч	РЯ1
	и	КСТР
	сда	64+2
	уи	М6	N СТРАНИЦЫ
 УИА 0(М2)    N СЕКТОРА
 УИА -3(М1)   ЧИСЛО АБЗАЦЕВ
	сч	РЯ1
 И Е19
 ПЕ КСМБ1  ОБМЕН СТРАНИЦЕЙ
 УИА 0(М1)    ОДИН АБЗАЦ
	сч	РЯ1
	и	КАБЗ
	сда	64+2
	зп	РЯ2
	счи	М6
	или	РЯ2
	уи	М6	НАЧАЛЬНЫЙ АДРЕС АБЗАЦА
	сч	РЯ1
 И Е2Е1
 УИ М2 N СЕКТОРА
КСМБ1 СЧ 0
 ЗП РЯ1 ОЧИСТИТЬ ДЛЯ КС
КСМБ2 СЧ 0
 УИА -255(М7)
 ЗП РЯ2
КСМБ3	мода	(М6)
	слц	'377'(М7) КОНТР. СУММИРОВАНИЕ АБЗАЦА
	цикл	КСМБ3(М7)
КСМБ4 ЗП РЯ3 КОНТР.СУММА АБЗАЦА
 И Е12П1
 СЛЦ РЯ2
 ЗП РЯ2
 СЧ РЯ3
 СДА 64+12
 ПЕ КСМБ4
 СЧ РЯ2 СВЕРТКА КОНТР.СУММЫ В 12 Р.
 ЗП РЯ3
 СДА 64+12
 ПЕ КСМБ4А
 СЧ РЯ2
 СД КСД1(М2)
 ИЛИ РЯ1
 ЗП РЯ1
 СЛИА 256(М6) ИЗМ.НАЧ.АДРЕС АБЗАЦА
 СЛИА 1(М2) +1 К N СЕКТОРА
 ЦИКЛ КСМБ2(М1) СУММИРОВАТЬ СЛЕД.АБЗ.
 СЧ ПРЧТМБ
 ПЕ (М10)
 СЧ РЯ1
 ЗП КСУМБ-1(М16)
 ПБ (М10)
КСМБ4А ЗП РЯ2
 СЧ РЯ3
 И Е12П1
 ПБ КСМБ4
*
***** БЛОК ИНДИКАЦИИ ОШИБОК *****
*
* М6 - НАЧАЛЬНЫЙ АДРЕС ТЕКТА
* М7 - АДРЕС ВОЗВРАТА, ИСП. М10
*!ГЕНС-117 СТР. 1614/1632
и	сч	прмк
	пе	рб1а
НАБРЗ	уиа	-8(М10)
	сч	
и1	зп	1
	цикл	и1(М10)	ВЫТ. С БРЗ
	рег	
	уиа	-5(М10)
и2	мода	(М6)
	сч	5(М10)
	рег	'6'(М10) ЗАП. В БРЗ1
	цикл	и2(М10)
	сч	
	рег	'7'	0 В БРЗ7
	уиа	-1000(М10)
и3	уиа	тип1(М3)
и4	сбр	
	цикл	и4(М3)	ВРЕМЕНННАЯ ВЫДЕРЖКА = 1 СЕК.
	цикл	и3(М10)
	пб	(М7)
Е19Е20 КОНД М18В’3’
*
К2003 КОНД В’2003’
К2013 КОНД В’2013’
КСТР КОНД М12В’37’
Е39П25 КОНД М24В’77777’
П1735 КОНД В’1735’
Е48П17 КОНД М16В’37777777777’
П13 КОНД В’13’
длякус	конд	в'0000000157777777'
ПРИП КОНД М33В’1’М29В’1’М5В’1777’
БТС КОНД М24В’7’М2В’377’
СТРЧТ	конд	в'0000000000310000'
	конд	в'0000000000320000'
Е22Е18 КОНД М23В’1’М21В’1’М17В’1’
КАБЗ КОНД М10В’3’
НАПД КОНД М37В’37’М7В’7’
ЗАП КОНД П’,,,,,,’
КОДТР КОНД В’0’
ТРСОХР КОНД М41В’7’      1-ТР.СОХР,48Р.-ТР-0
ТРСИСТ КОНД М18В’3003740377’   1 - ТАКТ ПОЛЬЗ
 КОНД В’0’
КОСВ	конд	в'0000000000011050'
е19е11	конд	п'000050'
прмк	пам	1
ид	пам	1
П16 КОНД В’16’
ИСКАТ КОНД В’0’
К КОНД М38В’1’М18В’1’М26В’1’
 КОНД А(РАБМД)
 КОНД А(НОМСВ)
СБ КОНД А(НОМЛД)
 КОНД А(НОММЛ)
ПРРО КОНД В’1’
Р3	пам	1
Е32П25	конд	в'0000037700000000'
	конд	в'0000000077600000'
	конд	в'0000000000177400'
	конд	в'0000000000000377'
*
***** НИТИ 29.08.76 - 12.12.83 *****
*
* ОТБР-КА ТРАКТОВ С АВТОМАТИЧЕСКИМ УПР-ЕМ ДЕФ. ПИТАНИЯ
* 27-25РР. ТР3 - КОЛ-ВО ПОВ. ДВОЙНОГО ЦИКЛА ОТБР. ТР.
* 28Р. ТР3 - БЛОКИРОВКА ДВОЙНЫХ ЦИКЛОВ ОТБРАКОВКИ ТР-В.
встав1	сч	3
	и	E29 ПРИ Е28=1 НА ТР3 БУДЕТ ТОЛЬКО ОДИН
	пе	РОСПМБ	ЦИКЛ ОТБРАКОВКИ, СТАНДАРТНЫЙ /ДЛЯ ЛЕНЫ/
	пам	0
	сч	3
	сда	64+23
	и	П16
	слц	E1
	нтж	Е15П1
	уи	М4	COUNTER OF STEPS
отбр	счи	М4
	по	зпд *THE LAST STEP
	сч	деф
	нтж	швстав
зпд	зп	деф
	увв	'147'	УСТ-КА РД
	уиа	1(М5)	ВЫДЕРЖКА ДЛЯ ВКЛЮЧ. РЕЛЕ
	сч	
реле	уи	М14
	цикл	реле(М5)
	сч	E1
	зп	РОСПБ
	пб	РОСПМБ
деф	конд	в'1771'	КМБ 1,2  -2В УВУ КМБ
********************************************
		ЛИТЕР
*
КЛЮЧ	конд	М24В'70707'
швстав	конд	п'000021'
УХСАLС	мод	ТУС
	зп	
ПРВОС	пам	1
	конд	в'0036005001117000'
е18е24	конд	в'0000000040400000'
ШК1 ПАМ 4
ШК ПАМ 4
КУС КОНД М17В’1’М24В’445’М12В’22’
КУСТ КОНД М17В’1’М24В’446’М12В’23’
КУС1 КОНД М17В’1’М24В’447’М12В’23’
КАТР КОНД М17В’1’М24В’450’М12В’23’
КУСП КОНД М17В’1’М24В’651’М12В’23’
ККИЕВ КОНД М17В’1’М24В’435’М12В’22’
ЛИСТА3 КОНД М28В’3’М19В’1’ ЛИСТЫ 22,23,34
ОУПР КОНД М47В’1’М23В’1’В’-1’
КУСПА КОНД М24В’600’М17В’1’
НОМЛИС	конд	в'006063'
	конд	в'006075'
	конд	в'006060'
	конд	в'006046'
	конд	в'006055'
	конд	в'006065'
	конд	в'006062'
	конд	в'006070'
	конд	в'006066'
	конд	в'006064'
	конд	в'006074'
КУСЛИС НОП
 КОНД В’4047401400000’    ПУСКАЧ
	конд	в'0004050301400000'
	конд	в'0004047301400000'
 КОНД В’4051601400000’    ПЕЧАТЬ (там разм код)
 КОНД В’4052101400000’    РОБОТ  (там данные)
 КОНД В’4053301400000’    ЭКВВОД (там ПАСП серпа)
 КОНД В’4056001400000’
 КОНД В’4067701400000’
 КОНД В’4055701400000’
	конд	в'006077'
НЕТОБМ	конд	в'2236040034704044'
	конд	в'2220040042444440'
	конд	в'2220040042444440'
	конд	в'3636040042704440'
	конд	в'2220040042405240'
	конд	в'2236370034746140'
ЗАКРЫТ	конд	в'3125216442134306'
	конд	в'4525212442124451'
	конд	в'0565352442524450'
	конд	в'1126256442534450'
	конд	в'4525250442520451'
	конд	в'3065350563334306'
ОШМБОС	конд	в'2112404444410442'
	конд	в'2112404444410440'
	конд	в'2112404447410440'
	конд	в'2112405244010442'
	конд	в'1612406147407034'
Е2Е1	конд	в'3'
Е3П1    ноп
П7	конд	п'7'
П17	конд	п'00000 '
П37	конд	в'0000000000000037'
	конд	п'00000D'
	конд	в'0000000000000177'
	конд	в'0000000000000777'
П1777	конд	в'0000000000001777'
Е12П1	конд	в'0000000000007777'
П77777  ноп
Е15П1	конд	в'0000000000077777'
е23п1	конд	в'0000000037777777'
Е46П1	конд	в'1777777777777777'
КСД1	конд	п'Ш00000'
	конд	в'2400000000000000'
	конд	в'3200000000000000'
	конд	в'4000000000000000'
КСД2	конд	в'6200000000000000'
	конд	в'5400000000000000'
	конд	в'4600000000000000'
	конд	в'4000000000000000'
ВСЕЕД	конд	в'7777777777777777'
	пам	1
КУСД1	пам	1
СТРЗП	пам	2
РЯ1	пам	1
РЯ2	пам	1
ЗАП20Р	пам	1
ЛСТБАЗ	конд	в'0000000000060000'
ЗАПМ7	пам	1
СЧВРЕМ	пам	1
ПРЧТМБ	пам	1
КУСБ	пам	2
ОШБ	пам	1
РОСПБ	пам	1
КСУМБ	пам	2
	конд	п'0000А0'
КРОСП	конд	п'000001'
	пам	1
прэ256	пам	2
КНАПР	конд	в'10'
	конд	в'20'
	конд	в'30'
	конд	в'40'
	конд	в'50'
	конд	в'60'
	конд	в'70'
СЧВЫД	пам	2
РЯ3	пам	1
ОТБРТР	пам	1
	конд	в'0070000000371777'
	конд	п'000036'
П17777	конд	в'0000000000017777'
	конд	в'0000072000000000'
прпе	пам	1
ГРАНБ	конд	в'0000000000000020'
LШЗНМБ	конд	в'60'
	конд	п'0000FS'
вг	сда	64-8
	мод	БУДГРА
вгу	пе	БМВ
	сч	E6
шкмб2	сч	ШКСВН
	пб	U4064
*ШОЧ	экв	'1675'
*ОБРМБ	экв	'1731'
*U4064	экв	'4064'
*КЗ1	экв	'10000'
*С17	экв	'11776'
*шкмб1	экв	'66001'
*
 ЭКВИВ (П7777,Е12П1),(МК,’46000’),(шкмб1,-5119)
*
КЗ1	ВНЕШ	КЗ1
РОБОТ	ВНЕШ	ШОЧ
ПЧКЗ 	ВНЕШ 	ПЧКЗ,(ВЛАД,ОРГ1),ВЕ,(ПЧГОД,ГОД)
ЭКСТ    ВНЕШ    ЭКСТ,ВЕ1,ВЛИ
БОЭК1	ВНЕШ	боэте3
ВЗУ	ВНЕШ	ФИЗОБМ,КРАХМЛ
ВИСП	ВНЕШ	ИЗШГП
ДИСКИ	ВНЕШ	НОМЛД,РАБМД,НОМСВ
ДИСП70	ВНЕШ	БВЗ,БМВ,ТАБТИП,РАБ600,БУДГРА,ТУС,НМБТР,ОАС,СТМГ
ДИСП70	ВНЕШ	НРЕ,ТЗФСТР,УКАЧП,НОММЛ,ШКСВН,R,u4064
КАЧКА	ВНЕШ	КАЧКА
КИТ	ВНЕШ	БРАКТР,КЛЮМК
СОСТАВ	ВНЕШ	ТБД,ТОП
ХЛАМ	ВНЕШ	ДИМОН
МОТТ	ВНЕШ	шас01,тип1,ход3
ГЕНС1	ВНЕШ	ГЕНС1,ПОИСКС,АЛИСТЫ,АОБЩГ,вав1,аобщ1
*
 ВХОД ШКVТ,NБЭСМ,NМБ,NМЛ,ТРСИСТ,ШКТТ,РАСПП,ИСКАТ,А,ОБЩГ
 ВХОД РЯ1,РЯ2,РЯ3,СЛОЙК,ОБМИМБ,ОТБРТР,УХСАLС,СБ,общ1
 ВХОД ДЛТ,ЗАП,КГОД,КНАПР,КОДТР,МБКЧТ,ГРАНБ,ПРРО
 ВХОД НОМЛИС,ЛИСТА3,ШКДТ,ШКУСТР,МБС5,УСТРМД,Р3
 ВХОД КУСИМБ,КУСД1,КУСЛИС,НОМРД,ЧТГП,ЧТМД,МДС
 ВХОД ЧТМДВ,ТРАКТЫ,Е32П25,РАСП3,РАСП1,ПМБ,МВВ,ПОИСК
 ВХОД ШКОПТ,NТАС,ШКТТ2,ШКVТ2,КОН1,ПРОГОН,ВСЕЕД
 ВХОД ЛСТБАЗ,ЗАП20Р,ПЕРЕПС,ШАС128,РОСМБ,нпк
 ВХОД ОУПР,КУСТ,КАТР,КУСП,КУСПА,КУС1,вг,вгу
 ВХОД АЦП,КУС,ККИЕВ,ШК1,ШК,П1777,LШЗНМБ,вав
* 
 ФИНИШ

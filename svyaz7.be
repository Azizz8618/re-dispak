СВЯЗЬ7	СТАРТ	'46000'
	Б
	Е
	М
*МОДУЛЬ ОРГАНИЗАЦИИ СВЯЗИ БЭСМ-6 И ДКС
*ЧЕРЕЗ УСТР-ВО КАДОПАМ
*              ВХОДНАЯ ИНФОРМАЦИЯ
*
* М2     - НОМЕР ТЕМИНАЛА
* М4	- РАБ. РЕГИСТР "СВЯЗЬ7"
* М13    - АДРЕС НАЧАЛА ВЫДАВАЕМОГО МАССИВА / ТЕКУЩИЙ АДРЕС /
* М16    - АДРЕС ВЫХОДА
* 41(М7) - АДРЕС КОНЦА ВЫДАВАЕМОГО МАССИВА + 1
* 42(М7) - ДЛЯ ПЕРВОГО ВХОДА <42>
*          - АДРЕС ПРОДОЛЖЕНИЯ РАБОТЫ ( М17 )
*
* КРОМЕ ЭТОГО ИСПООЛЬЗУЮТСЯ:
* М1  - АБСОЛЮТНЫЙ АДРЕС ЗАПИСИ В РЕКОРД
* М5  - СЧЕТЧИК СИМВОЛОВ В СТРОКЕ
* М6  - СДВИГ ПО  Т А Б К О Д
* М10 - СЧЕТЧИК СИМВОЛОВ В СЛОВЕ ИСХОДНОГО МАССИВА
* М12 - СЧЕТЧИК СИМВОЛОВ В СЛОВЕ РЕКОРДА
* М14 - РАБОЧИЙ ПРИ РАБОТЕ С АС ДКС
* М15 - ИСПОЛЬЗУЕТ ПРОЦЕДУРА ’ПОИСК’
* М17 - РАБОЧИЙ
******
*
*** БЛОК ОПР. ТИПА ОБОРУД., ПОДКЛ. ЧЕРЕЗ ДКС
*
*** !ПРИ ВЫЗ. Э50 '133' НЕ ЗАГР.
* ЗАГР. ПРИ УСТ. НАПР. КРК=0
*
	употр	СВЯЗЬ7(М4)
	уиа	'2003'
	сч	ШКОРАБ
	и	ЕЗВ
	пе	обдкс3
	сч	шзпри7
	и	ШКЭ71
	нтж	ШКЭ71
	и	шкут !НУЖНО ДЛЯ АС ДКС
	зп	D47171
обмдкс	уиа	'2003'
	по	обдкс3
	нед	Е15П1
	уи	М16
	мода	обдкс2
	пв	(М17)
обдкс1	сч	D47171
	нтж	E48(М16)
	зп	D47171
	уиа	'3'
	пб	обмдкс
обдкс2	сч	E41
	или	ВППС(М16)
	зп	ВППС(М16)
	сч	ЕЗВ
	или	ШКОРАБ
	зп	ШКОРАБ
	сч	E48(М16)
	или	ШБУДИЛ
	зп	ШБУДИЛ
	пб	(М17)
обдкс3	уиа	'2003'
	сч	рабдкс+18
	по	обдкс7 !ВЫХОД ТОЛЬКО ОТСЮДА
	нед	Е15П1
	уи	М17
	зп	D47152
	сч	E48(М17)
	нтж	рабдкс+18
	зп	рабдкс+18
	уиа	'3'
*
*** ПЕРЕКЛЮЧАТЕЛЬ
*
тут	мода	тут
	пб	(М17) !НА обдкс2. ПОЧЕМУ ПРОХОДИМ ДАЛЬШЕ?
*
	мода	
	пб	с7эко1  !БЕЗ ВЫЗ. АВТ. "ЭКО"

*
	мода	,
	пб	обдкс4
*
	уиа	'2003'
	пб	с7эк28 !НА ВЫХОД
*
*** ПРОВЕРКА НАЛИЧИЯ ЭВМ СМ НА СВЯЗИ
*
зпшксм	мода	D47133 ВХ. ЧЕРЕЗ НБОБ?
	сч	(М5)
	уи	М16
	сч	рсвсм1
	или	E48(М16)
	зп	рсвсм1
	сч	СМ
	зп	рсвясм(М16)
	пб	обдкс5
*****
обдкс4	уиа	'2003'
	сч	рабдкс+20
	по	СВЯЗЬ7
	нед	Е15П1
	уи	М5
	сч	рабдкс+20
	нтж	E48(М5)
	зп	рабдкс+20
*
обдкс5	счи	М5
	нтж	D47120 !ВСЕЕД
	пе	обдкс6
	счи	
	зп	рабдкс
	зп	рабдкс+1
обдкс6	уиа	'2003'
	сч	рабдкс+10(М5)
	по	обдкс8
	нед	
	уи	М2
	мод	НЗАД(М2)
	уиа	0(М15)
	сч	5(М15)
	или	E23
	нтж	E23
	зп	5(М15)
	уиа	29(М17)
	пв	АВОСТА(М16)
	сч	рабдкс+10(М5)
	нтж	E48-1(М2)
	зп	рабдкс+10(М5)
	сч	швзк7б
	нтж	E48-1(М2)
	зп	швзк7б
	уиа	'3'
	пб	обдкс6
********
*
*
обдкс7	уиа	'2003' ИЗ обдкс3
	сч	ШКСВН
	или	E24
	зп	ШКСВН
	сч	E24
	и	швзк7а
	нтж	швзк7а
	зп	швзк7а
	пб	ВИСП16 В ДИСП70.ВИСП16
*
*** ДЛЯ ДРУГОЙ ВЕРСИИ ДКС
*
обдкс8	уиа	'2003'
	сч	рабдкс+2(М5)
	по	обдкс4
	нед	Е15П1
	уи	М16
	мода	обдкс2
	пв	(М17)
	сч	СЛОN
	или	E48(М16)
	зп	СЛОN
	сч	шзпри7
	или	E48(М16)
	нтж	E48(М16)
	зп	шзпри7
	сч	рабдкс+2(М5)
	нтж	E48(М16)
	зп	рабдкс+2(М5)
	уиа	'3'
	пб	обдкс8
*
***** БЛОК ОБРАБОТКИ ИНФ. ЧЕРЕЗ АС ДКС
***** В АВТОМАТИЧ. РЕЖИМЕ
*
*** ПРИЕМ СИМВОЛА ЧЕРЕЗ АС ДКС?
*  (СТР.143-602)
* ЗАГР. ПРИ НАПР. КРК=0
с7эко1	уиа	'2003' !Из обдкс3 или ИЗ АВТ. ЭКО
	сч	рабдкс+19
	по	СВЯЗЬ7
	нед	Е15П1
	уи	М5
	сч	рабдкс+19
	нтж	E48(М5)
	зп	рабдкс+19
	уиа	25(М15)
	сч	E48-1(М15)
	или	швзк7а
	зп	швзк7а
	сч	рабдкс+21(М15)
	и	О12П9
	зп	рабдкс+21(М15)
	мода	D47133
	сч	(М5)
	сда	64-8
	или	рабдкс+21(М15)
	зп	рабдкс+21(М15)
	сда	64+8
	и	П7
*
	кк	26,'70000' !132,0 ПРИ НАПР. КРК=0
*!ЗДЕСь ЦИКЛИМСЯ ЕСЛИ N НАПР. КРК=0.
* !МАСКА 7707 NNNN NNNN NNN2
*
	счи	М5
	нтж	E2
	по	с7эко2
	нтж	П3
	пе	с7эко3 !НАПР. КРК=0
с7эко2	кк	26,'2' !032,2 - !МАСКА 7707 NNNN NNNN NNNN
	зп	D47121
	мод	D47240
	кк	26,'0' !МАСКА 7707 NNNN NNNN NNNN
	сда	64+24
	зп	экояч1
с7эко3	счи	М5
	нтж	D47120
	по	с7эко9
	уии	М16(М5)
	слиа	-3(М16)
	мода	с7эко4  !НАПР. КРК =0
	пино	(М16) !УХОД НА с7эко4
	сч	рабдкс+10(М5)
	по	с7эко1
	мода	уввад1
	сч	(М5)
	уи	М16
	кк	26,'1'(М16)
	зп	D47174
	кк	26,'2'(М16) !СЧИТ. РПР ДКС К-71 В 40-25 РР. СУММ.
	нтж	D47174
	по	с7эко1
	кк	26,'0'(М16)
	сда	64+24
	уи	М2
	вч	П40
	пе	с7эко1
	вч	П20
	по	с7эко1
	сч	рабдкс+10(М5)
	и	E48-1(М2)
	по	с7эко1
	сч	шкаэко
	и	E48-1(М2)
	мод	базэко
	пе	прцес1-ЭКО !В ЭКО
	сч	D47174
	сда	64+24
	кк	26,'70002'(М16)
	мода	с7эко1
	уиа	0(М16)
	счи	М2
	или	E26
	пб	ЗАПВЕТ
*
с7эко4	сч	D47176
	и	E48(М5)
	по	с7эко7 !НАПР. КРК=0
	мода	(М5)
	мод	уввад1
	кк	26,'1'  !МАСКА 7707
	сда	64+24
	зп	D47174
	мода	D47257
	и	(М5)
	нтж	D47174
	пе	с7эко7
	пб	с7эко6
с7эко5	уиа	'3'
	сч	D47174
	уи	М16
	мода	(М5)
	мод	уввад1
	кк	26,'2'(М16)
	сда	64+24
	уи	М2
	слиа	1(М16)
	счи	М16
	мода	D47257
	и	(М5)
	зп	D47174
	слиа	-48(М2)
	мода	с7эк11
	пио	(М2)
	слиа	'60'(М2)
	счи	М2
	вчоб	П60
	пе	с7эко6
	сч	рабдкс+10(М5)
	или	рабдкс+2(М5)
	и	E48-1(М2)
	по	с7эко6
	сч	рабдкс+2(М5)
	и	E48-1(М2)
	пе	с7эк20
с7эко6	уиа	'3'
	мода	(М5)
	мод	уввад1
	кк	26,'0' !МАСКА 7707
	сда	64+24
	мода	D47257
	и	(М5)
	нтж	D47174
	пе	с7эко5
	уиа	'2003'
	сч	D47174
	мода	(М5)
	мод	уввад1
	кк	26,'70001' !МАСКА 7707
с7эко7	сч	рабдкс+10(М5)
	зп	D47115
с7эко8	по	с7эко1 ---> !МАСКА 7707
	нед	
	уи	М2
	или	E26
	пв	ЗАПВЕТ(М16)
	уиа	'3'
	сч	D47115
	нтж	E48-1(М2)
	зп	D47115
	пб	с7эко8
*
с7эко9	сч	D47121
	сда	64+24
	и	П77
	по	с7эко1 !НАПР. КРК=0.
	нтж	П60
	по	с7эк10
	и	П3
	уи	М2
	нтж	П3
	по	с7эко1
	сч	рабнст+1(М2)
	и	Е16П2
	по	с7эко1
	или	D47121
	зп	рабнст+1(М2)
	и	Е16П2
	нед	
	уи	М2
	сч	E48-1(М2)
	и	шкаэко
	мод	базэко
	пе	прцс1а-ЭКО !В ЭКО
	мода	с7эко1
	уиа	0(М16)
	сч	рабдкс
	и	Е16П2
	по	с7эко1
	нед	
	или	E26
	пб	ЗАПВЕТ
*
с7эк10	сч	D47121
	сда	64-4
	счмр	
	пе	с7эко1
	зп      ,               неясно
	зп	
	пб	с7эко1
*
с7эк11	мода	(М5)
	мод	уввад3
	кк	26,'0'
	сда	64+24
	и	П3
	по	с7эко6
	уи	М17
	мода	с7эк12
	пб	-1(М17)
*
с7эк12	пб	с7эк13
*
        пам     0
        пб      с7эко6
        пам     0
        пб      с7эк19
*
с7эк13	сч	рабдкс+2(М5)
	зп	D47115
с7эк14	сч	D47115
	по	с7эк15
	нед	Е15П1
	уи	М16
	сч	E48(М16)
	нтж	D47115
	зп	D47115
	сч	рабдкс+22(М16)
	сда	64-6
	счмр	
	зп	D47116
	мода	(М5)
	мод	уввад3
	кк	26,'1'
	сда	64+24
	и	П77
	нтж	D47116
	по	с7эк18
	пб	с7эк14
*
с7эк15	сч	СЛОN
	или	E1
	нтж	E1
	по	с7эк17
	нед	Е15П1
	уи	М16
	уиа	'2003'
	сч	E48(М16)
	или	рабдкс+2(М5)
	зп	рабдкс+2(М5)
	сч	E48(М16)
	нтж	СЛОN
	зп	СЛОN
	сч	E48(М16)
	или	шзпри7
	зп	шзпри7
	сч	E48(М16)
	или	ШКУСТР
	зп	ШКУСТР
	сч	ШКАЛ
	или	E48(М16)
	зп	ШКАЛ
	сч	рабдкс+22(М16)
	и	D47127
	зп	рабдкс+22(М16)
	счи	М5
	сда	64-4
	или	рабдкс+22(М16)
	зп	рабдкс+22(М16)
	мода	(М5)
	мод	уввад3
	кк	26,'1'
	сда	64-18
	или	рабдкс+22(М16)
	зп	рабдкс+22(М16)
	мода	(М5)
	мод	уввад3
	кк	26,'2'
	сда	64+25
	и	Е15П1
	сда	64-24
	или	рабдкс+22(М16)
	зп	рабдкс+22(М16)
	пв	ПИЛОТ(М15)
	счи	
	зп	D47175
с7эк16	слиа	1(М16)
	счи	М16
	мода	(М5)
	мод	уввад4
	кк	26,'70001'
	сч	D47175
	мода	(М5)
	мод	уввад4
	кк	26,'70002'
	сч	E2
	мода	(М5)
	мод	уввад4
	кк	26,'70000'
	уиа	'60'(М2)
	уии	М15(М5)
	мода	с7эко6
	уиа	0(М17)
	пб	ас7об3
с7эк17	уи	М16
	сч	E2
	зп	D47175
	пб	с7эк16
с7эк18	сч	E1
	зп	D47175
	пб	с7эк16
с7эк19	мода	(М5)
	мод	уввад3
	кк	26,'1'
	сда	64+24
	и	П77
	уи	М16
	слиа	-1(М16)
	сч	рабдкс+2(М5)
	и	E48(М16)
	по	с7эко6
	нед	Е15П1
	и	Е15П1
	зп	D47115
	счи	М16
	нтж	D47115
	пе	с7эко6
	уиа	'2003'
	сч	E48(М16)
	нтж	ВСЕЕД
	зп	D47171
	и	рабдкс+2(М5)
	зп	рабдкс+2(М5)
	сч	СЛОN
	или	E48(М16)
	зп	СЛОN
	сч	шзпри7
	и	D47171
	зп	шзпри7
	мода	обдкс2
	пв	(М17)
	уиа	'3'
	пб	с7эко6
с7эк20	сч	рабдкс+21(М2)
	сда	64+24
	и	Е15П1
	уи	М10
	кк	26,'2'(М10)
	сда	64+24
	и	П7
	уи	М17
	слиа	-4(М17)
	мода	с7эк22
	пио	(М17)
	слиа	-1(М17)
	мода	с7эк21
	пио	(М17)
	слиа	-1(М17)
	мода	с7эко6
	пино	(М17)
	уиа	'2003'
	сч	ТСЛ-1(М2)
	или	E19
	зп	ТСЛ-1(М2)
с7эк21	слиа	-1(М2)
	мода	с7эко6
	уиа	0(М16)
	уиа	'2003'
	сч	E48(М2)
	или	ШКВЫД
	нтж	E48(М2)
	зп	ШКВЫД
	сч	E48(М2)
	или	ШЗПРИМ
	нтж	E48(М2)
	зп	ШЗПРИМ
	уии	М15(М2)
	кк	26,'1'(М10)
	сда	64+24
	и	D47107
	кк	26,'70001'(М10)
	счи	
	зп	ТАС+1(М2)
	пб	КВ2
с7эк22	слиа	-1(М2)
	уии	М17(М2)
	мод	ТБЛ(М2)
	уиа	0(М16)
	уиа	'2003'
	сч	ШЗПРИМ
	и	E48(М2)
	по	ОТКРЫТ
	кк	26,'0'(М10)
	сда	64+24
	и	D47107
	кк	26,'70000'(М10)
	пб	с7эко6
ОТКРЫТ	сч	ТСЛ(М2)
	или	E28
	или	E31
	зп	ТСЛ(М2)
	уиа	'3'
	кк	26,'3'(М10)
	сда	64+25
	уи	М13
	сч	ТСЛ(М2)
	сда	64+36
	и	E1
	уи	М11
	уиа	'3'
с7эк23	кк	26,'0'(М13)
	сда	64+24
	зп	D47171
	уиа	-1(М3)
с7эк24	и	П377
	уи	М15
	зп	D47172
	по	с7эк26
	мода	с7эк25
	пино	(М11)
	сч	ТАБКОД(М15)
	сда	64+8
	и	П377
	зп	D47172
с7эк25	мод	(М16)
	сч	9(М16)
	сда	64-8
	или	D47172
	пв	ЗПСЕС1(М14)
	мод	ТБЛ(М2)
	уиа	0(М16)
	сч	D47171
	сда	64+8
	мода	с7эк24
	цикл	(М3)
	слиа	1(М13)
	пб	с7эк23
с7эк26	сч	П3
	зп	D47171
	сч	ТСЛ(М2)
	и	E37
	пе	с7эк27
	сч	П377
	зп	D47171
с7эк27	мод	(М16)
	сч	9(М16)
	сда	64-8
	или	D47171
	сд	(М16)
	мод	(М16)
	зп	9(М16)
	сч	9(М16)
	зп	D47172
	сч	10(М16)
	зп	D47173
	кк	26,'0'(М10)
	сда	64+24
	и	D47107
	кк	26,'70000'(М10)
	мода	с7эко6
	уиа	0(М14)
	пб	КПФШ
*
с7эк28	сч	рабдкс+21
	по	СВЯЗЬ7
	нед	
	уи	М6
	сч	рабдкс+21
	нтж	E48-1(М6)
	зп	рабдкс+21
	сч	МШГ
	или	E48-1(М6)
	зп	МШГ
	сч	ВСЕЕД
	зп	D47120
	счи	
	зп	шкаэко
	сч	D47124
	мода	СВЯЗЬ7
	уиа	0(М15)
	пб	ТБУФ
*
***** ВЫДАЧА СИМВОЛА И ЗАВ. ОБМ.
*
*
св7ас1	уии	М1(М3) ИЗ ВЗУ.
	уии	М12(М4)
	мод	базсв7
	уиа	0(М4)
	уии	М3(М4)
	сч	ТУС-1+'23'      Е30
	нтж	ГУС
	и	Е48П42
	по	с7ас1а
	слиа	1(М3)
с7ас1а	счи	М1
	сда	64-15
	зп	КСВЗ+2(М3)
	счи	М12
	или	КСВЗ+2(М3)
	зп	КСВЗ+2(М3)
	уиа	'2003'
	сч	рабдкс+22(М2)
	сда	64+4
	и	П17
	уи	М1
	зп	ШККАН+1(М3)
	мода	D47133
	сч	(М1)
	уи	М1
	сч	НЗАД
	уи	М12
	сч	E48-1(М12)
	или	швзк7а
	зп	швзк7а
	сч	рабдкс+21(М12)
	и	О12П9
	зп	рабдкс+21(М12)
	счи	М1
*
	кк	26,'70000'
*
	сда	64-8
	или	рабдкс+21(М12)
	зп	рабдкс+21(М12)
	уиа	'3'
	сч	рабдкс+22(М2)
	сда	64+24
	зп	НРЕ(М3)
	уиа	-1(М12)
	сч	НРЕ(М3)
	слц	E4
	зп	П7(М3)
	уиа	0(М1)
	сч	ТСЛ(М2)
	и	E37
	нтж	E37
	сда	64+22
	или	E14
	мод	НРЕ(М3)
	кк	26,'70001'
	счи	
	мод	НРЕ(М3)
	кк	26,'70000'
	сч	E1
	зп	'56'(М7)
	пб	ОЧСИМВ
*
приас7	сч	42(М7) ИЗ МОТТ
	по	(М17)
	мода	прас7а
	пино	(М5)
	уиа	КС(М17)
прас7а	сч	42(М7)
	и	П377
	мода	прас7б
	цикл	(М12)
	сда	64-8
	или	РАБШК(М3)
	мод	П7(М3)
*
	кк	26,'70000'(М1) 132,0
*
	слиа	1(М1)
	уиа	-1(М12)
	счи	М1
	нтж	анспрп
	по	св7ас2
	пб	(М17)
прас7б	зп	РАБШК(М3)
	пб	(М17)
*
св7ас2	уиа	E45(М10)
	сч	ТСЛ(М2)
	и	Е36
	пе	с7ас2б
	уиа	0(М10)
	сч	ТСЛ(М2)
	и	E37
	пе	с7ас2б
	сч	п5215
	зп	42(М7)
с7ас2а	мода	прас7а
	пв	(М17)
	сч	42(М7)
	сда	64+8
	зп	42(М7)
	пе	с7ас2а
с7ас2б	мода	ас7обм
	пио	(М12)
	счи	
	зп	РАБШК(М3)
ас7обм	сч	РАБШК(М3)
	мод	П7(М3)
	кк	26,'70000'(М1)
	сч	ТСЛ(М2)
	зп	ТБЛ(М2)
	пам	0
	мода	
	пам	0
	мода	
	сч	ШККАН+1(М3)
	уи	М15
	сч	E3
	мода	ас7об1
	пио	(М10)
	мода	ас7об1
	пино	(М15)
	сч	П5
ас7об1	мод	НРЕ(М3)
*
	кк	26,'70005'
*
	сч	П7(М3)
	сда	64-1
	мод	НРЕ(М3)
*
	кк	26,'70006'
*
	счи	М1
	слц	E1
	мод	НРЕ(М3)
*
	кк	26,'70007'
*
	слиа	1(М2)
	мода	ас7об3 !
	пв	(М17) ! ВОЗВРАТ С ПОСЛ. СТРОКИ
ас7об2	слиа	-1(М2)
	уии	М17(М2)
	мод	НЗАД
	сч	E48-1
* СЧЕТЧИК ОП. ОБМЕНА?
	нтж	швзк7а
	зп	швзк7а
*
	уиа	'3'
	сч	КСВЗ+2(М3)
	уи	М4
	сда	64+15
	уи	М3
	уиа	0(М10)
	уиа	'2003'
	пб	км2в !В ТЕРМ
* НА ЗАВЕРШЕНИЕ ПРОЦЕССА
*
*** ПРИЕМ СИМВОЛА ЧЕРЕЗ АС ДКС
*
ас7об3	уиа	'2003'
	мода	(М15)
	мод	уввад2
*
	кк	26,'0' !ЕДИНСТВЕННАЯ 032 В ДАННОМ БЛОКЕ!
*
	сда	64+24
	мода	D47257
	и	(М15)
	уи	М1
	слц	E1
	мода	D47257
	и	(М15)
	мода	(М15)
	мод	уввад2
*
	кк	26,'70000'
*
	сч	рабдкс+21(М2)
	сда	64-6
	счмр	
	мода	(М15)
	мод	уввад2
*
	кк	26,'70002'(М1)
*
	мода	D47247
	сч	(М15)
	мода	(М15)
	мод	уввад5
*
	кк	26,'70000'
*
	пб	(М17) !CМ ВЫШЕ!
*
*** ДЕШИФРАТОР Э50 '133'
*
Э50133	мод	базсв7 ИЗ ЭК7
	уиа	0(М4)
	уии	М12(М2)
	уии	М13(М3)
	сч	(М3)
	сда	64+6
	и	П77
	по	э133б
	уи	М12
	вч	П40
	пе	вы133а
	вч	П20
	по	вы133а
	мод	НЗАД(М12)
	уиа	0(М13)
э133б	счи	М12
	сда	64-6
	или	(М3)
	зп	(М3)
	и	П7
	зп	рэ133б
	уи	М5
	нтж	П3
	пе	э133в
	сч	(М3)
	и	Е36
	сда	64+35
	слц	E1
	мода	D47133
	зп	(М5)
э133в	мода	D47133
	сч	(М5)
	уи	М17
	сч	(М3)
	нтж	Е48П43
	сда	64-12
	счмр	
	зп	рэ133в
	по	э133ош
	и	П7
	нтж	рэ133в
	мод	рэ133в
	по	э133г
	нтж	П60
	по	об133в
	нтж	п3260
	по	изсв7-ЭК7+768(М7)
э133ош	уиа	36(М6) !ЗАПРЕЩЕННЫЙ ЭКСТРАКОД
	пб	АВОС1-ЭКВВОД(М7)
*
*** ПЕРЕКЛЮЧАТЕЛЬ
*
э133г	сч	E24
	пб	э133м
*
э133д	сч	E23 !НЕТ ПЕРЕД. УПР.
	пб	э133м
*
э133е	сч	(М3) !НЕТ ПЕРЕД. УПР.
	пб	э133ош
*
э133ж	сч	E23 !НЕТ ПЕРЕД. УПР.
	пб	э133м
*
э133з	мода	, !НЕТ ПЕРЕД. УПР.
	пб	об133г
*
э133и	мода	, !НЕТ ПЕРЕД. УПР.
	пб	об133д
*
э133к	сч	(М3) !НЕТ ПЕРЕД. УПР.
	пб	об133е
*
э133л	сч	(М3) !ПРИ МАСКЕ 7707 NNNN NNNN NNNN
	пб	об133ж
*
э133м	или	рэ133в
	зп	рэ133в
	и	П77
	нтж	E1
	пе	э133о
	сч	(М3)
	и	E24
	пе	э133п
	сч	рабдкс
	по	э133н
	нтж	E48-1(М2)
	пе	э133ош
	сч	E48-1(М12)
	зп	рабдкс+1
	счи	М5
	зп	D47120
э133н	сч	E48-1(М2)
	зп	рабдкс
э133о	сч	швзк7б
	и	E48-1(М12)
	пе	вы133б
э133п	сч	МПРП
	и	E8(М17)
	пе	обм133
	сч	E8(М17)
	мода	
	пв	УСТПРП(М15)
	уиа	'2003'
	счи	
	зп	СЧЕТПР
э133р	увв	'4034'
	и	E8(М17)
	по	обм133
	сч	E8(М17)
	нтж	ВСЕЕД
	увв	'30'
	сч	СЧЕТПР
	слц	E1
	зп	СЧЕТПР
	сда	64+2
	по	э133р
	сч	E1
вых133	зп	(М3)
	уиа	'3'
	пб	КОНЕЦ1-ЭКВВОД(М7)
*
обход1	сч	E2 !НЕ ИСП.
	пб	вых133
*
вы133а	сч	E3
	пб	вых133
*
*** ПРОЦЕДУРА ОБМЕНА С ДКС К-71
*** В СООТВ. С ПАРАМЕТРАМИ
*** МАСКИ Э50 '133'
*
обм133	сч	е6свз7
	и	E8(М17)
	по	фикпрп
	счи	М17
	нтж	П7
	уи	М15
	кк	26,'0' !ПРИ РАБОТЕ С МАСКОЙ 7701
	и	E40(М15)
	пе	фикпрп
	сч	E1
	пб	вых133 !ВЫХОД
*
фикпрп	уиа	'2003'
	мода	уходы
	сч	(М17)
	рег	'101'
	мода	(М17)
	мод	адреса
	зп	
	рег	'107'
	сч	(М3)
	или	рэ133в
	и	E24
	пе	об133б
	сч	E48-1(М12)
	или	рабдкс+10(М5)
	зп	рабдкс+10(М5)
	сч	рэ133в
	и	E23
	пе	об133б
	счи	М17
	и	П7
	кк	26,'70000'
	счи	М12
	мода	(М5)
	мод	уввад4
	кк	26,'70001'
	сч	E1
	мода	(М5)
	мод	уввад4
	кк	26,'70000'
об133а	мода	(М5)
	мод	уввад2
	кк	26,'0' * !ПРИ РАБОТЕ С МАСКОЙ 7707
	сда	64+24
	мода	D47257
	и	(М5)
	уи	М1
	сч	П60
	мода	(М5)
	мод	уввад2
	кк	26,'70002'(М1) *
	слиа	1(М1)
	счи	М1
	мода	D47257
	и	(М5)
	мода	(М5)
	мод	уввад2
	кк	26,'70000' *
	мода	D47247
	сч	(М5)
	мода	(М5)
	мод	уввад5
	кк	26,'70000' *
	счи	
	пб	вых133 !ВЫХОД
*
об133б	сч	рабдкс+21(М12)
	и	О4П1
	зп	рабдкс+21(М12)
	счи	М17
	или	рабдкс+21(М12)
	зп	рабдкс+21(М12)
	сч	E48-1(М12)
	или	швзк7б
	зп	швзк7б
	сч	5(М13)
	или	E23
	зп	5(М13)
	сч	(М3)
	и	E24
	по	о133б1
	счи	
	пб	вых133 !ВЫХОД
о133б1	сч	рабдкс+10(М5)
	и	E48-1(М12)
	нтж	рабдкс+10(М5)
	по	вых133
	или	E48-1(М12)
	или	E48
	пб	вых133
*
вы133б	сч	П3
	пб	вых133 !ВЫХОД
*
об133в	сч	E8(М17)
	и	МПРП
	мода	
	пв	ГАШПРП(М15)
	мода	КОНЕЦ1-ЭКВВОД(М7)
	уиа	0(М15)
	сч	рабдкс+10(М5)
	или	рабдкс+2(М5)
	по	(М15)
	уии	М16(М4)
	пб	зв133а
*
*** ДЛЯ ДРУГОЙ ВЕРСИИ ДКС
*
об133г	сч	рабдкс+10(М5)
	и	E48-1(М12)
	по	э133ош
	сч	шкаэко
	пе	э133ош
	сч	E1
	мода	
	пв	ТБУФ(М15)
	сда	64-8
	уи	М6
	зп	базэко
	сда	64-2
	или	D47123
	или	НОММБ1
	зп	D47124
	уии	М16(М6)
	пб	ФИЗОБМ
*
об133д	сч	шкаэко
	и	E48-1(М12)
	по	э133ош
	или	рабдкс+21
	зп	рабдкс+21
	сч	E45
	зп	D47111
	уии	М16(М4)
	мода	вых133
	уиа	0(М15)
	пб	завобм
*
об133е	сч	рабдкс
	и	E48-1(М2)
	по	э133ош
	сч	рабдкс
	и	Е48П25
	зп	(М3)
	счи	М17
	и	П7
	кк	26,'70000'
	мод	D47240
	кк	26,'0'
	сда	64+24
	зп	экояч1
	или	(М3)
	зп	(М3)
	уиа	'3'
	пб	КОНЕЦ1-ЭКВВОД(М7) !ОБМЕН ЗАВЕРШ. ВЫХОД.
*
об133ж	уиа	'2003' !C э133л
	счи	М17
	и	П7
*
	кк	26,'70000' !ПРИ РАБОТЕ С МАСКОЙ
*                          7707 NNNN NNNN NNN2
	сч	(М3)
	и	E18
	по	о133ж1 ---> !МАС. 7707
	сч	(М3)
	сда	64+12
	и	П17
	мода	(М5)
	мод	уввад4
	кк	26,'70000'
	сч	(М3)
	сда	64+18
	мода	(М5)
	мод	уввад4
	кк	26,'70001'
	пб	об133а
*
о133ж1	сч	(М3) !МАСКА 7707
	сда	64+12
	сда	64-12
	мода	D47247
	или	(М5)
	мода	(М5)
	мод	уввад5
	кк	26,'70000'
	счи	
	пб	вых133
*****
ПРП12	мод	базсв7
	уиа	0(М16)
	сч	-1
	слц	E1
	и	П7777
	уи	М15
	кк	26,'0'
	зп	-1
	и	Е32П25
	сда	64+12
	или	-1
	зп	-1
	счи	М15
	или	-1
	зп	-1
	пб	БМВ
*****
* ПРИ УСТ. НАПР. КРК=0
ПРП8	уиа	0(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
*****
ПРП7	уиа	1(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
ПРП5	уиа	3(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
ПРП6	уиа	2(М17)
набзс7	мод	базсв7
	уиа	0(М16)
********************
        отмен   (М4)
        употр   СВЯЗЬ7(М16) !СМЕНА РАБ. РЕГИСТРА
********************
	счи	
	зп	СЧЕТПР
зав133	увв	'4034'
	и	E8(М17)
	по	зв133б !ПРИ КРК=0
	нтж	ВСЕЕД
	увв	'30'
	сч	СЧЕТПР
	слц	E1
	зп	СЧЕТПР
	сда	64+2
	по	зав133
	сч	E8(М17)
	и	МПРП
	мода	
	пв	ГАШПРП(М15)
	счи	М17
	сда	64-8
	нтж	рабдкс+22+24
	и	Е12П9
	уиа	БПУ(М15)
	пе	зв133а
	сч	швзк7а
	и	E24
	нтж	швзк7а !НА КОНТРОЛЬ!
	зп	швзк7а
*
зв133а	мода	е48э50
	сч	(М17)
	или	рабдкс+20
	зп	рабдкс+20
	сч	E46
	зп	D47111
	пб	завобм
*
зв133б	мода	е48э50
	сч	(М17)
	или	рабдкс+19
	зп	рабдкс+19
	сч	E47
	зп	D47111
	уиа	БПУ(М15)
	пб	завобм !В КОНФУС НА ПРОЦЕДУРУ ЗАВЕРШЕНИЯ
        отмен   (М16)
адреса	конд	а(ОБВНП3+24-8)
	конд	а(ОБВНП3+24-7)
	конд	а(ОБВНП3+24-6)
	конд	а(ОБВНП3+24-5)
	конд	а(ОБВНП3+24-12)
уходы	мод	базсв7
	пб	ПРП8-СВЯЗЬ7
	мод	базсв7
	пб	ПРП7-СВЯЗЬ7
	мод	базсв7
	пб	ПРП6-СВЯЗЬ7
	мод	базсв7
	пб	ПРП5-СВЯЗЬ7
	мод	базсв7
	пб	ПРП12-СВЯЗЬ7
*****
D47107	конд	в'0000000000157777' !КОМАНДА СМ?
	конд	в'0000000000071400'
D47111	пам	1
СЧЕТПР	пам	1
рэ133б	пам	2
D47115	пам	1
D47116	пам	1
рэ133в	пам	1
D47120	конд	в'7777777777777777'
D47121	пам	1
экояч1	пам	1
D47123	конд	в'0000000000400030'
D47124	пам	1
п3260	конд	в'0000000000003260'
Е16П2	конд	в'0000000000177776'
D47127	конд	п'0000  '
О12П9	конд	в'7777777777770377'
Е12П9	конд	п'0000 0'
О4П1	конд	в'7777777777777760'
D47133	пам	1
	конд	п'000001'
	конд	п'000002'
	конд	п'000002'
	конд	п'000003'
	конд	п'000002'
	конд	п'000002'
	конд	п'000002'
е48э50	конд	в'4000000000000000'
	конд	в'2400000000000000'
	конд	п'Р00000'
	конд	п'800000'
	конд	в'0000000000000200'
	конд	п'00000F'
	конд	п'00000А'
D47152	пам	7
анспрп	конд	A(НСТПРП)
	конд	A(НСТПРП)
	пам	4
п5215	конд	в'0000000000005215'
	конд	в'0000000000000162'
D47171	пам	1
D47172	пам	1
D47173	пам	1
D47174	пам	1
D47175	пам	1
D47176	конд	п'А00000'
уввад1	конд	в'110'
	конд	в'010'
	конд	в'140'
	конд	в'4010'
	пам	4
уввад2	конд	в'132'
	конд	в'032'
	конд	в'146'
	пам	5
уввад3	конд	в'154'
	конд	в'054'
	конд	в'154'
	пам	5
уввад4	конд	в'157'
	конд	в'057'
	конд	в'157'
	пам	5
уввад5	конд	в'0000000000077775'
D47240	конд	в'0000000000077777'
	конд	в'0000000000077777'
	конд	в'0000000000077777'
	конд	в'0000000000077777'
	конд	в'0000000000077777'
	конд	в'0000000000077777'
	конд	в'0000000000077777'
D47247	конд	в'0000000000000200'
	конд	п'000010'
	конд	п'000010'
	пам	5
D47257	конд	п'00000 '
	конд	п'00000 '
	конд	п'000003'
	пам	5
*
*** БЛОК РАБОТЫ С МБ
*
св7мб	мод	базсв7
	уиа	*-СВЯЗЬ7(М13)
        употр   св7мб(М13)
	зп	шмбсв7
	счи	М1
	зп	ИР1
	сч	НОММБ1
	сда	64-9
	счмр	
	и	П7
	уи	М1
	счи	М15
	вч	E3
	по	G47323
	сч	рсв7мб+25
	пе	G47323
	сч	КУС1Б-1(М16)
	нтж	рсв7мб+10
	и	мсв7мб
	по	G47365
	сч	КУС1Б-1(М16)
	зп	рсв7мб+25
	и	тсв7мб
	зп	рсв7мб
	счи	М15
	вч	E2
	пе	22(М13)
	нтж	E1
	уи	М5
	мод	КНАПР-1(М16)
	слиа	0(М5)
	мод	ИР1
	сч	(М5)
	мода	рсв7мб+5
	зп	-2(М15)
	сч	РЯОМБ
	мода	рсв7мб+7
	зп	-2(М15)
	сч	ИР1
	сда	64+1
	мода	рсв7мб+3
	зп	-2(М15)
	сч	КУС1Б-1(М16)
	мода	рсв7мб+1
	зп	-2(М15)
	рег	'237'
	и	Е47(М1)
	по	22(М13)
	уиа	'2003'
	сч	ЛИС1Б-1(М16)
	рзб	П14037
	или	рсв7мб
	или	п144
	или	НОММБ1
	увв	(М1)
	уиа	'3'
G47323	счи	М15
	вч	П6
	пе	G47327
	сч	КУС1Б-1(М16)
	зп	рсв7мб+24
	сч	ВРЕМЯ
	зп	рсв7мб+17
	пб	G47365
G47327	мода	рсв7мб+11
	сч	(М15)
	слц	E1
	и	Е15П1
	мода	рсв7мб+11
	зп	(М15)
	сч	ВРЕМЯ
	сда	64-24
	мода	рсв7мб+11
	или	(М15)
	мода	рсв7мб+11
	зп	(М15)
	сч	КУС1Б-1(М16)
	мода	рсв7мб+18
	зп	(М15)
	пб	G47365
****
*
*** ДАННАЯ ПРОЦЕДУРА ОТРАБ.
*** ПРИ "ХОЛОДНОМ СТАРТЕ"
*
вдмлмб	мод	базсв7
	уиа	св7мб-СВЯЗЬ7(М13)
	зп	шмбсв7
	сч	рсв7мб+25
	мод	шмбсв7
	по	
****
	нтж	ОЧКСУМ+15(М14)
	и	мсв7мб
	мод	шмбсв7
	пе	
	зп	рсв7мб+25
	счи	М1
	зп	ИР1
	сч	3
	и	E12
	по	G47352
	сч	ВСЕЕД
	зп	рсв7мб+25
	сч	ОЧКСУМ+15(М14)
	и	тсв7мб
	зп	рсв7мб
G47352	сч	НОММБ1
	сда	64-9
	счмр	
	и	П7
	уи	М1
G47355	рег	'237'
	и	E47(М1)
	по	G47355
	уиа	'2003'
	сч	ЯЛИСМБ(М14)
	рзб	П14037
	или	рсв7мб
	или	п120
	или	НОММБ1
	увв	(М1)
	уиа	'3'
	сч	ВРЕМЯ
	зп	рсв7мб+9
	сч	ОЧКСУМ+15(М14)
	зп	рсв7мб+10
G47365	рег	'237'
	и	E47(М1)
	по	G47365
	сч	ИР1
	уи	М1
	мод	шмбсв7
	пб	
мсв7мб	конд	в'7770000003607777'
п120	конд	в'120'
п144	конд	в'144'
тсв7мб	конд	п'0004/3'
ИР1     пам     1
рсв7мб    пам     33
ВЗУ	ВНЕШ	шкут,шзпри7,ФИЗОБМ,швзк7б,ФИЗО,шкаэко,швзк7а
ДИСП70	ВНЕШ	ЗАПВЕТ,ТБУФ,ВИСП16,ГАШПРП,УСТПРП,БПУ,БМВ,АВОСТА
ДИСП70	ВНЕШ	П14037,ЯЛИСМБ,ТСЛ,НЗАД,СМ,РШКИВ,МШГ,ВРЕМЯ,НРЕ,ИВ25А
ДИСП70	ВНЕШ	МПРП,П7,РАБШК,ТМД,ТЗФСТР,РЯОМБ,НОММБ1,П377,П60,ТАС
ДИСП70	ВНЕШ	П3,ГУС,ШКСВН,П17,П5,П6,П7777,П77,ВСЕЕД,П40
ДИСП70	ВНЕШ	ОБВНП3,(Е15П1,П77777),Е48П43,НСТПРП,П20,Е48П25
ДИСП70	ВНЕШ	КУС1Б,ЛИС1Б,КНАПР,ТУС,ШККАН,ОЧКСУМ,КСВЗ
КИТ	ВНЕШ	ВППС,ТБЛ,Е48П42,Е32П25
МОТТ	ВНЕШ	ШКАЛ,ШКУСТР,ШКОРАБ,ШЗПРИМ,ШКВЫД,ШКЭ71,КПФШ,СЛОN
МОТТ    ВНЕШ    ШБУДИЛ,ЕЗВ,КВ2,ЗПСЕС1,шмбсв7,ПИЛОТ,окно
ТЕРМ	ВНЕШ	ТАБКОД,ОЧСИМВ,км2в,КС
КОНФУС	ВНЕШ	базсв7
КОНФУС	ВНЕШ	рабдкс,завобм,базэко,рсвсм1,рсвясм
КОНФУС	ВНЕШ	е6свз7,рабнст
ЭКВВОД	ВНЕШ	ЭКВВОД,АВОС1,КОНЕЦ1
ЭК7	ВНЕШ	ЭК7,изсв7
ЭКО	ВНЕШ	ЭКО,прцес1,прцс1а
        ВХОД    адреса,уходы,зпшксм,Э50133,вдмлмб,св7мб,экояч1
	ВХОД	приас7,св7ас1,св7ас2,вых133,с7эко1
*
	ФИНИШ

СВЯЗЬ7	СТАРТ	'46000'
	Б
	Е
	М
*МОДУЛЬ ОРГАНИЗАЦИИ СВЯЗИ БЭСМ-6 И ДКС
*ЧЕРЕЗ УСТР-ВО КАДОПАМ
*
*              ВХОДНАЯ ИНФОРМАЦИЯ
*
* 1. ПРИ СОЕДИНЕНИИ   С  Д К С:
*
* М2     - НОМЕР ТЕРМИНАЛА
* М3 	- СЛОВО ДКС
* М4	- РАБ. РЕГИСТР "СВЯЗЬ7"
* М5	- N НАПР. КРК
* 41(М7) - АДРЕС КОНЦА ВЫДАВАЕМОГО МАССИВА + 1
* 42(М7) - ДЛЯ ПЕРВОГО ВХОДА <42>, СЛОВО Н-МАШИНЫ
* М10	- АДР. ИНФ. ПОЛЯ ТЕРМИНАЛА
* М13
* М14 - РАБОЧИЙ ПРИ РАБОТЕ С АС ДКС
* М16   - АДРЕС БУФЕРА S-ТЕРМИНАЛА
* М17 - РАБОЧИЙ
******
*
*** БЛОК ОПР. ТИПА ОБОРУД., ПОДКЛ. ЧЕРЕЗ ДКС
*
*** !ПРИ ВЫЗ. Э50 '133' НЕ ЗАГР.
* ЗАГР. ПРИ УСТ. НАПР. КРК=0
*
	употр	СВЯЗЬ7(М4)
	уиа	'2003'
	сч	ШКОРАБ
	и	ЕЗВ
	пе	обдкс3
	сч	шзпри7
	и	ШКЭ71
	нтж	ШКЭ71
	и	шкут !НУЖНО ДЛЯ АС ДКС
	зп	рприем
обмдкс	уиа	'2003'
	по	обдкс3
	нед	Е15П1
	уи	М16
	мода	ttvt7
	пв	(М17)
обдкс1	сч	рприем
	нтж	E48(М16)
	зп	рприем
	уиа	'3'
	пб	обмдкс
*
ttvt7	сч	E41
	или	ВППС(М16)
	зп	ВППС(М16)
	сч	ЕЗВ
	или	ШКОРАБ
	зп	ШКОРАБ
	сч	E48(М16)
	или	ШБУДИЛ
	зп	ШБУДИЛ
	пб	(М17)
*
обдкс3	уиа	'2003'
	сч	туsаб+18
	по	бинот7 !ВЫХОД ТОЛЬКО ОТСЮДА
	нед	Е15П1
	уи	М17
	зп	sслово
	сч	E48(М17)
	нтж	туsаб+18
	зп	туsаб+18
	уиа	'3'
*
*** ПЕРЕКЛЮЧАТЕЛЬ
*
тут	мода	тут
	пб	(М17) !НА ttvt7. ПОЧЕМУ ПРОХОДИМ ДАЛЬШЕ?
*
	мода
	пб	трмдкс  !БЕЗ ВЫЗ. АВТ. "ЭКО"

*
	мода	,
	пб	обдкс4
*
	уиа	'2003'
	пб	выднаh !НА ВЫХОД
*
*** ПРОВЕРКА НАЛИЧИЯ ЭВМ СМ НА СВЯЗИ
*
зпшксм	мода	D47133 ВХ. ЧЕРЕЗ НБОБ?
	сч	(М5)
	уи	М16
	сч	рсвсм1
	или	E48(М16)
	зп	рсвсм1
	сч	СМ
	зп	рсвясм(М16)
	пб	обдкс5
*****
обдкс4	уиа	'2003'
	сч	туsаб+20
	по	СВЯЗЬ7
	нед	Е15П1
	уи	М5
	сч	туsаб+20
	нтж	E48(М5)
	зп	туsаб+20
*
обдкс5	счи	М5
	нтж	всеед7 !ВСЕЕД
	пе	обдкс6
	счи
	зп	туsаб
	зп	туsаб+1
обдкс6	уиа	'2003'
	сч	туsаб+10(М5)
	по	обдкс8
	нед
	уи	М2
	мод	НЗАД(М2)
	уиа	0(М15)
	сч	5(М15)
	или	E23
	нтж	E23
	зп	5(М15)
	уиа	29(М17)
	пв	АВОСТА(М16)
	сч	туsаб+10(М5)
	нтж	E48-1(М2)
	зп	туsаб+10(М5)
	сч	швзк7б
	нтж	E48-1(М2)
	зп	швзк7б
	уиа	'3'
	пб	обдкс6
********
*
*
бинот7	уиа	'2003' ИЗ обдкс3
	сч	ШКСВН
	или	E24
	зп	ШКСВН
	сч	E24
	и	швзк7а
	нтж	швзк7а
	зп	швзк7а
	пб	ВИСП16 В ДИСП70.ВИСП16
*
***
*
обдкс8	уиа	'2003'
	сч	туsаб+2(М5)
	по	обдкс4
	нед	Е15П1
	уи	М16
	мода	ttvt7
	пв	(М17)
	сч	СЛОN
	или	E48(М16)
	зп	СЛОN
	сч	шзпри7
	или	E48(М16)
	нтж	E48(М16)
	зп	шзпри7
	сч	туsаб+2(М5)
	нтж	E48(М16)
	зп	туsаб+2(М5)
	уиа	'3'
	пб	обдкс8
*
***** БЛОК ОБРАБОТКИ ИНФ. ЧЕРЕЗ АС ДКС
***** В АВТОМАТИЧ. РЕЖИМЕ
*
* ЗАГР. ПРИ НАПР. КРК=0
трмдкс	уиа	'2003' !Из обдкс3 или ИЗ АВТ. ЭКО
	сч	туsаб+19
	по	СВЯЗЬ7
	нед	Е15П1
	уи	М5
	сч	туsаб+19
	нтж	E48(М5)
	зп	туsаб+19
	уиа	25(М15)
	сч	E48-1(М15)
	или	швзк7а
	зп	швзк7а
	сч	туsаб+21(М15)
	и	О12П9
	зп	туsаб+21(М15)
	мода	D47133
	сч	(М5)
	сда	64-8
	или	туsаб+21(М15)
	зп	туsаб+21(М15)
	сда	64+8
	и	П7
*
	кк	26,'70000' !132,0 ПРИ НАПР. КРК=0
*!ЗДЕСь ЦИКЛИМСЯ ЕСЛИ N НАПР. КРК=0.
* !МАСКА 7707 NNNN NNNN NNN2
*
	счи	М5
	нтж	E2
	по	номнпр
	нтж	П3
	пе	запрос !НАПР. КРК=0
*
* EСЛИ НОМ. НАПР. ДКС <> 0
номнпр	кк	26,'2' !032,2 - !МАСКА 7707 NNNN NNNN NNNN
	зп	nнпкрк
	мод	маснп1
	кк	26,'0' !СЧЕТЧИК ПРЕРЫВАНИЯ ИСТОЧНИКА
* !МАСКА 7707 NNNN NNNN NNNN
	сда	64+24
	зп	экояч1
*
*** ДЕШИФРАТОР ЗАПРОСОВ ОТ Э-60
*
запрос	счи	М5  НОМЕР НАПР КРК
	нтж	всеед7
	по	рспец
	уии	М16(М5)
	слиа	-3(М16)
	мода	зпрос1  !НАПР. КРК =0
	пино	(М16) !УХОД НА зпрос1
	сч	туsаб+10(М5)
	по	трмдкс
	мода	абуфs
	сч	(М5)
	уи	М16 АДРЕС БУФЕРА S-ТЕРМИНАЛА
	кк	26,'1'(М16)
	зп	D47174
	кк	26,'2'(М16)
	нтж	D47174
	по	трмдкс
	кк	26,'0'(М16)
	сда	64+24
	уи	М2 НОМЕР АБОНЕНТА
	вч	П40
	пе	трмдкс
	вч	П20
	по	трмдкс
	сч	туsаб+10(М5)
	и	E48-1(М2)
	по	трмдкс
	сч	шкаэко
	и	E48-1(М2)
	мод	базэко
	пе	прцес1-ЭКО !В ЭКО
	сч	D47174
	сда	64+24
	кк	26,'70002'(М16)
	мода	трмдкс
	уиа	0(М16)
	счи	М2
	или	E26
	пб	ЗАПВЕТ
*
зпрос1	сч	D47176
	и	E48(М5)
	по	непоня !НАПР. КРК=0
	мода	(М5)
	мод	абуфs
	кк	26,'1' СЧЕТЧИК ПРЕРЫВАНИЯ ПОЛУЧАТЕЛЯ.
	сда	64+24
	зп	D47174
	мода	D47257
	и	(М5)
	нтж	D47174
	пе	непоня НЕ НАШЕ ПРЕРЫВАНИЕ(?)
	пб	завобр
обраб	уиа	'3'
	сч	D47174
	уи	М16
	мода	(М5)
	мод	абуфs
	кк	26,'2'(М16)
	сда	64+24
	уи	М2 НОМЕР АБОНЕНТА
	слиа	1(М16)
	счи	М16
	мода	D47257
	и	(М5)
	зп	D47174
	слиа	-48(М2)
	мода	ос  СИСТЕМНЫЙ ЗАПРОС ОТ S-МАШИНЫ
	пио	(М2)
	слиа	48(М2)
	счи	М2
	вчоб	П60
	пе	завобр
	сч	туsаб+10(М5)
	или	туsаб+2(М5)
	и	E48-1(М2)
	по	завобр
	сч	туsаб+2(М5)
	и	E48-1(М2)
	пе	sтрзпр
завобр	уиа	'3'
	мода	(М5)
	мод	абуфs
	кк	26,'0' СЧЕТЧИК ПРЕРЫВАНИЯ ИСТОЧНИКА
	сда	64+24
	мода	D47257
	и	(М5)
	нтж	D47174
	пе	обраб
	уиа	'2003'
	сч	D47174
	мода	(М5)
	мод	абуфs
	кк	26,'70001' !МАСКА 7707
непоня	сч	туsаб+10(М5)
	зп	СЧЕТПР+3
снова	по	трмдкс ---> !МАСКА 7707
	нед
	уи	М2
	или	E26
	пв	ЗАПВЕТ(М16)
	уиа	'3'
	сч	СЧЕТПР+3
	нтж	E48-1(М2)
	зп	СЧЕТПР+3
	пб	снова
*
*** СПЕЦИАЛЬНЫЙ РЕЖИМ РАБОТЫ
*
рспец	сч	nнпкрк
	сда	64+24
	и	П77
	по	трмдкс !НАПР. КРК=0.
	нтж	П60
	по	плохо
	и	П3
	уи	М2
	нтж	П3
	по	трмдкс
	сч	рабнст+1(М2)
	и	Е16П2
	по	трмдкс
	или	nнпкрк
	зп	рабнст+1(М2)
	и	Е16П2
	нед
	уи	М2
	сч	E48-1(М2)
	и	шкаэко
	мод	базэко
* ДЛЯ ЗАПУСКА АВТ. ЭКО
	пе	прцс1а-ЭКО
	мода	трмдкс
	уиа	0(М16)
	сч	туsаб
	и	Е16П2
	по	трмдкс
	нед
	или	E26
	пб	ЗАПВЕТ
*
плохо	сч	nнпкрк
	сда	64-4
	счмр
	пе	трмдкс
	зп      ,               неясно
	зп
	пб	трмдкс
*
*         ПРИШЛА КОМАНДА ОТ СИСТЕМЫ
*
ос	мода	(М5)
	мод	абфосs
	кк	26,'0'
	сда	64+24
	и	П3
	по	завобр
	уи	М17  КОД КОМАНДЫ
	мода	перекл
	пб	-1(М17)
*
перекл	пб	нтерм7
        пам     0
        пб      завобр
        пам     0
        пб      отклтр
*
*         ПОДКЛЮЧЕНИЕ НОВОГО ТЕРМИНАЛА
*
нтерм7	сч	туsаб+2(М5)
	зп	СЧЕТПР+3
нтер7а	сч	СЧЕТПР+3
	по	нтер7б
	нед	Е15П1
	уи	М16
	сч	E48(М16)
	нтж	СЧЕТПР+3
	зп	СЧЕТПР+3
	сч	туsаб+22(М16)
	сда	64-6
	счмр
	зп	СЧЕТПР+4
	мода	(М5)
	мод	абфосs
	кк	26,'1' !СЧЕТЧИК ПРЕРЫВАНИЯ ПОЛУЧАТЕЛЯ.
	сда	64+24
	и	П77
	нтж	СЧЕТПР+4
	по	недстп ЛИНИЯ УЖЕ ЗАНЯТА
	пб	нтер7а
*
нтер7б	сч	СЛОN
	или	E1
	нтж	E1
	по	ответ1
	нед	Е15П1
	уи	М16
	уиа	'2003'
	сч	E48(М16)
	или	туsаб+2(М5)
	зп	туsаб+2(М5)
	сч	E48(М16)
	нтж	СЛОN
	зп	СЛОN
	сч	E48(М16)
	или	шзпри7
	зп	шзпри7
	сч	E48(М16)
	или	ШКУСТР
	зп	ШКУСТР
	сч	ШКАЛ
	или	E48(М16)
	зп	ШКАЛ
	сч	туsаб+22(М16)
	и	D47127
	зп	туsаб+22(М16)
	счи	М5
	сда	64-4
	или	туsаб+22(М16)
	зп	туsаб+22(М16) НОМЕР МАШИНЫ
	мода	(М5)
	мод	абфосs
	кк	26,'1' !СЧЕТЧИК ПРЕРЫВАНИЯ ПОЛУЧАТЕЛЯ.
	сда	64-18
	или	туsаб+22(М16)
	зп	туsаб+22(М16) .ОПИСАТЕЛЬ АБОНЕНТА S-МАШИНЫ
	мода	(М5)
	мод	абфосs
	кк	26,'2'
	сда	64+25
	и	Е15П1
	сда	64-24
	или	туsаб+22(М16)
	зп	туsаб+22(М16) .АДРЕС ОБЛАСТИ АБОНЕНТА
	пв	ПИЛОТ(М15)
	счи
	зп	яответ
*
*         ОТВЕТ
*
ответ	слиа	1(М16)
	счи	М16 НОМЕР ТЕРМИНАЛА
	мода	(М5)
	мод	абфосh
	кк	26,'70001'
	сч	яответ
	мода	(М5)
	мод	абфосh
	кк	26,'70002'
	сч	E2 КОД КОМАНДЫ
	мода	(М5)
	мод	абфосh
	кк	26,'70000'
	уиа	48(М2) .НОМ АБОНЕНТА - ОС
	уии	М15(М5)
	мода	завобр
	уиа	0(М17)
	пб	прервs
ответ1	уи	М16
	сч	E2
	зп	яответ
	пб	ответ
недстп	сч	E1    !ЕСЛИ ЛИНИЯ НЕ ДОСТУПНА
	зп	яответ
	пб	ответ
*
*         ОТКЛЮЧЕНИЕ ТЕРМИНАЛА
*
отклтр	мода	(М5)
	мод	абфосs
	кк	26,'1' !СЧЕТЧИК ПРЕРЫВАНИЯ ПОЛУЧАТЕЛЯ.
	сда	64+24
	и	П77
	уи	М16
	слиа	-1(М16)
	сч	туsаб+2(М5)
	и	E48(М16)
	по	завобр
	нед	Е15П1
	и	Е15П1
	зп	СЧЕТПР+3
	счи	М16
	нтж	СЧЕТПР+3
	пе	завобр
	уиа	'2003'
	сч	E48(М16)
	нтж	ВСЕЕД
	зп	рприем
	и	туsаб+2(М5)
	зп	туsаб+2(М5)
	сч	СЛОN
	или	E48(М16)
	зп	СЛОN
	сч	шзпри7
	и	рприем
	зп	шзпри7
	мода	ttvt7
	пв	(М17)
	уиа	'3'
	пб	завобр
*
*         ПОСТУПИЛ ЗАПРОС ОТ S-ТЕРМИНАЛА
*
sтрзпр	сч	туsаб+21(М2)
	сда	64+24
	и	Е15П1
	уи	М10 АДР. ИНФ. ПОЛЯ ТЕРМИНАЛА
	кк	26,'2'(М10)
	сда	64+24
	и	П7
	уи	М17 КОД КОМАНДЫ
	слиа	-4(М17)
	мода	ввдsтр 		4
	пио	(М17)
	слиа	-1(М17)
	мода	выднаs
	пио	(М17)
	слиа	-1(М17)
	мода	завобр
	пино	(М17)
	уиа	'2003'
	сч	ТСЛ-1(М2)
	или	E19
	зп	ТСЛ-1(М2)
*
***         ЗАВЕРШЕНА ВЫДАЧА НА S-ТЕРМИНАЛ
*
выднаs	слиа	-1(М2)
	мода	завобр
	уиа	0(М16)
	уиа	'2003'
	сч	E48(М2)
	или	ШКВЫД
	нтж	E48(М2)
	зп	ШКВЫД
	сч	E48(М2)
	или	ШЗПРИМ
	нтж	E48(М2)
	зп	ШЗПРИМ
	уии	М15(М2)
	кк	26,'1'(М10)
	сда	64+24
	и	D47107
	кк	26,'70001'(М10)
	счи
	зп	ТАС+1(М2)
	пб	КВ2 !ЗАВЕРШ. ВЫДАЧИ И ВЫД. ПРИГЛ.
*
*         ВОЗЬМИ МАССИВ С S-ТЕРМИНАЛА
*
ввдsтр	слиа	-1(М2)
	уии	М17(М2)
	мод	ТБЛ(М2)
	уиа	0(М16)
	уиа	'2003'
	сч	ШЗПРИМ
	и	E48(М2)
	по	вводсs
	кк	26,'0'(М10)
	сда	64+24
	и	D47107
	кк	26,'70000'(М10)
	пб	завобр
вводсs	сч	ТСЛ(М2)
	или	E28
	или	E31
	зп	ТСЛ(М2)
	уиа	'3'
	кк	26,'3'(М10)
	сда	64+25
	уи	М13  АДРЕС БУФЕРА S-ТЕРМИНАЛА
	сч	ТСЛ(М2)
	сда	64+36
	и	E1
	уи	М11
	уиа	'3'
ввдcs1	кк	26,'0'(М13)
	сда	64+24
	зп	рприем
	уиа	-1(М3)
ввдcs2	и	П377
	уи	М15
	зп	символ
	по	конмас КОНЕЦ МАССИВА
	мода	цклпрм
	пино	(М11)
	сч	ТАБКОД(М15)
	сда	64+8
	и	П377
	зп	символ
цклпрм	мод	(М16)
	сч	9(М16) ДОДВИЖКА
	сда	64-8
	или	символ
	пв	ЗПСЕС1(М14)
	мод	ТБЛ(М2)
	уиа	0(М16)
	сч	рприем
	сда	64+8
	мода	ввдcs2
	цикл	(М3)   !
	слиа	1(М13)
	пб	ввдcs1
конмас	сч	П3
	зп	рприем
	сч	ТСЛ(М2)
	и	E37
	пе	завsпр
	сч	П377
	зп	рприем
завsпр	мод	(М16)
	сч	9(М16)
	сда	64-8
	или	рприем
	сд	(М16)
	мод	(М16)
	зп	9(М16)
	сч	9(М16)
	зп	символ
	сч	10(М16)
	зп	счsпрм
	кк	26,'0'(М10)
	сда	64+24
	и	D47107
	кк	26,'70000'(М10) S-СТАТУС
	мода	завобр
	уиа	0(М14)
	пб	КПФШ РЕКОРДОВ НЕТ - БУФЕР ИСЧЕРПАН
*
*         ЗАВЕРШЕНА ВЫДАЧА НА Н-ТЕРМИНАЛ
*
выднаh	сч	туsаб+21
	по	СВЯЗЬ7
	нед
	уи	М6
	сч	туsаб+21
	нтж	E48-1(М6)
	зп	туsаб+21
	сч	МШГ
	или	E48-1(М6)
	зп	МШГ
	сч	ВСЕЕД
	зп	всеед7
	счи
	зп	шкаэко
	сч	D47124
	мода	СВЯЗЬ7
	уиа	0(М15)
	пб	ТБУФ

*
*         ЗАВЕРШЕН ПРИЕМ  С Н-ТЕРМИНАЛА
*
прмhтр	уии	М1(М3) ИЗ ВЗУ.
	уии	М12(М4)
	мод	базсв7
	уиа	0(М4)
	уии	М3(М4)
	сч	ТУС-1+'23'      Е30
	нтж	ГУС
	и	Е48П42
	по	прhтр1
	слиа	1(М3)
прhтр1	счи	М1
	сда	64-15
	зп	sслв+1(М3)
	счи	М12
	или	sслв+1(М3)
	зп	sслв+1(М3)
	уиа	'2003'
	сч	туsаб+22(М2)
	сда	64+4
	и	П17
	уи	М1
	зп	sслв+5(М3)
	мода	D47133
	сч	(М1)
	уи	М1
	сч	НЗАД
	уи	М12
	сч	E48-1(М12)
	или	швзк7а
	зп	швзк7а
	сч	туsаб+21(М12)
	и	О12П9
	зп	туsаб+21(М12)
	счи	М1
*
	кк	26,'70000'
*
	сда	64-8
	или	туsаб+21(М12)
	зп	туsаб+21(М12)
	уиа	'3'
	сч	туsаб+22(М2)
	сда	64+24
	зп	ак+2(М3)
	уиа	-1(М12) СЧЕТЧ. БАЙТ В СЛОВЕ S-МАШИНЫ
	сч	ак+2(М3)
	слц	E4
	зп	ак(М3)
	уиа	0(М1) СЧЕТЧ. СЛОВ S-МАШИНЫ
	сч	ТСЛ(М2)
	и	E37
	нтж	E37
	сда	64+22
	или	E14
	мод	ак+2(М3)
	кк	26,'70001'
	счи
	мод	ак+2(М3)
	кк	26,'70000'
	сч	E1
	зп	46(М7)
	пб	ОЧСИМВ
*ПРИЕМ СЛЕД. СИМВОЛА
*42(М7) - H-СЛОВО
слдсмв	сч	42(М7) ИЗ МОТТ
	по	(М17)
	мода	слсмв1
	пино	(М5)
	уиа	КС(М17)
слсмв1	сч	42(М7)
	и	П377
	мода	слсмв2
	цикл	(М12)
	сда	64-8
	или	sслв+3(М3) СТАРШИЙ БАЙТ - СЛЕВА
	мод	ак(М3)
*
	кк	26,'70000'(М1) ГОТОВО S-СЛОВО
*
	слиа	1(М1)
	уиа	-1(М12) СЧЕТЧ. БАЙТ В СЛОВЕ S-МАШИНЫ
	счи	М1 СЧЕТЧ. СЛОВ S-МАШИНЫ
	нтж	длнбуф
	по	кнцмсв БУФ. ПЕРЕПОЛНЕН
	пб	(М17) В ТЕРМ.КС
слсмв2	зп	sслв+3(М3)
	пб	(М17) В ТЕРМ.КС
*
*         ПОСЛЕДНЕЕ СЛОВО МАССИВА
*
кнцмсв	уиа	E45(М10)
	сч	ТСЛ(М2)
	и	Е36
	пе	кнмсв2
	уиа	0(М10)
	сч	ТСЛ(М2)
	и	E37
	пе	кнмсв2
	сч	п5215
	зп	42(М7)
кнмсв1	мода	слсмв1
	пв	(М17)
	сч	42(М7)
	сда	64+8
	зп	42(М7)
	пе	кнмсв1
кнмсв2	мода	кнмсв3
	пио	(М12)
	счи
	зп	sслв+3(М3)
кнмсв3	сч	sслв+3(М3)
	мод	ак(М3)
	кк	26,'70000'(М1)
	сч	ТСЛ(М2)
	зп	ТБЛ(М2)
	пам	0
	мода
	пам	0
	мода
	сч	sслв+5(М3)
	уи	М15
	сч	E3
	мода	прмсвs
	пио	(М10)
	мода	прмсвs
	пино	(М15)
	сч	П5
*
*         ВЫДАЧА КОМАНДЫ(ПРИМИ МАССИВ) В S-МАШИНУ
*
прмсвs	мод	ак+2(М3)
	кк	26,'70005' КОД КОМАНДЫ
	сч	ак(М3)
	сда	64-1
	мод	ак+2(М3)
	кк	26,'70006' АДРЕС МАССИВА
	счи	М1
	слц	E1
	мод	ак+2(М3)
	кк	26,'70007' ДЛИНА МАССИВА
	слиа	1(М2)
	мода	прервs !
	пв	(М17) ! ВОЗВРАТ С ПОСЛ. СТРОКИ
прмвs1	слиа	-1(М2)
	уии	М17(М2)
	мод	НЗАД
	сч	E48-1
* СЧЕТЧИК ОП. ОБМЕНА?
	нтж	швзк7а
	зп	швзк7а
*
	уиа	'3'
	сч	sслв+1(М3)
	уи	М4
	сда	64+15
	уи	М3
	уиа	0(М10)
	уиа	'2003'
	пб	км2в !В ТЕРМ
*
*** ВЫДАЧА ПРЕРЫВАНИЯ В ДКС (S-МАШИНУ)
*
*М15 - НОМ. МАШИНЫ, М2 - НОМ.АБОНЕНТА НА Н-МАШИНЕ
*
прервs	уиа	'2003'
	мода	(М15)
	мод	абуфh
*
	кк	26,'0' .СЧЕТЧИК ПРЕР.ОТ Н-МАШИНЫ
	сда	64+24
	мода	D47257
	и	(М15)
	уи	М1
	слц	E1
	мода	D47257
	и	(М15)
	мода	(М15)
	мод	абуфh
*
	кк	26,'70000' .ЗАПИСЬ НОВ. ЗНАЧ. СЧЕТЧИКА
*
	сч	туsаб+21(М2)
	сда	64-6
	счмр
	мода	(М15)
	мод	абуфh
*
	кк	26,'70002'(М1) НОМ. АБОНЕНТА
*
	мода	в200
	сч	(М15)
	мода	(М15)
	мод	маснп0 !ПЕРЕДАТЬ НА Э-60 НА 0 НАПР.
*
	кк	26,'70000'
*
	пб	(М17) !CМ ВЫШЕ!
*
*** ДЕШИФРАТОР Э50 '133'
*
Э50133	мод	базсв7 ИЗ ЭК7
	уиа	0(М4)
	уии	М12(М2)
	уии	М13(М3)
	сч	(М3)
	сда	64+6
	и	П77
	по	э133б
	уи	М12
	вч	П40
	пе	вы133а
	вч	П20
	по	вы133а
	мод	НЗАД(М12)
	уиа	0(М13)
э133б	счи	М12
	сда	64-6
	или	(М3)
	зп	(М3)
	и	П7
	зп	СЧЕТПР+1
	уи	М5
	нтж	П3
	пе	э133в
	сч	(М3)
	и	Е36
	сда	64+35
	слц	E1
	мода	D47133
	зп	(М5)
э133в	мода	D47133
	сч	(М5)
	уи	М17
	сч	(М3)
	нтж	Е48П43
	сда	64-12
	счмр
	зп	СЧЕТПР+5
	по	э133ош
	и	П7
	нтж	СЧЕТПР+5
	мод	СЧЕТПР+5
	по	э133г
	нтж	П60
	по	об133в
	нтж	п3260
	по	изсв7-ЭК7+768(М7)
э133ош	уиа	36(М6) !ЗАПРЕЩЕННЫЙ ЭКСТРАКОД
	пб	АВОС1-ЭКВВОД(М7)
*
*** ПЕРЕКЛЮЧАТЕЛЬ
*
э133г	сч	E24
	пб	э133м
*
э133д	сч	E23
	пб	э133м
*
э133е	сч	(М3)
	пб	э133ош
*
э133ж	сч	E23
	пб	э133м
*
э133з	мода	,
	пб	об133г
*
э133и	мода	,
	пб	об133д
*
э133к	сч	(М3)
	пб	об133е
*
э133л	сч	(М3) !ПРИ МАСКЕ 7707 NNNN NNNN NNNN
	пб	об133ж
*
э133м	или	СЧЕТПР+5
	зп	СЧЕТПР+5
	и	П77
	нтж	E1
	пе	э133о
	сч	(М3)
	и	E24
	пе	э133п
	сч	туsаб
	по	э133н
	нтж	E48-1(М2)
	пе	э133ош
	сч	E48-1(М12)
	зп	туsаб+1
	счи	М5
	зп	всеед7
э133н	сч	E48-1(М2)
	зп	туsаб
э133о	сч	швзк7б
	и	E48-1(М12)
	пе	вы133б
э133п	сч	МПРП
	и	E8(М17)
	пе	обм133
	сч	E8(М17)
	мода
	пв	УСТПРП(М15)
	уиа	'2003'
	счи
	зп	СЧЕТПР
э133р	увв	'4034'
	и	E8(М17)
	по	обм133
	сч	E8(М17)
	нтж	ВСЕЕД
	увв	'30'
	сч	СЧЕТПР
	слц	E1
	зп	СЧЕТПР
	сда	64+2
	по	э133р
	сч	E1
вых133	зп	(М3)
	уиа	'3'
	пб	КОНЕЦ1-ЭКВВОД(М7)
*
обход1	сч	E2 !НЕ ИСП.
	пб	вых133
*
вы133а	сч	E3
	пб	вых133
*
*** ПРОЦЕДУРА ОБМЕНА С ДКС К-71
*** В СООТВ. С ПАРАМЕТРАМИ
*** МАСКИ Э50 '133'
*
обм133	сч	е6свз7
	и	E8(М17)
	по	фикпрп
	счи	М17
	нтж	П7
	уи	М15
	кк	26,'0' !ПРИ РАБОТЕ С МАСКОЙ 7701
	и	E40(М15)
	пе	фикпрп
	сч	E1
	пб	вых133 !ВЫХОД
*
фикпрп	уиа	'2003'
	мода	уходы
	сч	(М17)
	рег	'101'
	мода	(М17)
	мод	адреса
	зп
	рег	'107'
	сч	(М3)
	или	СЧЕТПР+5
	и	E24
	пе	об133б
	сч	E48-1(М12)
	или	туsаб+10(М5)
	зп	туsаб+10(М5)
	сч	СЧЕТПР+5
	и	E23
	пе	об133б
	счи	М17
	и	П7
	кк	26,'70000'
	счи	М12
	мода	(М5)
	мод	абфосh
	кк	26,'70001'
	сч	E1
	мода	(М5)
	мод	абфосh
	кк	26,'70000'
об133а	мода	(М5)
	мод	абуфh
	кк	26,'0' * !ПРИ РАБОТЕ С МАСКОЙ 7707
	сда	64+24
	мода	D47257
	и	(М5)
	уи	М1
	сч	П60
	мода	(М5)
	мод	абуфh
	кк	26,'70002'(М1) *
	слиа	1(М1)
	счи	М1
	мода	D47257
	и	(М5)
	мода	(М5)
	мод	абуфh
	кк	26,'70000' *
	мода	в200
	сч	(М5)
	мода	(М5)
	мод	маснп0
	кк	26,'70000' *
	счи
	пб	вых133 !ВЫХОД
*
об133б	сч	туsаб+21(М12)
	и	О4П1
	зп	туsаб+21(М12)
	счи	М17
	или	туsаб+21(М12)
	зп	туsаб+21(М12)
	сч	E48-1(М12)
	или	швзк7б
	зп	швзк7б
	сч	5(М13)
	или	E23
	зп	5(М13)
	сч	(М3)
	и	E24
	по	о133б1
	счи
	пб	вых133 !ВЫХОД
о133б1	сч	туsаб+10(М5)
	и	E48-1(М12)
	нтж	туsаб+10(М5)
	по	вых133
	или	E48-1(М12)
	или	E48
	пб	вых133
*
вы133б	сч	П3
	пб	вых133 !ВЫХОД
*
об133в	сч	E8(М17)
	и	МПРП
	мода
	пв	ГАШПРП(М15)
	мода	КОНЕЦ1-ЭКВВОД(М7)
	уиа	0(М15)
	сч	туsаб+10(М5)
	или	туsаб+2(М5)
	по	(М15)
	уии	М16(М4)
	пб	зв133а
*
*** ДЛЯ ДРУГИХ РЕЖ. РАБОТЫ (Э52 '777', Э52 '3333'??)
*
об133г	сч	туsаб+10(М5)
	и	E48-1(М12)
	по	э133ош
	сч	шкаэко
	пе	э133ош
	сч	E1
	мода
	пв	ТБУФ(М15)
	сда	64-8
	уи	М6
	зп	базэко
	сда	64-2
	или	D47123
	или	НОММБ1
	зп	D47124
	уии	М16(М6)
	пб	ФИЗОБМ
*
об133д	сч	шкаэко
	и	E48-1(М12)
	по	э133ош
	или	туsаб+21
	зп	туsаб+21
	сч	E45
	зп	D47111
	уии	М16(М4)
	мода	вых133
	уиа	0(М15)
	пб	завобм
*
об133е	сч	туsаб
	и	E48-1(М2)
	по	э133ош
	сч	туsаб
	и	Е48П25
	зп	(М3)
	счи	М17
	и	П7
	кк	26,'70000'
	мод	маснп1
	кк	26,'0'
	сда	64+24
	зп	экояч1
	или	(М3)
	зп	(М3)
	уиа	'3'
	пб	КОНЕЦ1-ЭКВВОД(М7) !ОБМЕН ЗАВЕРШ. ВЫХОД.
*
об133ж	уиа	'2003' !C э133л
	счи	М17
	и	П7
*
	кк	26,'70000' !ПРИ РАБОТЕ С МАСКОЙ
*                          7707 NNNN NNNN NNN2
	сч	(М3)
	и	E18
	по	о133ж1 ---> !МАС. 7707
	сч	(М3)
	сда	64+12
	и	П17
	мода	(М5)
	мод	абфосh
	кк	26,'70000'
	сч	(М3)
	сда	64+18
	мода	(М5)
	мод	абфосh
	кк	26,'70001'
	пб	об133а
*
о133ж1	сч	(М3) !МАСКА 7707
	сда	64+12
	сда	64-12
	мода	в200
	или	(М5)
	мода	(М5)
	мод	маснп0
	кк	26,'70000'
	счи
	пб	вых133
*****
ПРП12	мод	базсв7
	уиа	0(М16)
	сч	-1
	слц	E1
	и	П7777
	уи	М15
	кк	26,'0'
	зп	-1
	и	Е32П25
	сда	64+12
	или	-1
	зп	-1
	счи	М15
	или	-1
	зп	-1
	пб	БМВ
*****
* ПРИ УСТ. НАПР. КРК=0
ПРП8	уиа	0(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
*****
ПРП7	уиа	1(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
ПРП5	уиа	3(М17)
	мод	базсв7
	пб	набзс7-СВЯЗЬ7
ПРП6	уиа	2(М17)
набзс7	мод	базсв7
	уиа	0(М16)
********************
        отмен   (М4)
        употр   СВЯЗЬ7(М16) !СМЕНА РАБ. РЕГИСТРА
********************
	счи
	зп	СЧЕТПР
зав133	увв	'4034'
	и	E8(М17)
	по	зв133б !ПРИ КРК=0
	нтж	ВСЕЕД
	увв	'30'
	сч	СЧЕТПР
	слц	E1
	зп	СЧЕТПР
	сда	64+2
	по	зав133
	сч	E8(М17)
	и	МПРП
	мода
	пв	ГАШПРП(М15)
	счи	М17
	сда	64-8
	нтж	туsаб+22+24
	и	Е12П9
	уиа	БПУ(М15)
	пе	зв133а
	сч	швзк7а
	и	E24
	нтж	швзк7а !НА КОНТРОЛЬ!
	зп	швзк7а
*
зв133а	мода	е48э50
	сч	(М17)
	или	туsаб+20
	зп	туsаб+20
	сч	E46
	зп	D47111
	пб	завобм
*
зв133б	мода	е48э50
	сч	(М17)
	или	туsаб+19
	зп	туsаб+19
	сч	E47
	зп	D47111
	уиа	БПУ(М15)
	пб	завобм !В КОНФУС НА ПРОЦЕДУРУ ЗАВЕРШЕНИЯ
        отмен   (М16)
адреса	конд	а(ОБВНП3+24-8)
	конд	а(ОБВНП3+24-7)
	конд	а(ОБВНП3+24-6)
	конд	а(ОБВНП3+24-5)
	конд	а(ОБВНП3+24-12)
уходы	мод	базсв7
	пб	ПРП8-СВЯЗЬ7
	мод	базсв7
	пб	ПРП7-СВЯЗЬ7
	мод	базсв7
	пб	ПРП6-СВЯЗЬ7
	мод	базсв7
	пб	ПРП5-СВЯЗЬ7
	мод	базсв7
	пб	ПРП12-СВЯЗЬ7
*****
D47107	конд	в'0000000000157777'
	конд	в'0000000000071400'
D47111	пам	1
СЧЕТПР	пам	6
всеед7	конд	в'-1'
nнпкрк	пам	1
экояч1	пам	1
D47123	конд	в'0000000000400030'
D47124	пам	1
п3260	конд	в'0000000000003260'
Е16П2	конд	в'0000000000177776'
D47127	конд	п'0000  '
О12П9	конд	в'7777777777770377'
Е12П9	конд	п'0000 0'
О4П1	конд	в'7777777777777760'
D47133	пам	1
	конд	п'000001'
	конд	п'000002'
	конд	п'000002'
	конд	п'000003'
	конд	п'000002'
	конд	п'000002'
	конд	п'000002'
е48э50	конд	в'4000000000000000'
	конд	в'2400000000000000'
	конд	п'Р00000'
	конд	п'800000'
	конд	в'200'
	конд	п'00000F'
	конд	п'00000А'
sслово	пам	7
длнбуф	конд	в'1171' ДЛИНА БУФ. ОБМ. С ДКС
	конд	в'1171'
адрком	пам	4
п5215	конд	в'5215'
	конд	в'162'
рприем	пам	1
символ	пам	1
счsпрм	пам	1
D47174	пам	1
яответ	пам	1
D47176	конд	п'А00000'
*
*** АДРЕСА БУФЕРОВ
*
*АДР. БУФЕРА ПРЕРЫВАНИЙ ОТ S-МАШИНЫ
абуфs	конд	в'110'  0 НАПР. КРК
	конд	в'010'  1 НАПР.
	конд	в'140'  2 НАПР.
	конд	в'4010' 3 НАПР.
	пам	4 	ЗАРЕЗЕРВ. ДЛЯ 4-7 НАПР.
*АДРЕС БУФЕРА ПРЕРЫВАНИЙ ОТ Н-МАШИНЫ
абуфh	конд	в'132'  0 НАПР.
	конд	в'032'  1 НАПР.
	конд	в'146'  2 НАПР.
	пам	5 ЗАРЕЗЕРВ. ДЛЯ 3-7 НАПР.
*АДРЕС БУФЕРА СИСТЕМНЫХ ЗАПРОСОВ S-МАШИНЫ
абфосs	конд	в'154'  0 НАПР.
	конд	в'054'  1 НАПР.
	конд	в'154'  2 НАПР.
	пам	5 ЗАРЕЗЕРВ. ДЛЯ 3-7 НАПР.
*АДРЕС БУФЕРА СИСТЕМНЫХ ЗАПРОСОВ Н-МАШИНЫ
абфосh	конд	в'157'  0 НАПР.
	конд	в'057'  1 НАПР.
	конд	в'157'  2 НАПР.
	пам	5 ЗАРЕЗЕРВ. ДЛЯ 3-7 НАПР.
*АДРЕС ЗАПИСИ (В РКС) ДЛЯ ПРЕРЫВАНИЯ S-МАШИНЫ
маснп0	конд	в'77775' 0 НАПР. ВЕДУЩАЯ Э-60?
маснп1	конд	в'77777' 1 НАПР.
	конд	в'77777' 2 НАПР.
	конд	в'77777' 3 НАПР.
	конд	в'77777' 4 НАПР.
	конд	в'77777' 5 НАПР.
	конд	в'77777' 6 НАПР.
	конд	в'77777' 7 НАПР.
*
в200	конд	в'200'    0 НАПР.
	конд	п'000010' 1 НАПР. '400'
	конд	п'000010' 2 НАПР.
	пам	5 ЗАРЕЗЕРВ. ДЛЯ 3-7 НАПР.
D47257	конд	п'00000 '  0 НАПР.
	конд	п'00000 '  1 НАПР.
	конд	п'000003'  2 НАПР.
	пам	5 ЗАРЕЗЕРВ. ДЛЯ 3-7 НАПР.
*
*** БЛОК РАБОТЫ С МБ
*
св7мб	мод	базсв7
	уиа	*-СВЯЗЬ7(М13)
        употр   св7мб(М13)
	зп	шмбсв7
	счи	М1
	зп	ИР1
	сч	НОММБ1
	сда	64-9
	счмр
	и	П7
	уи	М1
	счи	М15
	вч	E3
	по	G47323
	сч	рсв7мб+25
	пе	G47323
	сч	КУС1Б-1(М16)
	нтж	рсв7мб+10
	и	мсв7мб
	по	G47365
	сч	КУС1Б-1(М16)
	зп	рсв7мб+25
	и	тсв7мб
	зп	рсв7мб
	счи	М15
	вч	E2
	пе	22(М13)
	нтж	E1
	уи	М5
	мод	КНАПР-1(М16)
	слиа	0(М5)
	мод	ИР1
	сч	(М5)
	мода	рсв7мб+5
	зп	-2(М15)
	сч	РЯОМБ
	мода	рсв7мб+7
	зп	-2(М15)
	сч	ИР1
	сда	64+1
	мода	рсв7мб+3
	зп	-2(М15)
	сч	КУС1Б-1(М16)
	мода	рсв7мб+1
	зп	-2(М15)
	рег	'237'
	и	Е47(М1)
	по	22(М13)
	уиа	'2003'
	сч	ЛИС1Б-1(М16)
	рзб	П14037
	или	рсв7мб
	или	п144
	или	НОММБ1
	увв	(М1)
	уиа	'3'
G47323	счи	М15
	вч	П6
	пе	G47327
	сч	КУС1Б-1(М16)
	зп	рсв7мб+24
	сч	ВРЕМЯ
	зп	рсв7мб+17
	пб	G47365
G47327	мода	рсв7мб+11
	сч	(М15)
	слц	E1
	и	Е15П1
	мода	рсв7мб+11
	зп	(М15)
	сч	ВРЕМЯ
	сда	64-24
	мода	рсв7мб+11
	или	(М15)
	мода	рсв7мб+11
	зп	(М15)
	сч	КУС1Б-1(М16)
	мода	рсв7мб+18
	зп	(М15)
	пб	G47365
****
*
*** ДАННАЯ ПРОЦЕДУРА ОТРАБ.
*** ПРИ "ХОЛОДНОМ СТАРТЕ"
*
вдмлмб	мод	базсв7
	уиа	св7мб-СВЯЗЬ7(М13)
	зп	шмбсв7
	сч	рсв7мб+25
	мод	шмбсв7
	по
****
	нтж	ОЧКСУМ+15(М14)
	и	мсв7мб
	мод	шмбсв7
	пе
	зп	рсв7мб+25
	счи	М1
	зп	ИР1
	сч	3
	и	E12
	по	G47352
	сч	ВСЕЕД
	зп	рсв7мб+25
	сч	ОЧКСУМ+15(М14)
	и	тсв7мб
	зп	рсв7мб
G47352	сч	НОММБ1
	сда	64-9
	счмр
	и	П7
	уи	М1
G47355	рег	'237'
	и	E47(М1)
	по	G47355
	уиа	'2003'
	сч	ЯЛИСМБ(М14)
	рзб	П14037
	или	рсв7мб
	или	п120
	или	НОММБ1
	увв	(М1)
	уиа	'3'
	сч	ВРЕМЯ
	зп	рсв7мб+9
	сч	ОЧКСУМ+15(М14)
	зп	рсв7мб+10
G47365	рег	'237'
	и	E47(М1)
	по	G47365
	сч	ИР1
	уи	М1
	мод	шмбсв7
	пб
мсв7мб	конд	в'7770000003607777'
п120	конд	в'120'
п144	конд	в'144'
тсв7мб	конд	п'0004/3'
ИР1     пам     1
рсв7мб    пам     33
scлв	экв	scлово-СВЯЗЬ7
ак	экв 	адрком-СВЯЗЬ7
ВЗУ	ВНЕШ	шкут,шзпри7,ФИЗОБМ,швзк7б,ФИЗО,шкаэко,швзк7а
ДИСП70	ВНЕШ	ЗАПВЕТ,ТБУФ,ВИСП16,ГАШПРП,УСТПРП,БПУ,БМВ,АВОСТА
ДИСП70	ВНЕШ	П14037,ЯЛИСМБ,ТСЛ,НЗАД,СМ,РШКИВ,МШГ,ВРЕМЯ,ИВ25А
ДИСП70	ВНЕШ	МПРП,П7,ТМД,ТЗФСТР,РЯОМБ,НОММБ1,П377,П60,ТАС
ДИСП70	ВНЕШ	П3,ГУС,ШКСВН,П17,П5,П6,П7777,П77,ВСЕЕД,П40
ДИСП70	ВНЕШ	ОБВНП3,(Е15П1,П77777),Е48П43,НСТПРП,П20,Е48П25
ДИСП70	ВНЕШ	КУС1Б,ЛИС1Б,КНАПР,ТУС,ОЧКСУМ
КИТ	ВНЕШ	ВППС,ТБЛ,Е48П42,Е32П25
МОТТ	ВНЕШ	ШКАЛ,ШКУСТР,ШКОРАБ,ШЗПРИМ,ШКВЫД,ШКЭ71,КПФШ,СЛОN
МОТТ    ВНЕШ    ШБУДИЛ,ЕЗВ,КВ2,ЗПСЕС1,шмбсв7,ПИЛОТ,окно
ТЕРМ	ВНЕШ	ТАБКОД,ОЧСИМВ,км2в,КС
КОНФУС	ВНЕШ	базсв7
КОНФУС	ВНЕШ	туsаб,завобм,базэко,рсвсм1,рсвясм
КОНФУС	ВНЕШ	е6свз7,рабнст
ЭКВВОД	ВНЕШ	ЭКВВОД,АВОС1,КОНЕЦ1
ЭК7	ВНЕШ	ЭК7,изсв7
ЭКО	ВНЕШ	ЭКО,прцес1,прцс1а
        ВХОД    адреса,уходы,зпшксм,Э50133,вдмлмб,св7мб,экояч1
	ВХОД	слдсмв,прмhтр,кнцмсв,вых133,трмдкс
*
	ФИНИШ

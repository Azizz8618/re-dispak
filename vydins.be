ВЫДИНС	СТАРТ	'2000'
	Б
	М
*
* РАБ. РЕГИСТРЫ:
* М2 - ДЛЯ ТЕКСТ. КОНСТ.
* М4 - ОШИБКИ ПРИ ЗАДАНИИ ПАР. ВЫДИНС,
* СМЕЩ.
* М5 - СЛОВА ЗАП. ПАРАМЕТРОВ 72000-72037
* М3,М10 - НОМЕР СИМВОЛА В СТР. ПАРАМЕТРА?
* М16 - БИТ ДА/НЕТ
*** ЗАГРУЗКА ЗОН ВЫДИНС - 752,753
*
	мода	
	Э70	ЗОН753
	уиа	'76101'(М1)
подгот	сч	'1777'(М1)
	зп	'75677'(М1)
	сч	
	зп	'1777'(М1)
	цикл	подгот(М1)
	сч	ПОДЧ
	зп	РАЗДСИ
	сч	пдгмас
	зп	РАЗДСИ+7
	уиа	'50'(М2)
	счи	М2
	сда	64-15
	зп	'71400'
	пв	ДАЙ(М16)
	сч	РАЗДСИ+5
	сда	64+1
	или	Е48
	зп	РАЗДСИ+5
	зп	РАЗДСИ+6
	пио	подгт1(М1)
	чед	
	уи	М11
	счи	М2
	сда	64-15
	зп	'71400'(М11)
	счи	М1
	или	'71400'(М11)
	зп	'71400'(М11)
	пб	ДАЙ
*
подгт1	мода	
	пв	спдгт1(М14) *
*
	уиа	'77601'(М11)
подгт2	сч	'71177'(М11)
	зп	'71377'(М11)
	цикл	подгт2(М11)
подгт3	мода	
	пв	спдгт3(М14)
подгт4	сч	ПОДЧ
	нтж	РАЗДСИ
	уиа	0(М3)
	пв	спдгт4(М13)
загзон	мода	
	Э70	ЗОН752
	мода	
	Э70	ЗОН753
	пб	прдолж
ЗОН752	конд	в'0010010000300752'
********** нижеследующее не используется **********
ВЛСТ37	конд	в'0000000002370000'
НЕИСП1	сч	ВЛСТ37
	Э62	'41'
	нтж	П77777
	по	G02041
	сч	РАЗДСИ+7
	нтж	пдгмас
	пе	G02207
	Э62	
G02041	уиа	-3(М10)
G02042	сч	'76005'(М10)
	мода	'66000'
	зп	3(М10)
	цикл	G02042(М10)
	ржа	'3'
	уиа	'77601'(М10)
G02045	сч	'76205'(М10)
	мод	РАЗДСИ+7
	зп	'177'(М10)
	цикл	G02045(М10)
	сч	РАЗДСИ+7
	слц	Е8
	вч	Е3
	зп	РАЗДСИ+7
	сч	БАЙКАЛ
	пв	двадес(М16)
	сч	Е20
	Э62	'44'
	мода	
	Э71	Е40
	сч	РАЗДСИ+5
	пе	G02060
	сч	
	пв	двадес(М16)
	сч	РАЗДСИ+8
	зп	РАЗДСИ+9
	пб	G02207
G02060	сч	
	уиа	-1023(М10)
G02061	зп	'1777'(М10)
	цикл	G02061(М10)
	уиа	-1023(М10)
G02063	зп	'73777'(М10)
	зп	'7777'(М10)
	цикл	G02063(М10)
	пв	спдгт1(М14)
	сч	ттесты
	нтж	'71000'
	пе	подгт3
	сч	РАЗДСИ+8
	зп	РАЗДСИ+9
	уиа	-9(М10)
	сч	
G02071	зп	РАЗДСИ+29(М10)
	цикл	G02071(М10)
	уиа	НЕИСП2(М16)
	сч	МИНУС
	зп	РАЗДСИ
	сч	ПОДЧ
	зп	СТОПСИ
	уиа	1(М3)
	мода	
	пв	ДАЙ(М15)
	нтж	РАЗДСИ
	нтж	СТОПСИ
	по	G02121
	нтж	СТОПСИ
	зп	РАЗДСИ+28
	вч	плюс
	по	ДАЙ
	сч	РАЗДСИ+20
	сда	64-4
	или	РАЗДСИ+28
	зп	РАЗДСИ+20
	пб	ДАЙ
*****
НЕИСП2	сч	РАЗДСИ+20
	сда	64-32
	рзб	D02175
	или	ттест1
	зп	ттест1
	сч	РАЗДСИ+20
	мода	
	пв	ПЕРЕВ8(М16)
	и	П7777
	зп	РАЗДСИ+20
	пио	G02121(М1)
	сч	ПОДЧ
	зп	РАЗДСИ
	уиа	G02121(М16)
	мода	
	пв	ДАЙ(М15)
	нтж	РАЗДСИ
	зп	РАЗДСИ+28
	сда	64+3
	пе	ДАЙ
	сч	РАЗДСИ+21
	сда	64-3
	или	РАЗДСИ+28
	зп	РАЗДСИ+21
	пб	ДАЙ
*****
G02121	сч	РАЗДСИ+21
	сда	64-36
	рзб	М8П7Х4
	или	ТТЕСТ+2
	зп	ТТЕСТ+2
	Э64	КТЕСТ
	сч	РАЗДСИ+21
	сда	64-24
	или	РАЗДСИ+20
	пв	двадес(М16)
G02126	сч	
	зп	РАЗДСИ+20
	зп	СТОПСИ
	уиа	-7(М10)
	сч	ПОДЧ
	зп	РАЗДСИ
	сч	ВСЕЕД
G02132	зп	'107'(М10) !КОММЕНТАРИЙ ВАРИАНТА
	цикл	G02132(М10)
	сч	РАЗДСИ+5
	по	G02207
	сч	ткомва+3
	или	СТОПСИ-1
	зп	ткомва+3
	сч	СТОПСИ-1
	слц	Е17
	зп	СТОПСИ-1
	мода	
	Э64	ккомва
	сч	РАЗДСИ+5
	нед	
	уи	М11
	зп	РАЗДСИ+9
	сч	РАЗДСИ+5
	нтж	Е48-1(М11)
	зп	РАЗДСИ+5
	сч	'71377'(М11)
	уи	М1
	сда	64+15
	уи	М2
	уиа	G02161(М16)
	уиа	-7(М10)
G02147	уиа	-5(М11)
	пв	ДАЙ(М15)
	нтж	РАЗДСИ
	нтж	пробел
	пе	G02153
	сч	п374
	нтж	пробел
G02153	нтж	пробел
	зп	РАЗДСИ+28
	сч	РАЗДСИ+20
	сда	64-8
	или	РАЗДСИ+28
	зп	РАЗДСИ+20
	цикл	ДАЙ(М11)
	зп	'107'(М10)
	сч	
	зп	РАЗДСИ+20
	цикл	G02147(М10)
	пб	G02164
G02161	сч	РАЗДСИ+20
	сда	64-8
	или	п374
	зп	РАЗДСИ+20
	цикл	G02161(М11)
	зп	'107'(М10)
G02164	уиа	-7(М10)
G02165	сч	'107'(М10)
	пв	двадес(М16)
	цикл	G02165(М10)
	Э64	D02177
	пб	G02126
КТЕСТ	конк	A(ТТЕСТ)
	конк	A(ТТЕСТ+2)
	конд	п'000100'
ТТЕСТ	конд	п'ТЕСТЫ:'
ттест1	конд	п'0000 -'
	конд	п' 0000.'
D02175	конд	п'    00'
М8П7Х4	конд	п'077770'
D02177	конд	м24в'100'в'107'
	конд	п'000100'
ккомва	конк	A(ткомва)
	конк	A(ткомва+3)
	конд	п'0000А0'
ткомва	текст	п'КОММЕНТАРИЙ ВАРИАН'
	конд	п'ТА 0 :'
G02207	сч	РАЗДСИ+9
	вч	РАЗДСИ+8
	вчоб	Е4
	сда	64-3
	уи	М14
G02212	пио	G02215(М14)
	сч	ВСЕЕД
	мода	
	пв	двадес(М16)
G02214	слиа	-1(М14)
	пб	G02212
G02215	сч	РАЗДСИ+7
	слц	П3
	уи	М10
	уиа	'66000'(М7)
	мода	
	Э62	'42'
	мода	
	Э71	исэ71
	мода	
	Э62	
********** вышележащее не используется **********
спдгт1	сч	ПОДЧ
	зп	СТОПСИ
	сч	НАДЧ
	зп	РАЗДСИ
	сч	РАЗДСИ+5
	нед	
	зп	РАЗДСИ+8
	уи	М11
	сч	РАЗДСИ+5
	нтж	Е48-1(М11)
	зп	РАЗДСИ+5
	сч	'71377'(М11)
	зп	'71100'
	уи	М1
	сда	64+15
	уи	М2
	уиа	1(М11)
	уиа	0(М3)
	мода	
	пв	ДАЙ(М16)
	счи	М2
	сда	64-15
	зп	'71100'(М11)
	счи	М1
	или	'71100'(М11)
	зп	'71100'(М11)
	слиа	1(М11)
	пино	ДАЙ(М1)
	нтж	'71077'(М11)
	зп	'71077'(М11)
	уиа	0(М11)
	уиа	1(М3)
	сч	прбел
	зп	РАЗДСИ
	уиа	спдг1а(М16)
спдг1а	уиа	-48(М12)
	сч	'71100'(М11)
	по	(М14) !НА *. cпдгт1?
	уи	М1
	сда	64+15
	уи	М2
	слиа	1(М11)
	пв	ДАЙ(М15)
	нтж	РАЗДСИ
	сда	64+8(М12)
	пио	спдг1а(М12)
	или	'70777'(М11)
	зп	'70777'(М11)
	слиа	8(М12)
	пб	ДАЙ
*
спдгт3	уиа	0(М11)
	уиа	0(М6)
	уиа	-40(М7)
спдг3а	сч	'71000'(М11)
	зп	РАЗДСИ+3
	по	спд3а1
	уиа	-3(М12)
	сч	ВСЕЕД
зпвсе7	зп	РАЗДСИ+4
	уиа	-63(М10)
спдг3б	сч	'71377'(М10)
	и	Е48
	пе	спдг3в
	сч	'71277'(М10)
	нтж	РАЗДСИ+3
	и	РАЗДСИ+4
	по	спдг3е
спдг3в	цикл	спдг3б(М10)
	сч	РАЗДСИ+4
	сда	64-8
	цикл	зпвсе7(М12)
спдг3г	сч	'71100'(М11)
	пв	спд3г1(М13)
спдг3д	слиа	1(М11)
	пб	спдг3а
*
спдг3е	сч	'71377'(М10)
	или	Е48
	зп	'71377'(М10)
	счи	М6
	зп	РАЗДСИ+1
	счи	М7
	зп	РАЗДСИ+2
	сч	прбел
	зп	РАЗДСИ
	сч	'71100'(М11)
	уи	М1
	сда	64+15
	уи	М2
	пв	ДАЙ(М16) **
	сч	
	зп	РАЗДСИ+20
	уиа	-24(М7)
	уиа	'67727'(М6)
	сч	П377
	пв	спдг3л(М13) *
	сч	РАЗДСИ+1
	уи	М6
	сч	РАЗДСИ+2
	уи	М7
	сч	РАЗДСИ+20
	нтж	НЕТ
	пе	спдг3г
	пб	спдг3д
*
спд3а1	уиа	-63(М10)
спдг3ж	сч	'71377'(М10)
	и	Е48
	пе	спдг3з
	сч	'71377'(М10)
	по	спдг3з
	пв	спд3г1(М13) *
спдг3з	цикл	спдг3ж(М10)
	уиа	-63(М10)
спдг3и	сч	'71377'(М10)
	или	Е48
	нтж	Е48
	зп	'71377'(М10)
	цикл	спдг3и(М10)
	уиа	'77601'(М10)
спдг3к	сч	
	зп	'71007'(М10)
	цикл	спдг3к(М10)
	пб	(М14) !ВОЗВРАТ *.подгт1
*
спд3г1	уи	М1
	сда	64+15
	уи	М2
	сч	П377
спдг3л	зп	СТОПСИ
	сч	НАДЧ
	зп	РАЗДСИ
	уиа	1(М3)
	уиа	спд3л1(М16)
	пв	ДАЙ(М15) *
спдгт4	нтж	РАЗДСИ
	сда	64(М7)
	или	'100'(М6)
	зп	'100'(М6)
	пино	спдг4а(М7)
	слиа	1(М6)
	уиа	-48(М7)
спдг4а	слиа	8(М7)
	пино	ДАЙ(М3)
	пб	(М13) ВОЗВРАТ НА *.спдг3е
*
спд3л1	уиа	0(М3)
	пб	(М15) !ВОЗВРАТ НА спдг3л
*
ДАЙ	сч	'74000'(М1)
	сда	64(М2)
	и	П377
	пино	дай1(М2)
	слиа	1(М1)
	уиа	'60'(М2)
дай1	слиа	-8(М2)
	нтж	РАЗДСИ
	по	дай2
	мода	(М15)
	пино	(М3)
	пб	ДАЙ
*
дай2	сч	'74000'(М1)
	сда	64(М2)
	и	П377
	нтж	СТОПСИ
	пе	насп3е
	уиа	0(М1)
насп3е	сч	
	пб	(М16)  !ВОЗВРАТ на **.спдг3е
*
*** НЕИСПОЛЬЗ. ПРОЦЕДУРА
*** ПЕРЕВОДА ЧИСЛЕ В ДВОИЧ-ДЕСЯТИЧ. ФОРМАТ
*
двадес	зп	РАЗДСИ+21
	зп	РАЗДСИ+20
	сч	РАЗДСИ+7
	слц	Е3
	зп	РАЗДСИ+7
	уиа	-15(М15)
двадc	сч	РАЗДСИ+21
	сда	64-3
	зп	РАЗДСИ+21
	счмр	
	чед	Е1
	и	Е1
	зп	РАЗДСИ+27
	сч	РАЗДСИ+28
	сда	64-1
	или	РАЗДСИ+27
	зп	РАЗДСИ+28
	цикл	двадc(М15)
	сда	64-32
	зп	РАЗДСИ+28
	сч	РАЗДСИ+20
	рзб	прзб22
	зп	РАЗДСИ+22
	сч	РАЗДСИ+20
	сда	64-12
	зп	РАЗДСИ+20
	рзб	прзб23
	зп	РАЗДСИ+23
	сч	РАЗДСИ+20
	сда	64-12
	зп	РАЗДСИ+20
	рзб	прзб24
	зп	РАЗДСИ+24
	сч	РАЗДСИ+20
	сда	64-15
	рзб	Е48-1
	зп	РАЗДСИ+25
	сч	РАЗДСИ+28
	рзб	хрзб22
	или	РАЗДСИ+22
	или	м39с
	зп	РАЗДСИ+22
	сч	РАЗДСИ+28
	сда	64-4
	зп	РАЗДСИ+28
	рзб	хрзб23
	или	РАЗДСИ+23
	зп	РАЗДСИ+23
	сч	РАЗДСИ+28
	сда	64-4
	зп	РАЗДСИ+28
	рзб	хрзб24
	или	РАЗДСИ+24
	зп	РАЗДСИ+24
	сч	РАЗДСИ+28
	сда	64-5
	рзб	хрзб25
	или	РАЗДСИ+25
	зп	РАЗДСИ+25
	уиа	-3(М15)
двадc1	сч	РАЗДСИ+25(М15)
	мод	РАЗДСИ+7
	зп	3(М15)
	цикл	двадc1(М15)
	пб	(М16)
ПЕРЕВ8	сда	64-32
	зп	ДВДЕС
	уиа	-3(М15)
	ржа	'3'
	сч	
цдвдес	умн	Д10
	счмр	64
	зп	ДЕС
	сч	ДВДЕС
	сда	64-4
	зп	ДВДЕС
	счмр	
	слц	ДЕС
	цикл	цдвдес(М15)
	пб	(М16)
***
Д10	конд	в'4000000000000012'
ДВДЕС	пам	1
ДЕС	пам	1
хрзб22	конд	х'000080808080'
хрзб23	конд	х'008080808000'
хрзб24	конд	х'008080808080'
хрзб25	конд	х'008080800000'
прзб22	конд	п'007777'
прзб23	конд	п'077770'
прзб24	конд	п'077777'
	конд	п'077700'
	ОПРЕ
пдгмас	конд	х'000000006c04'
исэ71	конд	в'3410000040000000'
НЕТ	конд	п'00НЕТ0'в'115'
ВСЕЕД	конд	в'7777777777777777'
П3	конд	п'3'
П377	конд	в'377'
	конд	в'7777'
П77777	конд	в'77777'
прбел	конд	п' '
ПОДЧ	конд	п'_'
БАЙКАЛ	конд	п'БАЙКАЛ'
	конд	п'7'
пробел	конд	п' '
плюс	конд	п'+'
ттесты	конд	п'ТЕСТЫ0'
п374	конд	в'374'
МИНУС	конд	п'-'
м39с	конд	п'0С0000'
***** КОНЕЦ НЕИСП. УЧАСТКА
*
*** ПРОДОЛЖЕНИЕ РАБОТЫ ПОСЛЕ
*** ЗАГР. 752 И 753 ЗОНЫ
*
прдолж	сч	РАЗДСИ+6
	нтж	Е48
	по	безвар  !ШТАТНАЯ РАБОТА
	сч	варген+3
	или	СТОПСИ-2
	зп	варген+3
	сч	СТОПСИ-2
	слц	Е41
	зп	СТОПСИ-2
* ВЫДАЧА СООБЩ.
* "ВАРИАНТ ГЕНЕРАЦИИ"
*
	Э64	ИСВАР !НЕ ИСПОЛЬЗ.
*
безвар	ржа	'3'
	уиа	'100'(М6)
	уиа	'6000'(М13)
	уиа	-5(М14)
СЛЕДСТ	уии	М1(М13)
	уиа	-4(М16)
	сч	
	зп	рячрзб
	уиа	0(М17)
следс1	уиа	-5(М15)
	сч	
следс2	зп	РЯЧ
	уиа	рзпроб(М5)
следс3	сч	(М6)
	сбр	ЕТХ(М14)
	зп	СИМ
	цикл	рзсимв(М14)
	уиа	-5(М14)
	слиа	1(М6)
рзсимв	пб	(М5)
*
рзпроб	нтж	М40ПРБ
	пе	непроб
	уиа	0(М17)
	сч	Е1
	зп	рячрзб
	пб	следс3
непроб	сч	СИМ
	нтж	М40НДЧ
	по	КОНСТР
	сч	СИМ
	нтж	М40ПДЧ
	по	КОНТХТ
	пино	следс3(М17)
	сч	рячрзб
	пе	строка
рзбетх	сч	СИМ
	рзб	ЕТХ(М15)
	нтж	РЯЧ
	цикл	рзетх1(М15)
	зп	(М1)
	слиа	1(М1)
	пб	следс1
рзетх1	цикл	следс2(М16)
	уиа	1(М17)
	уиа	1(М16)
	пб	следс2
*
строка	сч	РЯЧ
	или	ПРОБ4+3(М15)
	зп	(М1)
	слиа	1(М1)
	уиа	-5(М15)
	сч	
	зп	РЯЧ
	зп	рячрзб
	уиа	1(М16)
	пб	рзбетх
*
КОНСТР	сч	ПРБЕЛЫ+5(М15)
	нтж	РЯЧ
	зп	(М1)
	слиа	1(М1)
	уии	М13(М1)
	пб	СЛЕДСТ
КОНТХТ	счи	М6
	нтж	Е7
	по	ДАЙСЕР !НА ВЫД. КОНФ. СЕРИЙН. МАШИНЫ
	сч	контек
	зп	(М1)
	Э64	квхоин
	пб	печвво
***
ЗОН753	конд	в'0010020000300753'
квхоин	конк	A(вхоинф)
	конк	A(вхоинф+2)
	конд	в'0000000050000000'
вхоинф	текст	п'ВХОДНАЯ ИНФОРМАЦИЯ'
*
*** ПЕЧАТЬ ИНФ. О ВАР. ГЕН. И
*** КОНФ. СЕРИЙН. МАШ.
*
ДАЙСЕР	уиа	СЕРИЙН(М7)
дайсе1	сч	(М7)
	зп	(М6)
	нтж	ПОСЛЕД
	по	дайсе2
	слиа	1(М7)
	слиа	1(М6)
	пб	дайсе1
дайсе2	мода	
	Э64	искон
	сч	ПОСЛЕД
	зп	ПОСЛСЛ
	пб	безвар
***
ИСВАР	конк	А(варген)
	конк	A(варген+3)
	конд	в'0000000050000000'
варген	текст	п'ВАРИАНТ ГЕНЕРАЦИИ '
	конд	п'0:    '
искон	конк	A(конфма)
	конк	A(конфма+6)
	конд	в'0000000050000000'
конфма	текст	п'!!! ВЫДАЮ КОНФИГУРАЦИЮ СЕРИЙНОЙ МАШИНЫ!!! '
***
печвво	Э64	ИСВВОД
	пб	прдлж2
ИСВВОД	конд	м24в'6000'в'6000'
	конд	в'0000000050000000'
*
*** ФОРМИРОВАНИЕ СООБЩЕНИЙ ОБ ОШИБКАХ
*
ОШИБ	уиа	-3(М17)
	уиа	24(М3)
	мода	(М4)
	уиа	ОШИБКИ(М7)
ошиб1	сч	(М7)
	зп	(М3)
	слиа	1(М3)
	слиа	1(М7)
	цикл	ошиб1(М17)
	уии	М11(М13)
	уиа	-5(М12)
ошиб2	сч	(М11)
	сбр	ЕТХ(М12)
	нтж	М40КСТ
	по	ошиб3
	цикл	ошиб2(М12)
	сч	(М11)
	зп	(М3)
	слиа	1(М11)
	слиа	1(М3)
	уиа	-5(М12)
	пб	ошиб2
ошиб3	сч	(М11)
	и	ЕТХ(М12)
	нтж	(М11)
	зп	(М3)
	сч	контек
	рзб	ЕТХ(М12)
	или	(М3)
	зп	(М3)
	мода	
	Э64	исошиб
	слиа	1(М11)
	уии	М1(М11)
	пб	ДАЛЬШЕ
***
исошиб	конд	м24в'30'в'30'
	конд	в'0000000050000000'
*
*** АНАЛИЗ СТРОКИ РАЗДЕЛА ВЫДИНС
*
прдлж2	уиа	'6000'(М13)
	уиа	-71(М3)
ИЩУ	сч	(М13)
	нтж	контек(М3)
	по	НАШЕЛ
	цикл	ИЩУ(М3)
*РАЗДЕЛ НЕ СУЩ
	уиа	рнесущ-ОШИБКИ(М4)
	пб	ОШИБ
*
НАШЕЛ	уии	М5(М3)
	уии	М11(М13)
	сч	(М13)
	нтж	контек
	по	ПРОВЕР
	уиа	ячсимв(М10)
	уиа	-64(М16)
нашел1	сч	
	зп	япоиск+61(М16)
	цикл	нашел1(М16)
	уиа	-5(М14)
нашел2	уиа	-5(М15)
	сч	
нашсим	зп	РЯЧ
	сч	1(М11)
	сбр	ЕТХ(М14)
	зп	СИМ
	цикл	нашси1(М14)
	уиа	-5(М14)
	слиа	1(М11)
нашси1	счи	М15
	нтж	П77777
	слц	Е2
	сда	64-3
	уи	М16
	сч	СИМ
	нтж	М40ПРБ
	по	слдст
	сч	СИМ
	нтж	М40КСТ
	по	слдст
	сч	СИМ
	нтж	М40ЗПТ
	по	РАЗД
	сч	СИМ
	нтж	М40МИН
	по	РАЗД
	сч	СИМ
	нтж	М40ДВТ
	по	РАЗД
	сч	СИМ
	рзб	ЕТХ(М15)
	нтж	РЯЧ
	цикл	нашсим(М15)
нашси2	зп	РЯЧ
	зп	(М10)
	слиа	1(М10)
	пб	нашел2
РАЗД	сч	РЯЧ
	сда	64(М16)
	зп	(М10)
	слиа	1(М10)
	сч	СИМ
	пб	нашси2
*
слдст	слиа	5(М14)
	пио	слдс1(М14)
	слиа	1(М11)
слдс1	слиа	1(М11)
	сч	РЯЧ
	сда	64(М16)
	зп	(М10)
	уии	М1(М11)
	уиа	ячсимв(М10)
	слиа	МПД12-перек(М5)
	пб	перек(М5) НА МПД12
*
*** ПЕРЕКЛЮЧАТЕЛЬ МЕЖДУ ПРОЦЕДУРАМИ
*** ЗАДАНИЯ ПАРАМЕТРОВ ДЛЯ ВЫДИНС
*
перек	мода	
	пб	НОММЛ
	мода	
	пб	НОММЛ
	мода	
	пб	НОММЛ
	мода	
	пб	НОММЛ
	сч	ячсимв
	пб	НЕИЗВ1
	мода	
	пб	НОММЛ
	мода	
	пб	СЛОЙКА
	мода	
	пб	МБКОН
	мода	
	пб	НОММБ
	мода	
	пб	НОММБ
	мода	
	пб	ОЗУ
	сч	(М10)
	пб	РЕСУР
	мода	
	пб	НОММА
	мода	
	пб	ЧАСЫ
	слиа	1(М5)
	пб	ГОД
	уиа	52(М4)
	пб	ДИСКО
	мода	
	пб	УВВК
	мода	
	пб	СВСВ
	мода	
	пб	ТРГЕН
	мода	
	пб	УКВП
	мода	
	пб	НРДИС
	мода	
	пб	СИМП
	уии	М16(М10)
	пб	ТКАНА
	мода	
	пб	ШКСВМ
	уиа	ТРАКТЫ-КОНФИГ(М5)
	пб	СОЮЗ
	сч	(М10)
	пб	СКБЭ
	сч	(М10)
	пб	КОНМД
	уиа	19(М4)
	пб	СВЯЗЬ
	уиа	ТРАКТЫ-КОНФИГ(М5)
	пб	УСТПЛ
	мода	
	пб	ПРОГО
	мода	
	пб	СЭВМ
	сч	(М10)
	пб	АСТРВ
	сч	(М10)
	пб	АРХИВ
	мода	
	пб	ШКУСТ
	уиа	24(М5)
	пб	ШКОПТ
	уиа	ТРАКТЫ-КОНФИГ(М5)
	пб	ЛИСТА
	уиа	0(М16)
	пб	РПГКЛК
	уиа	-1(М16)
	пб	РПГКЛК
	уиа	ТРАКТЫ-КОНФИГ(М5)
	пб	ТТ
	уиа	28(М5)
	пб	ТЕРМ
	мода	
	пб	МДРЗО
	мода	
	пб	УПДК
	пб	БЮДЖЕ
	пам	0
	пб	ВЛЭВМ
	пам	0
	пб	ПМДИС
	пам	0
	пб	АБОНЕ
	пам	0
	пб	ЕСАЦП
	пам	0
	пб	ЕСПИ
	пам	0
	пб	ЕСПЛ
	пам	0
	пб	ЕСТЕР
	пам	0
	пб	ОТАС
	пам	0
	пб	ЗАКЛИ
	пам	0
	пб	ЗАКСЕ
	пам	0
	сч	(М10)
	пб	VIDI
	мода	
	пб	МД29М
	сч	(М10)
	пб	ЕСМЛ
	сч	(М10)
	пб	СРОЧ
	сч	(М10)
	пб	ОБЩМЛ
	сч	(М10)
	пб	ЗАХВ
	сч	(М10)
	пб	УЧЕТ
	сч	(М10)
	пб	РПГБ
	сч	(М10)
	пб	ГРА
	сч	(М10)
	пб	КАНАЛ
	сч	(М10)
	пб	А0ЕС
	сч	(М10)
	пб	А1ЕС
	сч	(М10)
	пб	ЧКАН
	сч	(М10)
	пб	Э256К
	пб	ТМГУ
	пам	0
	сч	(М10)
	пб	СМИТА
	сч	(М10)
	пб	МБ32
	сч	(М10)
	пб	МПД12
*
*** УСТ. ПАРАМ. ВЫДИНС
*
МПД12	нтж	ДА
	пе	ДАЛЬШЕ
	сч	Е15
	или	ЯРАСП1
	зп	ЯРАСП1
	пб	ДАЛЬШЕ
МБ32	нтж	ДА
	пе	ДАЛЬШЕ
	сч	Е40
	пб	ВТРАКТ
СМИТА	и	Е1
	сда	64-25
	или	Е27
	или	ТРАКТЫ
	зп	ТРАКТЫ
	пб	ДАЛЬШЕ
ТМГУ	уиа	мб20т3-зппар(М4)
	пб	зппар
Э256К	нтж	ДА
	пе	ДАЛЬШЕ
	сч	Е40
	пб	ВТРАКТ
ЧКАН	сбр	Т77
	уиа	ЯРАСПП(М5)
	сда	64+40
	пб	ПИШИМ5
СКБЭ	уиа	ЯМБКЧТ(М5)
	сбр	Т77
	сда	64+42
	пб	ПИШИМ5
А0ЕС	по	ДАЛЬШЕ
	сч	Е25
	пб	ВТРАКТ
А1ЕС	по	ДАЛЬШЕ
	сч	Е24
	пб	ВТРАКТ
КАНАЛ	сбр	Т77
	сда	64+18
	или	ЯПМБ
	зп	ЯПМБ
	сч	2(М10)
	сбр	Т77
	сда	64+11
	уиа	ЯПМБ(М5)
	пб	ПИШИМ5
АРХИВ	нтж	ДА
	пе	ДАЛЬШЕ
	сч	Е16
	пб	ВТРАКТ
*
НЕИЗВ1	мода	
	Э74	
VIDI	и	П3
	сда	64-6
	уиа	ЯПМБ(М5)
ПИШИМ5	или	(М5)
	зп	(М5)
	пб	ДАЛЬШЕ
*
КОНМД	мода	
	пв	cпроб4(М15)
	сда	64-12
	или	ЯПМБ
	зп	ЯПМБ
	пб	ДАЛЬШЕ
*
ЗАХВ	нтж	ДА
	по	захв2
захв1	сч	(М10)
	сда	64+2
	и	Е1
	уи	М15
	сч	Е45(М15)
	или	КОНФИГ+1
	зп	КОНФИГ+1
	сч	1(М10)
	нтж	М40ЗПТ
	пе	ДАЛЬШЕ
	слиа	2(М10)
	пб	захв1
захв2	сч	Е45
	или	Е44
	пб	зпкнф1
*
УЧЕТ	по	ДАЛЬШЕ
	сч	Е1
зярасп	уиа	ЯРАСПП(М5)
	пб	ПИШИМ5
РПГБ	по	ДАЛЬШЕ
	сч	Е2
	пб	зярасп
ГРА	по	ДАЛЬШЕ
	уиа	26(М5)
	сч	Е48(М5)
ВТРАКТ	уиа	ТРАКТЫ(М5)
	пб	ПИШИМ5
ОБЩМЛ	по	ДАЛЬШЕ
	сда	64-45
	уиа	ЯКГОД(М5)
	пб	ПИШИМ5
ЕСМЛ	уи	М3
	нтж	П3
	по	есмл1
	нтж	П7
	по	есмл1
	слиа	-2(М3)
	нтж	Е1
	по	есмл1
	нтж	П3
	пе	ПЛОХО
есмл1	счи	М3
	сда	64-45
зпкнф1	или	КОНФИГ+1
	зп	КОНФИГ+1
	пб	ДАЛЬШЕ
СРОЧ	уиа	зпсроч-зппар(М4)
	пб	зппар
*
*** ПРОЦЕДУРА УСТ. ОПЕР. КАНАЛОВ
*** ЧЕРЕЗ АС
*
ОТАС	сч	(М10) !НЕТ ПЕРЕД. УПР.
	сбр	т177
	рзб	п177
	зп	(М10)
	вч	Е7
	по	ПЛОХО
	сч	2(М10)
	по	отас1
	слиа	2(М10)
	пб	ОТАС
отас1	уиа	ячсимв(М10)
отас2	сч	1(М10)
	по	ДАЛЬШЕ
	нтж	М40МИН
	пе	ДАЛЬШЕ
	сч	2(М10)
	по	ДАЛЬШЕ
	вчоб	Е4
	пе	ДАЛЬШЕ
	мод	2(М10)
	уиа	0(М15)
	сч	(М10)
отас3	слиа	-1(М15)
	мода	отас4
	пио	(М15)
	сда	64-6
	пб	отас3
отас4	или	NТАС
	зп	NТАС
	слиа	4(М10)
	пб	отас2
***
НОММЛ	уиа	'114'(М4)
ДИСКО	уиа	зпконф(М15) ПРЕДЕЛЬН. НОМЕР. ДИСКА
*, ДОСТ. ДЛЯ ПОЛЬЗ.
cпроб4	сч	(М10)
	сбр	ПРОБ4
ИЗ10В8	зп	РЯЧ
	ржа	'3'
	уиа	-3(М16)
	сч	
ц2деc	умн	ДЕСЯТЬ
	счмр	64
	зп	яч2дес
	сч	РЯЧ
	сда	64-4
	зп	РЯЧ
	счмр	
	слц	яч2дес
	цикл	ц2деc(М16)
	пб	(М15)
*
зпконф	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	сч	2(М10)
	сбр	т7777
	сда	(М4)
ЗАПИШИ	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	сч	3(М10)
	по	ДАЛЬШЕ
	нтж	М40ЗПТ
	пе	ПЛОХО
	слиа	4(М10)
	слиа	1(М5)
	пб	ДИСКО
ДАЛЬШЕ	уии	М13(М1)
	уиа	-71(М3)
	пб	ИЩУ
СЛОЙКА	слиа	-1(М5)
	уиа	17(М4) !
ДАНЕТ	сч	(М10)
	уиа	1(М16)
	нтж	ДА
	по	ЗАПБИТ
	уиа	0(М16)
ЗАПБИТ	счи	М16
	сда	(М4)
	пб	ЗАПИШИ
тнетда	конд	п'   НЕТ'
	конд	п'    ДА'
МБКОН	слиа	-1(М5)
	уиа	зпмб20(М15)
мб20т1	уиа	1(М16)
	сч	1(М10)
	по	мб20т2
	и	т7
	сда	64-6
	уиа	0(М16)
мб20т2	зп	РЯЧ
	сч	1(М13)
	нтж	М40КСТ
	по	мб20т3
зпсроч	уиа	0(М16)
мб20т3	сч	(М10) !C ТМГУ
	и	т7
	сда	64-6
	пино	мб20т4(М16)
	сда	64-3
	или	РЯЧ
мб20т4	зп	РЯЧ
	пам	0
	сч	(М10)
	сда	64+16
	сбр	Т77
	сда	64+42
	или	РЯЧ
	зп	РЯЧ
	уиа	1(М16)
	пб	(М15)
зпмб20	сч	РЯЧ
	пб	ЗАПИШИ
НОММБ	сч	(М10)
	сда	64-16
	зп	(М10)
	пб	МБКОН
ОЗУ	сч	(М10)
	сда	64+8
	сбр	ПРОБ4
	пв	ИЗ10В8(М15)
	уиа	0(М17)
	зп	СИМ
	сч	Е16
	или	ЯРАСП1
	зп	ЯРАСП1
	сч	СИМ
озу1	вч	Е6
	пе	ошбозу
	слиа	1(М17)
	пб	озу1
ошбозу	и	П77777
	нтж	е15п6
	пе	ПЛОХО
	уии	М16(М5)
	слиа	-1(М17)
	счи	М17
	вч	Е6
	по	ПЛОХО
	сч	Е48
	зп	КОНФИГ-1(М5)
зпозу	пио	ДАЛЬШЕ(М17)
	сч	Е48-1(М17)
	или	ЯРАСП1
	зп	ЯРАСП1
	слиа	-1(М17)
	пб	зпозу
	конд	в'0000037777600000'
	конд	в'7777777777600000'
е15п6	конд	в'0000000000077740'
*ОШИБКА ДИАПАЗОНА
ошдиап	уиа	нвдиап-ОШИБКИ(М4)
	пб	ОШИБ
*ОШИБКА НОМЕРА ЛИСТА
ономли	уиа	нвзнлт-ОШИБКИ(М4)
	пб	ОШИБ
ПМДИС	сч	(М10)
	сбр	ПРОБ4
	мода	
	пв	ИЗ10В8(М15)
	сда	64-12
	или	ЯСЛОЙК
	зп	ЯСЛОЙК
	сч	2(М10)
	сбр	т7777
	сда	64+12
зслойк	уиа	ЯСЛОЙК(М5)
	пб	ПИШИМ5
БЮДЖЕ	уиа	-31(М4)
зппар	уиа	ТРАКТЫ-КОНФИГ(М5)
	сч	(М10)
	уиа	1(М16)
	нтж	ДА
	по	зппар1
	уиа	0(М16)
зппар1	счи	М16
	сда	64(М4)
	пб	ЗАПИШИ
ВЛЭВМ	сч	М40КСТ
	зп	РЯЧ
	сч	(М10)
	зп	СИМ+2
	мода	
	пв	ЗАМЕНИ(М16)
	зп	ЯВЛА
	сч	
	зп	РЯЧ
	сч	(М10)
	сбр	ЕТХ
	зп	СИМ
	нтж	М40РМБ
	пе	влэвм1
	сч	М40ПРБ
	зп	СИМ
влэвм1	сч	СИМ
	нтж	РЯЧ
	зп	РЯЧ
	сч	1(М10)
	сда	64-8
	зп	СИМ+2
	мода	
	пв	ЗАМЕНИ(М16)
	зп	ЯВЛА1
	пб	ДАЛЬШЕ
ЗАМЕНИ	уиа	-4(М14)
замен1	сч	СИМ+2
	сбр	М8ЕТХ(М14)
	зп	СИМ
	нтж	М40РМБ
	пе	замен2
	сч	М40ПРБ
	зп	СИМ
замен2	сч	СИМ
	рзб	ЕТХ(М14)
	нтж	РЯЧ
	зп	РЯЧ
	цикл	замен1(М14)
	пб	(М16)
*
УПДК	уиа	ТРАКТЫ-КОНФИГ(М5)
	уиа	-32(М4)
	сч	(М10)
	уиа	1(М16)
	нтж	ДА
	по	упдк1
	уиа	0(М16)
упдк1	пио	упдк2(М16)
	сч	Е48
	или	ЯСЛОЙК
	зп	ЯСЛОЙК
упдк2	счи	М16
	сда	64(М4)
	пб	ЗАПИШИ
*
АБОНЕ	уиа	1(М17)
	сч	(М10)
	сбр	М8ЕТХ
	нтж	М40А
	по	првабн
	уиа	0(М17)
	сч	(М10)
	сбр	М16ЕТХ
	нтж	М40А
	по	првабн
	пб	ОШАБНТ
првабн	сч	(М10)
	сбр	Т77(М17)
	рзб	П77(М17)
	зп	(М10)
	вчоб	пробел
	пе	ОШАБНТ
	сч	2(М10)
	по	прваб1
	слиа	2(М10)
	пб	АБОНЕ
прваб1	уиа	ячсимв(М10)
абвозв	сч	1(М10)
	по	прваб2
	нтж	М40ЗПТ
	по	зпабн1
	сч	1(М10)
	нтж	М40МИН
	пе	ДАЛЬШЕ
	сч	(М10)
	вч	2(М10)
	по	ДАЛЬШЕ
	уи	М15
	сч	(М10)
	нтж	е21п1
	уи	М14
	сч	
	зп	РЯЧ
	сч	Е33
	сда	64+2(М14)
зпабон	сда	64-1
	или	РЯЧ
	зп	РЯЧ
	цикл	зпабон(М15)
	или	ЯПРОГО
	зп	ЯПРОГО
	слиа	4(М10)
	пб	абвозв
зпабн1	сч	(М10)
	нтж	е21п1
	уи	М14
	сч	Е33
	сда	64+1(М14)
	или	ЯПРОГО
	зп	ЯПРОГО
	слиа	2(М10)
	пб	абвозв
*
прваб2	сч	-1(М10)
	по	ДАЛЬШЕ
	нтж	М40ЗПТ
	по	зпабн1
	пб	ДАЛЬШЕ
*
ОШАБНТ	мода	
	Э64	выошаб
	мода	
	Э74	
выошаб	конк	А(тошиаб)
	конк	А(тошиаб)
	конд	в'0000000044000000'
тошиаб	конд	п'ОШИБК '
	конд	п'АБОНЕ0'в'377'
***
ЕСАЦП	сч	(М10)
	сда	64+1
	пе	ошибес
	сч	Е48
	мод	(М10)
	сда	64+5
	или	ЯСЛОЙК
	зп	ЯСЛОЙК
	сч	1(М10)
	сбр	М40ЕТХ
	нтж	М40ЗПТ
	пе	ДАЛЬШЕ
	слиа	2(М10)
	пб	ЕСАЦП
ошибес	мода	
	Э64	выошес
	мода	
	Э74	
выошес	конк	А(тошес)
	конк	А(тошес)
	конд	в'0000000044000000'
тошес	конд	п'ОШИБК '
	конд	п'ЕСАЦП0'в'377'
ЕСПИ	сч	(М10)
	пе	ДАЛЬШЕ
	сч	Е41
	или	ЯСЛОЙК
	зп	ЯСЛОЙК
	пб	ДАЛЬШЕ
ЕСТЕР	сч	(М10)
	уиа	0(М15)
	сбр	Т77
	сда	64+42
	зп	(М10)
	по	ошестр
	вчоб	п35
	пе	ошестр
	сч	2(М10)
	по	естер1
	слиа	2(М10)
	пб	ЕСТЕР
естер1	уиа	ячсимв(М10)
естпрв	сч	1(М10)
	по	естпр1
	нтж	М40ЗПТ
	по	зпест1
	сч	1(М10)
	нтж	М40МИН
	пе	ошестр
	сч	(М10)
	вч	2(М10)
	по	ошестр
	уи	М15
	пам	0
	сч	(М10)
	уи	М14
зпестр	сч	Е33(М14)
	или	ЯПРОГО
	зп	ЯПРОГО
	слиа	1(М14)
	цикл	зпестр(М15)
	слиа	4(М10)
	пб	естпрв
*
зпест1	сч	(М10)
	уи	М14
	сч	Е33(М14)
	или	ЯПРОГО
	зп	ЯПРОГО
	слиа	2(М10)
	пб	естпрв
естпр1	сч	-1(М10)
	по	ДАЛЬШЕ !ПРАВИЛЬНО
	нтж	М40ЗПТ
	по	зпест1
	пб	ошестр !ОШИБКА
ошестр	мода	
	Э64	аоестр
	пб	ДАЛЬШЕ
аоестр	конк	А(оестер)
	конк	А(оестер)
	конд	в'0000000044000000'
оестер	конд	п'ОШ В Е'
	конд	п'СТЕРМ0'в'377'
***
ЕСПЛ	сч	(М10)
	пе	ДАЛЬШЕ
	сч	Е40
	пб	зслойк
***
РЕСУР	сч	(М10)
	сбр	т7777
	сда	64+30
	или	ЯДЛТ
	зп	ЯДЛТ
	слиа	2(М5)
	сч	2(М10)
	сбр	т777
	сда	64+39
	пб	ЗАПИШИ
***
НОММА	слиа	2(М5)
	сч	(М10)
	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	пб	ДАЛЬШЕ
***
ЧАСЫ	уиа	ТРАКТЫ-КОНФИГ(М5)
	уиа	зпчасы(М15)
ипмитм	уиа	1(М16)
	уиа	-2(М14)
	сч	(М10)
	нтж	ИПМ
	и	е21п1
	по	часипм
	уиа	0(М16)
	уиа	0(М14)
часипм	уиа	17(М4)
	пб	(М15)
зпчасы	счи	М16
	сда	64-37
	пб	ЗАПИШИ
***
ГОД	сч	(М10)
	и	П17
	сда	64-20
	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	сч	(М10)
	и	М8ЕТХ
	сда	64-8
	пб	зпконф
***
УВВК	сч	
	пб	ЗАПИШИ
***
СВСВ	слиа	1(М5)
	пв	ипмитм(М15)
свсв1	счи	М16
	нтж	Е1
	уи	М16
	сч	Е2(М16)
	пб	ЗАПИШИ
***
ТРГЕН	слиа	1(М5)
	уиа	зпмб20(М15)
	пб	мб20т1
***
МД29М	сч	Е1
	пб	змд29м
УКВП	сч	
змд29м	зп	ЭТО29М
	уиа	ямд29м-КОНФИГ(М5)
	уии	М16(М10)
првквп	сч	1(М16)
	по	квпне1
	нтж	М40ДВТ
	по	номлин
	сч	1(М16)
	нтж	М40ЗПТ
	по	возвр1
	сч	1(М16)
	нтж	М40МИН
	по	нетси1
нетсим	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
номлин	сч	(М16)
	сбр	М8ЕТХ
	нтж	М40Л
	по	толко4
	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
*N ЛИНЕЙКИ ТОЛЬКО 4
толко4	сч	(М16)
	и	ЕТХ
	зп	СИМ+2
	вч	Е4      !НУЖНО ИСПРАВИТЬ
	по	ПЛОХО
cнова	слиа	2(М16)
	пб	првквп
*
нетси1	уии	М6(М16)
	уиа	-1(М12)
прдиап	сч	(М6)
	и	П7
	зп	СИМ+1(М12)
	нтж	(М6)
	пе	ПЛОХО
	сч	СИМ+1
	слиа	2(М6)
	цикл	прдиап(М12)
	вч	СИМ
	пе	ошдиап
	нтж	П77777
	слц	Е1
	уи	М14
зпсим	сч	СИМ
	вчоб	
	уи	М15
	сч	СИМ+2
	сда	64+2
	уи	М7
	сч	СИМ+2
	и	П3
	сда	64-3
	уи	М12
	сч	ЭТО29М
	по	зпквп1
	пино	нетсим(М12)
	пио	зпквп(М7)
	слиа	7(М7)
зпквп	сч	КОНФИГ(М5)
	мод	СИМ
	или	Е16(М7)
	зп	КОНФИГ(М5)
	слиа	1(М7)
	цикл	зпквп(М14)
	пб	cнова
*
зпквп1	мода	КОНФИГ(М7)
	сч	(М5)
	мода	(М15)
	или	Е41(М12)
	мода	(М7)
	зп	КОНФИГ(М5)
	слиа	-1(М15)
	цикл	зпквп1(М14)
	пб	cнова
*
возвр1	сч	-1(М16)
	нтж	М40МИН
	по	cнова
возвр2	сч	(М16)
	и	П7
	зп	СИМ
	нтж	(М16)
	пе	ПЛОХО
	уиа	0(М14)
	пб	зпсим
*
квпне1	сч	-1(М16)
	нтж	М40ЗПТ
	по	возвр2
	сч	-1(М16)
	нтж	М40ДВТ
	по	возвр2
	пб	ДАЛЬШЕ
М40Л	конд	п'Л00000' !ЛИНЕЙКА УКВП
***
НРДИС	уиа	0(М7)
	пв	зпнрд(М17) *
	сч	РЯЧ
	сда	64-36
зпядлт	уиа	ЯДЛТ(М5)
	пб	ПИШИМ5
зпнрд	уиа	'130'(М4)
	пв	cпроб4(М15) !ПЕРЕВ. ЗНАЧ ИЗ 10 В 8
	сда	64(М7)
	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	уиа	0(М16)
нрдвзв	сч	2(М10)
	сбр	т7777
	сда	64+36
	зп	янрдис(М16)
	пио	змдрз1(М16)
	вч	Е6
	по	ПЛОХО
	сч	янрдис(М16)
	и	п37
нрдвз1	сда	64-12(М7)
	пио	зпнрд1(М16)
	сда	64-6
зпнрд1	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	пино	првнрд(М16)
	уиа	1(М16)
	слиа	2(М10)
	пб	нрдвзв
првнрд	сч	янрдис+1
	по	ПЛОХО
	пб	(М17) ВОЗВРАТ НА зпнрд(М17)
янрдис	пам	2
	конд	п'А'
***
МДРЗО	уиа	20(М5)
	уиа	-24(М7)
	мода	
	пв	зпнрд(М17)
змдрзо	сч	РЯЧ
	сда	64-24
	пб	зпядлт
змдрз1	сда	64+5
	сда	64-10
	зп	РЯЧ
	сч	янрдис
	и	п37
	зп	янрдис
	пб	нрдвз1
***
СЭВМ	уиа	ТРАКТЫ-КОНФИГ(М5)
	уиа	-27(М4)
	сч	(М10)
	уиа	1(М16)
	нтж	ДА
	по	знетда
	уиа	0(М16)
знетда	сч	тнетда(М16)
	зп	(М2)
	счи	М16
	сда	64(М4)
	пб	ЗАПИШИ
ТКАНА	сч	1(М16)
	по	ткнет
	нтж	М40ДВТ
	по	ткдвт
	сч	1(М16)
	нтж	М40ЗПТ
	по	ткзпт
	сч	1(М16)
	нтж	М40МИН
	по	ткмин
	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
ткдвт	уиа	1(М17)
	сч	(М16)
	сбр	М8ЕТХ
	нтж	М40Ф
	по	ткпрв
	уиа	0(М17)
	сч	(М16)
	сбр	М16ЕТХ
	нтж	М40Ф
	по	ткпрв
	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
ткпрв	сч	(М16)
	и	Т77(М17)
	сбр	Т77
	зп	СИМ+2
	сда	64+42
	нтж	
	по	ПЛОХО
	вч	Д48
	по	ПЛОХО
тквозв	слиа	2(М16)
	пб	ТКАНА
ткмин	уии	М6(М16)
	уиа	-1(М12)
ткзпси	сч	(М6)
	и	П7
	зп	СИМ+1(М12)
	нтж	(М6)
	пе	ПЛОХО
	сч	СИМ+1
	слиа	2(М6)
	цикл	ткзпси(М12)
ткпрв1	вч	СИМ
	пе	ошдиап
	нтж	П77777
	слц	Е1
	уи	М14
зткана	сч	СИМ
	сда	64+3
	уи	М12
	сч	СИМ
	и	П7
	уи	М15
	сли	М12(М5)
	сч	КОНФИГ(М12)
	и	е6п1(М15)
	нтж	КОНФИГ(М12)
	зп	КОНФИГ(М12)
	сч	СИМ+2
	рзб	е6п1(М15)
	или	КОНФИГ(М12)
	зп	КОНФИГ(М12)
	сч	СИМ
	слц	Е1
	зп	СИМ
	цикл	зткана(М14)
	пб	тквозв
ткзпт	сч	-1(М16)
	нтж	М40МИН
	по	тквозв
ткпрв2	сч	(М16)
	и	П7
	зп	СИМ
	нтж	(М16)
	пе	ПЛОХО
	сч	СИМ
	пб	ткпрв1
ткнет	сч	-1(М16)
	нтж	М40ЗПТ
	по	ткпрв2
	сч	-1(М16)
	нтж	М40МИН
	по	ДАЛЬШЕ
	сч	-1(М16)
	нтж	М40ДВТ
	по	ткпрв2
	пб	ДАЛЬШЕ
е6п1	конд	в'0000000000000077'
	конд	в'0000000000007700'
	конд	в'0000000000770000'
	конд	в'0000000077000000'
	конд	в'0000007700000000'
	конд	в'0000770000000000'
	конд	в'0077000000000000'
	конд	в'7700000000000000'
	пам	1
	конд	х'00000000001f'
	конд	х'0000000003e0'
Е15П11	конд	х'000000007c00'
	конд	х'0000000f8000'
	конд	х'000001f00000'
	конд	х'00003e000000'
	конд	х'0007c0000000'
	конд	х'00f800000000'
М40Ф	конд	п'Ф00000'
	конд	в'32'
***
ШКУСТ	уиа	необсл-ОШИБКИ(М4)
	пб	ОШИБ
ТЕРМ	уиа	-3(М3)
	сч	ТРАКТЫ
	или	Е29
	зп	ТРАКТЫ
	уиа	ТИПЫ(М16) !ПЕРВЫЕ ДВА СИМВ.
првтип	сч	(М10)
	мода	(М16)
	нтж	3(М3)
	по	прврзд
	цикл	првтип(М3)
о1птер	уиа	нв1пар-ОШИБКИ(М4)
	пб	ОШИБ
прврзд	слиа	3(М3) !ГДЕ РАЗДЕЛИТЕЛЬ?
	пио	ттнеvt(М3)
	сч	1(М10)
	нтж	М40ДВТ
	пе	ПЛОХО
*
	уиа	ЯШКУСТ-КОНФИГ(М5) !ЯШКУСТ
*
	уиа	прнус2(М16)
првнус	сч	2(М10)
	и	мастер
	пе	нтрне8
	сч	2(М10)
	сбр	т177
	рзб	п177
	зп	2(М10)
	сч	3(М10)
	по	прнус1
	слиа	2(М10)
	пб	првнус
* ОШИБКА. НЕВЕРНЫЙ СИМВОЛ
ПЛОХО	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
* ОШИБКА. НОМ. УСТР-ВА НЕ ВОСЬМЕРИЧН.
нтрне8	уиа	нусне8-ОШИБКИ(М4)
	пб	ОШИБ
*
мастер	конд	в'7777777777574370'
прнус1	уиа	ячсимв(М10)
	пб	(М16) !ДАЛЬШЕ
прнус2	сч	2(М10)
	вч	ТЕCONS(М3)
	пе	ошнус
	сч	2(М10)
	нтж	п1(М3)
	по	ошнус
	сч	2(М10)
	нтж	п33(М3)
	по	ошнус
	сч	2(М10)
	вч	п31(М3)
	по	ошнус
	сч	3(М10)
	по	трне3с
	слиа	2(М10)
	пб	прнус2
трне3с	уиа	ячсимв+2(М10)
	пб	изтерм
ттнеvt	сч	1(М10)
	нтж	М40ЗПТ
	пе	ПЛОХО
	сч	2(М10)
	нтж	ТЕРТТ
	по	еслитт
	слиа	2(М5) !М5='31'
	сч	2(М10)
	нтж	ТЕРVТ
	пе	о1птер
еслитт	сч	3(М10)
	нтж	М40ДВТ
	пе	ПЛОХО
	слиа	2(М10)
	мода	
	пв	првнус(М16)
	слиа	4(М10)
усдиап	сч	1(М10)
	по	терпр2
	нтж	М40ЗПТ
	по	терзпт
	сч	1(М10)
	нтж	М40МИН
	пе	ПЛОХО
	сч	2(М10)
	вч	(М10)
	пе	ошдиап
	нтж	П77777
	слц	Е1
	уи	М14
усдиа1	сч	(М10)
	уи	М15
	уии	М16(М15)
	по	ошнус
вяшас	уиа	1(М11) !ЯШАС+1
	счи	М15
	вч	п61
	пе	зптерм
	счи	М15
	вч	п160
	пе	терпрв
	слиа	-111(М15)
зпяшас	сч	ЯШАС(М11)
	или	Е48-1(М15)
	зп	ЯШАС(М11) !ЗП ПО М11
	пб	терпр1
терпрв	счи	М15
	вч	Е7
	пе	обнули
	уиа	0(М11)
	слиа	-63(М15)
	пб	зпяшас
обнули	уиа	0(М11) !ОБНУЛЕНИЕ
	слиа	-48(М15)
зптерм	сч	Е48-1(М15)
	сли	М11(М5) М11+М5
	или	КОНФИГ(М11) !ЗДЕСЬ ЯШКУСТ
	зп	КОНФИГ(М11)
терпр1	слиа	1(М16)
	уии	М15(М16)
	цикл	вяшас(М14)
прдиа1	слиа	2(М10)
	пб	усдиап
терзпт	сч	-1(М10)
	нтж	М40МИН
	по	прдиа1
трзпт1	уиа	0(М14)
	пб	усдиа1
терпр2	сч	-1(М10)
	нтж	М40ЗПТ
	по	трзпт1
	сч	-1(М10)
	нтж	М40МИН
	по	ДАЛЬШЕ
	сч	(М10)
	пе	трзпт1
	пб	ДАЛЬШЕ
ошнус	уиа	невнус-ОШИБКИ(М4)
	пб	ОШИБ
ТИПЫ	конд	п'АС'
ТЕРТТ	конд	п'ТТ'
ТЕРVТ	конд	п'VТ'
ТЕCONS	конд	п'СОNS'
	конд	п'1'
п1	конд	п'1'
	конд	в'31'
п31	конд	в'31'
	конд	в'33'
	конд	в'33'
п33	конд	в'33'
	конд	в'32'
	конд	в'32'
	конд	в'33'
рятерм	пам	1
***
ШКОПТ	уиа	1(М17)
	сч	(М10)
	сбр	М8ЕТХ
	нтж	М40Ф
	по	шкпрв
	уиа	0(М17)
	сч	(М10)
	сбр	М16ЕТХ
	нтж	М40Ф
	по	шкпрв
	уиа	невсим-ОШИБКИ(М4)
	пб	ОШИБ
*
шкпрв	сч	(М10)
	сбр	Т77(М17)
	рзб	П77(М17)
	зп	(М10)
	нтж	
	по	ПЛОХО
	вч	Д48
	по	ПЛОХО
	сч	2(М10)
	по	шкпрв1
	слиа	2(М10)
	пб	ШКОПТ
шкпрв1	уиа	ячсимв(М10)
изтерм	сч	
	зп	рятерм
шкпрв2	сч	1(М10)
	по	шкнетс
	нтж	М40ЗПТ
	по	шкзпт
	сч	1(М10)
	нтж	М40МИН
	пе	ПЛОХО
	сч	2(М10)
	вч	(М10)
	пе	ошдиап
	нтж	П77777
	слц	Е1
	уи	М14
шквозв	сч	(М10)
	уи	М15
зшкопт	сч	Е48-1(М15)
	или	КОНФИГ(М5)
	зп	КОНФИГ(М5)
	сч	Е48-1(М15)
	или	рятерм
	зп	рятерм
	слиа	1(М15)
	цикл	зшкопт(М14)
шкмин	слиа	2(М10)
	пб	шкпрв2
*
шкзпт	сч	-1(М10)
	нтж	М40МИН
	по	шкмин
шксчт	уиа	0(М14)
	пб	шквозв
*
шкнетс	сч	-1(М10)
	нтж	М40ЗПТ
	по	шксчт
	сч	-1(М10)
	нтж	М40МИН
	по	зяшкvt
	сч	(М10)
	по	зяшкvt
	пб	шксчт
*
зяшкvt	уиа	ячсимв(М10)
	сч	(М10)
	нтж	ТЕРVТ
	пе	ДАЛЬШЕ
	сч	рятерм
	сда	64+24
	или	ЯШКVТ
	зп	ЯШКVТ
	пб	ДАЛЬШЕ
***
ШКСВМ	уиа	0(М4)
снеис3	сч	(М10)
	по	ДАЛЬШЕ
свмвзв	уиа	1(М17)
	сч	(М10)
	сбр	М8ЕТХ
	нтж	М40Л
	по	свмпрв
	уиа	0(М17)
	сч	(М10)
	сбр	М16ЕТХ
	нтж	М40Л
	пе	ПЛОХО
свмпрв	сч	(М10)
	сбр	Т77(М17)
	рзб	П77(М17)
	зп	СИМ
	вч	Е6
	по	ономли
	сч	(М10)
	сбр	М32ЕТХ(М17)
	нтж	М40А
	пе	ПЛОХО
	сч	(М10)
	сбр	М24ЕТХ(М17)
	сда	64+40
	зп	СИМ+1
	вч	Е3
	по	ономли
	сч	СИМ
	сда	64-2
	или	СИМ+1
	зп	(М10)
	сч	2(М10)
	по	свне2с
	слиа	2(М10)
	пб	свмвзв
свне2с	уиа	ячсимв(М10)
свпрв1	сч	1(М10)
	по	свне1с
	нтж	М40ЗПТ
	по	свмзпт
	сч	1(М10)
	нтж	М40МИН
	пе	ПЛОХО
	сч	2(М10)
	вч	(М10)
	пе	ошдиап
	нтж	П77777
	слц	Е1
	уи	М14
сввзв1	сч	(М10)
	уи	М15
	пино	свмпр2(М4)
зшксвм	сч	Е48(М15)
	или	КОНФИГ+3(М5)
	зп	КОНФИГ+3(М5)
	слиа	1(М15)
	цикл	зшксвм(М14)
свммин	слиа	2(М10)
	пб	свпрв1
свмзпт	сч	-1(М10)
	нтж	М40МИН
	по	свммин
свмсчт	уиа	0(М14)
	пб	сввзв1
свне1с	сч	-1(М10)
	нтж	М40ЗПТ
	по	свмсчт
	пб	ДАЛЬШЕ
***
СОЮЗ	уиа	'42'(М4)
	сч	(М10)
	уиа	1(М16)
	нтж	ДА
	по	cоюз1
	уиа	0(М16)
cоюз1	счи	М16
	сда	(М4)
	пб	ЗАПИШИ
*****
НЕИСП3	уиа	1(М4)
	пб	снеис3
*****
свмпр2	уиа	зшсвм1(М7)
свмвз1	сч	(М10)
	и	П3
	уи	М15
	вч	П3
	уи	М16
	сч	(М10)
	сда	64+2
	вч	Е3
	пе	ошкал
	зп	СИМ+1
	вч	п14
	по	ошкал
	сч	СИМ+1
	умн	е48е3
	счмр	64
	слц	Е3
	вчоб	Д48
	зп	РЯЧ
	пб	(М7)
зшсвм1	мод	РЯЧ
	сч	Е48(М15)
	или	КОНФИГ+3(М5)
	зп	КОНФИГ+3(М5)
	слиа	1(М15)
	цикл	свмпвт(М14)
	слиа	2(М10)
	пб	свпрв1
свмпвт	цикл	зшсвм1(М16)
	уии	М12(М14)
	слиа	3(М12)
	счи	М12
	и	Е15
	по	свмпв1
	уиа	-3(М16)
	уиа	0(М15)
	сч	РЯЧ
	вч	Е3
	зп	РЯЧ
	пб	зшсвм1
свмпв1	слиа	2(М10)
	пв	свмвз1(М7)
	слиа	1(М16)
	уиа	0(М15)
	пб	зшсвм1
е48е3	конд	в'4000000000000004'
Д48	конд	ф'48'
* ОШИБКА НЕВ. ЗАДАНА ШКАЛА
ошкал	уиа	невшкл-ОШИБКИ(М4)
	пб	ОШИБ
*
п14	конд	в'14'
***
СВЯЗЬ	уиа	ТРАКТЫ-КОНФИГ(М5)
	пб	ДАНЕТ
***
УСТПЛ	уиа	17(М4)
	сч	(М10)
	уиа	0(М16)
	нтж	ПЛ
	пе	ПЛОХО
	сч	2(М10)
	нтж	Е10
	по	ЗАПБИТ
	уиа	1(М16)
	сч	2(М10)
	нтж	Е12
	по	ЗАПБИТ
	сч	2(М10)
	нтж	T150
	пе	ПЛОХО
	пб	ЗАПБИТ
***
ПРОГО	уиа	18(М4)
	уиа	ТРАКТЫ-КОНФИГ(М5)
	уиа	0(М16)
	сч	(М10)
	нтж	БУКВАБ
	по	ЗАПБИТ
	уиа	1(М16)
	сч	(М10)
	нтж	БУКВАМ
	пе	ПЛОХО
	пб	ЗАПБИТ
***
АСТРВ	сбр	Т77
	сда	64+42
	зп	РЯЧ
	по	астрв1
	вч	Е5
	по	ПЛОХО
астрв1	уиа	ТРАКТЫ-КОНФИГ(М5)
	сч	РЯЧ
	сда	64-33
	пб	ЗАПИШИ
	текст	п'НЕ РАБОТАЕТ ПО СОБЫТИЮ Ф' не использ.
ПЛ	конд	п'ПЛ'
T150	конд	п'150'
БУКВАБ	конд	п'Б'
БУКВАМ	конд	п'М'
	текст	п'БЫСТРЫЙ'
	текст	п'МЕДЛЕННЫЙ'
ЛИСТА	уиа	4(М5)
	сч	(М10)
	вч	Е4
	по	ПЛОХО
	сч	(М10)
	пб	ЗАПИШИ
РПГКЛК	уиа	ТРАКТЫ(М5)
	уиа	1(М17)
	сч	(М10)
	нтж	БЭСМ
	по	рпгкл1
	уиа	0(М17)
рпгкл1	сч	Е34(М16)
	зп	РЯЧ
	пио	рпгкл2(М17)
	сда	64-2
рпгкл2	или	РЯЧ
	или	(М5)
	зп	(М5)
	пб	ДАЛЬШЕ
*
БЭСМ	конд	п'БЭСМ'
***
СИМП	уиа	25(М5)
	пб	ШКОПТ
*
ЗАКЛИ	уиа	-1(М17)
	сч	
	зп	рярасп
	сч	(М10)
закли1	и	е16п12
	пе	озакли
	сч	(М10)
	сбр	п70
	рзб	букваш+1(М17)
	или	рярасп
	зп	рярасп
	сч	(М10)
	сда	64-8
	зп	(М10)
	цикл	закли1(М17)
	пам	0
	сч	рярасп
	по	озакли
	и	Е6
	пе	озакли
	мод	рярасп
	сч	Е48
	или	ЯРАСПП !СЭВМ ШКСВМ
	зп	ЯРАСПП
	пб	ДАЛЬШЕ
ЗАКСЕ	сч	(М10)
	по	озаксе
	зп	рярасп
	и	о2п1
	пе	озаксе
	мод	рярасп
	сч	Е48-1
	и	ЯРАСП1 !ОЗУ
	нтж	ЯРАСП1
	зп	ЯРАСП1
	пб	ДАЛЬШЕ
***
озаксе	уиа	нзксег-ОШИБКИ(М4)
	пб	ОШИБ
озакли	уиа	нзклис-ОШИБКИ(М4)
	пб	ОШИБ
*****
о2п1	конд	в'7777777777777774'
букваш	конд	п'Ш'
	конд	п'7'
е16п12	конд	в'174000'
п70	конд	п'70'
рярасп	пам	1
*
***
*
ТТ	сч	(М10)
	сда	64+1
	пе	ПЛОХО
	сч	(М10)
	сда	64-29
	пб	ЗАПИШИ
ПРОВЕР	уиа	0(М12)
	уиа	-28(М13)
првер1	сч	ЯШКTT(М13)
	по	неттт
ПРСЛЕД	цикл	првер1(М13)
	уиа	ПРСЛЕД+3(М11)
	сч	ТРАКТЫ
	и	П777
	по	ОШРЕС
	пам	0
	уиа	ПРСЛЕД+6(М11)
	сч	ЯДЛТ
	сда	64+6
	и	П7777
	по	ОШРЕС
	пам	0
	уиа	ПРСЛЕД+8(М11)
	сч	NБЭСМ
	и	П7
	по	ОНОММА
	уиа	ПРСЛЕД+10(М11)
	сч	ЯКГОД
	и	М16ЕТХ
	по	ОШГОД
	пио	усттер(М12)
	Э64	внедос
	пб	усттер
*
внедос	конк	А(тнедос)
	конк	А(тнедос+4)
	конд	в'0000000050000000'
тнедос	текст	п'ОПИШИТЕ НЕДОСТАЮЩУЮ ИНФОРМАЦИЮ'
*
неттт	уии	М16(М13)
	слиа	снеттт-НЕТРАЗ(М16)
	уиа	ПРСЛЕД(М11)
	пб	НЕТРАЗ(М16)
НЕТРАЗ	уиа	нетмзг-ИМРАЗД(М12) !НЕТ ПЕРЕД УПР.
	пб	НЕТИНФ
	сч	
	пб	ПРСЛЕД
	уиа	нтмвв1-ИМРАЗД(М12) !НЕТ ПЕРЕД УПР.
	пб	НЕТИНФ
	уиа	нтмвв2-ИМРАЗД(М12) !НЕТ ПЕРЕД УПР.
	пб	НЕТИНФ
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	уиа	нмбинф-ИМРАЗД(М12)
	пб	НЕТИНФ
	уиа	нмбпро-ИМРАЗД(М12)
	пб	НЕТИНФ
	уиа	нетозу-ИМРАЗД(М12)
	пб	НЕТИНФ
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
*
ОШРЕС	уиа	нетрес-ИМРАЗД(М12)
	пб	НЕТИНФ
ОНОММА	уиа	нетном-ИМРАЗД(М12)
	пб	НЕТИНФ
ОШГОД	уиа	нетгод-ИМРАЗД(М12)
	пб	НЕТИНФ
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	уиа	нттркт-ИМРАЗД(М12)
	пб	НЕТИНФ
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
	сч	
	пб	ПРСЛЕД
снеттт	сч	
	пб	ПРСЛЕД
ИМРАЗД	пам	1
нетмзг	текст	п'МЛ ЗАГРУЗКИ '
нтмвв1	текст	п'МЛ ВВОДА    '
нтмвв2	текст	п'МЛ ВЫВОДА 1 '
	текст	п'МЛ ВЫВОДА 2 '
нмбинф	текст	п'МБ ИНФОРМАЦ.'
нмбпро	текст	п'МБ ПРОГРАММ.'
нетозу	текст	п'ПАМЯТИ ОЗУ  '
нетрес	текст	п'РЕСУРСАХ МАТ'
нетном	текст	п'НОМЕРЕ МАШИН'
нетгод	текст	п'ГОДЕ        '
нттркт	текст	п'ТРАКТЕ ГЕНС '
	текст	п'ТРАКТАХ ПОДК'
	текст	п'МЛ УПРЯТЫВ. '
ТШКУС	текст	п'ШКУСТР НЕ ВКЛЮЧАЕТ ШКОПТ'
*
НЕТИНФ	сч	ИМРАЗД(М12)
	зп	ТНЕТИН+2
	сч	ИМРАЗД+1(М12)
	зп	ТНЕТИН+3
	мода	
	Э64	анетин
	пб	(М11)
анетин	конк	A(ТНЕТИН)
	конк	А(ТНЕТИН+3)
	конд	в'0000000050000000'
ТНЕТИН	текст	п'НЕТ ИНФ-И О '
	конд	х'515151515151'
	конд	х'515151515151'
	конд	в'0220000000644544'
	конд	в'4700000002200000'
	конк	A(ТШКУС)
	конк	А(ТШКУС+3)
	конд	в'0000000050000000'
*
*** ЗАПОЛНЕНИЕ ШКАЛ ТЕРМИНАЛЬНЫХ УСТРОЙСТВ
*** ВИДЕОТОН, ТТ, КОНСУЛ
*
усттер	уиа	КОНФИГ(М12)
	сч	ямлста-КОНФИГ(М12)
	по	счшкус
	нтж	ямлвво-КОНФИГ(М12)
	и	П7777
	пе	счшкус
	сч 	ямлста-КОНФИГ(М12)
	сда	64+24
	зп	РЯЧ
	сч 	ямлвво-КОНФИГ(М12)
	сда	64+24
	зп	СИМ
	пб	счшкус
*
*** НЕ ИСПОЛЬЗ. ФРАГМЕНТ
*
пневра	мода	
	Э64	аневра
	пб	счшкус
аневра	конк	A(ТНЕВРА)
	конк	A(ТНЕВРА)
	конд	в'0000000050000000'
ТНЕВРА	текст	п'НЕВ.РАЗНОСТЬ ДЕЛЬТ МЛ СТАТ.И МЛ ВВОД'
	конд	п'А00000'м32в'172'
НЕИСП4	сч	РЯЧ
	вч	СИМ
	вч	Е8
	пе	пневра
****** КОН. НЕИСП.
*
*** ПРОДОЛЖ. РАБОТы ПО УСТАНОВКЕ ТЕРМ. УСТ.
*
счшкус	сч	ЯШКУСТ 
	нтж	ЯШКОПТ 
	и	ЯШКОПТ
	по	впреоб
	сч	ЯШКTT  
	сда	64+33
	зп	РЯЧ
	сч	ЯШКVТ  
	сда	64+33
	или	РЯЧ
	или	ЯШКTT2 
	или	ЯШКVТ2 
	пе	впреоб
	Э64	аневшк
	мода	
	Э74	
впреоб	пб	преобр
***
аневшк	конк	A(ТНЕВШК)
	конк	A(ТНЕВШК)
	конд	в'0000000040000000'
ТНЕВШК	текст	п'*******     НЕВЕРНО ЗАДАН РАЗДЕЛ  Ш К О П Т'
	конд	м40в'172'
П777	конд	в'777'
*
*** ПРЕОБРАЗОВАНИЕ СОДЕРЖИМОГО ЯЧЕЕК ВЫДИНСА В
*** ФОРМАТ ГЕНСА
*
преобр	уиа	КОНФИГ(М12)
	уиа	0(М17)
	сч	яшсвм-КОНФИГ(М12)
преоб1	зп	РЯЧ
	по	преоб3
	и	е48п45
	по	преоб2
	сч	маскнф
	или	Е48(М17)
	зп	маскнф
преоб2	слиа	1(М17)
	сч	РЯЧ
	сда	64-4
	пб	преоб1
преоб3	сч	ТРАКТЫ-КОНФИГ(М12)
	и	Е31
	по	преоб4
	сч	маскнф
	или	Е32
	или	Е31
	зп	маскнф
преоб4	сч	ТРАКТЫ-КОНФИГ(М12)
	и	Е28
	по	преоб5
	сч	маскнф
	или	Е20
	зп	маскнф
преоб5	сч ямлста-КОНФИГ(М12)
	и	е48п46
	по	пинфге
	сч	маскнф
	или	Е23
	зп	маскнф
*
* ПРОЦЕДУРА ВЫДАЧИ ИНФОРМАЦИИ К ГЕНСУ
* НА УСТР-ВА ВЫВОДА
*
пинфге	сч	9(М12)
	или	маскнф
	зп	9(М12)
	сч	ЯШКДТ
	нтж	ВСЕЕД
	зп	ЯШКДТ
	уиа	КОНФИГ(М12)
	Э64	аинфге
	уиа	-3(М13)
пслово	уии	М11(М12)
	счи	М11
	сда	64-33
	рзб	шесть7
	или	П172
	зп	СЛОВО
	мода	
	Э64	ИСАДР
	пб	неподх
***
аинфге	конк	A(ТИНФГЕ)
	конк	А(ТИНФГЕ+2)
	конд	в'0057000050000000'
ТИНФГЕ	текст	п'ИНФОРМАЦИЯ К ГЕНСУ'
ИСАДР	конк	A(СЛОВО)
	конк	A(СЛОВО)
	конд	в'0057000040000000'
п35	конд	в'35'
***
неподх	мода	
	Э64	ИС8СЛ
	слиа	8(М12)
	цикл	пслово(М13)
	уиа	КОНФИГ(М12)
	сч	ПОСЛСЛ
	нтж	ПОСЛЕД
	пе	ВЫДАТЬ
	мода	
	Э64	АЕСЛИ
	пб	ВЫДАТЬ
АЕСЛИ	конк	A(ТЕСЛИ)
	конк	A(ТЕСЛИ)
	конд	в'0000000050000000'
ТЕСЛИ	текст	п'ЕСЛИ ДАННАЯ КОНФИГУРАЦИЯ ВАМ НЕ ПОДХОДИТ,О'
	текст	п'ПИШИТЕ ЕЕ В СООТВЕТСТВИИ С ИНСТРУКЦИЕЙ И В'
	текст	п'ОЗМОЖНОСТЯМИ ВАШЕЙ МАШИН'
	конд	п'Ы00000'м32в'172'
ПОСЛСЛ	пам	1
ИС8СЛ	кк	0,(М12)
	кк	0,7(М12)
	конд	х'230010912000'
ВЫДАТЬ	сч	'77'
	по	НАПК
	мода	
	Э70	ИСКОНФ
	сч	'7000'
	нтж	Е15П11
	по	выдат1
	сч	
	уиа	-1023(М1)
циквыд	зп	'6000'+1023(М1)
	цикл	циквыд(М1)
	сч	Е15П11
	зп	'7000'
выдат1	уиа	0(М1)
	сч	'77'
	зп	СИМ
	сч	NБЭСМ
	и	П7
	зп	РЯЧ
	уиа	0(М5)
	уиа	-15(М4)
длязап	сч	'6016'(М5)
	и	П7
	нтж	РЯЧ
	пе	НЕТОТ
	слиа	1(М1)
	счи	М1
	нтж	СИМ
	по	НАДИСК
НЕТОТ	слиа	'40'(М5)
	цикл	длязап(М4)
	уиа	-15(М4)
	уиа	0(М5)
	слиа	1(М1)
	счи	М1
	зп	СИМ
пнетме	сч	'6016'(М5)
	и	П7
	по	НАДИСК
	слиа	'40'(М5)
	цикл	пнетме(М4)
	Э64	КНЕТМЕ
	мода	
	Э74	
КЗАПВА	конк	А(ТЗАПВА)
	конк	A(ТАНТ0)
	конд	в'0000000050000000'
ТЗАПВА	текст	п'ЗАПИСАН ВАРИ'
ТАНТ0	конд	п'АНТ  0'
КНЕТМЕ	конк	А(ТНЕТМЕ)
	конк	A(ТНЕТМЕ)
	конд	в'0000000041000000'
ТНЕТМЕ	конд	п'НЕТ МЕ'
	конд	п'СТА  0'в'172'
НАДИСК	уиа	-31(М16)
запвар	сч	ЯШКVТ2(М16)
	мода	(М5)
	зп	'6037'(М16)
	цикл	запвар(М16)
	сч	ИСКОНФ
	нтж	Е40
	зп	РЯЧ
	Э70	РЯЧ
	сч	СИМ
	или	ТАНТ0
	зп	ТАНТ0
	Э64	КЗАПВА
	мода	
	Э74	
НАПК	уиа	-32(М14)
	уиа	24(М10)
	сч	дляпк2
	зп	(М10)
	сч	дляпк3
	зп	1(М10)
	сч	дляпк1
	зп	2(М10)
	нтж	Е8
	зп	3(М10)
	слиа	4(М10)
цкнраб	сч	(М12)
	зп	РЯЧ
	сда	64+6
	рзб	шесть7
	или	м39с
	зп	(М10)
	сч	РЯЧ
	сда	64-12
	зп	РЯЧ
	и	е48п37
	сда	64+3
	рзб	шесть7
	зп	1(М10)
	сч	РЯЧ
	сда	64-12
	зп	РЯЧ
	сда	64+3
	рзб	шесть7
	зп	2(М10)
	сч	РЯЧ
	сда	64-15
	сда	64+3
	рзб	шесть7
	зп	3(М10)
	уиа	-3(М15)
	уии	М16(М10)
цкраб1	уиа	-4(М17)
	уиа	0(М6)
цкраб2	сч	(М10)
	и	ЕТХ(М17)
	чед	
	и	Е1
	пе	конраб
	сч	(М10)
	или	Е40(М6)
	зп	(М10)
конраб	слиа	8(М6)
	цикл	цкраб2(М17)
	слиа	1(М10)
	цикл	цкраб1(М15)
	сч	1(М16)
	нтж	Е8
	зп	1(М16)
	сч	3(М16)
	нтж	Е8
	нтж	Е16
	зп	3(М16)
	слиа	1(М12)
	цикл	цкнраб(М14)
	уиа	24(М7)
	слиа	-1(М10)
	Э71	иcэ71а
	мода	
	Э74	
иcэ71а	конд	в'3410000040000000'
шесть7	конд	п'777777'
дляпк1	конд	в'0010020040100200'
е48п37	конд	в'7777000000000000'
*
*** СООБЩЕНИЯ ОБ ОШИБКАХ
*
ОШИБКИ	текст	п'НЕТ КОНЦА ИНСТРУКЦИИ  : '
невшкл	текст	п'НЕВ.ЗАДАНИЕ ШКАЛЫ '
	конд	п'     0'в'172'
	текст	п'НЕТ ПРОБЕЛА ПОСЛЕ НАЗВ.:'
	текст	п'НЕДОПУСТИМЫЙ СИМВОЛ    :'
	текст	п'НЕТ КОНЦА РАЗДЕЛА '
	конд	п'     0'в'172'
рнесущ	текст	п'РАЗДЕЛ НЕ СУЩЕСТВУЕТ   :'
неопис	текст	п'НЕТ ОПИСАНИЯ РАЗДЕЛА   :'
нвдиап	текст	п'НЕВ.ЗАДАНИЕ ДИАПАЗОНА  :'
нвзнлт	текст	п'НЕВ.ЗАДАНИЕ НОМЕРАЛИСТА:'
невсим	текст	п'НЕВ.СИМВОЛИКА В ОПИСАН.:'
необсл	текст	п'РАЗДЕЛ НЕ ОБСЛУЖИВАЕТСЯ:'
нв1пар	текст	п'НЕВЕРНЫЙ 1-Й ПАРАМЕТР  :'
нусне8	текст	п'НОМ.УСТР.НЕ 8-Е ЧИСЛО  :'
невнус	текст	п'НЕДОПУСТ.НОМЕР УСТР-ВА :'
нзксег	текст	п'НЕВЕРН. ПАРАМЕТР З'
	конд	п'АКСЕГ0'в'172'
нзклис	текст	п'НЕВЕРН. ПАРАМЕТР З'
	конд	п'АКЛИС0'в'172'
	конд	м16в'200'
*
*** ПЕРЕЧЕНЬ ПАРАМЕТРОВ (РАЗДЕЛОВ) ВЫДИНСА
*
РАЗДЕЛ	конд	п'МЛЗАГ ' 
	конд	п'МЛСТА ' 
	конд	п'МЛВВО ' 
	конд	п'МЛВЫВ '
	конд	п'!!!!!!'
	конд	п'МЛСВЯ ' 
	конд	п'СЛОЙК '
	конд	п'МБКОН '
	конд	п'МБИНФ ' 
	конд	п'МБПРО '
	конд	п'ОЗУ   '  
	конд	п'РЕСУР '
	конд	п'НОММА ' 
	конд	п'ЧАСЫ  '
	конд	п'ГОД   ' 
	конд	п'ДИСКО ' 
	конд	п'УВВК  '
	конд	п'СВ СВ '
	конд	п'ТРГЕН '
	конд	п'УКВП  ' 
	конд	п'НРДИС '
	конд	п'СИМП  '
	конд	п'ТКАНА ' 
	конд	п'ШКСВМ '
	конд	п'СОЮЗ  ' 
	конд	п'СКБЭ  '
	конд	п'КОНМД '
	конд	п'СВЯЗЬ '
	конд	п'УСТПЛ '
 	конд	п'ПРОГО '
	конд	п'СЭВМ  ' 	
	конд	п'АСТРВ '
	конд	п'АРХИВ ' 
	конд	п'ШКУСТ '
	конд	п'ШКОПТ ' 
	конд	п'ЛИСТА '
	конд	п'РПГ   ' 
	конд	п'САLСО ' 
	конд	п'ТТ    '
	конд	п'ТЕРМ  '	
	конд	п'МДРЗО ' 
	конд	п'УПДК  '
	конд	п'БЮДЖЕ '  
	конд	п'ВЛЭВМ ' 
	конд	п'ПМДИС '
	конд	п'АБОНЕ '
	конд	п'ЕСАЦП ' 
	конд	п'ЕСПИ  ' 
	конд	п'ЕСПЛ  ' 
	конд	п'ЕСТЕР ' 
	конд	п'ОТАС  ' 
	конд	п'ЗАКЛИ '
	конд	п'ЗАКСЕ '
	конд	п'VIDI  '
	конд	п'МД29М ' 
	конд	п'ЕСМЛ  ' 
	конд	п'СРОЧ  '
	конд	п'ОБЩМЛ '
	конд	п'ЗАХВ  '
	конд	п'УЧЕТ  '
	конд	п'РПГБ  '
	конд	п'ГРА   '
	конд	п'КАНАЛ '
	конд	п'А0ЕС  ' 
	конд	п'А1ЕС  ' 
	конд	п'ЧКАН  '
	конд	п'Э256К '
	конд	п'ТМГУ  '
	конд	п'СМИТА '
	конд	п'МБ32  '
	конд	п'МПД12 '
***
контек	конд	м40в'172'
	пам	1
	конд	п'ИКОНЕЦ'
М40ЕТХ	конд	х'ff0000000000'
М32ЕТХ	конд	х'00ff00000000'
М24ЕТХ	конд	х'0000ff000000'
М16ЕТХ	конд	х'000000ff0000'
М8ЕТХ	конд	х'00000000ff00'
ЕТХ	конд	х'0000000000ff'
ПРОБ6	конд	п'      '
	конд	п'0     '
ПРОБ4	конд	п'00    '
	конд	п'000   '
	конд	п'0000  '
	конд	п'00000 '
	конд	в'3760000000000000'
ДЕСЯТЬ	конд	в'4000000000000012'
	конд	п'00000-'
	конд	п'00000,'
П17	конд	п'00000 '
т7	конд	п'000007'
т7777	конд	п'007777'
т777	конд	п'000777'
Т77	конд	п'000077'
	конд	п'000007'
т177	конд	п'000177'
	конд	п'ОСТАЛО'
	конд	п'СЬ 000'
М40А	конд	п'А00000'
	конд	п'У00000'
ПРБЕЛЫ	конд	п'     0'в'175'
	конд	п'0    0'в'175'
	конд	п'00   0'в'175'
	конд	п'000  0'в'175'
	конд	п'0000 0'в'175'
	конд	в'175'
М40КСТ	конд	м40в'175'
М40ДВТ	конд	п':00000'
М40ЗПТ	конд	п',00000'
М40МИН	конд	п'-00000'
	конд	м40в'115'
М40РМБ	конд	п'◇00000'
М40ПРБ	конд	п' 00000'
НАДЧ	конд	в'115'
П7777	конд	в'7777'
П77	конд	в'77'
П7	конд	в'7'
п37	конд	в'37'
	конд	в'31'
п61	конд	в'61'
	конд	в'12'
е48п45	конд	в'7400000000000000'
маскнф	конд	в'7700000015600000'
е21п1	конд	в'0000000007777777'
	конд	п'  0000'
ДА	конд	п'0000ДА'
	конд	п'     0'
ИПМ	конд	п'000ИПМ'
	конд	п' ЗАВОД'
	текст	п'   ИПМ АН СССР    '
РЯЧ	пам	1
СИМ	пам	3
яч2дес	пам	4
	конд	п'0000Н7'
	конд	п'0000Н8'
дляпк2	конд	х'00a280808080'
дляпк3	конд	х'000702808080'
ткус	текст	п'Н.КУС= ЧКУС=' !НЕИСП.
СЛОВО	пам	1
П172	конд	в'172'
рячрзб	пам	1
М40НДЧ	конд	м40в'115'
М40ПДЧ	конд	п'_00000'
СЕРИЙН	конд	п'МЛЗАГ '
	конд	п'50МЛСТ'м32в'115'
	конд	п'АТ 10М'м8в'115'
	конд	п'ЛВВО 2'
	конд	п'0МЛВЫВ'м40в'115'
	конд	п' 3-0,4'
	конд	п'-00МЛУ'м24в'115'
	конд	п'ПР 200'в'115'
	конд	п'МБИНФ '
	конд	п'МБ110М'м8в'115'
	конд	п'БПРО М'
	конд	п'Б210ОЗ'м16в'115'
	конд	п'У 32К0'в'115'
	конд	п'НОММА '
	конд	п'10ГОД 'м32в'115'
	конд	п'19730Т'м8в'115'
	конд	п'РГЕН М'
	конд	п'Б23Т00'
ЭТО29М	пам	1
	конд	п'0ТКАНА'м40в'115'
	конд	п'Л Ф2:0'
п177	конд	в'177'
п160	конд	в'160'
	конд	п'-70ТРП'м24в'115'
	конд	п'ОД 100'в'115'
	конд	п'ПРИК3 '
	конд	п'НЕТ0УС'м16в'115'
	конд	п'ТПЛ ПЛ'
	конд	п'-800СВ'м16в'115'
	конд	п'ЯЗЬ НЕ'
	конд	п'Т0РЕСУ'м32в'115'
	конд	п'Р 40,5'
ПОСЛЕД	конд	п'400_  'м24в'115'
ячсимв	пам	3
япоиск	пам	65
е48п46	конд	в'7000000000000000'
ИСКОНФ	конд	в'0010030000300750'
СТОПСИ	экв	'70002'
РАЗДСИ	экв	'70003'
*
*** СЛОВА КОНФИГУРАЦИИ ВЫДИНСА
*
КОНФИГ	экв	'72000'
ямлста  экв     '72001'
ямлвво  экв     '72002'
ЯПРОГО	экв	'72004'
ЯСЛОЙК	экв	'72005'
ЯМБКЧТ	экв	'72006'
ЯДЛТ	экв	'72007'
ЯПМБ	экв	'72010'
ЯРАСПП	экв	'72011'
ЯРАСП1	экв	'72012'
ЯШАС	экв	'72013'
ТРАКТЫ	экв	'72015'
NБЭСМ	экв	'72016'
ЯКГОД	экв	'72017'
ЯВЛА	экв	'72020'
ямд29м  экв     '72022'
ЯВЛА1	экв	'72025'
ЯШКУСТ	экв	'72027'
ЯШКОПТ	экв	'72030'
ЯШКДТ	экв	'72031'
яшсвм   экв     '72032'
NТАС	экв	'72033'
ЯШКTT	экв	'72034'
ЯШКTT2	экв	'72035'
ЯШКVТ	экв	'72036'
ЯШКVТ2	экв	'72037'
        вход    РАЗДЕЛ
	ФИНИШ

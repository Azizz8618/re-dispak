АВМЛ СТАРТ ’10000’
 Б
 УПОТР АВМЛ(М7)
 СЧИ М5
 НТЖ П37
 УИ М15
 СЧ Е31(М15)
 ЗП РОБ
 УИИ М15(М10)
 УИИ М16(М6)
 СЧИ М5
 НТЖ Е7
 ПО СИНЕН
        счи     М5
        и       D56753
        зп      D56754
 СЧ ШОЧМГ-24(М1)
 НЕД 0
 УИ М10
 СЧ ОЧЛИС-1(М10)
 СЛЦ Е20
 ЗП ОЧЛИС-1(М10)
        сч      3
        и       Е5
        по      G56015
        сч      ОЧЛИС-1(М10)
        и       Е26
        пе      G56017
        пб      СОШ
G56015        пам     0
        сч      ОЧЛИС-1(М10)
 И Е30
 ПО СОШ
G56017        пам     0
 НТЖ ОЧЛИС-1(М10)
 ЗП ОЧЛИС-1(М10)
 УИИ М14(М10)
        сч      3
        и       Е5
        по      ПР5
        сч      '573'(М1)
        или     Е40
        зп      '573'(М1)
        сч      КР1
        сда     64-8
        нтж     ВСЕЕД
        и       ТСММГ+1
        зп      ТСММГ+1
        счи     М1
        сда     64-42
        рзб     КР1
        сда     64-8
        или     ТСММГ+1
        зп      ТСММГ+1
        мода    ТСММГ
        уиа     (М16)
        мода    СМЕНИМ
        уиа     (М10)
        уии     М32(М10)
        пб      НАТ
        пб      ПР5
СОШ МОДА ВХОД1
 ПБ (М5)
ВХОД1 СЧ СС
 ПБ НКЛЮЧ         0.НЕ ПОДХОДИТ КЛЮЧ
 СЧ СС
 ПБ НКЛЮЧ   1 ОШ ЗОНА
 СЧ ТЗНМЛ-24(М1)
 ПБ ТРЗОН         2.ДРУГАЯ ЗОНА
 СЧ СС
 ПБ НКЛЮЧ   3 ДР БОБ
 СЧ КС
 ПБ КОНСМ         4.НЕТ СОВП.КОНТР.СУММ
 СЧ 0
 ПБ ВЫКЛМГ        5.ВЫКЛЮЧЕН МГ
 СЧ 0
 ПБ МЕС     6 - ВЫК.ЗП
 СЧ НОБМ
 ПБ ВЫКЛЗП   7 НЕ ОК ОБ ПРИ ЗП
 СЧ КС
 ПБ КОНСМ    8 ОШ КОН ЧТ НА ЗП
 СЧ ОЧЛИС-1(М10)
 ПБ ПРЕДЗО        9.ОБНАРУЖЕНЫ ОШИБКИ ПРИ ЧТ.СС ПРЕДЗОН
 СЧ НОБМ
 ПБ ОШЗП          10.НЕ ПРОХОДИТ ЗАПИСЬ
 ЗП 0
 ПБ ПР5           11.СНЯТЬ ЗАДАЧУ ПО 5 МУ ПРИКАЗУ
 ЗП 0
 ПБ ОПРБОБ        12.БЫЛ ОПРОС БОБИНЫ ДЛЯ НОМБОБА
 ЗП 0
 ПБ АВЧТ          13. ОШМ ИЛИ ОШВУ
 СЧ СС
 ПБ ОШЕС   14.ОШИБКА ЧЕТНОСТИ БАЙТА ЕС
 СЧ НКС
 ПБ ОШЕС   15 ОШ ЦКС ЕС
 СЧ 0
 ПБ ОШЕС   16.СБОЙ ЗАПИСИ ЕС
 СЧ ОБМН
 ПБ ОШЕС   17  ОШИБКА ПРОДОЛЬНОГ ЧЕКА ЕС
НКЛЮЧ УИА АВСС-АВМЛ(М10)
РАС СЛИ М10(М7)
 ПБ СТРАСП
КОНСМ УИА АВЧТ-АВМЛ(М10)
 ПБ РАС
СИНЕН СЧ КУС2-1(М2)
 СДА 64+42
 УИ М5
 МОД ТУСП-1(М5)
 УИА (М10)
 УИА (М4)
 СЧ КУС2-1(М2)
 И Е4
 ПЕ СИН
 УИИ М4(М10)
СИН ПАМ 0
 СЧ 32(М10)
 НТЖ 22(М10)
 И Е17
 ПЕ НЕТЛИ ЛИСТ В ОБМЕНЕ
 УИИ М6(М16)
 УИИ М17(М5)
 УИИ М10(М15)
 ПВ ЗАНПРП(М15)
 МОДА НЕТЛИ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИИ М16(М6)
 УИИ М15(М10)
 УИА 8(М10)
БОРЯ СЛИА -1(М10)
 ПВ ПРЛСЧА(М5)
 УИА ’2000’
 ЗП 8(М10)
 УИА 3
 МОДА БОРЯ
 ПИНО (М10)
НЕТЛИ УИА -6(М10)
 УИА (М17)
РР ЗП 1
 МОДА РР
 ЦИКЛ (М10)
 СЧ ГУС
 И КРОМЕ
 ЗП ГУС
 СЧ ОЧЛИС-1(М14)
 ПВ ПРИП1С(М10)
 СЧ КУС2-1(М2)
 УИИ М6(М16)
 И Е21
 ПО ПР
 СЧ РЯО5
 И Е24П1
 СЛЦ Е1
 ЗП ТЗОНМЛ-24(М1) КОРРЕКЦИЯ N ЗОНЫ ПРИ ПОДВ.С ЧТ.СС
ПР2 УИА ПРП2(М5)
 ПБ ВЫХОД
ПР УИА ПРП(М5)
 ПБ ВЫХОД
ВЫКЛЗП УИА АВМГ-АВМЛ(М10)
 ПБ РАС
ТРЗОН СЛЦ Е25
 ЗП ТЗНМЛ-24(М1)
 СЧ ОЧЛИС-1(М14)
 И О18
 ЗП ОЧЛИС-1(М14)
        счи     ,       свс:    СЧ СС
 МОДА СТРАСП
 ПВ (М10)
 СЧ РЯО5
 И Е11П1
 СЛЦ Е1            КОРРЕКЦИЯ ЗОНАМЛ ДЛЯ РЕВЕРСА НА ЗОНУ
 ЗП ЗОНАМЛ
 ПБ АВМГ4
ВЫКЛМГ УИИ М14(М10)
 МОДА СТРАСП
 ПВ (М10)
СМЕНИМ  ноп 
 СЧ ОЧЛИС-1(М14)
 ИЛИ Е16Е17
 НТЖ Е16Е17
 ЗП ОЧЛИС-1(М14)
АВСС СЧ ЗОЧ2-5(М2)
 НЕД 0
 УИ М14
 СЧ КУС2-1(М2)
 И Е5
 ПЕ АВСС3         ЗАКАЗ ОТ НОМБОБА
 СЧ КУС2-1(М2)
 И Е21
 ПЕ АВСС1         ЧТЕНИЕ СС
 СЧ КУС2-1(М2)
 ИЛИ Е23
 ЗП КУС2-1(М2)
АВСЧ СЧ ТЗОНМЛ-24(М1)
 И Е48
 ПЕ АВМГ1А        БЫЛ ОБМЕН С ДУБЛЬ-ЗОНОЙ
 СЧ ТЗОНМЛ-24(М1)
 ИЛИ Е48           УСТ.ПРИЗНАК ОБМЕНА С ДУБЛЬ-ЗОНОЙ
 ЗП ТЗОНМЛ-24(М1)
 УИА ЗКМЛ1А(М10)
 ПБ АВЛ
АВСС1 СЧ КУС2-1(М2)
 И Е11
 ПЕ АВМГ2         БЫЛО КОНТРОЛЬНОЕ ЧТЕНИЕ
 СЧ ОЧЛИС-1(М14)
 НТЖ Е18
 ЗП ОЧЛИС-1(М14)
 И Е18
 ПЕ АВМГ6         УСТ.ПРИЗНАК:ОШ.ПРИЧТ.СС ПРЕДЗОНЫ
 СЧ ТЗНМЛ-24(М1)
 СЛЦ Е25           +1 К СЧ-КУ ОБМЕНОВ С НАЛОЖЕНИЕМ
 ЗП ТЗНМЛ-24(М1)
 ПБ АВМГ2
АВСС3 СЧ 0
 ЗП БОБ           НОМЕР БОБИНЫ НЕОПРЕДЕЛЕН,Е48->БОБ
ОПРБОБ СЧ КУС2-1(М2)
 СДА 64+42
 УИ М10
 СЧ БОБ           ВЫДЕЛЕНИЕ НОМЕРА БОБИНЫ
 И Е12П1
        пе      БОБИ
        сч      БОБ
        сда     64+30
        и       Е12П1
        пе      БОБИ
БОБИ НОП ,     подозрительно !АВМЛ-100 МЕТКА "АВСС5"
 МОД ТУС-1(М10) НОМЕР БОБИНЫ -> СМ НОМБОБА
 ЗП 0
 ЗП БОБ
 СЧ ШОЧМГ-24(М1)
 ЧЕД 0
 СДА 64+1
 ПЕ Н
 СЧ РЯО5
 И Е24П1
 СЛЦ Е1
О ЗП ТЗОНМЛ-24(М1)
 ПБ ВЫХСН
АВЧТ СЧ ОШМЛ
 ПО АВЧТ1         ОШИБКИ НЕ ЗАФИКСИРОВАНО
 СЧ КУС2-1(М2)
 ИЛИ Е23           УСТ.ПРИЗНАК ЧТ.С НАЛОЖЕНИЕМ
АВЧТ2 ЗП КУС2-1(М2)
 ПБ АВСЧ
Н СЧ 0
 ПБ О
АВЧТ1 СЧ ВСЕЕД
 НТЖ Е23           ПОГАСИТЬ ПРИЗНАК НАЛОЖЕНИЯ
 И КУС2-1(М2)
 ПБ АВЧТ2
*
ОШЕС МОДА СТРАСП
 ПВ (М10)
 УИА ПРОШ1(М5)
 ПБ ВЫХОД
ОШЗП СЧ ОЧКУС-1(М14)
 ИЛИ Е18Е11
 ИЛИ Е21           УСТ.ПРИЗНАКИ КОНТРОЛЬНОГО ЧТЕНИЯ
 НТЖ Е21
 ИЛИ Е22           УСТ.ПРИЗНАКИ КОНТР.ЧТЕНИЯ ЗОНЫ
 ЗП ОЧКУС-1(М14)
 СЧ ОЧКСУМ-1(М14)
 ЗП СУММА-5(М2)   КОНТР.СУММА ЗАКАЗА-> СУММА
ЗПСЧМБ СЧ ОЧКУС-1(М14)
 И Е18
 НТЖ Е18
 ИЛИ НОММБ2
 ИЛИ Е19
 ЗП РЯ2
 УИА 1027
 СЧ ОЧЛИС-1(М14)
 ЗП РМР
 СЧИ М2
 И П3
 СЛЦ Е1
 СДА 64-2
 СЛЦ РЯ2
 ПВ ФИЗУР(М16)
 ЗП РЯ2
 УИА 3
 СЧ УХО
 НТЖ Е48-5(М2)     УСТ.(ГАШ.) ПРИЗН.ЗАПРЕТА ЗП НА НАПР.
 ЗП УХО
 И Е48-5(М2)
 ПЕ ВЫХПОВ        ЗП НА НАПРАВЛЕНИИ ЗАПРЕЩЕНА
 ЗП ШКЗЗП-5(М2)
 СЧ РЯ2
 ПЕ ВЫХПОВ        ОБМЕН С МБ ОКОНЧЕН НОРМАЛЬНО
 УИА 10(М5)
 МОДА ОШМБ
 ПВ (М10)         ПЧ: НЕТ ЗП
 СЧ КУС2-1(М2)
 СДА 64+42
 ПО ОБЩЛ ЗАКАЗ ОТ ДР Б
 УИ М17
 СЧ ТУСП-1(М17)
 УИ М10		АДРЕС НАЧ. ИПЗ -> М10
 СЧ КУС2-1(М2)
 И ЕФ
 ПО ОШЗП2	ПОЛЬЗ, СНЯТЬ ЗАДАЧУ
 СЧ ТУС-1(М17)
 УИ М10
 СЧ 0
 ЗП (М10)
ВЫХСН УИА ПРЛСЧМ(М5)
 ПБ ВЫХОД
ОШЗП2 УИА АНВО8А(М17)
 СЧИ М17
 ЗП 3(М10)        АНВО8А->В ПОЛЕ М33
 СЧ 5(М10)
 И Е20
 ИЛИ ИВ25
 НТЖ Е19           КОРРЕКЦИЯ М27 В ИПЗ
 ЗП 5(М10)
 УИА 73(М17)
 СЧИ М17
 ЗП 8(М10)
 ПБ ВЫХСН
ОБЩЛ УИА ПРЛСЧМ(М5)
ОБЩ1 СЧ ОЧКУС-1(М14)
 ИЛИ Е4
 ЗП ОЧКУС-1(М14)
 УИИ М3(М15)
ВЫХ1 УИА ЗН(М15)
 ПБ Б77
ПР5 СЧ ОЧКУС-1(М14)
 СДА 64+42
 ПО ОБЛ ЗАКАЗ ОТ ДР Б
 УИ М10
 СЧ ОЧКУС-1(М14)
 И ЕФ
 ПЕ ВЫХПРО	ЗАКАЗ ОТ СЛУЖЕБНОЙ ЗАДАЧИ
 СЧ ОЧЛИС-1(М14)
 ИЛИ Е19
 НТЖ Е19
 ЗП ОЧЛИС-1(М14)
 УИА -5(М3)
 СЧ ТЗНМЛ-24(М1)
 МОДА ДАЙБ
 ПВ (М4)
 СДА 64-8
 ИЛИ ВЫВТТ+10
 ЗП БОБПЧ
 СЧ ТУСП-1(М10)
 УИ М3
 СЧ ОЧКУС-1(М14)
 И П7
 НТЖ П5
 ПО ПР5Е
 СЧ ОЧКУС-1(М14)
 И Е3
 ПЕ ПРТЕ
 СЧ ОЧКУС-1(М14)
 И П37
 ПЕ ВЫХПРО        5÷1 РР КУС ≠ 0 (ПОДВ1,2,ТЕСТ,РАЗМ,NБ)
ПРТЕ УИА (М17)
 СЧ ТЗНМЛ-24(М1)
 И Е13
 ПЕ ПР5Е
 УИИ М16(М6)
 СЧ ОЧЛИС-1(М14)
 ПВ ПРИП1С(М10)   УСТ.ПРИПИСКУ 1-ОЙ СТРАНИЦЕ
 УИИ М6(М16)
 УИА -1023(М10)
 СЧ 0
ПР5А УИА 0
 ЗП 2047(М10)     ОЧИСТИТЬ СТРАНИЦУ ОБМЕНА
 МОДА ПР5А
 ЦИКЛ (М10)
ПР5Е УИА 3
 СЧ ОЧКУС-1(М14)
 СДА 64+24
 И Е12П1
 ЗП ИНФАВ(М3)
 СЧ ОЧКУС-1(М14)  ФОРМИР. ИНФ. СЛОВА ДЛЯ ОТВЕТА ПОЛЬЗОВАТЕЛЮ:
 И Е18           40Р-ПРИЗНАК ЧТЕНИЯ
 СДА 64+4	35-31РР. - НОМЕР СТРАНЦЫ
 ИЛИ ИНФАВ(М3)	12-1РР. - НОМЕР ЗОНЫ
 ЗП ИНФАВ(М3)   ЗОНА
        слц     Е10П3
        нтж     Е11
 ЗП ИНФАВ(М3)        ИФН. СЛОВО -> В ПОЛЕ СМ. В ИПЗ ПОЛЬЗОВ.
 СЧ БОБПЧ
 СДА 64+8
 СДА 64-20
 ИЛИ ИНФАВ(М3)
 ЗП ИНФАВ(М3)	НОМЕР БОБИНЫ ДЛЯ АВОСТНОЙ РАСП.
 УИА ВЫБМГ(М5)
 СЧИ М5
 ЗП 3(М3)
 СЧ 5(М3)
 И Е20
 ИЛИ ИВ25
 НТЖ Е19
 ЗП 5(М3)         КОРРЕКЦИЯ М27 В ИПЗ
 СЧ ОЧКУС-1(М14)
 СДА 64+42
 УИ М5
 УИА 1027
 СЧ ШКОЛП
 И Е48-1(М5)
 ПО ВНО
 НТЖ ШКОЛП
 ЗП ШКОЛП
 УИИ М3(М15)
 СЧ Е48-1(М5)
 ПВ ВШГП(М15)
 УИИ М15(М3)
ВНО УИА 3
 МОДА ПР5Б
 УИА (М5)
ПР5С СЧ ОЧКУС-1(М14)
 И Е11
 ПО (М5)	УХО НЕ ОБНУЛЯТЬ
 СЧ ВСЕЕД
 НТЖ Е48-5(М2)
 И УХО
 ЗП УХО
 СЧ 0
 ЗП ШКЗЗП-5(М2)
 ПБ (М5)
ПР5Б СЧ СОЛ-3(М6)
 И Е16(М15)
 ПО ВЫХСН         БЫЛ ОБМЕН
 НТЖ СОЛ-3(М6)
 ЗП СОЛ-3(М6)     ПОГАСИТЬ ПРИЗНАК ПОДВОДА
 ПБ ВЫХСН1
ОБЛ МОДА ПР5С
 ПВ (М5)
 УИА 3
 СЧ ОЧЛИС-1(М14)
 И Е48П37
 СДА 64+26
 УИ М5
 УИА -1023(М3)
 СЧ 0
РЛО МОДА (М5)
 ЗП 1023(М3)
 МОДА РЛО
 ЦИКЛ (М3)
 СЧ СОЛ-3(М6)
 И Е16(М15)
 ПО ОБЩЛ
 НТЖ СОЛ-3(М6)
 ЗП СОЛ-3(М6)
 УИА ОБХОД(М5)
 ПБ ОБЩ1
ВЫХПОВ СЧ 0
 ЗП КУС2-1(М2)    ОСВОБОДИТЬ НАПРАВЛЕНИЕ
 СЧИ М6
 НТЖ ЕС5017
 ПО БЕЗГАШ
 СЧ ШОЧНАП-3(М2)
 НТЖ Е48-1(М14)    ПОГАСИТЬ ЗАКАЗ В ШКАЛЕ ГОТОВЫХ
 ЗП ШОЧНАП-3(М2)  ЗАКАЗОВ К НАПРАВЛ.
КИТ ВНЕШ ЕС5017
БЕЗГАШ УИА ПОДОШ(М10)
 ПБ АВЛ
ВЫХСН1 УИА ОБХОД(М5)
 ПБ ВЫХОД
ВЫХПРО УИА НОРМА(М5)
 ПБ ВЫХОД
СТРАСП ЗП РЯ1
 СЧ Е19
 ИЛИ ТАБЛО
 ЗП ТАБЛО
 УВВ ’147’         КОМАНДА НА ТАБЛО: ЦИКЛИТСЯ МЛ
 СЧ ТЗНМЛ-24(М1)
 И Е12П1
 ПО (М10)         БОБИНА НЕ ОПРЕДЕЛЕНА
 ЗП РЯ2
 СЧ РЯ1
 ПО СТ6           ВЫКЛ.МГ,ВЫКЛ.КН.ЗП
 НТЖ НОБМ
 ПО СТ6           НЕ ОКОНЧЕН ОБМЕН
 СЧИ М5
 И П7
 НТЖ П7
 УИ М3
 СЧ Е8(М3)
 И К
 ПО СТ
 СЧ РМЛ1
 МОДА (М16)
 СД КСД8-3
 ИЛИ ИСЛОБМ
 И Е8(М15)
 ЗП БЛРАСП
 ПЕ СТ6
СТ СЧ Е8(М3)
 И КА
 ПО СТБ
 СЧ КУС2-1(М2)
 И Е5
 ЗП БЛРАСП
 ПЕ СТ6           ЗАКАЗ ОТ НОМБОБ’А
 СЧ ТЗОНМЛ-24(М1)
 И Е48
 ПО СТ6           Н.КЛЮЧ,ОШЗОНЕ,ДРБОБ В О/ЗОНЕ НЕ РАСП.
СТБ СЧ ТЗНМЛ-24(М1)
 НТЖ БОБПРП
 И Е11П1
 ПО СТ6           СБОИ ПРОГР.ПЕРЕПИСИ В СТ.НЕ ЗАНОС
 МОДА СТ6
 УИА (М4)
СТАТ СЧ ЗОНАТР        ФОРМИРОВАНИЕ ИНФ.ОБ ОШИБКЕ
 СДА 64+6          ДЛЯ СТАТИСТИКИ:
 СДА 64-12
 ИЛИ РЯ2           40Р - +1 К СЧ-КУ ОШ.В СС,
 СДА 64-6          33Р - +1 К СЧ-КУ ОШ.В КС,
 ЗП РЯ2           26Р - +1 К СЧ-КУ ОШ: ОБМЕН НЕ ОКОНЧЕН
 СЧИ М1            21÷18РР - N КУСКА (В КУСКЕ 32 ЗОНЫ),
 ИЛИ РЯ2           17÷7РР - НОМЕР БОБИНЫ,
 ИЛИ Е46
 ЗП РЯ2
 СЧ РЯ1
 И КВТ
 ИЛИ РЯ2
 ЗП РЯ2
	сч	ТЗОНМЛ-24(М1)
	и	E48
	сда	64+1
	слц	E47
	мод	D56754
	или	-16
	зп	D56752
	и	РЯ1
	нтж	РЯ1
	по	G56421
	сч	РЯ1
	и	КВТ
	слц	D56752
	мод	D56754
	зп	-16
G56421  ноп        
 СЧ СТМГ-24(М1)
 СДА 64+18
 И П77
 ПЕ ОПРМ
ДСВ СЧ СТМГ-24(М1)
 И Н19П24
 ЗП СТМГ-24(М1)
 СЧ РЯ2
 ПВ ДСЛ(М3)
 ПО (М4)
 СДА 64-18
 ИЛИ СТМГ-24(М1)
 ЗП СТМГ-24(М1)
 СЧ РМР
ОПРМ УИА 3
 УИ М3
 СЧ СТМГ+Д-1(М3)
 И РЯ1
 НТЖ РЯ1
 ПО ДСВ
 СЧ РЯ1
 И КВТ
 СЛЦ СТМГ+Д-1(М3)
 ЗП СТМГ+Д-1(М3)
 ПБ (М4)
*
*
СТ8 УИА -2(М5)
СТ9А СЧ 3
 И Е9
 ПО ОБХ
 СЧ ШКАЛА
 И РОБ
 ПЕ БЛ
ОБХ СЧ ВРЕМЯР
 ВЧОБ ВРЕМЯ
 ВЧАБ МИНУТА
 ПО РЛ            ПРОШЛО БОЛЬШЕ МИНУТЫ ОТ ПРЕДЫД.РАСП.
БЛ СЧ 0
 ЗП БЛРАСП
 ПБ (М10)
СТ6 ЭКВ СТ9А
СТ9 ЭКВ СТ9А
РЛ СЧИ М5
 НТЖ П12
 ПО ОШМБ          БЫЛ СБОЙ ПРИ ЧТ.С МБ СТРАНИЦЫ ЗАПИСИ
 СЧ БЛРАСП
 ПЕ БЛ
 УИА -5(М3)
 СЧ ТЗНМЛ-24(М1)
 МОДА Р1ЛА
 УИА (М4)
ДАЙБ И Е12П1
 ЗП РЯ1
Р1Л СДА 64+8
 ЗП РЯ2
 СЧ РЯ1
 УМН Ч01Д
 ЗП РЯ1
 СЧМР 64
 УМН R
 СДА 64-40
 ИЛИ РЯ2
 МОДА Р1Л
 ЦИКЛ (М3)
 ЗП РЯ2
 ПБ (М4)
Р1ЛА СДА 64+24
 ПЕ Р2Л           N БОБИНЫ ≥ 1000
 СЧ РЯ2
 СДА 64-8
 ИЛИ ВЫВТТ+3
 ПБ Р3Л
Р2Л СЧ РЯ2
 ИЛИ ВЫВТТ
Р3Л ЗП БУФМЛ         0-Е СЛОВО: НОМЕР БОБИНЫ
 МОДА ТИПРАС
 СЧ (М5)
 ЗП БУФМЛ+1       1-Е СЛОВО: ТИП ОШИБКИ
 СЧИ М1
 СДА 64-42
 РЗБ КР1
 ИЛИ ВЫВТТ+1
 ЗП БУФМЛ+2       2-Е СЛОВО: НОМЕР НАПРАВЛ.И УСТР-ВА
 СЧ КУС2-1(М2)
 СДА 64+24
 СЛЦ Е10П3
 И П777
 СДА 64-39
 РЗБ КР
 ИЛИ ВЫВТТ+2
 ЗП БУФМЛ+3       3-Е СЛОВО: НОМЕР ЗОНЫ
 СЧ ВЫВТТ+11
 ЗП БУФМЛ+4       4-Е СЛОВО:’ВРЕМЯ’
 МОДА ВР
 ПВ (М4)          5-Е СЛОВО: ВРЕМЯ РАСПЕЧАТКИ
 ЗП БУФМЛ+5
 СЧ КУС2-1(М2)
 И Е11
 ПЕ Р5Л1          ОШ.КСМ ПРИ КОНТРОЛЕ ЗП
Р3Л1 СЧИ М5
 НТЖ Е2
 ПО Р5Л           НЕТ СОВП.КЛЮЧА
 НТЖ П7
 ПО Р9Л           ДРУГАЯ БОБИНА
 НТЖ Е1
 ПО Р6Л           ДРУГАЯ ЗОНА
 ПБ ВХТТ
Р5Л СЧ РЯО5
 СДА 64+24
 СДА 64-33
 РЗБ КРЗБ6
 ИЛИ ВЫВТТ+5
 ПБ Р
Р5Л1 СЧ ВЫВТТ+9
 ПБ Р
Р6Л НОП
 СДА 64+1
 СЛЦ Е10П3
 И П777
 СДА 64-39
 РЗБ КРЗБ7
 ЗП БУФМЛ+4
 СЧ РЯО5
 И Е1
 ПЕ Р7Л
 СЧ ВЫВТТ+6
 ПБ Р8Л
Р7Л СЧ ВЫВТТ+7
Р8Л ИЛИ БУФМЛ+4
 ПБ Р
Р9Л СЧ БОБ
 УИА -5(М3)
 МОДА ДАЙБ
 ПВ (М4)
 СДА 64-8
 ИЛИ ВЫВТТ+8
Р ЗП БУФМЛ+4
Р10Л СЧ БУФМЛ+3
 СДА 64+8
 СДА 64-8
 ИЛИ ВЫВТТ+4
 ЗП БУФМЛ+3
 ПБ ВХТТ
ОШМБ СЧ ТОШМБ
 ЗП БУФМЛ
 СЧИ М2
 И Е2            ФОРМИРОВАНИЕ РАСП: ОШ.МБ УХОN
 СДА 64-7
 ИЛИ ТОШМБ+1
 ЗП БУФМЛ+1
ВХТТ СЧ ШКСВН
 И Е23
 ПО (М10)         НЕРЕЗИДЕНТ ЗАНЯТ
 СЧ ШКСВМ
 ПО (М10)         НЕТ МЕСТА В ОП
 СЧ ВРЕМЯ
 ЗП ВРЕМЯР
 МОДА БУФМЛ
 УИА (М16)
 УИИ ’32’(М10)
НАТ СЧ Е23
 ПБ ТВ40          ВЫДАТЬ РАСП.НА ТТ. ВЫХОД ПО <М10>.
ПРЕДЗО ИЛИ Е19
 И О18
 ЗП ОЧЛИС-1(М10)
 ПБ АВМГ2
АВМГ СЧ Е8(М2)
 УВВ ’31’          ИММИТАЦИЯ ОКОНЧАНИЯ ОБМЕНА С МЛ
 СЧ КУС2-1(М2)
 И Е11
 ПЕ АВМГ2         БЫЛО КОНТРОЛЬНОЕ ЧТЕНИЕ
 СЧ КУС2-1(М2)
 И Е5
 ПЕ АВМГ6         ЗАКАЗ ОТ НОМБОБА
АВМГ1А СЧ КУС2-1(М2)
 И Е18
 ПО АВМГ1         ШЛА ЗАПИСЬ НА МЛ
 ПВ ОЧ3И6(М10)    ОЧИСТИТЬ 3-Е И 6-Е СС
 ПБ АВМГ2
АВМГ1 ПВ ПРЛВОС(М10)   ВОССТАНОВИТЬ ПОСЛ.СЛОВО
АВМГ2 СЧ ЗОЧ2-5(М2)
 НЕД 0
 УИ М14
 СЧ КУС2-1(М2)
 И Е11
 ПО АВМГ3    КНТ НЕТ
 СЧ ВСЕЕД
 НТЖ Е21Е18
 НТЖ Е11
 И КУС2-1(М2)    ПОГАСИТЬ ПРИЗНАКИ КОНТР.ЧТЕНИЯ СС
 ЗП КУС2-1(М2)
 И Е22
 ПО АВМГ3         БЫЛО КОНТР.ЧТЕНИЕ ЗОНЫ ПОСЛЕ ЗП
 ИЛИ Е23
 НТЖ ВСЕЕД
 И КУС2-1(М2)
 ЗП ОЧКУС-1(М14)  ВОССТАНОВЛЕНИЕ ОЧКУС
 ПБ ЗПСЧМБ        НА ЧТЕНИЕ С МБ
АВМГ3 СЧ КУС2-1(М2)
 ЗП ОЧКУС-1(М14)
АВМГ4 СЧ ТЗНМЛ-24(М1)
 И Е26Е25
 НТЖ Е26Е25
 ПЕ ВЫХПОВ        ЦИКЛ ОБМЕНОВ НЕ ЗАКОНЧЕН
 СЧ ОЧКУС-1(М14)
 ИЛИ Е23
 НТЖ Е23
 ЗП ОЧКУС-1(М14)
 СЧ ОЧЛИС-1(М14)
 ИЛИ Е16Е17
 НТЖ Е16Е17
 ЗП ОЧЛИС-1(М14)
 СЧ ТЗНМЛ-24(М1)
 НТЖ Е26Е25
 ЗП ТЗНМЛ-24(М1)
 И Е27
 ПЕ ГАШС
 СЧ НОБМ
 ЗП РЯ1
 СЧ ТЗНМЛ-24(М1)
 ИЛИ Е27
 ЗП ТЗНМЛ-24(М1)
 И Е11П1
 ЗП РЯ2
 НТЖ БОБПРП
 ПО АВМГ5
 МОДА СТАТ
 ПВ (М4)
АВМГ5 СЧ СМЕЩ
 ИЛИ Е48-24(М1)
 ЗП СМЕЩ
 ЧЕД 0
 НТЖ Е1
 ПЕ ВЫХПОВ        КОМАНДА КАЧ.СМЕЩЕНИЯ ВЫДАНА
 СЧ Е15
 ИЛИ ТАБЛО
 ЗП ТАБЛО
 УВВ ’147’         КАЧ.СМЕЩЕНИЯ В СТОРОНУ МЕНЬШЕ
 ПБ ВЫХПОВ
АВМГ6 УИА ЗКМЛ4(М10)
АВЛ УИА ВЫХ0(М5)
ВЫХОД УИИ М3(М15)
 ПБ ВЫХ1
ГАШС СЧ ТЗНМЛ-24(М1)
 НТЖ Е27
 МОДА ВЫХПОВ
 УИА (М10)
ГАШСЧК СЧ П7
 СДА 64-24
 НТЖ ВСЕЕД
 И ТЗНМЛ-24(М1)
 ЗП ТЗНМЛ-24(М1)
 СЧ СМЕЩ
 ИЛИ Е48-24(М1)
 НТЖ Е48-24(М1)
 ЗП СМЕЩ
 ПЕ (М10)         КАЧАНИЕ НОМИН.НУЖНО ДЛЯ ДРУГИХ МЛ
 СЧ ТАБЛО
 ИЛИ Е15
 НТЖ Е15
 ЗП ТАБЛО
 УВВ ’147’         СБРОС КАЧАНИЯ СМЕЩЕНИЯ
 ПБ (М10)
ВР СЧ ВРЕМЯ
 ЗП РЯ1
 СЧ 0
 УИА -3(М3)
ВРС ЗП РЯ
ВР1 СЧ РЯ1
 МОДА (М3)
 ВЧАБ КВР+3
 ПЕ ВР2
 ЗП РЯ1
 СЧ РЯ
 СЛЦ Е29
 ПБ ВРС
ВР2 СЧ РЯ
 СДА 64-4
 ЗП РЯ
 МОДА ВР1
 ЦИКЛ (М3)
 РЗБ КВР1
 ИЛИ КВР2
 ПБ (М4)
Б77 НОП ,
 СЧ ИПЗМЛ+31
 ЗП СОХПР
 СЧИ М15
 ЗП СОХПР+1
 СЧИ М7
 СДА 64-2
 ИЛИ КВОЗВ
 ПВ ТБУФ(М15)
 СЧ СОХПР
 ЗП ИПЗМЛ+31
 РЕГ ’20’
 УИ 0
 МОД СОХПР+1
 ПБ 0
МЕС СЧ ТЗНМЛ-24(М1)
 И Е13
 ПО ВЫКЛЗП
 ПБ ОШЕС
СОХПР ПАМ 2
КВОЗВ КОНД М42В’60’М18В’1’
БОБПРП КОНД В’1747’
КВР КОНД Ф’1800000’
 КОНД Ф’180000’
 КОНД Ф’30000’
 КОНД Ф’3000’
КВР1 КОНД П’  0  0’
КВР2 КОНД П’00.00↑’
СН КОНД В’0’
ОБМН КОНД М23В’177’
НСС КОНД М30В’177’
НКС КОНД М37В’177’
КВТ КОНД М37В’1’М30В’1’М23В’1’
КР КОНД П’007770’
КР1 КОНД П’000077’
К КОНД В’207’
КА КОНД В’13’
Е32Е31 КОНД М30В’3’
Е27П25 КОНД М24В’7’
КСД8 КОНД М41В’130’
 КОНД М41В’120’
 КОНД М41В’110’
 КОНД М41В’100’
ВЫВТТ КОНД П’5Б0000’
 КОНД П’ МГ 00’
 КОНД П’  000 ’
  КОНД П’5Б000 ’
 КОНД П’00000/’
 КОНД П’00000 ’
 КОНД П’000-О ’
 КОНД П’000-Д ’
 КОНД П’Б0000 ’
 КОНД П’ОШ.ЗП ’
 КОНД П’ 0000 ’
 КОНД П’ВРЕМЯ ’
ТИПРАС КОНД П’Н.КЛЮЧ’
 КОНД П’ОШ.ЗОН’
 КОНД П’ТР.ЗОН’
 КОНД П’ДР.БОБ’
 КОНД П’КОН.СМ’
 КОНД П’ВКЛЮЧИ’
 КОНД П’ВКЛ ЗП’
 КОНД П’КОНОБМ’
 КОНД П’ОШ.МБ ’
 КОНД (5)В’0’
 КОНД П’Ч.БАЙТ’
 КОНД П’ОШ.ЦКС’
 КОНД П’СБ.ЗП ’
 КОНД П’ОШ.ЧЕК’
ТСММГ ТЕКСТ П’5СМЕНИ МГ00↑’
ТОШМБ ТЕКСТ П’5ОШ.МБ УХО0↑’
D56752        пам     1
D56753        конд    п' '
D56754        конд    в'-1'
РЯ КОНД В’0’
РЯ1 КОНД В’0’
РЯ2 КОНД В’0’
РЯ3 КОНД В’0’
РЯ4 КОНД В’0’
БЛРАСП КОНД В’0’
ПРАСП ПАМ 32
БУФМЛ КОНД (6)В’0’
Н19П24 КОНД В’-1’М18В’77’
КРЗБ6 КОНД П’777770’
КРЗБ7 КОНД П’777000’
БОБПЧ КОНД В’0’
О18 КОНД В’-1’М17В’1’
ШКАЛА КОНД М14В’1’      ЦКС
РОБ ПАМ 1
 ЭКВИВ (СС,НСС),(КС,НКС),(НОБМ,ОБМН)
ДИСП70 ВНЕШ БОБ,ИВ25,ВРЕЗП,ВРЕМЯ,ВРЕМЯР,ВСЕЕД,Е10П3
ДИСП70 ВНЕШ (Е11П1,П3777),(Е12П1,П7777),Е16Е17,Е18Е11
ДИСП70 ВНЕШ Е21Е18,Е24П1,Е26Е25,Е33П25,Е46П25,Е48П43
ДИСП70 ВНЕШ П77,П777,ЕNR,ЗОНАМЛ,ЗОНАТР
ДИСП70 ВНЕШ ОЧЛИС,ЛИС2,П14037,ЗАНПРП,ШКОЛП,ТУСП
ДИСП70 ВНЕШ ЗОЧ2,ИСЛОБМ,КУС2,МГНЕИС,МИНУТА,НАПРМГ
ДИСП70 ВНЕШ НОММБ2,ОАС,ОБЩТОМ,ОЧКСУМ,ОЧКУС,ОШМЛ,П30
ДИСП70 ВНЕШ П12,РМЛ1,ШОЧМГ,РЯО5,СМЕЩ,СОЛ,СТМГ,СУММА
ДИСП70 ВНЕШ ТБУФ,ШКСВН,ТВ40,ТЗНМЛ,ТЗОНМЛ,ТУС,УХО,Ч01Д
ДИСП70 ВНЕШ ШКЗЗП,ШКСВМ,ШКОМЛ,ШГ
ВЗУ ВНЕШ АНВО8А,ДСЛ,ПРИП1С,ФИЗОБМ
ВЗУ ВНЕШ ПОДОШ
ДМЛМБ ВНЕШ ВЫБМГ,ВЫХ0,ДЛЯДИС,ЗКМЛ1А,ЗКМЛ4,НОРМА,ОБХОД
ДМЛМБ ВНЕШ ОЧ3И6,ПРЛВОС,ПРЛСЧМ,ПРЛСЧА,ПРП,ПРП2,ЗН
ДИСП70 ВНЕШ РМР,ШОЧНАП,R,П37,П7,П3,ТАБЛО
ДИСП70 ВНЕШ Д,П5,ЗАНПР,ГУС,КРОМЕ,Е48П37
ДИСП70 ВНЕШ ИПЗМЛ
БОЭК1 ВНЕШ ЗАПВЫХ
ВЗУ ВНЕШ ФИЗУР,ЕФ
ВИСП ВНЕШ ВШГП
ЕСМЛ ВНЕШ ПРОШ1
СОСТАВ ВНЕШ ИНФАВ
 Е
 М
 ФИНИШ
